;-------------------------------------------------
;骑乘位
;性交系指令、レベル2
;-------------------------------------------------
@COM65

;前回の调教が3Pのとき3Pへ
IF PREVCOM == 71
	CALL COM_ABLE71
	SIF RESULT
		JUMP COM71
ENDIF
;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = TARGET
;侍奉快乐フラグ
TCVAR:108 = 1

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛门 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛门侍奉 10=道具 11=触手 12=Ａ调教 13=Ｖ调教）
TCVAR:PLAYER:2 = 1
TCVAR:PLAYER:童贞丧失 = 1
;强度
TCVAR:PLAYER:4 = 2000

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛门 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛门侍奉 10=道具 11=触手 12=Ａ调教 13=Ｖ调教）
TCVAR:TARGET:2 = 13
;强度
TCVAR:TARGET:4 = 1300

DOWNBASE:PLAYER:体力 += 20
DOWNBASE:PLAYER:气力 += 40

;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 150
	SOURCE:PLAYER:快Ｃ = 400

	SOURCE:快Ｖ = 500
	SOURCE:苦痛 = 250

	;处女だった场合は、苦痛のソースと反抗のソースを追加する
	IF TALENT:处女 == 1
		SOURCE:苦痛 += 1000
	ENDIF

	;再生处女だった场合は、苦痛のソースと反抗のソースを追加する
	IF TALENT:处女 == 2
		SOURCE:苦痛 += 250
	ENDIF

ELSE

	DOWNBASE:体力 += 80
	DOWNBASE:气力 += 50
	SOURCE:PLAYER:快Ｃ = 400

	SOURCE:快Ｖ = 500
	SOURCE:情爱 = 250
	SOURCE:性行动 = 250
	SOURCE:达成 = 250
	SOURCE:苦痛 = 500
	SOURCE:露出 = 120
	SOURCE:偏离正轨 = 600
	SOURCE:反感 = 800

	;上のほうで计算した污れデータ
	SOURCE:不洁 = Y*10 + 60
	CALL 特殊ソース处理
ENDIF
;Vへの苦痛は先に处理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;污れの处理
;-------------------------------------------------
;射精に关系なく行われる处理はここから
CALL STAIN_膣内Ｐ插入

;-------------------------------------------------
;経験の处理
;-------------------------------------------------
;性交经验
EXP:PLAYER:插入经验 ++
EXP:Ｖ经验 ++
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	ENDIF
		EXP:PLAYER:时奸经验 += 1
ELSE
	;异常经验
	IF TALENT:处女 && TALENT:处女 != -1 && !GETBIT(CFLAG:3,1)
		IF ASSIPLAY && (TALENT:PLAYER:2 & 1)
			EXP:异常经验 += 3
		ELSE
			EXP:异常经验 += 2
		ENDIF
		SETBIT CFLAG:3,1
	ENDIF
	;同性爱　レズなら返回值1ゲイなら返回值2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 7
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	ENDIF
	EXP:Ｖ性交经验 ++
	IF !ASSIPLAY
		EXP:爱情经验 += TALENT:恋慕 * 2
		;处女
		SIF TALENT:处女 && TALENT:处女 != -1 && TALENT:恋慕
			EXP:爱情经验 += 5
	ENDIF
ENDIF
RETURN 1

@CAN_COM65(ARG)
;-------------------------------------------------
;实行判定处理
;-------------------------------------------------
IF ARG == 1
	CALL ChkBeforeInsert(TARGET)
	IF RESULT == -1
		TFLAG:插入不可 = 1
	ENDIF
ENDIF
SIF TFLAG:插入不可
	RETURN 0
IF FLAG:70 == 1
	SKIPDISP ARG
	PRINTL 用骑乘位侵犯
ELSE
	SKIPDISP ARG
	PRINTL 骑乘位
	A = 0
	S = 0

	;全部の命令に共通の要素を考虑
	;(顺从が高いと命令に从いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 3
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ｖ感觉
	IF ABL:Ｖ感觉
		SIF S
			PRINT  + 
		A += ABL:Ｖ感觉 * 2
		PRINT Ｖ感觉
		PRINTV 'LV,ABL:Ｖ感觉,'(,ABL:Ｖ感觉 * 2,')
		S = 1
	ENDIF
	;ABL:侍奉精神
	IF ABL:侍奉精神
		SIF S
			PRINT  + 
		A += ABL:侍奉精神 * 4
		PRINT 侍奉精神
		PRINTV 'LV,ABL:侍奉精神,'(,ABL:侍奉精神 * 4,')
		S = 1
	ENDIF
	IF MARK:快乐刻印
		SIF S
			PRINT  + 
		A += MARK:快乐刻印 * 3
		PRINT 快乐刻印
		PRINTV 'LV,MARK:快乐刻印,'(,MARK:快乐刻印 * 3,')
		S = 1
	ENDIF

	;PALAM:润滑（の不足）
	IF PALAM:润滑 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT 润滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:情欲
	IF GETPALAMLV(PALAM:情欲,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:情欲,5) * 3
		PRINT 情欲
		PRINTV 'LV,GETPALAMLV(PALAM:情欲,5),'(,GETPALAMLV(PALAM:情欲,5) * 3,')
		S = 1
	ENDIF
	;害羞
	IF TALENT:羞耻心 > 0
		PRINT  - 
		A -= 2
		PRINT 害羞
		PRINTV '(,2,')
		S = 1
	ENDIF
	;否定快感（淫乱、娼妇で无效）
	IF TALENT:快感应答 < 0 && !TALENT:恋慕
		PRINT  - 
		A -= 5
		PRINT 否定快感
		PRINTV '(,5,')
		S = 1
	ENDIF
	;讨厌男人（恋慕、亲爱で无效）
	IF OTOKOGIRAI(TARGET)
		PRINT  - 
		A -= 12
		PRINT 讨厌男人
		PRINTV '(,12,')
		S = 1
	ENDIF
	;恋慕
	IF TALENT:恋慕 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 15
		PRINT 恋慕
		PRINTV '(,15,')
		S = 15
	ENDIF
	;亲爱
	IF TALENT:恋慕 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 亲爱
		PRINTV '(,5,')
		S = 5
	ENDIF


	;处女
	LOCAL = MAX(10,40 - EXP:Ｖ经验)
	IF TALENT:处女 && TALENT:处女 != -1
		PRINT  - 
		A -= LOCAL
		PRINT 处女
		PRINTV '(,LOCAL,')
		S = 1
	ELSEIF EXP:Ｖ经验 < EXPLV:2
		PRINT  - 
		A -= 15
		PRINT Ｖ经验不足
		PRINTV '(,15,')
		S = 1
	ENDIF

	;调教者が扶她
	IF (TALENT:PLAYER:2 & 3) == 3
		SIF S
			PRINT  + 
		A += 4
		PRINT 扶她
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚药
	IF TCVAR:媚药
		SIF S
			PRINT  + 
		A += 6
		PRINT 媚药
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;爱液的污垢
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;精液的污垢
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;肛门的污垢
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15

	Y = Y * (2 - TALENT:污臭耐性) / 2

	;有污垢
	IF Y
		PRINT  - 
		A -= Y
		PRINT 有污垢
		;污臭钝感
		IF TALENT:污臭耐性 > 0
			PRINT 、污臭钝感
		ELSEIF TALENT:污臭耐性 < 0
			PRINT 、污臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF


	;合计を表示(35以上で实行)
	PRINT  = 
	PRINTV A

	V = 35
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 实行值
	PRINTV V

	WAIT
	SKIPDISP 0
	;实行できない
	SIF A < V
		RETURN 0
ENDIF
SKIPDISP 0
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE65
;骑乘位实行判定
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(65)
	RETURN RESULT
;毒人形だと性器がついてないのでダメ
SIF TALENT:196 == 99
	RETURN 0
;调教者が男人、扶她の场合はある程度勃起てないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;振动棒插入中はダメ
SIF TEQUIP:13
	RETURN 0
;男人だとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
;体格差がある场合は経験がないとダメ
SIF 插入不可(TARGET) == 1 && EXP:Ｖ经验 < 50 && !TALENT:PLAYER:禁断的知识
	RETURN 0
;さらに体格差があると禁断的知识が必要
SIF 插入不可(TARGET) == 2 && (!TALENT:PLAYER:禁断的知识 || EXP:Ｖ经验 < 50)
	RETURN 0
SIF 插入不可(TARGET) == 3
	RETURN 0
;调教者が男人か半人半妖か扶她じゃないとダメ
SIF !(TALENT:PLAYER:2 & 2)
	RETURN 0

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;浴室PLAY中は气垫床が无いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;处女で、顺从4以下・百合属性4以下のASSIの场合はできない（施虐狂ならＯＫ）
IF TALENT:处女 && ASSIPLAY
	SIF (ABL:ASSI:顺从 <= 4 || ABL:ASSI:技巧 <= 4) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
;时间停止中はスキップ
IF FLAG:70 != 1
	;处女かつ润滑不足はだめ
	SIF PALAM:润滑 < PALAMLV:2 && TALENT:处女 && TALENT:处女 != -1
		RETURN 0
	;绳使用中は不可
	SIF TEQUIP:绳
		RETURN 0
ENDIF
;睡眠奸中
SIF CFLAG:恶作剧 > 1
	RETURN 0
;ベッドルームor浴室ルームで气垫床がないとダメ
SIF !BEDROOM(CFLAG:PLAYER:当前位置) && !(BATHROOM() && ITEM:20)
	RETURN 0
RETURN 1

