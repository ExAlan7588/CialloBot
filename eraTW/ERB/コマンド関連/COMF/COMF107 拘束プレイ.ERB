;-------------------------------------------------
;拘束
;SM系指令、レベル1
;-------------------------------------------------
@COM107
IF CFLAG:强行内射 == 1 && !TEQUIP:拘束
	PRINTFORML 以前被强制中出过的%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%对此深恶痛绝的样子…
	PRINTFORML [0] 放弃
	PRINTFORML [1] 最后再来一次吧
	$LOOP
	INPUT
	IF RESULT == 0
		PRINTFORML 为了不惹恼%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%、放弃了捆绑
		RETURN -1
	ELSEIF RESULT == 1
		IF ABL:受虐属性 >= 3 || (CFLAG:4 + ABL:PLAYER:话术技能 * 100) > 1500
			PRINTFORML 在苦苦哀求之下总算把%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%说服了
			CFLAG:强行内射 = 2
		ELSE
			PRINTFORML 无论%NAME_OUTPUT_TRANSLATION("CALLNAME", PLAYER)%怎么说、%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%就是不答应…
			SOURCE:反感 += 10000
			BASE:情绪 /= 2
			TFLAG:193 = 1
			RETURN -1
		ENDIF
	ELSE
		GOTO LOOP
	ENDIF
ELSEIF CFLAG:强行内射 == 3 && !TEQUIP:拘束
	PRINTFORML 面对厚颜无耻再次提出想玩捆绑的%NAME_OUTPUT_TRANSLATION("CALLNAME", PLAYER)%、%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%投来了冰冷的视线
	CALL ASK_YN("对不起",@"但是我还是想和%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%来拘束PLAY")
	IF RESULT == 0
		PRINTFORML 察觉到危险的%NAME_OUTPUT_TRANSLATION("CALLNAME", PLAYER)%立刻打了退堂鼓
		RETURN -1
	ELSEIF RESULT == 1
		IF ABL:受虐属性 >= 3 || (CFLAG:4 + ABL:PLAYER:话术技能 * 100) > 3000
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%在软磨硬泡之下、最后还是同意被捆绑了
			PRINTW 真好对付！
		ELSE
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%一言不发背朝着%NAME_OUTPUT_TRANSLATION("CALLNAME", PLAYER)%穿上了衣服
			CUP:情欲 = 0
			SOURCE:反感 += 10000
			BASE:情绪 = 0
			FOR LOCAL,0,CHARANUM
				SIF CFLAG:诶嘿嘿 == 2
					CALL SET_DERAY(5,LOCAL)
				CALL ENDUFUFU(LOCAL)
			NEXT
			TFLAG:106 = 0
			;PLAYERとの接触位置をリセット
			CALL TOUCH_SET(0,0,0,1)
			;はだけフラグをオフ
			TEQUIP:2 = 0
			TEQUIP:上半身裸露状态 = 0
			;角色全员の服再セッティング
			FOR LOCAL,0,CHARANUM
				CALL CLOTHES_SETTING_TRAIN(LOCAL)
				;上衣脱衣完毕フラグをオフ
				TEQUIP:LOCAL:5 = 0
			NEXT
			TFLAG:193 = 1
			RETURN 1
		ENDIF
	ENDIF
ENDIF
;-------------------------------------------------
;着脱の处理
IF TEQUIP:拘束
	TEQUIP:拘束 = 0
	TCVAR:强行 = 0
ELSE
	TEQUIP:拘束 = 1
ENDIF

;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = PLAYER
;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
DOWNBASE:体力 += 10
DOWNBASE:气力 += 80

SOURCE:苦痛 = 60
SOURCE:恐怖 = 180
SOURCE:屈从 = 100
SOURCE:反感 = 350
;-------------------------------------------------
;特殊ソース处理
;-------------------------------------------------
;紧缚经验が高いと消费减少
IF EXP:紧缚经验 < EXPLV:3
	TIMES DOWNBASE:体力 , 1.00
	TIMES DOWNBASE:气力 , 1.00
ELSEIF EXP:紧缚经验 < EXPLV:4
	TIMES DOWNBASE:体力 , 0.90
	TIMES DOWNBASE:气力 , 0.90
ELSE
	TIMES DOWNBASE:体力 , 0.70
	TIMES DOWNBASE:气力 , 0.70
ENDIF
;-------------------------------------------------
;経験の处理
;-------------------------------------------------
EXP:紧缚经验 += 1
;同性爱　レズなら返回值1ゲイなら返回值2
;IF HETEROSEX(TARGET,PLAYER)
;	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
;	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
;ENDIF
RETURN 1
;-------------------------------------------------
;装着继续の处理
;-------------------------------------------------
@EQUIP_COM107(ARG)
PRINTL ＜拘束中＞
;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
DOWNBASE:ARG:体力 += 10

SOURCE:ARG:苦痛 += 30
SOURCE:ARG:恐怖 += 180
SOURCE:ARG:屈从 += 50
SOURCE:ARG:反感 += 200
;-------------------------------------------------
;特殊ソース处理
;-------------------------------------------------
;紧缚经验が高いと消费减少
LOCAL = 60
IF EXP:ARG:紧缚经验 < EXPLV:3
	TIMES LOCAL , 1.00
ELSEIF EXP:ARG:紧缚经验 < EXPLV:4
	TIMES LOCAL , 0.90
ELSE
	TIMES LOCAL , 0.70
ENDIF
DOWNBASE:ARG:气力 += LOCAL

;-------------------------------------------------
;経験の处理
;-------------------------------------------------
EXP:ARG:紧缚经验 ++
EXP:PLAYER:嗜虐快乐经验 += 1
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE107
;拘束PLAY实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(107)
	RETURN RESULT
;绳でしばってるとダメ
SIF TEQUIP:绳
	RETURN 0
;睡眠奸中
SIF CFLAG:恶作剧 > 1
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
;解除是つでも可能
SIF TEQUIP:拘束
	RETURN 1
;道具を持っているか、道具无设定でないとダメ
SIF ITEM:27 == 0 && NOITEM == 0
	RETURN 0
RETURN 1

