;-------------------------------------------------
;吸乳头
;爱抚系指令、レベル1
;-------------------------------------------------
@COM10
;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = PLAYER

;侍奉快乐
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:快Ｂ = ABL:PLAYER:舌 * 5

ELSE

	DOWNBASE:体力 += 5
	DOWNBASE:气力 += 50

	SOURCE:快Ｂ = 20
	SOURCE:情爱 = 30
	SOURCE:露出 = 20
	SOURCE:不洁 = 30
	SOURCE:反感 = 30

	SOURCE:快Ｂ += ABL:PLAYER:舌 * 5
	SOURCE:情爱 += ABL:PLAYER:舌 * 5

	SIF ABL:亲密 >= 10
		SOURCE:情爱 += (ABL:亲密 * 20)

	;调教者のTALENT:舌头灵活をみる
	SIF TALENT:PLAYER:舌头灵活
		TIMES SOURCE:快Ｂ , 1.50

	;调教者のTALENT:猫舌をみる
	SIF TALENT:PLAYER:猫舌
		TIMES SOURCE:快Ｂ , 0.75
	;-------------------------------------------------
	;特殊ソース处理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑郁ソース追加
	SIF TALENT:自己爱 < 0 || TALENT:抵抗
		SOURCE:郁闷 += 100
ENDIF
;-------------------------------------------------
;污れの处理
;-------------------------------------------------
;奴隷のＢ⇔调教者の口的污垢が移动
STAIN:5 |= STAIN:PLAYER:0
STAIN:PLAYER:0 |= STAIN:5
;-------------------------------------------------
;経験の处理
;-------------------------------------------------
;同性爱　レズなら返回值1ゲイなら返回值2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 5
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	ENDIF
ENDIF
;调教者が猫舌持ちの场合は、调教者に嗜虐快乐经验を追加
SIF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快乐经验 += 1
EXP:PLAYER:口淫经验 ++
RETURN 1


;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE10
;吸乳头实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(10)
	RETURN RESULT
;上半身着衣
SIF (TEQUIP:1 & 2) && TEQUIP:3 == 0
	RETURN 0
;搾乳器使用中は不可
SIF TEQUIP:17
	RETURN 0
;乳头夹使用中は不可
SIF TEQUIP:16
	RETURN 0
;乳房电极装着中はダメ
SIF TEQUIP:24
	RETURN 0
SIF TEQUIP:体位 == 5 || TEQUIP:体位 == 2 || TEQUIP:体位 == 3
	RETURN 0
RETURN 1

