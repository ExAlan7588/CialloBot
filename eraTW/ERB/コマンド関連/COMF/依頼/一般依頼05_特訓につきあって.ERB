;存否函数
;F函数はTRYCALL系が使えないので通常函数で存否配列を作っちゃう
@EXIST_一般依赖5
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依赖5(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依赖名"
	CALLF MAKE_STR(V_NAME, "关于特训")
CASE "依赖内容表示"
	CALLF MAKE_STR(V_NAME, @"帮助%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%做弹幕特训")
CASE "依赖报酬表示"
	CALLF MAKE_STR(V_NAME, @"%金额表示(500)%")
CASE "依赖实行指令名"
	CALLF MAKE_STR(V_NAME, "特训！")
CASE "依赖备考表示"
	CALLF MAKE_STR(V_NAME, "首先移动到可以特训的场所吧")

CASE "依赖内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "作业"))
CASE "依赖サブ"
CASE "依赖期限"
;发生时ではなく受托时に确定します
CASE "依赖发生率"
;これは他の一般依赖との相对概率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 10)
CASE "依赖发生条件"
	;现状の人里では达成不能なため据点が人里の场合は除外
	SIF ABL:ARG:战斗能力 <= ABL:MASTER:战斗能力 && ABL:ARG:亲密 >= 2 && MAIN_MAP != 2
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖受托可能条件"
	IF CFLAG:ARG:宴会参加
	;依赖发生条件で人里が除外できないようなのでこちらで除外
	ELSEIF MAIN_MAP == 2
	ELSEIF GET_MAPID(CFLAG:MASTER:当前位置) == MAIN_MAP
		CALLF MAKE_INT(V_NAME, 1)
	ENDIF
CASE "依赖实行可能条件"
	SIF 战斗训练可(CFLAG:MASTER:当前位置)
		CALLF MAKE_INT(V_NAME, 1)
;成功/失败处理はCARRYOUTで直接书く
;SOURCEやITEM入手、消费が烦杂になるので
CASE "依赖消灭条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖人连れ出し可能"
	CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_一般5(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM  BAD_WEATHER

CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依赖条件表示"
;何か専用の表示情报を设けたい时用
;受托済みの依赖表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも无しで
;ここ以外の表示项目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが见栄えが良いです

CASE "依赖实行时"
;このCASEに限り、RESULTが意味を持ちます
;依赖实行时のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な参数は"中止", "实行して成功", "实行して失败", "实行して未解决"です
;独自处理を仕込みたい时は"实行して未解决"で先延ばしするなりしてください
;
;"依赖实行时"に限っていえば、CHARAはTARGETと同一であり、必ずしもCLIENT_IDと一致するとは限りません
;个々のケースで确认する必要があります
;この依赖は「作业」扱いであり、CLIENTがTARGETになっている时のみ实行できます
;

;天气を参照
	BAD_WEATHER = 0
	SELECTCASE TIME:5
	CASE 4, 6, 8, 10, 12, 13
		;恶劣天气
		BAD_WEATHER = 1
	CASE 5, 9
		;ヤバい天气
		BAD_WEATHER = 2
	;それ以外
	CASEELSE
		;夏场は死ねる
		SIF DAY:2 == 2 && TIME:5 == 1 && INRANGE(TIME, 361, 1019)
			BAD_WEATHER = 3
	ENDSELECT
	SIF !OUTROOF(CFLAG:MASTER:当前位置)
		BAD_WEATHER = 0
	
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%和%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%进行着弹幕游戏的特训
	SELECTCASE TIME:5
	CASE 4, 6
		PRINTL 进行雨中的弹幕训练
		PRINTW 非常的累
	CASE 1
		IF BAD_WEATHER == 3
			PRINTL 在强烈的直射日光下进行弹幕训练
			PRINTW 好像要中暑了…
		ELSE
			PRINTW 在万里无云的蓝天下进行弹幕训练
		ENDIF
	CASE 5
		PRINTL 在暴雨中、强行进行严格的训练
		PRINTW 累得快死了
	CASE 8, 10, 12, 13
		PRINTL 进行雪中的弹幕训练
		PRINTW 非常的累
	CASE 9
		PRINTL 在暴风雪中、强行进行严格的训练
		PRINTW 累得快死了
	CASEELSE
;		PRINTL 进行弹幕训练
	ENDSELECT
	
	TCVAR:MASTER:空腹时刻 -= 15
	TCVAR:TARGET:空腹时刻 -= 15
	DOWNBASE:MASTER:体力 = 100 * (2 + BAD_WEATHER)
	DOWNBASE:MASTER:气力 = 100 * (2 + BAD_WEATHER)
	DOWNBASE:体力 = 50 * (2 + BAD_WEATHER)
	DOWNBASE:气力 = 50 * (2 + BAD_WEATHER)
	SOURCE:欢乐 = 600 + 100 * (1 + BAD_WEATHER)
	TIME += 60
	EXP:MASTER:战斗经验 += 1 + ABL:战斗能力
	EXP:战斗经验 += MAX(ABL:战斗能力 - ABL:MASTER:战斗能力 + 1, 1) * 3 + BAD_WEATHER
	
	IF ITEM:战术指南书
		EXP:MASTER:战斗经验 ++
		EXP:TARGET:战斗经验 ++
	ENDIF
	IF ITEM:运动饮料 >= 2
		IF BAD_WEATHER == 3
			DOWNBASE:MASTER:体力 -= 200
			DOWNBASE:MASTER:气力 -= 300
			DOWNBASE:体力 -= 70
			DOWNBASE:气力 -= 100
		ELSE
			DOWNBASE:MASTER:体力 -= 50
			DOWNBASE:MASTER:气力 -= 50
			DOWNBASE:体力 -= 20
			DOWNBASE:气力 -= 20
		ENDIF
		SOURCE:欢乐 += 1000
		PRINTL 训练后二人一起喝了运动饮料
		PRINTW 水分渗透进了疲惫的身体里…
		ITEM:运动饮料 -= 2
		;元の构文だと消费は1个っぽいが2个に
	ENDIF
	RETURN IRAI_CONST_RESULT("实行して成功")
CASE "依赖成功时"
;成功报告时の报酬など
	CALL CHANGE_CFLAG(2, TARGET, 30)
	CALL CHANGE_CFLAG(4, TARGET, 20)
	CALL GET_MONEY_YEN(500, 2)
CASE "依赖失败时"
;失败报告时の处理
	CALL CHANGE_CFLAG(2, TARGET, -10)
	CALL CHANGE_CFLAG(4, TARGET, -10)
ENDSELECT
