;存否函数
;F函数はTRYCALL系が使えないので通常函数で存否配列を作っちゃう
@EXIST_一般依赖18
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依赖18(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依赖名"
	; CALLF MAKE_STR(V_NAME, "谁か作ってくれないかなー！")
	CALLF MAKE_STR(V_NAME, "有谁能帮我做一下吗！")
CASE "依赖内容表示"
	; CALLF MAKE_STR(V_NAME, @"注文された料理を作って届ける")
	CALLF MAKE_STR(V_NAME, @"将点的菜肴送到")
CASE "依赖报酬表示"
	;CALLF MAKE_STR(V_NAME, @"\\2000＋おまけ")
	CALLF MAKE_STR(V_NAME, @"%金额表示(2000)%＋奖金")
CASE "依赖实行指令名"
	;何か问题があったら变えてください
	;IF TALENT:MASTER:性别 == 2
	;	CALLF MAKE_STR(V_NAME, "出前汉です")
	;ELSE 
	;	CALLF MAKE_STR(V_NAME, "UmanEatsです")
	;ENDIF
	IF TALENT:MASTER:性别 == 2
		CALLF MAKE_STR(V_NAME, "您好，外卖到了")
	ELSE 
		CALLF MAKE_STR(V_NAME, "您好，UmanEats外送")
	ENDIF
CASE "依赖内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "其他"))
CASE "依赖サブ"
	;これは乱数シード
	CALLF MAKE_INT(V_NAME, RAND:1000000)
CASE "依赖期限"
;发生时ではなく受托时に确定します
	CALLF MAKE_INT(V_NAME, 3)
CASE "依赖发生率"
	CALLF MAKE_INT(V_NAME, 5)
CASE "依赖发生条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依赖受托可能条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依赖实行可能条件"
	SIF DISH_NAME != "" && TARGET == ARG
		CALLF MAKE_INT(V_NAME, 1)
;成功/失败处理はCARRYOUTで直接书く
;SOURCEやITEM入手、消费が烦杂になるので
CASE "依赖消灭条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖人连れ出し可能"
ENDSELECT


@IRAI_一般18(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM 评价基礎 ;料理自体の评价
#DIM 一致评价 ;一致した料理を出せたかでBOUNS
#DIMS 味要求  ;依赖者の要求した味を置いておく变量
#DIM おまけ   ;大成功时のBOUNS。成功/大成功判定が出てから报酬受け取りまでに他の依赖函数の实行が挟まるケースないよね？
#DIM CHK_SLOT
#DIM SAVECLR  ;色保存用
#DIM CNT
#DIM RANDOM
#DIMS TYPE
#DIMS NAMETEMP  ;TEMP系は变量待避用
#DIMS TASTETEMP
#DIMS TYPETEMP
#DIMS COMMENTTEMP
#DIMS TEMPS , 2
#DIM TIMETEMP
#DIM MAXLIMITTEMP
#DIMS DISH_REQUEST ;依赖者の注文した料理を格纳
#DIMS DISH_DELIVER ;プレイヤーが届けた料理の名前
#DIM CONST 评价点补正 = 75    ;料理自体のスコアに对する补正。百分率
#DIM CONST 评价基准 = 50 , 75 ;评价基准:0未满のスコアなら失败、评价基准:0以上なら成功、评价基准:1以上なら大成功
#DIM CONST 一致BOUNS = 30  ;注文通りの品を届けたBOUNS
#DIM CONST ユニーク阈值 = 5   ;キャラがユニーク料理を发注しはじめる亲密度
#DIMS CONST SPLITTER = "／"   ;料理名と料理IDの区切りに使う文字

CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_SLOT  = SEARCH_IRAI_SLOT(IRAI_ID)



;**********依赖料理生成部**********
IF REQUEST_DISHNAME:CHARA:0 == ""
	;料理の定义函数が通常函数で@一般依赖18から呼べないので乱数シードを固定して同じ料理名を出す
	;ユニーク料理の卖り込み机能を舍てればERHとこの烦杂な实装を除去できる(受托后、期限内に连れ出し可能になると结果が变わってしまうため变量に入れる必要がある)
	;DISH_NAMEなどを破坏するので待避。力业にしたって污すぎる…
	NAMETEMP = %DISH_NAME%
	TASTETEMP = %DISH_TASTE%
	TYPETEMP = %DISH_TYPE%
	COMMENTTEMP = %DISH_COMMENT%
	TIMETEMP = DISH_TIME
	MAXLIMITTEMP = DISH_MAXLIMIT

	;100番台＝轻食、200番台＝主食、300番台＝デザート、400番台＝各キャラのユニーク料理
	;亲密だと自分のユニーク料理をしれっと卖り込んでくることがある
	IF ABL:TARGET:亲密 >= ユニーク阈值 && COM_351_CONDITION(CHARA) && RAND:4 == 0
		;仲がいい、かつ连れだし可能なら25%の概率でユニーク料理确定
		RANDOM = 400 + CHARA
	ELSE
		RANDOM = RAND:3 * 100 + 100
	ENDIF

	DUMPRAND
	RANDOMIZE IRAI_SUB:CHK_SLOT
	;注文される料理を抽选。跳蛋系はなしで
	DO
		IF RANDOM >= 400
			;キャラのユニーク料理が抽选された
			CALL DISHDATA(RANDOM , TYPE , 1)
		ELSE
			;泛用の料理が抽选された
			CALL DISHDATA(RAND(RANDOM , RANDOM + 101) , TYPE , 1)
		ENDIF

		;******キャラ别处理******
		;嫌いLv10クラスの嫌い方でなければ强制削除はしないようにした(小兔姫の辛いLv8は除去していない)
		;明确な理由はないので必要とあらばいじってほしい
		;あくまで料理の基本要素として加わっているものだけ。プレイヤーがあとから足せるものは除去不能

		;くたかとミスティアは禽肉と卵を除去する
		IF GROUPMATCH(CHARA, [[米斯蒂亚]] , [[久侘歌]])
			SIF STR_MULTI_COUNT(DISH_TASTE , "禽肉/卵")
				DISH_NAME = 
		ENDIF

		;リグル、ラルバは虫肉を除去する
		IF GROUPMATCH(CHARA, [[莉格露]] , [[拉尔瓦]])
			SIF STR_MULTI_COUNT(DISH_TASTE , "虫肉")
				DISH_NAME = 
		ENDIF

		;诹访子はカエルを除去する
		IF GROUPMATCH(CHARA, [[诹访子]])
			SIF STR_MULTI_COUNT(DISH_TASTE , "青蛙")
				DISH_NAME = 
		ENDIF

		;わかさぎ姫は鱼orうまい鱼を除去する
		IF GROUPMATCH(CHARA, [[若鹭姫]])
			SIF STR_MULTI_COUNT(DISH_TASTE , "鱼/好吃的鱼")
				DISH_NAME = 
		ENDIF
		;************************
	LOOP DISH_NAME == "" || STRCOUNT(DISH_NAME , "跳蛋") > 0

	REQUEST_DISHNAME:CHARA:0 = %DISH_NAME%%SPLITTER%{RANDOM}

	DISH_NAME = %NAMETEMP%
	DISH_TASTE = %TASTETEMP%
	DISH_TYPE = %TYPETEMP%
	DISH_COMMENT = %COMMENTTEMP%
	DISH_TIME = TIMETEMP
	DISH_MAXLIMIT = MAXLIMITTEMP
	INITRAND
ENDIF
;**********************************

SPLIT REQUEST_DISHNAME:CHARA:0 , SPLITTER , TEMPS
DISH_REQUEST = %TEMPS:0%
RANDOM = TOINT(TEMPS:1)

SELECTCASE SCENE
CASE "依赖条件表示"
	;何か専用の表示情报を设けたい时用
	;受托済みの依赖表示で使用されます
	;
	;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも无しで
	;ここ以外の表示项目は
	;PRINTFORML 　　　　　：
	;となっているのでスペースを揃えたほうが见栄えが良いです
	; PRINTFORM 欲しい料理：%DISH_REQUEST%
	PRINTFORM 想要的菜肴：%DISH_REQUEST%
	IF RANDOM >= 400
		; PRINTFORML ……%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%の得意料理じゃないか？
		PRINTFORML ……这是%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%的拿手菜来着？
	ELSE
		PRINTL 
	ENDIF
CASE "依赖实行时"
	;このCASEに限り、RESULTが意味を持ちます
	;依赖实行时のRESULTにはIRAI_CONST_RESULT()を使用してください
	;可能な参数は"キャンセル", "实行して成功", "实行して失败", "实行して未解决"です
	;独自处理を仕込みたい时は"实行して未解决"で先延ばしするなりしてください
	VARSET おまけ
	; PRINTFORML 「ご注文の%DISH_NAME%になりまーす」
	PRINTFORML 「这是您点的%DISH_NAME%」
	; PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%は%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%に<%DISH_NAME%>を届けた
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%将%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%点的<%DISH_NAME%>交给了%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%

	;ローカル变量にグローバル变量をコピーしたのち消去する
	DISH_DELIVER = %DISH_NAME%
	DISH_NAME = 
	REQUEST_DISHNAME:CHARA:0 = 
	;#;PRINTFORML 依赖したもの＝%DISH_REQUEST%
	;#;PRINTFORML 届けた料理　＝%DISH_DELIVER%

	CALL REVIEW_DISH(CHARA , DISH_TASTE)

	;料理が美味しいかどうか、要求した物かどうか吟味
	;料理自体の味は评价点を低く补正(补正值は定数の评价点补正)
	评价基礎 = TCVAR:CHARA:料理评价值 * 评价点补正 / 100 / 10
	一致评价 = STRCOUNT(DISH_DELIVER , DISH_REQUEST) > 0 ? 一致BOUNS # 0

	;#;PRINTFORML 评价基礎：{评价基礎}    一致评价：{一致评价}
	SAVECLR = GETCOLOR()
	PRINTL 
	SETCOLORBYNAME YELLOW
	PRINTFORM %"★" * (评价基礎 / 10)%
	SETCOLORBYNAME RED
	PRINTFORM %"★" * (一致评价 / 10)%
	PRINTL 
	SETCOLOR SAVECLR

	SELECTCASE 评价基礎 + 一致评价
		CASE IS < 评价基准:0
			;失败
			PRINT <--
			SETCOLORBYNAME RED
			PRINT BAD
			SETCOLOR SAVECLR
			PRINTL -->
			IF 一致评价 == 0
				;注文违い
				; PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%は注文と违う料理が届いて困惑している
				PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%看着手上与要求不一样的菜肴，陷入了困惑
			ELSE
				;ここに来ると言うことは料理の出来がよほど恶い
				; PRINTFORML 料理のできばえを见た%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%は引きつった表情をしている
				PRINTFORML 看着眼前所谓制作完成的菜品，%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%表情僵硬
			ENDIF
			; PRINTFORMW 依赖に応えられなかった……
			PRINTFORMW 没能完成委托……
			PRINTL 
			RETURN IRAI_CONST_RESULT("实行して失败")
		CASE IS < 评价基准:1
			;成功
			PRINT <--
			SETCOLORBYNAME PINK
			PRINT GOOD!
			SETCOLOR SAVECLR
			PRINTL -->
			IF 一致评价 == 0
				; PRINTFORMW 依赖した料理とは违うが%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%はまあまあ纳得してくれた
				PRINTFORMW 虽然与点的不太一样，但%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%觉得还可以接受
			ELSE
				; PRINTFORMW 依赖した料理が届いて%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%は喜んでいる
				PRINTFORMW 拿到了所委托的料理的%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%看上去很开心
			ENDIF
			PRINTL 
			SOURCE:CHARA:欢乐 += 300
			RETURN IRAI_CONST_RESULT("实行して成功")
		CASEELSE
			;大成功
			PRINT <--
			SETCOLORBYNAME GOLD
			PRINT EXCELLENT!!
			SETCOLOR SAVECLR
			PRINTL -->
			IF 一致评价 == 0
				; PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%の依赖とは违ったが、これはこれで好印象のようだ！
				PRINTFORMW 虽然和%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%的委托内容不一样，但好像还是给对方留下了好印象！
			ELSE
				; PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%は依赖通りのおいしそうな料理が届いてご满悦だ！
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%收到了所委托的好吃的菜肴而十分满足！
			ENDIF
			PRINTL 
			SOURCE:CHARA:欢乐 += 1000
			おまけ = 1
			RETURN IRAI_CONST_RESULT("实行して成功")
	ENDSELECT
CASE "依赖成功时"
;成功报告时の报酬など
	CALL CHANGE_CFLAG(2, CHARA, 20)
	CALL CHANGE_CFLAG(4, CHARA, 20)
	CALL GET_MONEY_YEN(2000, 2)
	IF おまけ
		; PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%はさらにBOUNSをくれた！
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%支付了奖金！
		CALL CHANGE_CFLAG(2, CHARA, 20)
		CALL CHANGE_CFLAG(4, CHARA, 20)
		CALL GET_MONEY_YEN(1000, 2)
	ENDIF
	CALL RESET_DISH
CASE "依赖失败时"
;失败报告时の处理
	CALL CHANGE_CFLAG(2, CHARA, -5)
	CALL CHANGE_CFLAG(4, CHARA, -5)
	CALL RESET_DISH
ENDSELECT

