;存否函数
;F函数はTRYCALL系が使えないので通常函数で存否配列を作っちゃう
@EXIST_一般依赖6
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依赖6(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依赖名"
	CALLF MAKE_STR(V_NAME, "谁来告诉我！")
CASE "依赖内容表示"
	CALLF MAKE_STR(V_NAME, @"回答%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的疑问")
CASE "依赖报酬表示"
	CALLF MAKE_STR(V_NAME, @"%金额表示(500)%")
CASE "依赖实行指令名"
	IF IS_LIBRARY(CFLAG:MASTER:当前位置)
		CALLF MAKE_STR(V_NAME, "查资料")
	ELSE
		CALLF MAKE_STR(V_NAME, "进行解答")
	ENDIF
CASE "依赖内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "其他"))
CASE "依赖サブ"
	CALLF MAKE_INT(V_NAME, (ABL:ARG:教养 + RAND(1, 10)) * 10)
CASE "依赖期限"
;发生时ではなく受托时に确定します
CASE "依赖发生率"
;これは他の一般依赖との相对概率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 10)
CASE "依赖发生条件"
	SIF ABL:ARG:教养 <= ABL:MASTER:教养
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖受托可能条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依赖实行可能条件"
	;查资料是つでも
	IF FLAG:70
	ELSEIF IS_LIBRARY(CFLAG:MASTER:当前位置)
		CALLF MAKE_INT(V_NAME, 1)
	;教えるのはCLIENTをTARGETにする必要がある
	ELSEIF TARGET == ARG && SHIRAHU(ARG)
		CALLF MAKE_INT(V_NAME, 1)
	ENDIF
;成功/失败处理はCARRYOUTで直接书く
;SOURCEやITEM入手、消费が烦杂になるので
CASE "依赖消灭条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖人连れ出し可能"
	CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_一般6(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM  LIBRARY_ID
#DIMS LIBRARY_NAME
#DIM  CHK_SLOT
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_SLOT  = SEARCH_IRAI_SLOT(IRAI_ID)
SELECTCASE SCENE
CASE "依赖条件表示"
PRINTFORML 　　理解度：%IRAI_一般6_DIFFICULTY(CHK_SLOT)%
;何か専用の表示情报を设けたい时用
;受托済みの依赖表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも无しで
;ここ以外の表示项目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが见栄えが良いです

CASE "依赖实行时"
;依赖实行时にはRESULTが意味を持ちます
;このCASEに限り、RESULTが意味を持ちます
;依赖实行时のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な参数は"中止", "实行して成功", "实行して失败", "实行して未解决"です
;独自处理を仕込みたい时は"实行して未解决"で先延ばしするなりしてください
	LIBRARY_ID = IS_LIBRARY(CFLAG:MASTER:当前位置)
	IF LIBRARY_ID
		IF INRANGE(LIBRARY_ID, 1, 99)
			LIBRARY_NAME '= STR:LIBRARY_ID
		ELSE
			LIBRARY_NAME '= STR:(8000 + LIBRARY_ID)
		ENDIF
		IF TARGET == CLIENT_ID
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%为了回答%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的疑问访问了%LIBRARY_NAME%
			PRINTFORML 在那里找到了有用的资料
			CALL ASK_M("停止", 1, "调查", 1, "试着回答", 1)
			SELECTCASE RESULT
			CASE 0
				RETURN IRAI_CONST_RESULT("中止")
			CASE 1
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%和%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%在%LIBRARY_NAME%做着调查
				EXP:MASTER:学习经验 ++
				EXP:TARGET:学习经验 ++
				IRAI_SUB:CHK_SLOT -= ABL:MASTER:教养 + ABL:TARGET:教养 + 5
				IRAI_SUB:CHK_SLOT = MAX(IRAI_SUB:CHK_SLOT, 1)
				TIME += 30
			CASE 2
				GOTO ANSWER
			ENDSELECT
		ELSE
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%在%LIBRARY_NAME%做着调查
			EXP:MASTER:学习经验 ++
			IRAI_SUB:CHK_SLOT -= ABL:MASTER:教养 + 3
			IRAI_SUB:CHK_SLOT = MAX(IRAI_SUB:CHK_SLOT, 1)
		ENDIF
		PRINTFORML 现在你的理解度
		PRINTFORMW 　%IRAI_一般6_DIFFICULTY(CHK_SLOT)%
		RETURN IRAI_CONST_RESULT("实行して未解决")
	ELSE
		$ANSWER
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%回答了%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的疑问
		IF IRAI_一般6_SUCCESS(CHK_SLOT)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的疑问冰消瓦解了！
			SOURCE:欢乐 = 300
			EXP:MASTER:学习经验 += ABL:TARGET:教养 * 5
			EXP:TARGET:学习经验 += ABL:TARGET:教养 * 5
			IF ITEM:103
				EXP:MASTER:学习经验 ++
				EXP:TARGET:学习经验 ++
			ENDIF
			DOWNBASE:MASTER:体力 = 70
			DOWNBASE:MASTER:气力 = 100
			DOWNBASE:体力 = 50
			DOWNBASE:气力 = 70
			TIME += 30
			RETURN IRAI_CONST_RESULT("实行して成功")
		ELSE
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%好像没能理解……
			RETURN IRAI_CONST_RESULT("实行して失败")
		ENDIF
	ENDIF
CASE "依赖成功时"
;成功报告时の报酬など
	CALL CHANGE_CFLAG(2, TARGET, 50)
	CALL CHANGE_CFLAG(4, TARGET, 30)
	CALL KIGEN_CHANGE(TARGET,100,1)
	CALL GET_MONEY_YEN(500, 2)
CASE "依赖失败时"
;失败报告时の处理
	CALL CHANGE_CFLAG(2, TARGET, -10)
	CALL CHANGE_CFLAG(4, TARGET, -10)
ENDSELECT

@IRAI_一般6_SUCCESS(CHK_SLOT)
#FUNCTION
#DIM CHK_SLOT
SIF RAND:MAX(1, IRAI_SUB:CHK_SLOT) <= (1 + ABL:MASTER:教养) * 20
	RETURNF 1


@IRAI_一般6_DIFFICULTY(CHK_SLOT)
#FUNCTIONS
#DIM CHK_SLOT
SIF IRAI_SUB:CHK_SLOT <= (1 + ABL:MASTER:教养) * 20
	RETURNF "非常完美的让对方理解了"
;难易度全体で差分をパーセント化したもの
SELECTCASE (IRAI_SUB:CHK_SLOT - ((1 + ABL:MASTER:教养) * 20)) * 100 / IRAI_SUB:CHK_SLOT
CASE 0, 1, 2, 3
	RETURNF "好像可以给出高明的回答"
CASE 4, 5, 6
	RETURNF "已经可以回答了"
CASE 7 TO 10
	RETURNF "能够回答出来吧"
CASE 11 TO 33
	RETURNF "这样算是理解了么"
CASE 34 TO 50
	RETURNF "一定会有办法的、大概"
CASE 51 TO 70
	RETURNF "很难的疑问"
CASEELSE
	RETURNF "完全无法推测"
ENDSELECT


