;存否函数
;F函数はTRYCALL系が使えないので通常函数で存否配列を作っちゃう
@EXIST_一般依赖12
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依赖12(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依赖名"
	CALLF MAKE_STR(V_NAME, "想要制裁TA！")
CASE "依赖内容表示"
	CALLF MAKE_STR(V_NAME, @"用弹幕胜负打败讨伐对象")
CASE "依赖报酬表示"
	CALLF MAKE_STR(V_NAME, @"赏金（时价）")
CASE "依赖实行指令名"
	CALLF MAKE_STR(V_NAME, "制裁")
CASE "依赖内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(RAND_HANTING(ARG), "讨伐"))
CASE "依赖サブ"
CASE "依赖期限"
;发生时ではなく受托时に确定します
	CALLF MAKE_INT(V_NAME, 10)
CASE "依赖发生率"
;これは他の一般依赖との相对概率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 2)
CASE "依赖发生条件"
;CSVの战斗能力が1以上
	SIF CSVABL(ARG, GETNUM(ABL, "战斗能力")) >= 1
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖受托可能条件"
;你の战斗能力が1以上
	SIF ABL:MASTER:战斗能力 >= 1
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖实行可能条件"
	CALLF MAKE_INT(V_NAME, 1)
;成功/失败处理はCARRYOUTで直接书く
;SOURCEやITEM入手、消费が烦杂になるので
CASE "依赖消灭条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖人连れ出し可能"
ENDSELECT



@IRAI_一般12(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM  CHK_SLOT
#DIM  OPPONENT_REMAIN
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_SLOT  = SEARCH_IRAI_SLOT(IRAI_ID)
SELECTCASE SCENE
CASE "依赖条件表示"
;何か専用の表示情报を设けたい时用
;受托済みの依赖表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも无しで
;ここ以外の表示项目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが见栄えが良いです

CASE "依赖实行时"
;このCASEに限り、RESULTが意味を持ちます
;依赖实行时のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な参数は"中止", "实行して成功", "实行して失败", "实行して未解决"です
;独自处理を仕込みたい时は"实行して未解决"で先延ばしするなりしてください
;
;
	;实行できないパターン
	;据点では屋内だとダメ（原作だとめっちゃやってるけど）
	IF CANNOT_DANMAKU() == 1
		PRINTFORMW 到室外再说…
		RETURN IRAI_CONST_RESULT("实行して未解决")
	;仕事中はダメ
	ELSEIF CANNOT_DANMAKU() == 2
		PRINTFORMW 对方正忙着呢…
		RETURN IRAI_CONST_RESULT("实行して未解决")
	;你が疲劳
	ELSEIF CANNOT_DANMAKU() == 3
		PRINTFORMW 今天已经很累了…
		RETURN IRAI_CONST_RESULT("实行して未解决")
	;对象が疲劳
	ELSEIF CANNOT_DANMAKU() == 4
		PRINTFORMW 现在挑战不公平……
		RETURN IRAI_CONST_RESULT("实行して未解决")
	;既に挑んでる
	ELSEIF CANNOT_DANMAKU() == 5
		PRINTFORMW 今天已经不能挑战了…
		RETURN IRAI_CONST_RESULT("实行して未解决")
	ELSE
		;ほぼ弹幕胜负のコピペ
		SETCOLOR C_GREEN
		PRINTFORML ＜讨伐战＞
		RESETCOLOR
		PRINTFORMW 胜负开始！！
		CALL DANMAKU_BATTLE(TARGET)
		OPPONENT_REMAIN = RESULT
		CALL KOJO_MESSAGE_SEND("DANMAKU", OPPONENT_REMAIN, TARGET, -1, -1, "战斗后")
		PRINTL 
		TCVAR:TARGET:弹幕胜负不能 = 1
		SIF OPPONENT_REMAIN == 0
			CFLAG:TARGET:弹幕胜负胜利 = 1
		DOWNBASE:气力 += 500
		DOWNBASE:MASTER:气力 += 500
		DOWNBASE:体力 += 500
		DOWNBASE:MASTER:体力 += 500
		SOURCE:欢乐 += (3000 + ABL:亲密 * 100) / (OPPONENT_REMAIN + 1)
		TFLAG:好感度BOUNS = 1600 / (OPPONENT_REMAIN + 1)
		TFLAG:信赖度管理 = 3 - LOCAL

		EXP:MASTER:战斗经验 += 3

		IF !BATTLEDLC
		    TIME += 60
		ELSE
		    TIME += 15
		ENDIF

		SIF AT_HOME(MASTER)
			YOGORE:(CFLAG:MASTER:当前位置) = LIMIT(YOGORE:(CFLAG:MASTER:当前位置) + 3000, 0, 6000)
		IF OPPONENT_REMAIN == 0
			PRINTL 
			SETCOLOR C_YELLOW
			PRINTFORMW 成功惩罚%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%了！
			RESETCOLOR
			RETURN IRAI_CONST_RESULT("实行して成功")
		ELSE
			SETCOLOR C_GREEN
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的讨伐失败了……
			PRINTFORMW ……这样的话就只能改天再来了
			RESETCOLOR
			RETURN IRAI_CONST_RESULT("实行して未解决")
		ENDIF
	ENDIF
CASE "依赖成功时"
;成功报告时の报酬など
	SELECTCASE ABL:(IRAI_SLOT_TO_PERSON(CHK_SLOT)):战斗能力
	CASE IS <= 1
		SOURCE:欢乐 = 1000
		CALL CHANGE_CFLAG(2, TARGET, 10 + RAND:5)
		CALL CHANGE_CFLAG(4, TARGET, 15 + RAND:5)
		CALL GET_SPELL_CARD_BONUS_YEN(1000, 2)
	CASE 2
		SOURCE:欢乐 = 1500
		CALL CHANGE_CFLAG(2, TARGET, 15 + RAND:5)
		CALL CHANGE_CFLAG(4, TARGET, 20 + RAND:10)
		CALL GET_SPELL_CARD_BONUS_YEN(2000, 2)
	CASE 3
		SOURCE:欢乐 = 2000
		CALL CHANGE_CFLAG(2, TARGET, 20 + RAND:10)
		CALL CHANGE_CFLAG(4, TARGET, 25 + RAND:10)
		CALL GET_SPELL_CARD_BONUS_YEN(5000, 2)
	CASE 4
		SOURCE:欢乐 = 2500
		CALL CHANGE_CFLAG(2, TARGET, 25 + RAND:10)
		CALL CHANGE_CFLAG(4, TARGET, 30 + RAND:10)
		CALL GET_SPELL_CARD_BONUS_YEN(10000, 2)
	CASE 5
		SOURCE:欢乐 = 3000
		CALL CHANGE_CFLAG(2, TARGET, 30 + RAND:20)
		CALL CHANGE_CFLAG(4, TARGET, 35 + RAND:20)
		CALL GET_SPELL_CARD_BONUS_YEN(15000, 2)
	CASEELSE
		SOURCE:欢乐 = 4000
		CALL CHANGE_CFLAG(2, TARGET, 40 + RAND:20)
		CALL CHANGE_CFLAG(4, TARGET, 40 + RAND:20)
		CALL GET_SPELL_CARD_BONUS_YEN(30000, 2)
	ENDSELECT
CASE "依赖失败时"
;失败报告时の处理
;过酷な依赖なので少しだけ
	PRINTFORML BoNus FAilEd...
	CALL CHANGE_CFLAG(2, TARGET, -3)
	CALL CHANGE_CFLAG(4, TARGET, -2)
ENDSELECT

;依赖での弹幕胜负ができない场合は真
@CANNOT_DANMAKU
#FUNCTION
;据点では屋内だとダメ（原作だとめっちゃやってるけど）
IF AT_HOME(MASTER) && !OUTROOF(CFLAG:MASTER:当前位置) && !战斗训练可(CFLAG:MASTER:当前位置)
	RETURNF 1
;仕事中はダメ
ELSEIF CFLAG:TARGET:行动 == 5
	RETURNF 2
;你が疲劳
ELSEIF BASE:MASTER:气力 < 500 || BASE:MASTER:体力 < 500
	RETURNF 3
;对象が疲劳
ELSEIF BASE:TARGET:气力 < 500 || BASE:TARGET:体力 < 500
	RETURNF 4
;既に挑んでる
ELSEIF TCVAR:TARGET:弹幕胜负不能
	RETURNF 5
ENDIF
RETURNF 0
