@固有依赖42(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE ARG % 1000
;ID101：美食报告
CASE 101
	SELECTCASE O_DATA
	CASE "依赖名"
		CALLF MAKE_STR(V_NAME, "美食报告")
	CASE "依赖内容表示"
		CALLF MAKE_STR(V_NAME, "总结人里料理的感想")
	CASE "依赖报酬表示"
		CALLF MAKE_STR(V_NAME, @"%金额表示(3000)%")
	CASE "依赖实行指令名"
		CALLF MAKE_STR(V_NAME, "食品报告")
		
	CASE "依赖内容"
		;小料理屋(218)が对象
		CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(218, "传令（场所）"))
	CASE "依赖サブ"
	CASE "依赖期限"
	;发生时ではなく受托时に确定します
		CALLF MAKE_INT(V_NAME, 7)
	CASE "依赖发生率"
	;これは他の固有依赖との相对概率なんで10％ではありません
		CALLF MAKE_INT(V_NAME, 10)
	CASE "依赖发生条件"
	SIF CFLAG:1005 >= 1 && ABL:MASTER:教养 >= 1
		CALLF MAKE_INT(V_NAME, 1)
	CASE "依赖受托可能条件"
	;受托可能は@COM_ABLE366も确认のこと
	;ここを参照するのは依赖人をTARGETにしてる时なので素通しでよし
		CALLF MAKE_INT(V_NAME, 1)
	CASE "依赖实行可能条件"
	;实行可能は@COM_ABLE367も确认のこと
		IF FLAG:70
		ELSE
			CALLF MAKE_INT(V_NAME, 1)
		ENDIF
	;成功/失败处理はCARRYOUTで直接书く
	;SOURCEやITEM入手など、内容が复杂なので
	CASE "依赖消灭条件"
	;30％で消失
		SIF RAND:100 < 30
			CALLF MAKE_INT(V_NAME, 1)
	ENDSELECT
ENDSELECT



;固有依赖の设定用
;この函数は实行/成功/失败时の
;报酬とペナルティと地の文のみを扱います
;各角色のメッセージは别所にて
@IRAI_固有42(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE STR_DATA_IRAI(IRAI_ID, "依赖名", CLIENT_ID)
	
CASE "美食报告"
	SELECTCASE SCENE
	CASE "依赖条件表示"
	;何か専用の表示情报を设けたい时用
	;受托済みの依赖表示で使用されます
	;
	;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも无しで
	;ここ以外の表示项目は
	;PRINTFORML 　　　　　：
	;となっているのでスペースを揃えたほうが见栄えが良いです
	
	CASE "依赖实行时"
	;このCASEに限り、RESULTが意味を持ちます
	;依赖实行时のRESULTにはIRAI_CONST_RESULT()を使用してください
	;可能な参数は"中止", "实行して成功", "实行して失败", "实行して未解决"です
	;独自处理を仕込みたい时は"实行して未解决"で先延ばしするなりしてください
		DOWNBASE:MASTER:气力 += 50
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%走进了一家小餐馆、向店主请求后拍摄了料理的照片、一边食用一边总结感想…
		TIME += 120
		IF (ABL:话术技能 + ABL:教养 + ABL:料理技能 + RAND:5) >= RAND:12
			PRINTFORML 好照片。也写出了能激起食欲的文章。这样应该就可以了吧
			PRINTW 
			RETURN IRAI_CONST_RESULT("实行して成功")
		ELSEIF (ABL:话术技能 + ABL:教养 + ABL:料理技能 + RAND:5) == RAND:11
			SELECTCASE RAND:2
			CASE 0
				PRINTFORML 文章还算可以、但照片拍得不算很美味…不想就这么妥协
				PRINTW 
			CASE 1
				PRINTFORML 照片拍得不错、但感觉文章并没能传达料理的魅力…
				PRINTW 
			ENDSELECT
			RETURN IRAI_CONST_RESULT("实行して未解决")
		ELSE
			PRINTFORML 照片很模糊、文章也是写得不知所云…这可不行
			PRINTFORML 没办法、现在也只能就这样上交了
			PRINTW 
			RETURN IRAI_CONST_RESULT("实行して失败")
		ENDIF
	CASE "依赖成功时"
	;成功报告时の报酬など
		CALL KIGEN_CHANGE(TARGET, 100, 1)
		SOURCE:TARGET:欢乐 = 1000
		SOURCE:TARGET:征服 = 500
		SOURCE:TARGET:达成 = 500
		;システムメッセージつき好感度变化
		;最后の参数だけ好感度が变化
		CALL CHANGE_CFLAG(2, TARGET, 40)
		;システムメッセージつき信赖度变化
		;最后の参数だけ信赖度が变化
		CALL CHANGE_CFLAG(4, TARGET, 25)
		CALL GET_MONEY_YEN(3000, 2)
	CASE "依赖失败时"
	;失败报告时の处理
		;システムメッセージつき好感度变化
		;最后の参数だけ好感度が变化
		CALL CHANGE_CFLAG(2, TARGET, -20)
		;システムメッセージつき信赖度变化
		;最后の参数だけ信赖度が变化
		CALL CHANGE_CFLAG(4, TARGET, -10)
	ENDSELECT
ENDSELECT
