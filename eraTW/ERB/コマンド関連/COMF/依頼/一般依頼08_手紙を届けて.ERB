;存否函数
;F函数はTRYCALL系が使えないので通常函数で存否配列を作っちゃう
@EXIST_一般依赖8
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依赖8(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依赖名"
	CALLF MAKE_STR(V_NAME, "送信")
CASE "依赖内容表示"
	CALLF MAKE_STR(V_NAME, @"给某个人物送信")
CASE "依赖报酬表示"
	CALLF MAKE_STR(V_NAME, @"素材4つ")
CASE "依赖实行指令名"
	CALLF MAKE_STR(V_NAME, "送出信件")

CASE "依赖内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(WHO_LETTER_TO(ARG), "传令（人物）"))
CASE "依赖サブ"
CASE "依赖期限"
;发生时ではなく受托时に确定します
	CALLF MAKE_INT(V_NAME, 3)
CASE "依赖发生率"
	CALLF MAKE_INT(V_NAME, 10)
CASE "依赖发生条件"
	SIF CFLAG:ARG:信赖度 >= 30 && WHO_LETTER_TO(ARG)
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖受托可能条件"
	SIF CFLAG:ARG:信赖度 >= 30 && WHO_LETTER_TO(ARG)
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖实行可能条件"
	CALLF MAKE_INT(V_NAME, 1)
;成功/失败处理はCARRYOUTで直接书く
;SOURCEやITEM入手、消费が烦杂になるので
CASE "依赖消灭条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_一般8(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依赖条件表示"
;何か専用の表示情报を设けたい时用
;受托済みの依赖表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも无しで
;ここ以外の表示项目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが见栄えが良いです

CASE "依赖实行时"
;このCASEに限り、RESULTが意味を持ちます
;依赖实行时のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な参数は"中止", "实行して成功", "实行して失败", "实行して未解决"です
;独自处理を仕込みたい时は"实行して未解决"で先延ばしするなりしてください
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%把保管着的信交给了%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%
	PRINTFORML 
	SOURCE:欢乐 = 500
	CALL CHANGE_CFLAG(2, TARGET, 20)
	RETURN IRAI_CONST_RESULT("实行して成功")
CASE "依赖成功时"
;成功报告时の报酬など
	SOURCE:欢乐 = 500
	CALL CHANGE_CFLAG(2, TARGET, 50)
	CALL CHANGE_CFLAG(4, TARGET, 30)
	CALL GetRandMaterial(4, 3000)
CASE "依赖失败时"
;失败报告时の处理
	CALL CHANGE_CFLAG(2, TARGET, -20)
	CALL CHANGE_CFLAG(4, TARGET, -10)
ENDSELECT
