;存否函数
;F函数はTRYCALL系が使えないので通常函数で存否配列を作っちゃう
@EXIST_一般依赖3
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依赖3(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依赖名"
	CALLF MAKE_STR(V_NAME, "一起玩吧")
CASE "依赖内容表示"
	CALLF MAKE_STR(V_NAME, @"%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%と一起玩")
CASE "依赖报酬表示"
	CALLF MAKE_STR(V_NAME, "关系变好")
CASE "依赖实行指令名"
	CALLF MAKE_STR(V_NAME, "玩游戏")

CASE "依赖内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "作业"))
CASE "依赖サブ"
CASE "依赖期限"
;发生时ではなく受托时に确定します
CASE "依赖发生率"
;これは他の一般依赖との相对概率なんで15％ではありません
	CALLF MAKE_INT(V_NAME, 15)
CASE "依赖发生条件"
;幼稚角色限定
;蕾米莉亚と帝でも发生してしまうが气にするな
	SIF TALENT:ARG:幼稚
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖受托可能条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依赖实行可能条件"
	CALLF MAKE_INT(V_NAME, 1)
;成功/失败处理はCARRYOUTで直接书く
;SOURCEやITEM入手、消费が烦杂になるので
CASE "依赖消灭条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
ENDSELECT



@IRAI_一般3(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依赖条件表示"
;何か専用の表示情报を设けたい时用
;受托済みの依赖表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも无しで
;ここ以外の表示项目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが见栄えが良いです

CASE "依赖实行时"
;このCASEに限り、RESULTが意味を持ちます
;依赖实行时のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な参数は"中止", "实行して成功", "实行して失败", "实行して未解决"です
;独自处理を仕込みたい时は"实行して未解决"で先延ばしするなりしてください
;
;"依赖实行时"に限っていえば、CHARAはTARGETと同一であり、必ずしもCLIENT_IDと一致するとは限りません
;个々のケースで确认する必要があります
;この依赖は「作业」扱いであり、CLIENTがTARGETになっている时のみ实行できます
;
;
	SOURCE:欢乐 = 1500
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%陪%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%一起玩
	PRINTFORML 
	TIME += 20
	DOWNBASE:体力 = 150
	DOWNBASE:气力 = 100
	DOWNBASE:MASTER:体力 = 150
	DOWNBASE:MASTER:气力 = 100
	RETURN IRAI_CONST_RESULT("实行して成功")
CASE "依赖成功时"
;成功报告时の报酬など
	TALENT:TARGET:心情 = LIMIT(TALENT:心情 + 1, -1, 1)
	CALL CHANGE_CFLAG(2, TARGET, 30)
	CALL CHANGE_CFLAG(4, TARGET, 5)
CASE "依赖失败时"
;失败报告时の处理
	CALL CHANGE_CFLAG(2, TARGET, -10)
	CALL CHANGE_CFLAG(4, TARGET, -5)
ENDSELECT
