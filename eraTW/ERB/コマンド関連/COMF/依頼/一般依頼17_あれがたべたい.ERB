;存否函数
;F函数はTRYCALL系が使えないので通常函数で存否配列を作っちゃう
@EXIST_一般依赖17
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依赖17(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依赖名"
	;「名前忘れたけどこんな感じの料理あったよね」とは别に「こんな料理があったら食べてみたいね」みたいな形式もアリなのかな
	;やっぱり同じ依赖の名前が复数あるの好ましくないかな？
	;IF RAND:2
		;CALLF MAKE_STR(V_NAME, "あれが食べたい！……なんて名前だっけ")
		CALLF MAKE_STR(V_NAME, "我想吃那个！那个叫什么来着？")
	;ELSE
	;	CALLF MAKE_STR(V_NAME, "こんな料理あったらいいね")
	;	CALLF MAKE_STR(V_NAME, "如果有这样的料理就好了")
	;ENDIF
CASE "依赖内容表示"
	;CALLF MAKE_STR(V_NAME, @"あいまいな记忆を赖りに料理を作って届ける")
	CALLF MAKE_STR(V_NAME, @"凭借模糊的记忆做出并送上料理")
CASE "依赖报酬表示"
	;CALLF MAKE_STR(V_NAME, @"\\2500＋おまけ")
	CALLF MAKE_STR(V_NAME, @"%金额表示(2500)%＋奖金")
CASE "依赖实行指令名"
	;何か问题があったら变えてください
	;IF TALENT:MASTER:性别 == 2
	;	CALLF MAKE_STR(V_NAME, "出前汉です")
	;ELSE 
	;	CALLF MAKE_STR(V_NAME, "UmanEatsです")
	;ENDIF
	IF TALENT:MASTER:性别 == 2
		CALLF MAKE_STR(V_NAME, "您好，外卖到了")
	ELSE 
		CALLF MAKE_STR(V_NAME, "您好，UmanEats外送")
	ENDIF
CASE "依赖内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "其他"))
CASE "依赖サブ"
	;これは乱数シード
	CALLF MAKE_INT(V_NAME, RAND:1000000)
CASE "依赖期限"
;发生时ではなく受托时に确定します
	CALLF MAKE_INT(V_NAME, 3)
CASE "依赖发生率"
	CALLF MAKE_INT(V_NAME, 5)
CASE "依赖发生条件"
;阿求以外
	SIF ARG != ([[阿求]])
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖受托可能条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依赖实行可能条件"
	SIF DISH_NAME != "" && TARGET == ARG && SHIRAHU(ARG)
		CALLF MAKE_INT(V_NAME, 1)
;成功/失败处理はCARRYOUTで直接书く
;SOURCEやITEM入手、消费が烦杂になるので
CASE "依赖消灭条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖人连れ出し可能"
ENDSELECT


@IRAI_一般17(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM 评价基礎 ;料理自体の评价
#DIM タグ评价 ;依赖者の出した料理の特征とどれだけ一致したか评价
#DIMS 味要求  ;依赖者の要求した味を置いておく变量
#DIM おまけ   ;大成功时のBOUNS。成功/大成功判定が出てから报酬受け取りまでに他の依赖函数の实行が挟まるケースないよね？
#DIM CONST 评价基准 = 50 , 75 ;评价基准:0未满のスコアなら失败、评价基准:0以上なら成功、评价基准:1以上なら大成功
#DIM CONST タグ一致加点 = 10  ;タグ一致ひとつあたり贳える点数
#DIM CONST 基礎补正 = 50      ;料理自体の评价にかかる补正值。百分率。タグ一致による加点があるので料理自体の评价は低く补正する
#DIM  CHK_SLOT
#DIM SAVECLR  ;色保存用

CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
CHK_SLOT  = SEARCH_IRAI_SLOT(IRAI_ID)

;任意の文字列を保存しておく方法が分からなかったので力业
DUMPRAND
RANDOMIZE IRAI_SUB:CHK_SLOT
味要求 = %IRAI_一般17_味设定(CHARA)%
INITRAND

SELECTCASE SCENE
CASE "依赖条件表示"
	;何か専用の表示情报を设けたい时用
	;受托済みの依赖表示で使用されます
	;
	;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも无しで
	;ここ以外の表示项目は
	;PRINTFORML 　　　　　：
	;となっているのでスペースを揃えたほうが见栄えが良いです
	;PRINTFORML 要求する味：%味要求%
	PRINTFORML 要求的味道：%味要求%
CASE "依赖实行时"
	;このCASEに限り、RESULTが意味を持ちます
	;依赖实行时のRESULTにはIRAI_CONST_RESULT()を使用してください
	;可能な参数は"キャンセル", "实行して成功", "实行して失败", "实行して未解决"です
	;独自处理を仕込みたい时は"实行して未解决"で先延ばしするなりしてください
	VARSET おまけ
	;PRINTFORML 「ご注文の%DISH_NAME%になりまーす」
	PRINTFORML 「这是您点的%DISH_NAME%」
	;PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%は%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%に<%DISH_NAME%>を届けた
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%将%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%点的<%DISH_NAME%>交给了%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%
	CALL REVIEW_DISH(CHARA , DISH_TASTE)
	DISH_NAME = 

	;#;PRINTFORML 要求するもの＝%味要求%
	;#;PRINTFORML 料理の味　　＝%DISH_TASTE%

	;料理が美味しいかどうか、要求した物に近いかどうかの两方を吟味
	;料理自体の味は评价点を低く补正
	评价基礎 = TCVAR:CHARA:料理评价值 * 基礎补正 / 100 / 10
	タグ评价 = IRAI_一般17_ジャンル评价(DISH_TASTE , 味要求) * タグ一致加点

	;きれいに★を并べるために杂に补正
	;#;PRINTFORML 补正前评价基礎：{评价基礎}    タグ评价：{タグ评价}
	IF 评价基礎 % 10 < 5
		タグ评价 += 评价基礎 % 10
		评价基礎 -= 评价基礎 % 10
	ELSE
		タグ评价 -= 10 - 评价基礎 % 10
		评价基礎 += 10 - 评价基礎 % 10
	ENDIF
	;#;PRINTFORML 补正后评价基礎：{评价基礎}    タグ评价：{タグ评价}

	SAVECLR = GETCOLOR()
	PRINTL 
	SETCOLORBYNAME YELLOW
	PRINTFORM %"★" * (评价基礎 / 10)%
	SETCOLORBYNAME RED
	PRINTFORM %"★" * (タグ评价 / 10)%
	PRINTL 
	SETCOLOR SAVECLR

	SELECTCASE 评价基礎 + タグ评价
		CASE IS < 评价基准:0
			;失败
			PRINT <--
			SETCOLORBYNAME RED
			PRINT BAD
			SETCOLOR SAVECLR
			PRINTL -->
			IF 评价基礎 < (评价基礎 + タグ评价) / 2
				;出来が恶い
				;PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%はものすごく渋い颜をしている
				PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%的表情有些不自然
			ELSE
				;思ってたのと违う
				; PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%は想像とかけ离れた料理を前に、困惑している
				PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%面对着与想象相差甚远的料理，有些不知所措
			ENDIF
			; PRINTFORMW 要求に応えられなかった……
			PRINTFORMW 没有达成%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%的要求……
			PRINTL 
			RETURN IRAI_CONST_RESULT("实行して失败")
		CASE IS < 评价基准:1
			;成功
			PRINT <--
			SETCOLORBYNAME PINK
			PRINT GOOD!
			SETCOLOR SAVECLR
			PRINTL -->
			; PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%はまあまあ纳得してくれたようだ
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%好像勉强接受了
			PRINTL 
			SOURCE:CHARA:欢乐 += 500
			RETURN IRAI_CONST_RESULT("实行して成功")
		CASEELSE
			;大成功
			PRINT <--
			SETCOLORBYNAME GOLD
			PRINT EXCELLENT!!
			SETCOLOR SAVECLR
			PRINTL -->
			; PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%は注文通りの料理が届いて满足している！
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%很高兴地接受了符合自己要求的菜肴！
			PRINTL 
			SOURCE:CHARA:欢乐 += 2000
			おまけ = 1
			RETURN IRAI_CONST_RESULT("实行して成功")
	ENDSELECT
CASE "依赖成功时"
;成功报告时の报酬など
	CALL CHANGE_CFLAG(2, CHARA, 25)
	CALL CHANGE_CFLAG(4, CHARA, 25)
	CALL GET_MONEY_YEN(2500, 2)
	IF おまけ
		; PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%はさらにBOUNSをくれた！
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%支付了奖金！
		CALL CHANGE_CFLAG(2, CHARA, 15)
		CALL CHANGE_CFLAG(4, CHARA, 15)
		CALL GET_MONEY_YEN(1000, 2)
	ENDIF
	CALL RESET_DISH
CASE "依赖失败时"
;失败报告时の处理
	CALL CHANGE_CFLAG(2, CHARA, -5)
	CALL CHANGE_CFLAG(4, CHARA, -5)
	CALL RESET_DISH
ENDSELECT


;MINTAG、MAXTAGは中盘のTASTETAG_ORIからの抽选にのみ影响する
;TASTEタグ自体の抽选数はMAXTAGより多めになる倾向
@IRAI_一般17_味设定(CHR = -1 , MINTAG = 5 , MAXTAG = 9)
#FUNCTIONS
#DIM CNT
#DIM MAXCNT
#DIM MINTAG  ;选ばれうる味タグの最低数
#DIM MAXTAG  ;选ばれうる味タグの最大数
#DIM TAGNUMBER
#DIM CHR
#DIMS TASTE
#DIMS TASTETAG , 100
#DIMS TASTETAG_ORI

VARSET TASTE

;50%で和洋中が付く。排他
IF RAND:2
	SELECTCASE RAND:3
		CASE 0
			TASTE = 和食/
		CASE 1
			TASTE = 洋食/
		CASE 2
			TASTE = 中华/
	ENDSELECT
ENDIF

;50%で料理のジャンルが付く
;これらを并せ持つ料理もあるけど大まかな分类ってことで排他に
IF RAND:2
	;LOCALS = %TEXTR("肉料理/鱼料理/面类/锅物/汁物/ケーキ/パイ/おつまみ/扬げ物/煮物/谜料理")% += "/
	SELECTCASE RAND:11
		CASE 0
			TASTE += "肉料理/"
		CASE 1
			TASTE += "鱼料理/"
		CASE 2
			TASTE += "面类/"
		CASE 3
			TASTE += "锅物/"
		CASE 4
			TASTE += "汁物/"
		CASE 5
			TASTE += "蛋糕/"
		CASE 6
			TASTE += "派/"
		CASE 7
			TASTE += "开胃/"
		CASE 8
			TASTE += "油炸食品/"
		CASE 9
			TASTE += "煮物/"
		CASE 10
			TASTE += "谜料理/"
	ENDSELECT
ENDIF

;杂多なタグから3～7个选んで付与。DISHDATA眺めながらピックアップしたけど数え落としや重复あったらごめんね
;キャラごとの固有タグっぽいのは勘案しない
;矛盾するタグを消去することも考えたが、「なんだそりゃ？」みたいな注文も生成された方が面白いんじゃないか
{
TASTETAG_ORI = 
甜/
咸/
辣/
酸/
简易/
丰盛/
健康/
垃圾/
浓厚/
清淡/

冷/
温暖/
中立温度/

豪华/
上品/
豪快/
家庭的/
机能的/
药效/

米/
豆/
野菜/
叶物/
面包/
乳酪/
卵/
好吃的鱼/
果物/
虫肉/
禽肉/
野味/
青蛙/
芥末/
牛奶/
香料/

甜味料/

卷心菜/
草莓/
芜菁/
黄瓜/
番茄/
洋葱/
西瓜/
茄子/
马铃薯/
南瓜/
白菜/
胡萝卜/
葱/

仙桃/
木莓/
山葡萄/

粘稠/
扑鼻/
软绵绵/
松脆/
软糯的/

新鲜/
艺术的/
怪诞/
朴素/
野趣/

春物/
夏物/
秋物/
冬物/

精力/
魔性/
银吉兽/
}
;跳蛋/はなしで

;うまい鱼は2/3で鱼に化ける
IF STRCOUNT(TASTETAG_ORI , "好吃的鱼/") && RAND:3
	TASTETAG_ORI = %REPLACE(TASTETAG_ORI , "好吃的鱼/" , "鱼/")%
ENDIF

;连结表记を用いると改行が半角スペースに置き换わるので除去
;REPLACE TASTETAG_ORI , " " , ""
TASTETAG_ORI = %REPLACE(TASTETAG_ORI , " " , "")%

;******キャラ别の要素处理******
;嫌いLv10クラスの嫌い方でなければ强制削除はしないようにした(小兔姫の辛いLv8は除去していない)
;明确な理由はないので必要とあらばいじってほしい

;くたかとミスティアは禽肉と卵を除去する
IF GROUPMATCH(CHR, [[米斯蒂亚]] , [[久侘歌]])
	TASTETAG_ORI = %REPLACE(TASTETAG_ORI , "禽肉/" , "")%
	TASTETAG_ORI = %REPLACE(TASTETAG_ORI , "卵/" , "")%
ENDIF

;リグル、ラルバは虫肉を除去する
IF GROUPMATCH(CHR, [[莉格露]] , [[拉尔瓦]])
	TASTETAG_ORI = %REPLACE(TASTETAG_ORI , "虫肉/" , "")%
ENDIF

;诹访子はカエルを除去する
IF GROUPMATCH(CHR, [[诹访子]])
	TASTETAG_ORI = %REPLACE(TASTETAG_ORI , "青蛙/" , "")%
ENDIF

;わかさぎ姫は鱼orうまい鱼を除去する
IF GROUPMATCH(CHR, [[若鹭姫]])
	TASTETAG_ORI = %REPLACE(TASTETAG_ORI , "鱼/" , "")%
	TASTETAG_ORI = %REPLACE(TASTETAG_ORI , "好吃的鱼/" , "")%
ENDIF
;うさぎ势やきつね势やねこ势やたぬきなどは野味を消すべきなんだろうか。野味ってすごいざっくりだよね
;迷ったがやらない
;******************************

;タグ一览からタグを指定个抜き出す作业
VARSET TASTETAG
SPLIT TASTETAG_ORI, "/", TASTETAG

;何かのミスで味タグの最低抽选数以下のタグしか与えられていなければエラー(无限ループだと落ちるまで时间かかる)
SIF RESULT < MINTAG
	THROW 文字列が不正

MAXCNT = RAND(MINTAG , MAXTAG + 1)
DO
	TAGNUMBER = RAND:VARSIZE("TASTETAG")
	SIF TASTETAG:TAGNUMBER == ""
		CONTINUE
	TASTE = %TASTE%%TASTETAG:TAGNUMBER%/
	MAXCNT--
	;タグの重复を排除するため、选んだものは削除。重复を许容するなら以下の处理を消去する
	TASTETAG:TAGNUMBER = 
LOOP MAXCNT != 0

;希に季节の物。突然季节外れの物食べたくなることもあるよね！
IF RAND:10 == 0
	IF RAND:3
		TASTE += "缘起物/"
	ELSE
		TASTE += "过季/"
	ENDIF
ENDIF

;@IRAI_一般17_ジャンル评价での处理が不正になるため、最后尾の"/"を除去
SIF SUBSTRINGU(TASTE , STRLENSU(TASTE) - 1 , 1) == "/"
	TASTE = %SUBSTRINGU(TASTE , 0 , STRLENSU(TASTE) - 1)%

RETURNF TASTE


;料理味=甘い/甘い/しょっぱい、要求味=甘い/甘い/冷たいの场合に2を返す式中函数
;料理味=甘い/しょっぱい/辛い、要求味=甘い/甘い/サクサクの场合は1
;TWの@STR_MULTI_COUNTだと前者は4、后者は2を返す(はず)
@IRAI_一般17_ジャンル评价(料理味 = "" , 要求味 = "")
#FUNCTION
#DIMS 料理味
#DIMS 要求味
#DIMS 料理味分割 , 20
#DIMS 要求味分割 , 20
#DIM SCORE
#DIM CNT
#DIM MAXCNT

VARSET SCORE
VARSET 料理味分割
VARSET 要求味分割

SPLIT 料理味 , "/" , 料理味分割
SPLIT 要求味 , "/" , 要求味分割
MAXCNT = RESULT

;DEBUGPRINTFORM 料理味=%料理味%  要求味=%要求味%  MAXCNT={MAXCNT}
FOR CNT , 0 , MAXCNT
	FINDELEMENT 料理味分割 , 要求味分割:CNT
	IF RESULT >= 0
		;DEBUGPRINTFORM 料理味分割=<%料理味分割:RESULT%>  味要求分割=<%要求味分割:CNT%>  CNT={CNT}
		料理味分割:RESULT = 
		SCORE++
	ENDIF
NEXT
;DEBUGPRINTFORML 　　SCORE={SCORE}

RETURNF SCORE
