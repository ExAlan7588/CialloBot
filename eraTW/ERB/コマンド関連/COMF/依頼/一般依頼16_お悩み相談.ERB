;存否函数
;F函数はTRYCALL系が使えないので通常函数で存否配列を作っちゃう
@EXIST_一般依赖16
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依赖16(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依赖名"
	CALLF MAKE_STR(V_NAME, "倾听烦恼！")
CASE "依赖内容表示"
	CALLF MAKE_STR(V_NAME, @"倾听烦恼的事情")
CASE "依赖报酬表示"
	CALLF MAKE_STR(V_NAME, @"野菜或果物（如果解决的话）")
CASE "依赖实行指令名"
	CALLF MAKE_STR(V_NAME, "烦恼相谈")
CASE "依赖内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(0, "其他"))
CASE "依赖サブ"
CASE "依赖期限"
;发生时ではなく受托时に确定します
CASE "依赖发生率"
;これは他の一般依赖との相对概率なんで10％ではありません
	CALLF MAKE_INT(V_NAME, 5)
CASE "依赖发生条件"
	SIF ABL:MASTER:话术技能 >= 1
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖受托可能条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依赖实行可能条件"
	SIF TARGET == ARG && SHIRAHU(ARG)
		CALLF MAKE_INT(V_NAME, 1)
;成功/失败处理はCARRYOUTで直接书く
;SOURCEやITEM入手、消费が烦杂になるので
CASE "依赖消灭条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖人连れ出し可能"
ENDSELECT



@IRAI_一般16(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
#DIM 正解の选择肢
#DIM 逆张りナシ
#DIM お恼みナンバー
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依赖条件表示"
;何か専用の表示情报を设けたい时用
;受托済みの依赖表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも无しで
;ここ以外の表示项目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが见栄えが良いです

CASE "依赖实行时"
;このCASEに限り、RESULTが意味を持ちます
;依赖实行时のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な参数は"中止", "实行して成功", "实行して失败", "实行して未解决"です
;独自处理を仕込みたい时は"实行して未解决"で先延ばしするなりしてください
	VARSET RESULT
	VARSET LOCAL
	;口上に委托するか判定
	CALL KOJO_MESSAGE_SEND("IRAI", TARGET, IRAI_ID, 0, 0, "TARGET", "口上委托率")
	;LOCAL:1～に发生させない番号を保存
	FOR LOCAL, 1, 100
		LOCAL:LOCAL = RESULT:LOCAL
	NEXT
	;口上専用ルート
	IF RESULT:100 > RAND:100
		VARSET RESULT
		VARSET RESULTS
		CALL KOJO_MESSAGE_SEND("IRAI", TARGET, IRAI_ID, 0, 0, "TARGET", "口上専用")
	;泛用ルート
	ELSE
		RESULT = 0
		WHILE !RESULT
			RESULT = 0
			;お恼みパターンを增やしたらここを调整（あえて稍微多めにしてます）
			お恼みナンバー = RAND:26
			SIF LOCAL:お恼みナンバー
				CONTINUE
			CALL IRAI_一般16_お恼みデータベース(お恼みナンバー, TARGET)
		WEND
	ENDIF
	正解の选择肢 = RESULT:1
	逆张りナシ = RESULT:2
	DRAWLINE
	[IF_DEBUG]
		PRINTFORML ｺﾀｴﾊ No.{正解の选择肢} ﾀﾞﾖ
	[ENDIF]
	PRINTFORML ～%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的今天的烦恼～
	PRINTFORML 　　　『%RESULTS%』
SIF 正解の选择肢 == 1 || 正解の选择肢 == 7
	SETCOLOR C_YELLOW
SIF 正解の选择肢 == 4 || 正解の选择肢 == 8
	SETCOLOR C_D_RED
	PRINTFORML [ 1] - %RESULTS:1%
RESETCOLOR

SIF 正解の选择肢 == 2 || 正解の选择肢 == 7
	SETCOLOR C_YELLOW
SIF 正解の选择肢 == 5 || 正解の选择肢 == 8
	SETCOLOR C_D_RED
	PRINTFORML [ 2] - %RESULTS:2%
RESETCOLOR

SIF 正解の选择肢 == 3 || 正解の选择肢 == 7
	SETCOLOR C_YELLOW
SIF 正解の选择肢 == 6 || 正解の选择肢 == 8
	SETCOLOR C_D_RED
	PRINTFORML [ 3] - %RESULTS:3%
RESETCOLOR
	INPUT
	LOCAL = RESULT
	TIME += 10
	;ひとつだけハズレのパターン（"正解の选择肢"がハズレの选择肢）
	IF GROUPMATCH(正解の选择肢, 4, 5, 6)
		IF LOCAL == 正解の选择肢 - 3
			CALL COLORMESSAGE(@"＜BAD…＞", C_RED, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的回答不满意…
			SOURCE:欢乐 = 50 + ABL:亲密 * 5
			SOURCE:反感 = 100
			TFLAG:193 = -1
			RETURN IRAI_CONST_RESULT("实行して失败")
		ELSE
			CALL COLORMESSAGE(@"＜NICE!!＞", C_YELLOW, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的回答很满意！
			TFLAG:193 = 1
			SOURCE:欢乐 = 500 + ABL:亲密 * 50
			EXP:MASTER:会话经验 += 5
			TFLAG:好感度BOUNS = ABL:亲密 * 5
			TFLAG:信赖度管理 = 2
			RETURN IRAI_CONST_RESULT("实行して成功")
		ENDIF
	;全部正解のパターン
	ELSEIF 正解の选择肢 == 7
		CALL COLORMESSAGE(@"＜NICE!!＞", C_YELLOW, 0)
		PRINTFORML 【%RESULTS:LOCAL%】
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的回答很满意！
		TFLAG:193 = 1
		SOURCE:欢乐 = 500 + ABL:亲密 * 50
		EXP:MASTER:会话经验 += 5
		TFLAG:好感度BOUNS = ABL:亲密 * 5
		TFLAG:信赖度管理 = 2
		RETURN IRAI_CONST_RESULT("实行して成功")
	;全部ハズレのパターン
	ELSEIF 正解の选择肢 == 8
		CALL COLORMESSAGE(@"＜BAD…＞", C_RED, 0)
		PRINTFORML 【%RESULTS:LOCAL%】
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的回答不满意…
		SOURCE:欢乐 = 50 + ABL:亲密 * 5
		SOURCE:反感 = 100
		TFLAG:193 = -1
		RETURN IRAI_CONST_RESULT("实行して失败")
	ELSE
		;もし正解の选择肢がおかしい场合、警告を出して修正する
		IF !GROUPMATCH(正解の选择肢, 1, 2, 3)
			PRINTFORMW ※正解的选择肢发生异常（{正解の选择肢}）
			正解の选择肢 = RAND:3 + 1
		ENDIF
		;正解の选择肢を选ぶ
		IF LOCAL == 正解の选择肢
			;6分の5の概率でそのまま正解
			IF RAND:6 || 逆张りナシ
				CALL COLORMESSAGE(@"＜NICE!!＞", C_YELLOW, 0)
				PRINTFORML 【%RESULTS:LOCAL%】
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的回答很满意！
				TFLAG:193 = 1
				SOURCE:欢乐 = 500 + ABL:亲密 * 50
				EXP:MASTER:会话经验 += 5
				TFLAG:好感度BOUNS = ABL:亲密 * 5
				TFLAG:信赖度管理 = 2
				RETURN IRAI_CONST_RESULT("实行して成功")
			;6分の1の概率で逆にハズレ
			ELSE
				CALL COLORMESSAGE(@"＜SAD…＞", C_P_PURPLE, 0)
				PRINTFORML 【%RESULTS:LOCAL%】
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%老套的回答感到失望…
				SOURCE:欢乐 = 100 + ABL:亲密 * 10
				SOURCE:反感 = 50
				EXP:MASTER:会话经验 += 1
				TFLAG:193 = -2
				RETURN IRAI_CONST_RESULT("实行して失败")
			ENDIF
		;6分の1の概率で逆に正解
		ELSEIF !RAND:6 && !逆张りナシ
			CALL COLORMESSAGE(@"＜COOL!!＞", C_AQUA, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%因%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%意外的回答而惊讶！
			TFLAG:193 = 2
			SOURCE:欢乐 = 2000 + ABL:亲密 * 100
			EXP:MASTER:会话经验 += 10
			TFLAG:好感度BOUNS = ABL:亲密 * 10
			TFLAG:信赖度管理 = 5
			RETURN IRAI_CONST_RESULT("实行して成功")
		;ハズレの选择肢を选ぶ
		ELSE
			CALL COLORMESSAGE(@"＜BAD…＞", C_RED, 0)
			PRINTFORML 【%RESULTS:LOCAL%】
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的回答不满意…
			SOURCE:欢乐 = 50 + ABL:亲密 * 5
			SOURCE:反感 = 100
			TFLAG:193 = -1
			RETURN IRAI_CONST_RESULT("实行して失败")
		ENDIF
	ENDIF
CASE "依赖成功时"
;成功报告时の报酬など
	IF TFLAG:192 == 2
		CALL CHANGE_CFLAG(4, TARGET, 10 + ABL:亲密 * 2)
		TALENT:心情 = MIN(TALENT:心情 + 1, 1)
		LOCAL:1 = 3
	ELSE
		CALL CHANGE_CFLAG(4, TARGET, 5 + ABL:亲密)
		TALENT:心情 = MIN(TALENT:心情 + 1, 0)
		LOCAL:1 = 1
	ENDIF
	;野菜召唤の仪
	LOCALS = 
	WHILE LOCALS == ""
		LOCAL = RAND:40 + GETNUM(ITEM, "卷心菜")
		LOCALS = %ITEMNAME:LOCAL%
	WEND
	CALL GET_ITEM(LOCALS, LOCAL:1, 2)
CASE "依赖失败时"
;失败报告时の处理
	IF TFLAG:192 == -2
		CALL CHANGE_CFLAG(4, TARGET, -5)
	ELSE
		CALL CHANGE_CFLAG(4, TARGET, -1)
	ENDIF
ENDSELECT


;---------------------------------------
;お恼み相谈のデータ
;ARG           お恼みナンバー
;RETURN        发生判定
;RESULT:1      正解の选择肢
;RESULT:2      逆张り正解／ハズレ判定の消去
;RESULTS:0     お恼み文章
;RESULTS:1～3  选择肢１～３
;---------------------------------------
@IRAI_一般16_お恼みデータベース(ARG, CHARA)
#DIM CHARA
VARSET RESULT
VARSET RESULTS
VARSET LOCAL
SELECTCASE ARG
CASE 1, 2
	;弹幕ごっこの恼み
	;もともとの战斗能力がないなら除外
	SIF CSVABL(CHARA, GETNUM(ABL, "战斗能力")) <= 0
		RETURN
	RESULTS:0 = 新的符卡该怎么设计才好%性格倾向语尾(CHARA, "呐？")%
	IF ARG % 2 == 0
		RESULTS:1 = 分散弹幕
		RESULTS:2 = 封位弹幕
		RESULTS:3 = 诱导弹幕
		SELECTCASE CFLAG:CHARA:性格倾向
		;气弱系
		CASE 1
			RESULT:1 = 3
		;强气系
		CASE 2
			RESULT:1 = 2
		;坦率系
		CASE 3
			RESULT:1 = 1
		;真面目系
		CASE 4
			RESULT:1 = 3
		;其他
		CASEELSE
			RESULT:1 = 1
		ENDSELECT
	ELSE
		RESULTS:1 = 重视外观的美丽弹幕
		RESULTS:2 = 杀意外露的鬼畜弹幕
		RESULTS:3 = 令人幻惑的不思议弹幕
		SELECTCASE CFLAG:CHARA:性格倾向
		;气弱系
		CASE 1
			RESULT:1 = 1
		;强气系
		CASE 2
			RESULT:1 = 2
		;坦率系
		CASE 3
			RESULT:1 = 2
		;真面目系
		CASE 4
			RESULT:1 = 1
		;其他
		CASEELSE
			RESULT:1 = 3
		ENDSELECT
	ENDIF
CASE 3, 4
	;晩酌の恼み
	;酒が饮めないなら除外
	SIF TALENT:CHARA:酒耐性 <= 0
		RETURN
	IF ARG % 2 == 0
		RESULTS:0 = 今天的晩酌喝什么好呢？你%性格倾向语尾(CHARA, "认为呢？")%
		RESULTS:1 = 啤酒
		RESULTS:2 = 葡萄酒
		RESULTS:3 = 日本酒
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "料理：喜欢的味道"), "和食")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "料理：喜欢的味道"), "洋食")
			RESULT:1 = 2
		ELSE
			SELECTCASE CFLAG:CHARA:性格倾向
			;气弱系
			CASE 1
				RESULT:1 = 2
			;强气系
			CASE 2
				RESULT:1 = 3
			;坦率系
			CASE 3
				RESULT:1 = 1
			;真面目系
			CASE 4
				RESULT:1 = 2
			;其他
			CASEELSE
				RESULT:1 = 1
			ENDSELECT
		ENDIF
	ELSE
		RESULTS:0 = 今天的晩酌%性格倾向语尾(CHARA, "怎么办？")%
		RESULTS:1 = 在足以洗浴的啤酒之中深醉
		RESULTS:2 = 在微甜的香甜酒中沉醉
		RESULTS:3 = 今日禁酒！
		IF TALENT:CHARA:酒耐性 > 2
			RESULT:1 = 1
		ELSEIF TALENT:CHARA:酒耐性 < 1
			RESULT:1 = 2
		ELSE
			SELECTCASE CFLAG:CHARA:性格倾向
			;气弱系
			CASE 1
				RESULT:1 = 3
			;强气系
			CASE 2
				RESULT:1 = 1
			;坦率系
			CASE 3
				RESULT:1 = 1
			;真面目系
			CASE 4
				RESULT:1 = 3
			;其他
			CASEELSE
				RESULT:1 = 2
			ENDSELECT
		ENDIF
	ENDIF
CASE 5, 6
	;本の恼み
	;本を读まないようなら除外
	SIF ABL:CHARA:教养 <= 0
		RETURN
	RESULTS:0 = 读哪一本新书%性格倾向语尾(CHARA, "呐？")%
	IF ARG % 2 == 0
		RESULTS:1 = 恋爱小说
		RESULTS:2 = 推理小说
		RESULTS:3 = 科幻小说
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(壮大|不思议|狂热|未来)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(威严|知的|纤细|奇妙|怀旧)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(可爱的|活泼|洛丽塔)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:1 = 流行的轻小说
		RESULTS:2 = 严肃的纯文学
		RESULTS:3 = 狂人奇书
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(狂热|知的|奇妙)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(威严|怀旧|别致)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(可爱的|活泼|洛丽塔)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ENDIF
CASE 7, 8
	;香料の恼み
	;条件无
	RESULTS:0 = 房间里放置哪种香料比较好%性格倾向语尾(CHARA, "呐？")%
	IF ARG % 2 == 0
		RESULTS:1 = 清新系
		RESULTS:2 = 醇厚系
		RESULTS:3 = 浓郁系
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(妖术|不思议|威严)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(艺术的|时尚|纤细)")
			RESULT:1 = 3
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(自然|朴素)")
			RESULT:1 = 1
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:1 = 简单的消臭剂
		RESULTS:2 = 芳香精油
		RESULTS:3 = %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%的气味就很好
		IF TALENT:CHARA:污臭耐性 < 0
			RESULT:1 = 1
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")), "妖兽", "妖鸟", "妖虫", "鬼")
			RESULT:1 = 3
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "妖怪")), "吸血鬼", "恶魔")
			RESULT:1 = 2
		ELSEIF GROUPMATCH(GET_TRIBENAME(CHARA, GETNUM(TALENT, "人类")), "天人", "月人", "魔界人")
			RESULT:1 = 2
		ELSEIF TALENT:CHARA:妖精
			RESULT:1 = 2
		ELSEIF TALENT:CHARA:调合知识
			RESULT:1 = 2
		ELSE
			RESULT:1 = 1
		ENDIF
	ENDIF
CASE 9, 10
	;音乐の恼み
	;条件无
	IF ARG % 2 == 0
		RESULTS:0 = 想转换心情该听哪种音乐%性格倾向语尾(CHARA, "呐？")%
		RESULTS:1 = 时尚的爵士乐
		RESULTS:2 = 超酷的摇滚乐
		RESULTS:3 = 苦涩的经典乐
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(帅气的|活泼|时尚|不思议)")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(可怕的|时尚|充满力量|夸张)")
			RESULT:1 = 2
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(美的|艺术的|知的|威严|壮大)")
			RESULT:1 = 3
		ELSE
			RESULT:1 = RAND:3 + 1
		ENDIF
	ELSE
		RESULTS:0 = 哪种东方曲最好%性格倾向语尾(CHARA, "呐？")%
		RESULTS:1 = 果然是Boss曲！
		RESULTS:2 = 通过道中曲！
		RESULTS:3 = 旧作曲也很棒！
		;めんどいけどちまちま
		IF GROUPMATCH(CHARA, [[魅魔]], [[里香]], [[明罗]], [[艾伦]], [[卡娜]], [[理香子]], [[千百合]], [[梦美]], [[留琴]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[胡桃]], [[艾丽]], [[梦月]], [[幻月]], [[露易兹]], [[雪]], [[​舞​]], [[梦子]], [[神绮]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[阿求]], [[幽香]], [[莲子]], [[梅莉]], [[丰姫]], [[依姫]], [[铃仙二号]], [[朱鹭子]], [[小铃]], [[典]])
			RESULT:1 = 3
		ELSEIF GROUPMATCH(CHARA, [[大妖精]], [[小恶魔]], [[莉莉Ｗ]], [[莉莉Ｂ]], [[帝]], [[静叶]], [[椛]], [[琪斯美]], [[屠自古]])
			RESULT:1 = 2
		ELSEIF CSVABL(CHARA, GETNUM(ABL, "战斗能力")) >= 5
			RESULT:1 = 1
		ELSE
			;3番にはならない
			RESULT:1 = RAND:2 + 1
		ENDIF
	ENDIF
CASE 11, 12
	;服の恼み
	;条件无
	RESULTS:0 = 该换什么样的衣服%性格倾向语尾(CHARA, "呐？")%
	IF ARG % 2 == 0
		RESULTS:1 = 可爱的连衣裙
		RESULTS:2 = 酷酷的敞领衬衫
		RESULTS:3 = 性感的吊带背心
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "可爱的")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "帅气的")
			RESULT:1 = 2
		ELSE
			;可怕的/美的派
			RESULT:1 = 3
		ENDIF
	ELSE
		RESULTS:1 = 斩新的时尚搭配
		RESULTS:2 = 沉稳的休闲搭配
		RESULTS:3 = 喜欢现在的服装
		IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(艺术的|活泼|时尚|夸张)")
			RESULT:1 = 1
		ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(朴素|别致|纤细|知的)")
			RESULT:1 = 2
		ELSE
			RESULT:1 = 3
		ENDIF
	ENDIF
CASE 13
	;发型の恼み
	;条件无
	RESULTS:0 = 想要剪发、要什么发型%性格倾向语尾(CHARA, "呐？")%
	RESULTS:1 = 喜欢现在的发型
	;めんどいけどちまちま
	IF GROUPMATCH(CHARA, [[小町]], [[荷取]], [[桑尼]], [[琪斯美]], [[果]], [[小铃]], [[千百合]])
		RESULTS:2 = 单马尾
	ELSE
		RESULTS:2 = 双马尾
	ENDIF
	;参考『东方ロング化タグ』,ありがとうございます
	IF GROUPMATCH(CHARA, [[魅魔]], [[明罗]], [[艾伦]], [[理香子]], [[胡桃]], [[艾丽]], [[神绮]])
		RESULTS:3 = 短发
	ELSEIF GROUPMATCH(CHARA, [[美铃]], [[小恶魔]], [[帕秋莉]], [[莉莉Ｗ]], [[莉莉Ｂ]], [[紫]], [[萃香]])
		RESULTS:3 = 短发
	ELSEIF GROUPMATCH(CHARA, [[慧音]], [[铃仙]], [[永琳]], [[辉夜]], [[妹红]], [[早苗]], [[天子]], [[勇仪]], [[阿空]])
		RESULTS:3 = 短发
	ELSEIF GROUPMATCH(CHARA, [[白莲]], [[布都]], [[心]], [[影狼]], [[弁弁]], [[克劳恩皮丝]], [[纯狐]])
		RESULTS:3 = 短发
	ELSEIF GROUPMATCH(CHARA, [[紫苑]], [[合欢乃]], [[阿吽]], [[成美]], [[隐岐奈]], [[斯塔]], [[丰姫]], [[依姫]])
		RESULTS:3 = 短发
	ELSE
		RESULTS:3 = 长发
	ENDIF
	IF TALENT:CHARA:性的兴趣 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:胆量 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:自尊心 > 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:羞耻心 > 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:开朗／阴郁 < 0
		RESULT:1 = 1
	ELSEIF TALENT:CHARA:性的兴趣 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:开朗／阴郁 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:羞耻心 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:回应 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:态度 < 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:回应 > 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:自己爱 > 0
		RESULT:1 = 3
	ELSE
		;1番にはならない
		RESULT:1 = RAND:2 + 2
	ENDIF
CASE 14
	;参拝の恼み
	;所属している人らは除外
	IF GROUPMATCH(CHARA, [[灵梦]], [[留琴]], [[卡娜]], [[魅魔]], [[萃香]], [[心]], [[针妙丸]], [[阿吽]])
		RETURN
	ELSEIF GROUPMATCH(CHARA, [[纳兹琳]], [[小伞]], [[鵺]], [[白莲]], [[猯藏]], [[一轮]], [[村纱]], [[星]], [[响子]], [[女苑]])
		RETURN
	ELSEIF GROUPMATCH(CHARA, [[早苗]], [[神奈子]], [[诹访子]])
		RETURN
	;月の人たちと若鹭姫
	ELSEIF GROUPMATCH(CHARA, [[依姫]], [[丰姫]], [[铃仙二号]], [[探女]], [[若鹭姫]])
		RETURN
	ENDIF
	;ほぼ运ゲーです
	RESULTS:0 = 去哪里祈愿最好%性格倾向语尾(CHARA, "呐？")%
	RESULTS:1 = 博丽神社
	RESULTS:2 = 守矢神社
	RESULTS:3 = 命莲寺
	;命莲寺は不要！（3番にはならない）
	IF GROUPMATCH(CHARA, [[芳香]], [[青娥]], [[屠自古]], [[布都]], [[神子]])
		RESULT:1 = RAND:2 + 1
	ELSEIF GROUPMATCH(CHARA, [[阿燐]], [[阿空]], [[帕露西]], [[琪斯美]], [[山女]], [[村纱]], [[星]], [[响子]], [[女苑]])
		RESULT:1 = RAND:2 + 1
	ELSEIF GROUPMATCH(CHARA, [[早苗]], [[神奈子]], [[诹访子]])
		RESULT:1 = RAND:2 + 1
	ELSEIF TALENT:CHARA:妖精
		RESULT:1 = RAND:2 + 1
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
	;この恼みは逆张りナシ
	RESULT:2 = 1
CASE 15
	;ダイエットの恼み
	;条件无
	RESULTS:0 = 很在意肚子上的肉、该怎么办%性格倾向语尾(CHARA, "呐？")%
	RESULTS:1 = 每天节食！
	RESULTS:2 = 健康的运动！
	RESULTS:3 = 药力！
	IF TALENT:CHARA:大胃王
		RESULT:1 = 1
	ELSEIF CSVABL(CHARA, GETNUM(ABL, "战斗能力")) <= 0
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:调合知识
		RESULT:1 = 3
	ELSEIF ABL:CHARA:料理技能 >= 3
		RESULT:1 = 1
	ELSEIF ABL:CHARA:战斗能力 <= 3
		RESULT:1 = 2
	ELSEIF TALENT:CHARA:调合Lv >= 3
		RESULT:1 = 3
	ELSE
		SELECTCASE CFLAG:CHARA:性格倾向
		;气弱系
		CASE 1
			RESULT:1 = 1
		;强气系
		CASE 2
			RESULT:1 = 2
		;坦率系
		CASE 3
			RESULT:1 = 1
		;真面目系
		CASE 4
			RESULT:1 = 2
		;其他
		CASEELSE
			RESULT:1 = 2
		ENDSELECT
	ENDIF
	;この恼みは逆张りナシ
	RESULT:2 = 1
CASE 16
	;菜园の恼み
	;冬は除外
	SIF DAY:2 == 4
		RETURN
	RESULTS:0 = 家庭菜园里种什么%性格倾向语尾(CHARA, "呐？")%
	;答え＝DAY:2だと简单すぎるのでシャッフルする
	RESULT:1 = RAND:3 + 1
	FOR LOCAL, 1, 4
		;LOCAL:2に季节を入れる
		;答えはDAY:2
		IF LOCAL == RESULT:1
			LOCAL:2 = DAY:2
		;１つ目の间违い
		ELSEIF !LOCAL:3
			LOCAL:2 = DAY:2 + 1
			LOCAL:3 ++
		;２つ目の间违い
		ELSE
			;秋なら1つ前、それ以外なら2つ先の季节
			IF DAY:2 == 3
				LOCAL:2 = DAY:2 - 1
			ELSE
				LOCAL:2 = DAY:2 + 2
			ENDIF
		ENDIF
		;卷心菜を基准に季节的作物を抽选
		LOCAL:1 = GETNUM(ITEM, "卷心菜") + (10 * LOCAL:2) - 10 + RAND:3
		RESULTS:LOCAL = %ITEMNAME:(LOCAL:1)%
	NEXT
	;この恼みは逆张りナシ
	RESULT:2 = 1
CASE 17
	;菜园の恼み～花编～
	;条件无
	RESULTS:0 = 花盆里种植什么%性格倾向语尾(CHARA, "呐？")%
	;答え＝DAY:2だと简单すぎるのでシャッフルする
	RESULT:1 = RAND:3 + 1
	FOR LOCAL, 1, 4
		;答えには正解の花
		IF LOCAL == RESULT:1
			;函数化してもいいけどするほどでもないかなと思い随意
			SELECTCASE DAY:2
			CASE 1
				RESULTS:LOCAL '= TEXTR("郁金香/秋牡丹/紫罗兰")
			CASE 2
				RESULTS:LOCAL '= TEXTR("喇叭花/桔梗/百合")
			CASE 3
				RESULTS:LOCAL '= TEXTR("波斯菊/菊花/赏月草")
			CASE 4
				RESULTS:LOCAL '= TEXTR("雏菊/水仙/雪花莲")
			ENDSELECT
		ELSE
			;LOCAL:2に季节を入れる
			;１つ目の间违い
			IF !LOCAL:3
				LOCAL:2 = DAY:2 + 1
				LOCAL:3 ++
			;２つ目の间违い
			ELSE
				;秋なら1つ前、それ以外なら2つ先の季节
				IF DAY:2 == 3
					LOCAL:2 = DAY:2 - 1
				ELSE
					LOCAL:2 = DAY:2 + 2
				ENDIF
			ENDIF
			SELECTCASE LOCAL:2
			CASE 1
				RESULTS:LOCAL '= TEXTR("郁金香/秋牡丹/紫罗兰")
			CASE 2
				RESULTS:LOCAL '= TEXTR("喇叭花/桔梗/百合")
			CASE 3
				RESULTS:LOCAL '= TEXTR("波斯菊/菊花/赏月草")
			CASE 4
				RESULTS:LOCAL '= TEXTR("雏菊/水仙/雪花莲")
			ENDSELECT
		ENDIF
	NEXT
	;この恼みは逆张りナシ
	RESULT:2 = 1
CASE 18
	;休日の恼み
	;条件无,仕事ない角色もいるけど
	RESULTS:0 = 下个假期做什么%性格倾向语尾(CHARA, "呐？")%
	RESULTS:1 = 外出散步
	RESULTS:2 = 町上购物
	RESULTS:3 = 在家无所事事
	SELECTCASE CFLAG:CHARA:性格倾向
	;气弱系
	CASE 1
		RESULT:1 = 1
	;强气系
	CASE 2
		RESULT:1 = 1
	;坦率系
	CASE 3
		RESULT:1 = 3
	;真面目系
	CASE 4
		RESULT:1 = 2
	;其他
	CASEELSE
		RESULT:1 = 2
	ENDSELECT
CASE 19
	;人气の恼み
	;条件无
	RESULTS:0 = 最近有人气下降了的%性格倾向语尾(CHARA, "感觉")%……
	RESULTS:1 = 没有那回事！
	RESULTS:2 = 磨炼自己吧！
	RESULTS:3 = 只有脱一个选择了
	SELECTCASE CFLAG:CHARA:性格倾向
	;气弱系
	CASE 1
		RESULT:1 = 2
	;强气系
	CASE 2
		RESULT:1 = 2
	;坦率系
	CASE 3
		RESULT:1 = 1
	;真面目系
	CASE 4
		RESULT:1 = 2
	;其他
	CASEELSE
		;3番にはならない）
		RESULT:1 = RAND:2 + 1
	ENDSELECT
	;この恼みは逆张りナシ
	RESULT:2 = 1
CASE 20
	;集中力の恼み
	;条件无
	RESULTS:0 = 最近打不起精神%性格倾向语尾(CHARA, "了啊")%……
	RESULTS:1 = 还是好好睡一觉比较好
	RESULTS:2 = 散散心吧
	RESULTS:3 = 闘魂注入掌！！
	SELECTCASE CFLAG:CHARA:性格倾向
	;气弱系
	CASE 1
		RESULT:1 = 3
	;强气系
	CASE 2
		RESULT:1 = 3
	;坦率系
	CASE 3
		RESULT:1 = 2
	;真面目系
	CASE 4
		RESULT:1 = 1
	;其他
	CASEELSE
		RESULT:1 = 2
	ENDSELECT
	;この恼みは逆张りナシ
	RESULT:2 = 1
CASE 21
	;御三家の恼み
	RESULTS:0 = 最初的三匹之中%性格倾向语尾(CHARA, "该选哪一个？")%
	RESULTS:1 = 草系
	RESULTS:2 = 火系
	RESULTS:3 = 水系
	IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(土|植物|绿|虫)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(火|日|赤|朱)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(水|冰|青|鱼)")
		RESULT:1 = 3
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "自然")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "充满力量")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "知的")
		RESULT:1 = 3
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
CASE 22
	;进化の恼み
	RESULTS:0 = %性格倾向语尾(CHARA, "让它")%进化成哪个属性呢？
	RESULTS:1 = 水系
	RESULTS:2 = 电系
	RESULTS:3 = 火系
	IF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(水|冰|青|鱼)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(雷|星|黄)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(火|日|赤|朱)")
		RESULT:1 = 3
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(美的|知的)")
		RESULT:1 = 1
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(帅气的|夸张)")
		RESULT:1 = 2
	ELSEIF STRCOUNT(GET_STR(, "キャラデータ", CHARA, "感性：好き"), "(可爱的|充满力量)")
		RESULT:1 = 3
	ELSE
		RESULT:1 = RAND:3 + 1
	ENDIF
CASEELSE
	;礼物の恼み,一番クイズっぽいので概率を稍微上げてます
	;相性のいい角色を寻找
	CALL FISHER_YATES_SHAFFLE(CHARANUM)
	FOR LOCAL:1, 1, CHARANUM
		LOCAL = SHAFFLE_ARRAY:(LOCAL:1)
		IF RELATION:CHARA:LOCAL > 100
			LOCAL:2 = LOCAL
			BREAK
		ENDIF
	NEXT
	;いないなら除外
	SIF !LOCAL:2
		RETURN
	;友人角色を隔离
	LOCAL:10 = LOCAL:2
	[IF_DEBUG]
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", (LOCAL:10))%
	[ENDIF]
	;答えを先に入れるパターン
	LOCAL = RAND:3 + 1
	;答えの选择肢に高得点を入れる,あえて701点ではなく700点から
	CALL CREATE_GIFT_FROM_CHARA(LOCAL:10, 700, 1000)
	LOCALS:LOCAL = %RESULTS%
	;ハズレの选择肢にダミーを入れる
	FOR LOCAL:1, 1, 4
		SIF LOCAL:1 == LOCAL
			CONTINUE
		CALL CREATE_GIFT_FROM_CHARA(LOCAL:10, 0, 699)
		LOCALS:(LOCAL:1) = %RESULTS%
	NEXT
	;函数を呼んだのでこの时点ではRESULT/RESULTSがぐちゃってます
	RESULTS:0 = 送给%NAME_OUTPUT_TRANSLATION("CALLNAME", (LOCAL:10))%的礼物哪个更好%性格倾向语尾(CHARA, "呐？")%
	RESULTS:1 = %LOCALS:1%
	RESULTS:2 = %LOCALS:2%
	RESULTS:3 = %LOCALS:3%
	RESULT:1 = LOCAL
	RESULT:2 = 0
[SKIPSTART]
CASE 
	RESULTS:0 = 
	RESULTS:1 = 
	RESULTS:2 = 
	RESULTS:3 = 
	RESULT:1 = 
[SKIPEND]
ENDSELECT
RETURN 1

;性格倾向によって泛用台词の语尾を变える
@性格倾向语尾(ARG, ARGS)
#FUNCTIONS
;气弱系＆坦率系＆其他系はそのまま返す
SIF !GROUPMATCH(CFLAG:ARG:性格倾向, 2, 4)
	RETURNF ARGS
SELECTCASE ARGS
CASE "呐？"
	SELECTCASE CFLAG:ARG:性格倾向
	;强气系
	CASE 2
		RETURNF "啊？"
	;真面目系
	CASE 4
		RETURNF "呢？"
	ENDSELECT
CASE "认为呢？"
	SELECTCASE CFLAG:ARG:性格倾向
	;强气系
	CASE 2
		RETURNF "觉得呢？"
	;真面目系
	CASE 4
		RETURNF "怎么看？"
	ENDSELECT
CASE "怎么办？"
	SELECTCASE CFLAG:ARG:性格倾向
	;强气系
	CASE 2
		RETURNF "该怎么做啊？"
	;真面目系
	CASE 4
		RETURNF "做些什么？"
	ENDSELECT
CASE "该选哪一个？"
	SELECTCASE CFLAG:ARG:性格倾向
	;强气系
	CASE 2
		RETURNF "究竟选哪一个啊？"
	;真面目系
	CASE 4
		RETURNF "选哪一个？"
	ENDSELECT
CASE "让它"
	SELECTCASE CFLAG:ARG:性格倾向
	;强气系
	CASE 2
		RETURNF "应该让它"
	;真面目系
	CASE 4
		RETURNF "将它"
	ENDSELECT
CASE "感觉"
	SELECTCASE CFLAG:ARG:性格倾向
	;强气系
	CASE 2
		RETURNF "感觉哇"
	;真面目系
	CASE 4
		RETURNF "感觉的"
	ENDSELECT
CASE "了啊"
	SELECTCASE CFLAG:ARG:性格倾向
	;强气系
	CASE 2
		RETURNF "了哇"
	;真面目系
	CASE 4
		RETURNF "了呢"
	ENDSELECT
ENDSELECT
