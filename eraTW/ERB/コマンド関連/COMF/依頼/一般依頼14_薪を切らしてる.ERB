;存否函数
;F函数はTRYCALL系が使えないので通常函数で存否配列を作っちゃう
	;暂定的コメントアウト
@EXIST_一般依赖14
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
;ここでARGで渡されるのはCLIENT_IDです
@一般依赖14(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
SELECTCASE O_DATA
CASE "依赖名"
	CALLF MAKE_STR(V_NAME, "正在砍柴")
CASE "依赖内容表示"
	CALLF MAKE_STR(V_NAME, @"运送木柴")
CASE "依赖报酬表示"
	CALLF MAKE_STR(V_NAME, @"平价食材３个")
CASE "依赖实行指令名"
	CALLF MAKE_STR(V_NAME, "给5块木柴")
CASE "依赖内容"
	CALLF MAKE_INT(V_NAME, MAKE_IRAI_DATA(GETNUM(ITEM, "木材"), "调达"))
CASE "依赖サブ"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依赖期限"
;发生时ではなく受托时に确定します
	CALLF MAKE_INT(V_NAME, 5)
CASE "依赖发生率"
	IF DAY:2 == 4
	;冬は发生しやすい
		CALLF MAKE_INT(V_NAME, 10)
	ELSE
		CALLF MAKE_INT(V_NAME, 3)
	ENDIF
CASE "依赖发生条件"
	CALLF MAKE_INT(V_NAME, 1)
CASE "依赖受托可能条件"
;木材を持ってる
	;SIF ITEM:木材
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖实行可能条件"
	SIF ITEM:木材 >= 5
		CALLF MAKE_INT(V_NAME, 1)
;成功/失败处理はCARRYOUTで直接书く
;SOURCEやITEM入手、消费が烦杂になるので
CASE "依赖消灭条件"
	SIF RAND:100 < 30
		CALLF MAKE_INT(V_NAME, 1)
CASE "依赖人连れ出し可能"
ENDSELECT



@IRAI_一般14(CHARA, IRAI_ID, SCENE)
#DIM  CHARA
#DIM  IRAI_ID
#DIMS SCENE
#DIM  CLIENT_ID
CLIENT_ID = IRAI_ID_TO_CLIENT(IRAI_ID)
SELECTCASE SCENE
CASE "依赖条件表示"
;何か専用の表示情报を设けたい时用
;受托済みの依赖表示で使用されます
;
;PRINT系だけでSOURCEとかは入れちゃダメ、WAITも无しで
;ここ以外の表示项目は
;PRINTFORML 　　　　　：
;となっているのでスペースを揃えたほうが见栄えが良いです

CASE "依赖实行时"
;このCASEに限り、RESULTが意味を持ちます
;依赖实行时のRESULTにはIRAI_CONST_RESULT()を使用してください
;可能な参数は"中止", "实行して成功", "实行して失败", "实行して未解决"です
;独自处理を仕込みたい时は"实行して未解决"で先延ばしするなりしてください
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%把木柴交给了%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%
	IF DAY:2 == 4
	;冬はメッセージあり
		PRINTFORML 这样就不会因为寒冷而发抖了
		SOURCE:TARGET:欢乐 += 2000
	ELSE
		PRINTFORML 
		SOURCE:TARGET:欢乐 += 1000
	ENDIF
	CALL LOST_ITEM("木材", 5, 2)
	RETURN IRAI_CONST_RESULT("实行して成功")
CASE "依赖成功时"
;成功报告时の报酬など
	CALL CHANGE_CFLAG(2, TARGET, 30)
	CALL CHANGE_CFLAG(4, TARGET, 20)
	TALENT:心情 = LIMIT(TALENT:心情 + 1, -1, 1)
	CALL GET_ITEM("平价食材", 3, 2)
CASE "依赖失败时"
;失败报告时の处理
	CALL CHANGE_CFLAG(2, TARGET, -5)
	CALL CHANGE_CFLAG(4, TARGET, -5)
ENDSELECT
