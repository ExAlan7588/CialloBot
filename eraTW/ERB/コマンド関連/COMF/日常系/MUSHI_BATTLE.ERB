;-------------------------------------------------
;斗虫：战斗の统括函数
;-------------------------------------------------
@MUSHI_BATTLE
#DIM LPCOUNT
VARSET MUSHI_FINISH
;全员のETB处理
FOR LOCAL, 1, MUSHI_CHARANUM + 1
	CALL MUSHI_BATTLE_ETB(LOCAL, MUSHI_STANDNUM:LOCAL)
NEXT
MUSHI_ROUND = 0

;以下ループ
WHILE !MUSHI_FINISH
	;最初に战场状态を表示
	IF !MUSHI_ROUND
		CALL MUSHI_BATTLE_FIELD
	ELSE
		CALL MUSHI_MSGSET(@"%" " * 30%＜回合　%TOFULL(TOSTR(MUSHI_ROUND))%＞")
		MUSHI_MSGCL:RESULT = C_YELLOW
		CALL MUSHI_BATTLE_MSG
	ENDIF
	;行动顺判定
	VARSET MUSHI_ORDER
	FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
		;一番高いものを抽出していく
		MUSHI_ORDER:0 = 0
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			;すでに抽出してたら弹く
			SIF MATCH(MUSHI_ORDER, MUSHI_STANDNUM:LOCAL)
				CONTINUE
			IF MUSHI_ORDER:0 < MUSHI_SPD:(MUSHI_STANDNUM:LOCAL)
				MUSHI_ORDER:LPCOUNT = MUSHI_STANDNUM:LOCAL
				MUSHI_ORDER:0 = MUSHI_SPD:(MUSHI_STANDNUM:LOCAL)
			ENDIF
		NEXT
	NEXT
;	[IF_DEBUG]
;		FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
;			LOCAL = MUSHI_ORDER:LPCOUNT / 10
;			DRAWLINE
;			PRINTFORML MUSHI_ORDER:{LPCOUNT}＝{MUSHI_ORDER:LPCOUNT}
;			PRINTFORML MUSHI_STANDNUM:{LOCAL} ＝ {MUSHI_STANDNUM:LOCAL}
;			PRINTFORML %BATTLERNAME:LOCAL%
;			PRINTFORML %MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%
;			PRINTFORML MUSHI_HP:{(MUSHI_STANDNUM:LOCAL)} ＝ {MUSHI_HP:(MUSHI_STANDNUM:LOCAL)}
;			PRINTFORML MUSHI_ATK:{(MUSHI_STANDNUM:LOCAL)} ＝ {MUSHI_ATK:(MUSHI_STANDNUM:LOCAL)}
;			PRINTFORML MUSHI_SPD:{(MUSHI_STANDNUM:LOCAL)} ＝ {MUSHI_SPD:(MUSHI_STANDNUM:LOCAL)}
;			PRINTFORML MUSHI_SKL:{(MUSHI_STANDNUM:LOCAL)} ＝ %MUSHI_SKL:(MUSHI_STANDNUM:LOCAL)%
;			PRINTFORML MUSHI_STATUS:{LOCAL} ＝ %MUSHI_STATUS:LOCAL%
;		NEXT
;		WAIT
;	[ENDIF]
	IF MUSHI_ROUND
		;各行动处理
		FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
			SIF !MUSHI_FINISH
				CALL MUSHI_BATTLE_COMBAT(MUSHI_ORDER:LPCOUNT / 10, MUSHI_ORDER:LPCOUNT)
		NEXT
		;ターン结束时处理
		SIF !MUSHI_FINISH
			CALL MUSHI_BATTLE_ENDSTEP
	ELSE
		;开幕メッセージ
		CALL MUSHI_BATTLE_MSG
	ENDIF
	MUSHI_ROUND ++
WEND

;-------------------------------------------------
;斗虫：メインの战场表示函数（上半分）
;PLAYER_A_ID, PLAYER_B_ID   プレイヤー番号
;LOCAL:3-4   角色番号
;MUSHI_ID   战场に出ているムシのID
;MUSHI_FLAG   战场に出ているムシのフラグ番号（プレイヤー番号 * 10 + 选出番号）
;LOCAL:99    偶数人目の表示の有无
;-------------------------------------------------
@MUSHI_BATTLE_FIELD
#DIM LPCOUNT
#DIMS SHIFT
#DIM MUSHI_A_ID
#DIM MUSHI_A_FLAG
#DIM MUSHI_B_ID
#DIM MUSHI_B_FLAG
#DIM PLAYER_A_ID
#DIMS PLAYER_A_NAME
#DIM PLAYER_B_ID
#DIMS PLAYER_B_NAME
MUSHI_LINE = 3
PRINTFORM ◆回合%TOFULL(TOSTR(MUSHI_ROUND))%　＜场地类型：
CALL PRINT_FIELDTYPE(MUSHI_FIELD:99)
PRINTL ＞
PRINTFORML ┏%"━" * 40%┓
;2人ずつ表示していく
FOR LOCAL, 0, (MUSHI_CHARANUM > 2) + 1
	;フラグがごっちゃごちゃだ～
	VARSET LOCALS
	PLAYER_A_ID = 1 + LOCAL * 2
	PLAYER_B_ID = 2 + LOCAL * 2
	PLAYER_A_NAME '= @"%BATTLERNAME:PLAYER_A_ID%"
	PLAYER_B_NAME '= @"%BATTLERNAME:PLAYER_B_ID%"
	LOCAL:99 = !LOCAL || !(MUSHI_CHARANUM % 2)
	;キャラの名前
	PRINT ┃
	SIF !MUSHI_REST:(PLAYER_B_ID)
		LOCALS:2 += "Ｋ．Ｏ．"
	LOCALS:1 += PLAYER_A_NAME
	LOCALS:2 += PLAYER_B_NAME
	SIF MUSHI_TEAM && PLAYER_A_ID != 1
		LOCALS:1 += "(仲间)"
	IF !MUSHI_REST:(PLAYER_A_ID)
		LOCALS:1 += "Ｋ．Ｏ．"
		SETCOLOR C_RED
	ELSE
		SETCOLOR MUSHI_COLOR:(PLAYER_A_ID)
	ENDIF
	PRINTFORM %LOCALS:1, 40, LEFT%
	RESETCOLOR
	IF LOCAL:99
		IF !MUSHI_REST:(PLAYER_B_ID)
			SETCOLOR C_RED
		ELSE
			SETCOLOR MUSHI_COLOR:(PLAYER_B_ID)
		ENDIF
		PRINTFORM %LOCALS:2, 40, RIGHT%
		RESETCOLOR
	ELSE
		PRINTFORM %" "* 40%
	ENDIF
	PRINTL ┃
	MUSHI_A_ID = MUSHI_STAND:(PLAYER_A_ID)
	MUSHI_B_ID = MUSHI_STAND:(PLAYER_B_ID)
	MUSHI_A_FLAG = MUSHI_STANDNUM:(PLAYER_A_ID)
	MUSHI_B_FLAG = MUSHI_STANDNUM:(PLAYER_B_ID)
	;ムシの名前
	PRINT ┃
	CALL PRINT_MUSHINAME(MUSHI_NAME:MUSHI_A_FLAG, MUSHI_A_ID, 40)
	IF LOCAL:99
		CALL PRINT_MUSHINAME(MUSHI_NAME:MUSHI_B_FLAG, MUSHI_B_ID, 40, 1)
	ELSE
		PRINTFORM %" "* 40%
	ENDIF
	PRINTL ┃
	;HPバーと残り手持ち数
	PRINT ┃HP 
	CALL PRINT_COLORBAR(MUSHI_HP:MUSHI_A_FLAG, MUSHI_MAXHP:MUSHI_A_FLAG, 20, UNICODE(0x2588), UNICODE(0x2588), BARCOLORSET("绿"), RESULT:1)
	PRINT  
	CALL PRINT_RESTMUSHI(PLAYER_A_ID)
	IF LOCAL:99
		PRINT 　　　　　　　　　　
		CALL  PRINT_RESTMUSHI(PLAYER_B_ID)
		PRINT  
		CALL PRINT_COLORBAR(MUSHI_HP:MUSHI_B_FLAG, MUSHI_MAXHP:MUSHI_B_FLAG, 20, UNICODE(0x2588), UNICODE(0x2588), BARCOLORSET("绿"), RESULT:1)
		PRINT  HP
	ELSE
		PRINTFORM 　　　　　%" "* 40%
	ENDIF
	PRINTL ┃
	;属性と状态变化
	PRINT ┃
	;専用参数を使ってRESULT:1を出力する
	CALL MUSHI_TYPE_PRIINT(MUSHI_TYPE:MUSHI_A_FLAG, MUSHI_TYPE:MUSHI_B_FLAG)
	PRINTFORM %MUSHI_STATUS:PLAYER_A_ID, 40 - RESULT, LEFT%
	IF LOCAL:99
		PRINTFORM %MUSHI_STATUS:(PLAYER_B_ID), 40 - RESULT:1, RIGHT%
		CALL MUSHI_TYPE_PRIINT(MUSHI_TYPE:MUSHI_B_FLAG)
	ELSE
		PRINTFORM %" "* 40%
	ENDIF
	PRINTL ┃
	IF !LOCAL
		PRINT ┃
		CALL PRINT_FIELD_EFFECT(MUSHI_FIELD:99)
		PRINTL ┃
;		PRINTFORML ┃%" " * 37%ＶＳ．%" " * 37%┃
		MUSHI_LINE += 5
	ELSE
		MUSHI_LINE += 4
	ENDIF
NEXT
PRINTFORML ┣%"━" * 40%┫
;＜メッセージ表示に续く＞

;-------------------------------------------------
;斗虫：メッセージを设定する函数
;-------------------------------------------------
@MUSHI_MSGSET(ARGS)
FOR LOCAL, 0, 14
	IF MUSHI_MSG:LOCAL == ""
		MUSHI_MSG:LOCAL = %ARGS%
		RESULT = LOCAL
		BREAK
	ENDIF
NEXT
;色を着ける用に入れた番号を返す
RETURN RESULT

;-------------------------------------------------
;斗虫：メッセージ表示函数（下半分）
;-------------------------------------------------
@MUSHI_BATTLE_MSG
MUSHI_LINE += 1
FOR LOCAL, 0, 14
	PRINTFORM ┃
	SIF MUSHI_MSGCL:LOCAL
		SETCOLOR MUSHI_MSGCL:LOCAL
	PRINTFORM %MUSHI_MSG:LOCAL, 80, LEFT%
	RESETCOLOR
	PRINTL ┃
	MUSHI_LINE ++
NEXT
PRINTFORML ┗%"━" * 40%┛
WAIT
;履历を残すか？どうするか？
;CLEARLINE MUSHI_LINE
VARSET MUSHI_MSG
VARSET MUSHI_MSGCL
SIF !MUSHI_FINISH
	CALL MUSHI_BATTLE_FIELD

;-------------------------------------------------
;斗虫：战斗计算函数
;ARG    ターンプレイヤー番号
;NUM    ターンプレイヤーの战场に出しているムシのフラグ番号
;-------------------------------------------------
@MUSHI_BATTLE_COMBAT(ARG, NUM)
#DIM NUM
#DIM 攻击对象
#DIM 对象NUM
#DIM ダメージ补正
#DIM 固定ダメージ
#DIMS 攻击スキル, 2
#DIM 回避率
#DIM 与えるダメージ
;行动不能判定
IF MUSHI_HP:NUM <= 0
	RETURN
;败北している
ELSEIF !MUSHI_REST:ARG
	RETURN
ELSEIF STRCOUNT(MUSHI_STATUS:ARG, "<睡眠>")
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME:NUM%》正在睡眠……zZZ")
	MUSHI_MSGCL:RESULT = C_P_PURPLE
	CALL MUSHI_BATTLE_MSG
	RETURN
ENDIF

;对象判定
攻击对象 = 0
对象NUM = 0
攻击スキル = 
攻击スキル:1 = 
LOCAL = 0
WHILE !对象NUM
	攻击对象 = RAND:MUSHI_CHARANUM + 1
	;自分はダメ
	IF 攻击对象 == ARG
		攻击对象 = 0
	;味方はダメ
	ELSEIF MUSHI_TEAM && (攻击对象 % 2) == (ARG % 2)
		攻击对象 = 0
	;KOしてたらダメ
	ELSEIF MUSHI_HP:(MUSHI_STANDNUM:攻击对象) <= 0
		攻击对象 = 0
	;败退してたらダメ
	ELSEIF !MUSHI_REST:攻击对象
		攻击对象 = 0
	;[拟态]の效果
	ELSEIF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:攻击对象), "拟态") && !RAND:2
		攻击对象 = 0
	ENDIF
	SIF 攻击对象
		对象NUM = MUSHI_STANDNUM:攻击对象
	LOCAL ++
	;ある程度やって决まらなければ失败
	IF LOCAL > 100
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》的攻击索敌没有成功…")
		CALL MUSHI_BATTLE_MSG
		RETURN
	ENDIF
WEND

;攻击时判定
ダメージ补正 = 100
固定ダメージ = 0
;＠攻击时口上
CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 残りムシ数(ARG), MUSHI_BATTLER:ARG, ARG, 0, "攻击时")
;单独の攻击スキル
;猛毒攻击の毒攻击を防止
IF STRCOUNT(MUSHI_SKL:NUM, "毒攻击") && !STRCOUNT(MUSHI_SKL:NUM, "猛毒攻击") && スキル判定(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻击对象, "<毒>")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『毒攻击』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ补正, 0.80
	攻击スキル = 毒攻击
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "猛毒攻击") && スキル判定(20, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻击对象, "<猛毒>")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『猛毒攻击』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ补正, 0.80
	攻击スキル = 猛毒攻击
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "麻痹攻击") && スキル判定(30, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻击对象, "<麻痹>")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『麻痹攻击』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ补正, 0.80
	攻击スキル = 麻痹攻击
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "睡眠粉") && スキル判定(20, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻击对象, "<睡眠>")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『睡眠粉』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ补正, 0.80
	攻击スキル = 睡眠粉
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "音波攻击") && スキル判定(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻击对象, "<混乱>")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『音波攻击』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ补正, 0.80
	攻击スキル = 音波攻击
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "粘液攻击") && スキル判定(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻击对象, "<缓慢>")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『粘液攻击』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ补正, 0.80
	攻击スキル = 粘液攻击
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "恶臭攻击") && スキル判定(40, ARG, NUM) && !STRCOUNT(MUSHI_STATUS:攻击对象, "<恶臭>")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『恶臭攻击』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ补正, 0.80
	攻击スキル = 恶臭攻击
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "诅咒攻击") && スキル判定(30, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『诅咒攻击』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ补正, 0.80
	攻击スキル = 诅咒攻击
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "吸收攻击") && スキル判定(100, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『吸收攻击』！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	TIMES ダメージ补正, 1.00
	攻击スキル = 吸收攻击
ENDIF
;复合攻击スキル
IF STRCOUNT(MUSHI_SKL:NUM, "强攻击") && スキル判定(40, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『强攻击』！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	IF STRCOUNT(MUSHI_SKL:对象NUM, "光の装甲")
		CALL MUSHI_MSGSET(@"但是【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》用『光の装甲』只受到了通常的伤害！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"造成伤害1.3倍！")
		TIMES ダメージ补正, 1.30
		攻击スキル:1 += "强攻击"
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "要害攻击") && スキル判定(20, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『要害攻击』！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	IF STRCOUNT(MUSHI_SKL:对象NUM, "光の装甲")
		CALL MUSHI_MSGSET(@"但是【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》用『光の装甲』只受到了通常的伤害！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"造成伤害2倍！！")
		TIMES ダメージ补正, 2.00
		攻击スキル:1 += "要害攻击"
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "致命攻击") && スキル判定(10, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『致命攻击』！")
	MUSHI_MSGCL:RESULT = C_RED
	IF STRCOUNT(MUSHI_SKL:对象NUM, "光の装甲")
		CALL MUSHI_MSGSET(@"但是【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》用『光の装甲』只受到了通常的伤害！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"造成致命伤害！！")
		IF MUSHI_HP:对象NUM < MUSHI_MAXHP:对象NUM * 50 / 100
			固定ダメージ = MUSHI_HP:对象NUM
		ELSE
			固定ダメージ = MUSHI_HP:对象NUM - 1
		ENDIF
		攻击スキル:1 += "致命攻击"
	ENDIF
ENDIF
{
IF STRCOUNT(MUSHI_SKL:NUM, "红色极限黄金最大燃烧") 
&& スキル判定(30, ARG, NUM) && MUSHI_HP:NUM < MUSHI_MAXHP:NUM / 3
}
	CALL MUSHI_MSGSET(@"【%NAME_OUTPUT_TRANSLATION("CALLNAME", (MUSHI_BATTLER:ARG))%】《%MUSHI_NAME:NUM%》の『红色极限黄金最大燃烧』！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"谜之能量溢出！造成3倍伤害！")
	TIMES ダメージ补正, 3.00
	攻击スキル:1 += "红色极限黄金最大燃烧"
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "激光攻击") && スキル判定(20, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『激光攻击』！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"本次攻击无法避开！！")
	攻击スキル:1 += "激光攻击"
ENDIF
;通常攻击
SIF 攻击スキル == 攻击スキル:1
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『通常攻击』！")
;追加ダメージUP
IF STRCOUNT(MUSHI_SKL:NUM, "先手必胜") && MUSHI_ORDER:1 == ARG
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『先手必胜』发动！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"依照速度提升伤害！")
	TIMES ダメージ补正, 1.20
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "駆逐") && 残りムシ数(攻击对象) == 1
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『駆逐』发动！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】的虫子被彻底地殲灭了！")
	TIMES ダメージ补正, 1.50
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "主宰") && MUSHI_HP:NUM == MUSHI_MAXHP:NUM
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『主宰』发动！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"HP满时必定攻击要害！！")
	TIMES ダメージ补正, 2.00
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "水栖特效") && STRCOUNT(MUSHI_TRIBE:对象NUM, "水栖")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『水栖特效』发动！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"对[水栖]的敌人造成高伤害！！")
	TIMES ダメージ补正, 2.00
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "飞行特效") && STRCOUNT(MUSHI_TRIBE:对象NUM, "飞行")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『飞行特效』发动！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"对[飞行]的敌人造成高伤害！！")
	TIMES ダメージ补正, 2.00
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "软体特效") && STRCOUNT(MUSHI_TRIBE:对象NUM, "软体")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『软体特效』发动！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_MSGSET(@"对[软体]的敌人造成高伤害！！")
	TIMES ダメージ补正, 2.00
ENDIF

;回避判定
IF MUSHI_SPD:NUM * 500 / 100 < MUSHI_SPD:对象NUM
	回避率 = 70
ELSEIF MUSHI_SPD:NUM * 400 / 100 < MUSHI_SPD:对象NUM
	回避率 = 60
ELSEIF MUSHI_SPD:NUM * 300 / 100 < MUSHI_SPD:对象NUM
	回避率 = 50
ELSEIF MUSHI_SPD:NUM * 250 / 100 < MUSHI_SPD:对象NUM
	回避率 = 40
ELSEIF MUSHI_SPD:NUM * 200 / 100 < MUSHI_SPD:对象NUM
	回避率 = 30
ELSEIF MUSHI_SPD:NUM * 150 / 100 < MUSHI_SPD:对象NUM
	回避率 = 20
ELSEIF MUSHI_SPD:NUM * 100 / 100 < MUSHI_SPD:对象NUM
	回避率 = 10
ELSE
	回避率 = 0
ENDIF
SIF STRCOUNT(MUSHI_SKL:对象NUM, "极小")
	回避率 += 30
SIF STRCOUNT(MUSHI_SKL:对象NUM, "高速回避")
	回避率 += 30
SIF STRCOUNT(MUSHI_STATUS:攻击对象, "睡眠|麻痹|缓慢")
	回避率 = 0
SIF STRCOUNT(攻击スキル:1, "激光攻击")
	回避率 = 0
;コッソリ怪诞攻击は必中
SIF STRCOUNT(MUSHI_SKL:NUM, "怪诞攻击")
	回避率 = 0
;最大80%
回避率 = MIN(回避率, 80)
IF RAND:100 < 回避率
	IF STRCOUNT(MUSHI_SKL:对象NUM, "高速回避")
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》の『高速回避』发动！")
		MUSHI_MSGCL:RESULT = C_AQUA
		CALL MUSHI_MSGSET(@"以俊敏的动作回避了攻击！！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSEIF STRCOUNT(MUSHI_SKL:对象NUM, "极小")
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》の『极小』发动！")
		MUSHI_MSGCL:RESULT = C_AQUA
		CALL MUSHI_MSGSET(@"因为身体过小而没有被攻击击中！！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ELSE
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》回避了攻击！")
		MUSHI_MSGCL:RESULT = C_AQUA
	ENDIF
	CALL MUSHI_BATTLE_MSG
	RETURN
ENDIF

;防御时判定
IF STRCOUNT(MUSHI_STATUS:攻击对象, "<阳炎>")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》只是幻影！")
	MUSHI_MSGCL:RESULT = C_CREAM
	MUSHI_STATUS:攻击对象'= REPLACE(MUSHI_STATUS:攻击对象, "<阳炎>", "")
	ダメージ补正 = 0
ELSEIF STRCOUNT(MUSHI_SKL:对象NUM, "Counter") && スキル判定(10, 攻击对象, 对象NUM)
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》の『Counter』发动！！")
	MUSHI_MSGCL:RESULT = C_CREAM
	CALL MUSHI_MSGSET(@"返还等量伤害！！")
	攻击对象 = ARG
	对象NUM = NUM
ELSEIF STRCOUNT(MUSHI_SKL:对象NUM, "仅防禦") && スキル判定(30, 攻击对象, 对象NUM)
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》の『仅防禦』发动！")
	MUSHI_MSGCL:RESULT = C_CREAM
	CALL MUSHI_MSGSET(@"受到伤害减半！")
	TIMES ダメージ补正, 0.50
ENDIF
IF STRCOUNT(MUSHI_STATUS:攻击对象, "<缓慢>")
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME:对象NUM%》在<缓慢>状态下受到伤害增加！")
	TIMES ダメージ补正, 1.50
ENDIF

;ダメージ判定
;无效化されたら结束
IF !ダメージ补正
	CALL MUSHI_BATTLE_MSG
	RETURN
ENDIF
[SKIPSTART]
	;レアリティ差补正※保留
	SELECTCASE MUSHI_RARE:NUM - MUSHI_RARE:对象NUM
	CASE 1
		TIMES ダメージ补正, 1.05
	CASE 2
		TIMES ダメージ补正, 1.10
	CASE 3
		TIMES ダメージ补正, 1.15
	CASE 4
		TIMES ダメージ补正, 1.20
	CASE IS >= 5
		TIMES ダメージ补正, 1.30
	ENDSELECT
[SKIPEND]
;相性补正
VARSET LOCAL
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "赤") * STRCOUNT(MUSHI_TYPE:对象NUM, "绿")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "绿") * STRCOUNT(MUSHI_TYPE:对象NUM, "青")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "青") * STRCOUNT(MUSHI_TYPE:对象NUM, "赤")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "白") * STRCOUNT(MUSHI_TYPE:对象NUM, "黑")
LOCAL += STRCOUNT(MUSHI_TYPE:NUM, "黑") * STRCOUNT(MUSHI_TYPE:对象NUM, "白")
WHILE LOCAL:1 < LOCAL
	TIMES ダメージ补正, 1.50
	LOCAL:1 ++
WEND
SIF LOCAL:1
	CALL MUSHI_MSGSET(@"属性相性卓越！")

;ダメージ决定
与えるダメージ = MUSHI_ATK:NUM * ダメージ补正 / 100
;乱数补正
与えるダメージ = 与えるダメージ * (80 + RAND:40) / 100

SIF 固定ダメージ
	与えるダメージ = 固定ダメージ

CALL MUSHI_DAMAGE(对象NUM, 与えるダメージ)
CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》受到了 {与えるダメージ} 的伤害！")
MUSHI_MSGCL:RESULT = C_ORANGE

;[吸收攻击]の效果
IF 攻击スキル == "吸收攻击"
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》恢复了 {与えるダメージ} ！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	MUSHI_HP:NUM = MIN(MUSHI_HP:NUM + 与えるダメージ, MUSHI_MAXHP:NUM)
ENDIF
;[逆袭]の效果
IF INRANGE(MUSHI_HP:对象NUM, 1, MUSHI_MAXHP:对象NUM / 2) && STRCOUNT(MUSHI_SKL:对象NUM, "逆袭") && !STRCOUNT(MUSHI_STATUS:攻击对象, "<逆袭>")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》の『逆袭』发动！")
	MUSHI_MSGCL:RESULT = C_ORANGE
	CALL MUSHI_MSGSET(@"受到攻击后变为了<逆袭>状态！！")
	MUSHI_STATUS:攻击对象 += "<逆袭>"
	CALL MUSHI_PALAM(MUSHI_STAND:攻击对象, MUSHI_POWER:攻击对象, MUSHI_HP:对象NUM)
	CALL MUSHI_FLAGSETTING(MUSHI_STAND:攻击对象, 对象NUM)
ENDIF

;◆◆ここで一度メッセージ表示◆◆
CALL MUSHI_BATTLE_MSG

VARSET LOCAL
;KO判定
IF MUSHI_HP:对象NUM <= 0
	CALL MUSHI_BATTLE_CHECK_KO(ARG, NUM)
	SIF MUSHI_FINISH
		RETURN
ELSEIF 攻击スキル != "" && STRCOUNT(MUSHI_SKL:对象NUM, "太古の力")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》の『太古の力』发动！！")
	MUSHI_MSGCL:RESULT = C_CREAM
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》不受%攻击スキル%效果的影响！")
	CALL MUSHI_BATTLE_MSG
ELSE
	;ダメージ后处理
	SELECTCASE 攻击スキル
	CASE "毒攻击"
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》中<毒>了！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻击对象 += "<毒>"
	CASE "猛毒攻击"
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》中<猛毒>了！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻击对象 += "<猛毒>"
	CASE "麻痹攻击"
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》被<麻痹>了！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻击对象 += "<麻痹>"
	CASE "睡眠粉"
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》落入<睡眠>中了！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻击对象 += "<睡眠>"
	CASE "音波攻击"
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》<混乱>了！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻击对象 += "<混乱>"
	CASE "粘液攻击"
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》陷入了<缓慢>状态！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻击对象 += "<缓慢>"
	CASE "恶臭攻击"
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》陷入了<恶臭>状态！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		MUSHI_STATUS:攻击对象 += "<恶臭>"
	CASE "诅咒攻击"
		;现在HPの1/2(切り上げ)ダメージを与える
		LOCAL = MUSHI_HP:对象NUM / 2 + 1
		CALL MUSHI_DAMAGE(对象NUM, LOCAL)
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》因诅咒遭受了 {LOCAL} 的伤害！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
	ENDSELECT
	IF 攻击スキル != ""
		;状态变化を考虑してステータス再设定
		CALL MUSHI_PALAM(MUSHI_STAND:攻击对象, MUSHI_POWER:攻击对象, MUSHI_HP:对象NUM)
		CALL MUSHI_FLAGSETTING(MUSHI_STAND:攻击对象, 对象NUM)
		CALL MUSHI_BATTLE_MSG
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "蝴蝶效应") && スキル判定(5, ARG, NUM)
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『蝴蝶效应』发动！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	CALL MUSHI_MSGSET(@"蝴蝶的翅膀扇动引起了竜卷风！！")
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		IF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) > 1
			LOCAL:1 = MUSHI_HP:(MUSHI_STANDNUM:LOCAL) / 2
			CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LOCAL, LOCAL:1)
			CALL MUSHI_MSGSET(@"【%BATTLERNAME:LOCAL%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》受到了 {LOCAL:1} 的伤害！")
			MUSHI_MSGCL:RESULT = C_ORANGE
		ENDIF
	NEXT
	;致命伤はうけない（MSGだけでいい）けど一応
	CALL MUSHI_BATTLE_CHECK_KO(ARG, NUM)
	SIF MUSHI_FINISH
		RETURN
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "骚音攻击") && MUSHI_CHARANUM > 2
	LOCAL:1 = 0
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		;やられてる
		SIF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) <= 0
			CONTINUE
		;败北してる
		SIF !MUSHI_REST:LOCAL
			CONTINUE
		;自身
		SIF LOCAL == ARG
			CONTINUE
		;攻击对象は除外
		SIF LOCAL == 攻击对象
			CONTINUE
		;仲间
		SIF MUSHI_TEAM && (LOCAL % 2) == (ARG % 2)
			CONTINUE
		;一度でもダメージを与えるなら表示する
		IF !LOCAL:1
			CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『骚音攻击』发动！")
			MUSHI_MSGCL:RESULT = C_L_GREEN
			CALL MUSHI_MSGSET(@"对其他所有敌人造成伤害！")
			LOCAL:1 = 1
		ENDIF
		CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LOCAL, 与えるダメージ)
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:LOCAL%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》受到了 {与えるダメージ} 的伤害！")
		MUSHI_MSGCL:RESULT = C_ORANGE
	NEXT
	IF LOCAL:1
		CALL MUSHI_BATTLE_CHECK_KO(ARG, NUM)
		SIF MUSHI_FINISH
			RETURN
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:对象NUM, "发光")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:攻击对象%】《%MUSHI_NAME:对象NUM%》の『发光』发动！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》<混乱>了！")
	MUSHI_MSGCL:RESULT = C_P_PURPLE
	MUSHI_STATUS:ARG += "<混乱>"
	CALL MUSHI_BATTLE_MSG
ENDIF
IF STRCOUNT(MUSHI_STATUS:ARG, "<混乱>")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》在<混乱>中！")
	MUSHI_MSGCL:RESULT = C_P_PURPLE
	CALL MUSHI_DAMAGE(NUM, 与えるダメージ / 2)
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》受到了 {与えるダメージ / 2} 的伤害！")
	MUSHI_MSGCL:RESULT = C_ORANGE
	CALL MUSHI_BATTLE_CHECK_KO
	SIF MUSHI_FINISH
		RETURN
ENDIF
VARSET LOCAL
IF STRCOUNT(MUSHI_SKL:NUM, "怪诞攻击")
	CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『怪诞攻击』发动！")
	MUSHI_MSGCL:RESULT = C_RED
	CALL MUSHI_MSGSET(@"毛骨悚然的挙动对全员造成了精神伤害！！")
	MUSHI_MSGCL:RESULT = C_RED
	CALL MUSHI_BATTLE_MSG
	FOR LOCAL, 1, MUSHI_CHARANUM + 1
		SIF MUSHI_MENTAL:LOCAL && MUSHI_REST:LOCAL
			MUSHI_MENTAL:LOCAL = MAX(MUSHI_MENTAL:LOCAL - 与えるダメージ, 0)
		IF !MUSHI_MENTAL:LOCAL && MUSHI_REST:LOCAL
			CALL MUSHI_MSGSET(@"【%BATTLERNAME:LOCAL%】无法忍耐眼前的惨状而弃权了！")
			MUSHI_MSGCL:RESULT = C_RED
			LOCAL:1 = 1
		ENDIF
	NEXT
	IF LOCAL:1
		CALL MUSHI_BATTLE_CHECK_DO
		SIF MUSHI_FINISH
			RETURN
	ENDIF
ENDIF

;-------------------------------------------------
;斗虫：スキル发动判定函数
;ARG  プレイヤー番号
;NUM  フラグ番号
;-------------------------------------------------
@スキル判定(概率, ARG, NUM)
#FUNCTION
#DIM 概率
#DIM NUM
SIF STRCOUNT(MUSHI_STATUS:ARG, "<得意地形>")
	TIMES 概率, 1.5
SIF STRCOUNT(MUSHI_SKL:NUM, "猛攻")
	TIMES 概率, 1.5
SIF GET_MUSHIDATA(MUSHI_SELECT:NUM, "Ｘ型")
	TIMES 概率, 1.5
SIF STRCOUNT(MUSHI_STATUS:ARG, "<麻痹>")
	概率 = 0

RETURNF RAND:100 < 概率

;-------------------------------------------------
;斗虫：ダメージ处理函数
;-------------------------------------------------
@MUSHI_DAMAGE(ARG, ARG:1)
MUSHI_HP:ARG = MAX(MUSHI_HP:ARG - ARG:1, 0)

;-------------------------------------------------
;斗虫：Ｋ．Ｏ．の判定函数
;ARG    攻击プレイヤー番号
;NUM    攻击プレイヤーの战场に出しているムシのフラグ番号
;-------------------------------------------------
@MUSHI_BATTLE_CHECK_KO(ARG, NUM)
#DIM NUM
FOR LOCAL, 1, MUSHI_CHARANUM + 1
	;すでにやられてる
	IF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) < 0
		CONTINUE
	;やられた（HP=0）
	ELSEIF !MUSHI_HP:(MUSHI_STANDNUM:LOCAL)
		IF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LOCAL), "硬撑") && スキル判定(15, LOCAL, MUSHI_STANDNUM:LOCAL)
			CALL MUSHI_MSGSET(@"【%BATTLERNAME:LOCAL%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》の『硬撑』发动！！")
			MUSHI_MSGCL:RESULT = C_YELLOW
			MUSHI_HP:(MUSHI_STANDNUM:LOCAL) = 1
		ELSE
			CALL MUSHI_MSGSET(@"【%BATTLERNAME:LOCAL%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》倒下了！！")
			MUSHI_MSGCL:RESULT = C_RED
			;倒れたらHP=-1にしておく
			MUSHI_HP:(MUSHI_STANDNUM:LOCAL) = -1
			CLEARBIT MUSHI_REST:LOCAL, MUSHI_STANDNUM:LOCAL % 10
			;＠倒れた时口上
			CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 残りムシ数(LOCAL), MUSHI_BATTLER:LOCAL, LOCAL, 0, "倒れた时")
			;[高扬]の效果
			IF STRCOUNT(MUSHI_SKL:NUM, "高扬") && !STRCOUNT(MUSHI_STATUS:ARG, "<高扬>")
				CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】《%MUSHI_NAME:NUM%》の『高扬』发动！")
				MUSHI_MSGCL:RESULT = C_L_GREEN
				CALL MUSHI_MSGSET(@"因敌人倒下而进入了<高扬>状态！！")
				MUSHI_STATUS:ARG += "<高扬>"
				CALL MUSHI_PALAM(MUSHI_STAND:ARG, MUSHI_POWER:ARG, MUSHI_HP:NUM)
				CALL MUSHI_FLAGSETTING(MUSHI_STAND:ARG, NUM)
			ENDIF
		ENDIF
	;[逆袭]の效果
	ELSEIF MUSHI_HP:(MUSHI_STANDNUM:LOCAL) <= MUSHI_MAXHP:(MUSHI_STANDNUM:LOCAL) / 2 && STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LOCAL), "逆袭") && !STRCOUNT(MUSHI_STATUS:LOCAL, "<逆袭>")
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:LOCAL%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》の『逆袭』发动！")
		MUSHI_MSGCL:RESULT = C_ORANGE
		CALL MUSHI_MSGSET(@"受到攻击后变为了<逆袭>状态！！")
		MUSHI_STATUS:LOCAL += "<逆袭>"
		CALL MUSHI_PALAM(MUSHI_STAND:LOCAL, MUSHI_POWER:LOCAL, MUSHI_HP:(MUSHI_STANDNUM:LOCAL))
		CALL MUSHI_FLAGSETTING(MUSHI_STAND:LOCAL, MUSHI_STANDNUM:LOCAL)
	ENDIF
NEXT
CALL MUSHI_BATTLE_CHECK_DO
;-------------------------------------------------
;斗虫：ドロップアウト判定とそれに续くゲームセット判定の函数
;LOCAL:2     ドロップアウトしたプレイヤーの顺位
;-------------------------------------------------
@MUSHI_BATTLE_CHECK_DO
#DIM LPCOUNT
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	;MENTAL=-1は除外
	IF (!MUSHI_REST:LPCOUNT || !MUSHI_MENTAL:LPCOUNT) && MUSHI_MENTAL:LPCOUNT >= 0
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:LPCOUNT%】败退！！")
		MUSHI_MSGCL:RESULT = C_RED
		;＠败退时口上
		CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 残りムシ数(LPCOUNT), MUSHI_BATTLER:LPCOUNT, LPCOUNT, 0, "败退时")
		;败退したらMENTAL=-1にしておく
		MUSHI_MENTAL:LPCOUNT = -1
		MUSHI_REST:LPCOUNT = 0
		;顺位の设定,从后面将チェックしていく
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			LOCAL:1 = MUSHI_CHARANUM + 1 - LOCAL
			IF !MUSHI_FINISH:(LOCAL:1)
				MUSHI_FINISH:(LOCAL:1) = LPCOUNT
				LOCAL:2 = LOCAL:1
				BREAK
			ENDIF
		NEXT
	ENDIF
NEXT
;◆◆◆ここでKOおよび败退メッセージ表示◆◆
CALL MUSHI_BATTLE_MSG
;引き分け时の判定
IF LOCAL:2 == 1
	SETBIT MUSHI_REST:(MUSHI_FINISH:1), 3
	CALL MUSHI_MSGSET(@"什么、【%BATTLERNAME:(MUSHI_FINISH:1)%】《%MUSHI_NAME:(MUSHI_STANDNUM:(MUSHI_FINISH:1))%》用最后的力量站了起来！！")
	MUSHI_MSGCL:RESULT = C_YELLOW
	CALL MUSHI_BATTLE_MSG
	LOCAL:2 = 2
ENDIF
;チーム战
IF MUSHI_TEAM
	;奇数か偶数どっちかが全灭したらゲームセット
	IF (!MUSHI_REST:1 && !MUSHI_REST:3 && !MUSHI_REST:5 && !MUSHI_REST:7) || (!MUSHI_REST:2 && !MUSHI_REST:4 && !MUSHI_REST:6 && !MUSHI_REST:8)
		VARSET MUSHI_FINISH
		;めんどいので手捏ね
		IF (!MUSHI_REST:1 && !MUSHI_REST:3 && !MUSHI_REST:5 && !MUSHI_REST:7)
			MUSHI_FINISH:1 = 2
			MUSHI_FINISH:2 = 4
			MUSHI_FINISH:3 = 6
			MUSHI_FINISH:4 = 8
		ELSE
			MUSHI_FINISH:1 = 1
			MUSHI_FINISH:2 = 3
			MUSHI_FINISH:3 = 5
			MUSHI_FINISH:4 = 7
		ENDIF
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			IF MUSHI_REST:LOCAL
				CALL MUSHI_MSGSET(@"%" " * (24 - STRLENS(BATTLERNAME:LOCAL))%☆☆☆胜者　团队【%BATTLERNAME:LOCAL%】☆☆☆")
				MUSHI_MSGCL:RESULT = C_YELLOW
				MUSHI_FINISH:0 = LOCAL
				BREAK
			ENDIF
		NEXT
		;◆◆◆ゲームセットメッセージ表示◆◆
		CALL MUSHI_BATTLE_MSG
	ENDIF
;个人战
ELSE
	;2位が决まったらゲームセット
	IF LOCAL:2 == 2
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			IF MUSHI_REST:LOCAL
				CALL MUSHI_MSGSET(@"%" " * (30 - STRLENS(BATTLERNAME:LOCAL))%☆☆☆胜者　【%BATTLERNAME:LOCAL%】☆☆☆")
				MUSHI_MSGCL:RESULT = C_YELLOW
				MUSHI_FINISH:1 = LOCAL
				MUSHI_FINISH:0 = LOCAL
				BREAK
			ENDIF
		NEXT
		;◆◆◆ゲームセットメッセージ表示◆◆
		CALL MUSHI_BATTLE_MSG
	ENDIF
ENDIF

;-------------------------------------------------
;斗虫：ターン结束时处理
;-------------------------------------------------
@MUSHI_BATTLE_ENDSTEP
#DIM LPCOUNT
#DIM ダメージ
;[流砂]の效果（全员分）
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	IF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LPCOUNT), "流砂") && NAME_FIELD_TYPE(MUSHI_FIELD:99) == "[砂地]"
		FOR LOCAL, 1, MUSHI_CHARANUM + 1
			;やられてる
			SIF MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) <= 0
				CONTINUE
			;自身
			SIF LOCAL == LPCOUNT
				CONTINUE
			;仲间
			SIF MUSHI_TEAM && (LOCAL % 2) == (LPCOUNT % 2)
				CONTINUE
			;一度でもダメージを与えるなら表示する
			IF !LOCAL:1
				CALL MUSHI_MSGSET(@"【%BATTLERNAME:LPCOUNT%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》の『流砂』发动！")
				MUSHI_MSGCL:RESULT = C_CREAM
				CALL MUSHI_MSGSET(@"将全部敌人拖入了流砂！")
				LOCAL:1 = 1
			ENDIF
			ダメージ = MUSHI_MAXHP:(MUSHI_STANDNUM:LOCAL) / 4
			CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LOCAL, ダメージ)
			CALL MUSHI_MSGSET(@"【%BATTLERNAME:LOCAL%】《%MUSHI_NAME:(MUSHI_STANDNUM:LOCAL)%》受到了 {ダメージ} 的伤害！")
			MUSHI_MSGCL:RESULT = C_ORANGE
		NEXT
		IF LOCAL:1
			CALL MUSHI_BATTLE_CHECK_KO(LPCOUNT, MUSHI_STANDNUM:LPCOUNT)
			SIF MUSHI_FINISH
				RETURN
		ENDIF
	ENDIF
NEXT

;毒ダメージ处理,猛毒优先
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	IF STRCOUNT(MUSHI_STATUS:LPCOUNT, "<猛毒>") && MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) > 0
		ダメージ = MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) / 2
		CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LPCOUNT, ダメージ)
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:LPCOUNT%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》受到了猛毒的 {ダメージ} 伤害！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		LOCAL:1 = 1
	ELSEIF STRCOUNT(MUSHI_STATUS:LPCOUNT, "<毒>") && MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) > 0
		ダメージ = MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) / 4
		CALL MUSHI_DAMAGE(MUSHI_STANDNUM:LPCOUNT, ダメージ)
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:LPCOUNT%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》受到了毒的 {ダメージ} 伤害！")
		MUSHI_MSGCL:RESULT = C_P_PURPLE
		LOCAL:1 = 1
	ENDIF
NEXT
;ダメージが发生したらKO判定
IF LOCAL:1
	CALL MUSHI_BATTLE_CHECK_KO(LPCOUNT, MUSHI_STANDNUM:LPCOUNT)
	SIF MUSHI_FINISH
		RETURN
ENDIF

;睡眠が起きる处理
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	;概率1/2
	IF STRCOUNT(MUSHI_STATUS:LPCOUNT, "<睡眠>") && !RAND:2 && MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) > 0
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:LPCOUNT%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》从睡眠中醒来了！")
		MUSHI_MSGCL:RESULT = C_YELLOW
		MUSHI_STATUS:LPCOUNT'= REPLACE(MUSHI_STATUS:LPCOUNT, "<睡眠>", "")
		LOCAL:1 = 1
	ENDIF
NEXT
;起床が发生したらメッセージ表示
SIF LOCAL:1
	CALL MUSHI_BATTLE_MSG

;再生处理
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	IF STRCOUNT(MUSHI_SKL:(MUSHI_STANDNUM:LPCOUNT), "再生") && INRANGE(MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT), 1, MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) - 1)
		LOCAL = MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT) / 4
		MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) = MIN(MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) + LOCAL, MUSHI_MAXHP:(MUSHI_STANDNUM:LPCOUNT))
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:LPCOUNT%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》の『再生』发动！")
		MUSHI_MSGCL:RESULT = C_L_GREEN
		CALL MUSHI_MSGSET(@"【%BATTLERNAME:LPCOUNT%】《%MUSHI_NAME:(MUSHI_STANDNUM:LPCOUNT)%》恢复了 {LOCAL} ！")
		MUSHI_MSGCL:RESULT = C_L_GREEN
		LOCAL:1 = 1
	ENDIF
NEXT
;再生が发动したらメッセージ表示
SIF LOCAL:1
	CALL MUSHI_BATTLE_MSG

;入れ替え
VARSET LOCAL
FOR LPCOUNT, 1, MUSHI_CHARANUM + 1
	;败退済み
	SIF !MUSHI_REST:LPCOUNT
		CONTINUE
	IF MUSHI_HP:(MUSHI_STANDNUM:LPCOUNT) <= 0
		;你は手动
		IF MUSHI_BATTLER:LPCOUNT == MASTER
			CLEARLINE MUSHI_LINE
			CALL MUSHI_BATTLE_CHANGE(LPCOUNT)
		ELSE
			;角色は上から顺に出していく
			FOR LOCAL, 1, MUSHI_MAXSELECT + 1
				IF GETBIT(MUSHI_REST:LPCOUNT, LOCAL)
					CALL MUSHI_BATTLE_ETB(LPCOUNT, LPCOUNT * 10 + LOCAL)
					BREAK
				ENDIF
			NEXT
		ENDIF
		LOCAL:1 = 1
	ENDIF
NEXT
;入れ替えが发生したらメッセージ表示
SIF LOCAL:1
	CALL MUSHI_BATTLE_MSG

;-------------------------------------------------
;斗虫：MASTERの手动入れ替え函数
;战场表示とメッセージ表示の间にINPUTが入るので事前に战场表示を消しておき、
;メッセージ表示（ETB）の前に战场表示をする
;-------------------------------------------------
@MUSHI_BATTLE_CHANGE(ARG)
#DIM 选んだムシ
PRINTFORML ◆选择下一只虫子！
FOR LOCAL, 1, MUSHI_MAXSELECT + 1
	SIF !GETBIT(MUSHI_REST:ARG, LOCAL)
		SETCOLOR C_GRAY
	PRINTFORM [{LOCAL, 2}] 
	CALL SHOW_MUSHI_DATA(MUSHI_SELECT:(ARG * 10 + LOCAL), "一行")
	PRINTL 
	RESETCOLOR
NEXT
INPUT
IF INRANGE(RESULT, 1, MUSHI_MAXSELECT) && GETBIT(MUSHI_REST:ARG, RESULT)
	选んだムシ = RESULT
	CALL MUSHI_BATTLE_FIELD
	CALL MUSHI_BATTLE_ETB(ARG, ARG * 10 + 选んだムシ)
	MUSHI_LINE += 2 + MUSHI_MAXSELECT
	RETURN
ENDIF
CLEARLINE 2 + MUSHI_MAXSELECT
RESTART

;-------------------------------------------------
;斗虫：战场に出た时のスキル处理・能力值计算函数
;ARG  プレイヤー番号
;NUM  フラグ番号
;-------------------------------------------------
@MUSHI_BATTLE_ETB(ARG, NUM)
#DIM NUM
;STANDを设定
MUSHI_STAND:ARG = MUSHI_SELECT:NUM
MUSHI_STANDNUM:ARG = NUM
MUSHI_STATUS:ARG = 
MUSHI_NAME = %MUSHI_NAME:NUM%
CALL MUSHI_MSGSET(@"【%BATTLERNAME:ARG%】派《%MUSHI_NAME%》を进入了战场！")
;＠ETB口上
CALL KOJO_MESSAGE_SEND("MUSHI_BATTLE", 残りムシ数(ARG), MUSHI_BATTLER:ARG, ARG, 0, "战场に出した")

;フィールド判定
IF STRCOUNT(MUSHI_SKL:NUM, "浄化")
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》の[浄化]发动！")
	MUSHI_MSGCL:RESULT = C_AQUA
	CALL MUSHI_BATTLE_フィールド变化(7)
ELSEIF MUSHI_FIELD:NUM & MUSHI_FIELD:99
	MUSHI_STATUS:ARG += "<得意地形>"
ENDIF
;得意季节
IF STRCOUNT(MUSHI_SKL:NUM, "春の虫") && GET_MONTH() == "春之月"
	MUSHI_STATUS:ARG += "<得意季节>"
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "夏の虫") && GET_MONTH() == "夏之月"
	MUSHI_STATUS:ARG += "<得意季节>"
ELSEIF STRCOUNT(MUSHI_SKL:NUM, "秋の虫") && GET_MONTH() == "秋之月"
	MUSHI_STATUS:ARG += "<得意季节>"
ENDIF
;ETBスキル
IF 残りムシ数(ARG) == 1
	IF STRCOUNT(MUSHI_SKL:NUM, "大将")
		MUSHI_STATUS:ARG += "<大将>"
		CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》の[大将]发动！")
		MUSHI_MSGCL:RESULT = C_L_GREEN
		CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》因为是最后的一匹而充满活力！")
		MUSHI_MSGCL:RESULT = C_YELLOW
	ELSE
		CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》是最后的一匹！")
		MUSHI_MSGCL:RESULT = C_ORANGE
	ENDIF
ENDIF
IF STRCOUNT(MUSHI_SKL:NUM, "阳炎")
	MUSHI_STATUS:ARG += "<阳炎>"
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》の[阳炎]发动！")
	MUSHI_MSGCL:RESULT = C_L_GREEN
	CALL MUSHI_MSGSET(@"《%MUSHI_NAME%》身边环绕着热雾！")
	MUSHI_MSGCL:RESULT = C_CREAM
ENDIF
;能力值再设定,战斗HPは满タン
CALL MUSHI_PALAM(MUSHI_STAND:ARG, MUSHI_POWER:ARG, 0, MUSHI_STATUS:ARG)
CALL MUSHI_FLAGSETTING(MUSHI_STAND:ARG, NUM)

;-------------------------------------------------
;斗虫：フィールドタイプが变わった时の处理函数
;得意地形が变わってステータスも变わるので再设定する
;-------------------------------------------------
@MUSHI_BATTLE_フィールド变化(ARG)
#DIM NUM
MUSHI_FIELD:99 = 0
SETBIT MUSHI_FIELD:99, ARG
CALL MUSHI_MSGSET(@"场地变化为＜%NAME_FIELD_TYPE(MUSHI_FIELD:99)%＞了！")
MUSHI_MSGCL:RESULT = C_L_GREEN
;得意地形の再设定とそれによる能力值の再设定
FOR LOCAL, 1, MUSHI_CHARANUM + 1
	NUM = MUSHI_STANDNUM:LOCAL
	IF MUSHI_FIELD:NUM & MUSHI_FIELD:99
		MUSHI_STATUS:LOCAL += "<得意地形>"
	ELSEIF STRCOUNT(MUSHI_STATUS:LOCAL, "<得意地形>")
		MUSHI_STATUS:LOCAL '= REPLACE(MUSHI_STATUS:LOCAL, "<得意地形>", "")
	ENDIF
	CALL MUSHI_PALAM(MUSHI_STAND:LOCAL, MUSHI_POWER:LOCAL, MUSHI_HP:NUM)
	CALL MUSHI_FLAGSETTING(MUSHI_STAND:LOCAL, NUM)
NEXT
;-------------------------------------------------
;斗虫：プレイヤー番号ARGの残りムシ数を返す函数
;-------------------------------------------------
@残りムシ数(ARG)
#FUNCTION
LOCAL:1 = 0
FOR LOCAL, 1, MUSHI_MAXSELECT + 1
	SIF GETBIT(MUSHI_REST:ARG, LOCAL)
		LOCAL:1 ++
NEXT
RETURNF LOCAL:1
