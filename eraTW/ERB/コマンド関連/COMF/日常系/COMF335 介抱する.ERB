;-------------------------------------------------
;照料
;日常系指令、レベル1
;-------------------------------------------------
@COM335
SELECTCASE TFLAG:192
	;通常
	CASE 0
		;成否の判定
		LOCAL = RAND:100
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 99
			LOCAL:1 = 99
		IF LOCAL <= 9
			;10％で大成功
			TFLAG:193 = 1
			TFLAG:98 = 2
			LOCAL:2 = 6
			EXP:MASTER:会话经验 ++
		ELSEIF LOCAL >= LOCAL:1
			;10～1％で失败
			TFLAG:193 = -1
			TFLAG:98 = -1
			LOCAL:2 = -1
		ELSE
			;残りは成功
			TFLAG:193 = 0
			TFLAG:98 = 0
			LOCAL:2 = 2
		ENDIF
	;强制成功or大成功
	CASE 1
		IF RAND:100 <= 9
			TFLAG:193 = 1
			TFLAG:98 = 2
			LOCAL:2 = 6
			EXP:MASTER:会话经验 ++
		ELSE
			TFLAG:193 = 0
			TFLAG:98 = 0
			LOCAL:2 = 2
		ENDIF
	;强制失败
	CASE -1
		TFLAG:193 = -1
		TFLAG:98 = -1
		LOCAL:2 = -1
	;指令结束
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT

SIF RAND:2
	EXP:MASTER:会话经验 ++

;随机に(0～2)+(成功补正)%の酒气减少
LOCAL:3 = MAX(MAXBASE:酒气 * (RAND:3 + LOCAL:2) / 100, 0)
BASE:酒气 = MAX(BASE:酒气 - LOCAL:3, 0)

DOWNBASE:MASTER:体力 += 20
DOWNBASE:MASTER:气力 += 100

;所要时间
TIME += 30

DOWNBASE:气力 += 10

;信赖
TFLAG:98 += 10

;固定で获得するソース
SOURCE:欢乐 = 400

;ABL:顺从をみる
IF ABL:顺从 <= 1
	SOURCE:欢乐 += (ABL:顺从 * 50)
ELSEIF ABL:顺从 <= 3
	SOURCE:欢乐 += 100 + (ABL:顺从 * 40)
ELSEIF ABL:顺从 <= 5
	SOURCE:欢乐 += 200 + (ABL:顺从 * 50)
ELSEIF ABL:顺从 <= 8
	SOURCE:欢乐 += 400 + (ABL:顺从 * 70)
ELSEIF ABL:顺从 <= 11
	SOURCE:欢乐 += 500 + (ABL:顺从 * 60)
ELSE
	SOURCE:欢乐 += 800 + (ABL:顺从 * 40)
ENDIF

;ABL:亲密チェック
IF ABL:亲密 <= 1
	SOURCE:欢乐 += (ABL:亲密 * 50)
ELSEIF ABL:亲密 <= 3
	SOURCE:欢乐 += 80 + (ABL:亲密 * 50)
ELSEIF ABL:亲密 <= 5
	SOURCE:欢乐 += 200 + (ABL:亲密 * 50)
ELSEIF ABL:亲密 <= 8
	SOURCE:欢乐 += 500 + (ABL:亲密 * 40)
ELSEIF ABL:亲密 <= 11
	SOURCE:欢乐 += 800 + (ABL:亲密 * 40)
ELSE
	SOURCE:欢乐 += 1200 + (ABL:亲密 * 30)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:欢乐 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:欢乐 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:欢乐 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF

SIF SOURCE:欢乐 < 0
	SOURCE:欢乐 = 0

SOURCE:被动 = 200 + 200 * ABL:顺从
SOURCE:征服 = 40 + 40 * ABL:施虐属性
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE335
;照料实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(335)
	RETURN RESULT
;仕事内容が醉いつぶれ以外
SIF CFLAG:职业种类 != JOB_醉いつぶれる
	RETURN 0
;气力0
SIF BASE:MASTER:气力 <= 0
	RETURN 0
;时间停止中
SIF FLAG:70
	RETURN 0
RETURN 1
