;-------------------------------------------------
;掀起裙子
;日常系指令、レベル1
;-------------------------------------------------
@COM330
#DIM MEKURI
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 10
ELSE
	;固定で获得するソース
	SOURCE:露出 = 300
	SOURCE:反感 = 700
	IF FLAG:异常气象 == 6
		MEKURI = MAX(0,5000 - 500 * ABL:亲密)
		SELECTCASE MARK:反抗刻印
			CASE 0
				MEKURI /= 2
			CASE 2
				MEKURI *= 2
			CASE 3
				MEKURI *= 4
		ENDSELECT
		[IF_DEBUG]
		PRINTFORMW 掀裙子{MEKURI}
		[ENDIF]
		CUP:快Ｖ += MEKURI
	ENDIF
	;好感度をみる
	SELECTCASE CFLAG:2
		CASE 1000 TO 3000
			SOURCE:情爱 += CFLAG:2 / 100
			SOURCE:反感 -= (ABL:亲密 * 10)
		CASE 3001 TO 5000
			SOURCE:情爱 += 100 + (CFLAG:2 - 500) / 5
			SOURCE:欢乐 += 100 + (CFLAG:2 - 500) / 4
			SOURCE:情欲 += (ABL:亲密 * 10)
			SOURCE:露出 += (ABL:亲密 * 15)
			SOURCE:反感 -= (ABL:亲密 * 15)
		CASE IS > 5000
			SOURCE:情爱 += 500 + (CFLAG:2 - 5000) / 7
			SOURCE:欢乐 += 500 + (CFLAG:2 - 5000) / 4
			IF !TALENT:恋慕
				SOURCE:情欲 += (ABL:亲密 * 15)
				SOURCE:露出 += (ABL:亲密 * 20)
				SOURCE:反感 -= (ABL:亲密 * 25)
			ELSE
				SOURCE:情欲 += (ABL:亲密 * 20)
				SOURCE:露出 += (ABL:亲密 * 30)
				SOURCE:反感 -= (ABL:亲密 * 40)
			ENDIF
	ENDSELECT
	SIF SOURCE:反感 < 0
		SOURCE:反感 = 0
	SIF SOURCE:情爱 < 0
		SOURCE:情爱 = 0

	LOCAL = 0
	;好感度が低いと好感度低下
	IF CFLAG:2 <= 100
		TFLAG:193 = -5
		DOWNBASE:MASTER:体力 -= ABL:战斗能力 * 100
		TFLAG:掀裙子すぎ += RAND:30
	ELSEIF CFLAG:2 <= 1000
		TFLAG:193 = -3
		DOWNBASE:MASTER:体力 -= ABL:战斗能力 * 30
		TFLAG:掀裙子すぎ += RAND:20
	ELSEIF CFLAG:2 <= 3000
		TFLAG:193 = -1
		TFLAG:掀裙子すぎ += RAND:10
	ELSEIF CFLAG:2 <= 5000
		TFLAG:193 = 0
		TFLAG:掀裙子すぎ += RAND:10
	ELSE
		TFLAG:193 = 1
		TFLAG:掀裙子すぎ += RAND:5
	ENDIF

	SOURCE:被动 = 100 + 150 * ABL:顺从
	SOURCE:征服 = 100 + 150 * ABL:施虐属性

	;二人きりだと效果UP、そうでないと露出以外ダウン=============
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:LOCAL:同室
			LOCAL:2 ++
	NEXT
	IF LOCAL:2 == 1
		TIMES SOURCE:情欲 , 1.20
		TIMES SOURCE:露出 , 0.80
		LOCAL:3 = 1
	ELSE
		TIMES SOURCE:情欲 , 0.50
		TIMES SOURCE:露出 , 2.00
		TIMES SOURCE:反感 , 1.50
	ENDIF
	;情绪の影响=====================================================
	IF BASE:情绪 == MAXBASE:情绪
		TIMES SOURCE:情欲 , 1.20
		TIMES SOURCE:露出 , 1.20
		TIMES SOURCE:反感 , 0.50
	ELSEIF BASE:情绪 < 600 && BASE:情绪
		TIMES SOURCE:情欲 , 1.00
		TIMES SOURCE:露出 , 1.00
		TIMES SOURCE:反感 , 1.00
	ELSEIF BASE:情绪 <= 200
		TIMES SOURCE:情欲 , 0.80
		TIMES SOURCE:露出 , 0.80
		TIMES SOURCE:反感 , 1.50
	ENDIF

	TIME += 5
	IF CHK_DATENOW(CFLAG:TARGET:约会中)
		EXP:MASTER:约会经验 ++
		EXP:TARGET:约会经验 ++
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE330
;掀裙子实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(330)
	RETURN RESULT
;时间停止中はスキップ
IF !FLAG:70
	;睡眠中
	SIF CFLAG:睡眠
		RETURN 0
	;仕事中
	SIF CFLAG:行动 == 5 && !(TARGET == [[美宵]])
		RETURN 0
	;约会中
	SIF CHK_DATENOW(CFLAG:约会中) && CFLAG:当前位置 != OMANEKIBEYA()
		RETURN 0
	;陪睡中
	SIF CFLAG:陪睡中
		RETURN 0
	;甲斐性无しにそんな度胸はない
	SIF FLAG:甲斐性无
		RETURN 0
ENDIF
;服装制限
SIF !(EQUIP:裙子 || EQUIP:连衣裙 || EQUIP:和服 || EQUIP:裤子) || EQUIP:0 == 0
	RETURN 0
;气力0
SIF BASE:MASTER:气力 <= 0
	RETURN 0
SIF !TARGET
	RETURN 0
;被逆推中の制御
SIF GETBIT (TCVAR:26,7)
	RETURN 0
RETURN 1

