;-------------------------------------------------
;物色路人女子
;日常系指令、レベル1
;-------------------------------------------------

@COM444
#DIMS YUKIZURI_RACE  ;生成するMOB娘の种类

IF FLAG:约会的对象
	PRINTFORMW 身边已有一位非常可爱的女孩
	RETURN -1
ENDIF

;潜伏モード中は不可
IF GETBIT(FLAG:潜伏,0)
	PRINTFORMW 现在不是将目光转移到其他人身上的时候
	RETURN -1
ENDIF

$BUSSYOKU
IF !FLAG:70
	TIME += 3
ELSE
	BASE:MASTER:TSP -= 10
ENDIF
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%物色着周围是否有可爱的女孩
PRINTFORMW ……那个女孩怎么样？
SETBIT TFLAG:一日一回, 9
SELECTCASE CAN_NANPA(CFLAG:MASTER:当前位置)
	CASE 1
		YUKIZURI_RACE '= TEXTR("町娘/人妻")
	CASE 2
		YUKIZURI_RACE = 妖精
	CASE 3
		YUKIZURI_RACE = 女仆妖精
	CASE 4
		;３日に一度贩卖员が出没
		IF ((GETTIME()/1000000000) % 100) % 3 == 1 && !RAND:7
			YUKIZURI_RACE = 贩卖员
		ELSE
			YUKIZURI_RACE = 因幡
		ENDIF
	CASE 5
		YUKIZURI_RACE = 僵尸妖精
	CASE 6
		YUKIZURI_RACE '= TEXTR("妖怪狐/妖怪猫")
	CASE 7
		YUKIZURI_RACE '= TEXTR("山童/河童")
	CASE 8
		YUKIZURI_RACE '= TEXTR("白狼天狗/鸦天狗")
	CASE 9
		IF !RAND:10
			YUKIZURI_RACE = 鬼
		ELSE
			YUKIZURI_RACE = 僵尸妖精
		ENDIF
	CASE 10
		YUKIZURI_RACE = 玉兔
	CASE 11
		YUKIZURI_RACE '= TEXTR("妖怪狸/茨狸")
	CASE 12
		IF MORE_RANDOM_CHARACTER == 1
			YUKIZURI_RACE  = 学生
			SIF !RAND:50
				YUKIZURI_RACE = 学生​
			SIF !RAND:100
				YUKIZURI_RACE = ​学生​
		ENDIF
ENDSELECT



;PRINTFORMW %YUKIZURI_RACE%
CALL MAKE_YUKIZURI(RANDOM_CHARANUM, YUKIZURI_RACE)
TEQUIP:RANDOM_CHARANUM:上半身着衣状况 = 1
$CHOICE
CALL CHARA_STATE(RANDOM_CHARANUM)
IF CSTR:RANDOM_CHARANUM:处女丧失履历 != ""
	DRAWLINE
	CALL COLORMESSAGE(@"处女丧失履历：%CSTR:RANDOM_CHARANUM:处女丧失履历%",C_PINK,1)
	PRINTFORML 
ENDIF
DEBUGPRINTFORML %CSTR:RANDOM_CHARANUM:路人子画像文字列_服_通常%
CALL ASK_M("很中意",1,"寻找别的女孩",1,"寻找之前的女孩", TFLAG:直前路人子Map == CFLAG:MASTER:当前位置 /100,"寻找以前认识的女孩",1,"物色中止",1)
IF RESULT == 4
	CFLAG:RANDOM_CHARANUM:当前位置 = SUKIMA()
	CFLAG:RANDOM_CHARANUM:更衣实行 = 0
	CFLAG:RANDOM_CHARANUM:出禁 = 2
	SIF TARGET == RANDOM_CHARANUM
		TARGET = 0
	RETURN -1
ENDIF
IF RESULT == 1
	TFLAG:直前路人子Map = CFLAG:MASTER:当前位置 /100
	SAVECHARA "TEMP_MOB", "刚刚遇到的女孩子", RANDOM_CHARANUM
	GOTO BUSSYOKU
ELSEIF RESULT == 3
	CALL LOAD_MOBGIRL(0)
	IF RESULT == -1
		;念のためまっさらにしておく
		CALL 路人子画像リセット(RANDOM_CHARANUM)
		DELCHARA RANDOM_CHARANUM
		ADDCHARA RANDOM_CHARANUM
		RETURN -1
	ENDIF
	CFLAG:RANDOM_CHARANUM:出禁 = 0
ELSEIF RESULT == 2
	CALL 路人子画像リセット(RANDOM_CHARANUM)
	DELCHARA RANDOM_CHARANUM
	LOADCHARA "TEMP_MOB"
	TFLAG:直前路人子Map = 0
	GOTO CHOICE
ENDIF
CFLAG:RANDOM_CHARANUM:当前位置 = CFLAG:MASTER:当前位置
CFLAG:RANDOM_CHARANUM:约会中 = MAIN_MAP

IF CFLAG:MASTER:当前位置 / 100 == MAIN_MAP
	;据点内で时间停止せずに物色した场合は、同行扱い
	IF !FLAG:70
		CFLAG:RANDOM_CHARANUM:同行 = 180
		CFLAG:MASTER:同行 = 180
	ENDIF
	CFLAG:RANDOM_CHARANUM:据点来访 = 1
	CFLAG:RANDOM_CHARANUM:遭遇位置 = 0
	;CFLAG:RANDOM_CHARANUM:自宅位置 = 0
	CFLAG:RANDOM_CHARANUM:来访时间 = CSVCFLAG(RANDOM_CHARANUM,352)
	CFLAG:RANDOM_CHARANUM:回家时间 = CSVCFLAG(RANDOM_CHARANUM,353)
	CFLAG:RANDOM_CHARANUM:就寝时间 = CSVCFLAG(RANDOM_CHARANUM,354)
	CFLAG:RANDOM_CHARANUM:起床时间 = CSVCFLAG(RANDOM_CHARANUM,355)
ELSE
	;引っ越ししたりして定义しなおす场合もあるので、每回确认
	CFLAG:RANDOM_CHARANUM:据点来访 = 0
	CFLAG:RANDOM_CHARANUM:遭遇位置 = CFLAG:MASTER:当前位置
	;CFLAG:RANDOM_CHARANUM:自宅位置 = CFLAG:MASTER:当前位置
	CFLAG:RANDOM_CHARANUM:来访时间 = 0
	CFLAG:RANDOM_CHARANUM:回家时间 = 0
	CFLAG:RANDOM_CHARANUM:就寝时间 = 0
	CFLAG:RANDOM_CHARANUM:起床时间 = 0
ENDIF
CFLAG:RANDOM_CHARANUM:延迟 = 30
TARGET = RANDOM_CHARANUM
RETURN 1


@COM_ABLE444
;批量管理
SIF GLOBAL_COMABLE(622)
	RETURN RESULT
SIF !CAN_NANPA(CFLAG:MASTER:当前位置)
	RETURN 0
SIF !INRANGE(TIME,540,1140)
	RETURN 0
;SIF !CFLAG:RANDOM_CHARANUM:出禁
;	RETURN 0
RETURN 1

@CAN_NANPA(ARG)
#FUNCTION

SIF ARG == GET_MAP_REPLACEMENT(202)
	RETURNF 1
SIF ARG == GET_MAP_REPLACEMENT(438)
	RETURNF 2
SIF ARG == GET_MAP_REPLACEMENT(309)
	RETURNF 3
SIF ARG == GET_MAP_REPLACEMENT(415)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(416)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(431)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(434)
	RETURNF 4
SIF ARG == GET_MAP_REPLACEMENT(927)
	RETURNF 5
SIF ARG == GET_MAP_REPLACEMENT(928)
	RETURNF 5
SIF ARG == GET_MAP_REPLACEMENT(601)
	RETURNF 6
SIF ARG == GET_MAP_REPLACEMENT(704)
	RETURNF 7
SIF ARG == GET_MAP_REPLACEMENT(806)
	RETURNF 8
SIF ARG == GET_MAP_REPLACEMENT(938)
	RETURNF 9
SIF ARG == GET_MAP_REPLACEMENT(1003)
	RETURNF 10
SIF ARG == GET_MAP_REPLACEMENT(540)
	RETURNF 11
SIF MORE_RANDOM_CHARACTER == 1 && ARG == GET_MAP_REPLACEMENT(222)
	RETURNF 12
RETURNF 0


@PRINT_SAVED_GIRL_LIST
DRAWLINE
FOR LOCAL,0,MAXMOB
	IF FIND_CHARADATA(TOSTR(LOCAL))
		CHKCHARADATA RESULTS:0
		SIF MOBGIRL_PLACE:LOCAL == CFLAG:MASTER:当前位置 / 100
			SETCOLOR C_P_GREEN
		SIF MOBGIRL_PLACE:LOCAL == 90
			SETCOLOR C_PINK
		PRINTBUTTON @"[%RESULTS:0%]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[无]", LOCAL
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[放弃]", 99

@LOAD_MOBGIRL(ARG)
CALL PRINT_SAVED_GIRL_LIST
INPUT
LOCAL = RESULT
SIF !INRANGE(LOCAL, 0, MAXMOB -1)
	RETURN -1
IF !ARG && MOBGIRL_PLACE:LOCAL != CFLAG:MASTER:当前位置 / 100
	IF MOBGIRL_PLACE:LOCAL == 90
		PRINTFORMW 在店外是遇不到的…
	ELSE
		;魔改召唤路人女子
		;PRINTFORML {MOBGIRL_PLACE:LOCAL}
		;PRINTFORMW 不在这里吧
		PRINTFORMW 不在这里吧，是否消耗1000TPS进行召唤传送？
		PRINTBUTTON "[是]", 0
		PRINTBUTTON "[否]", 1
		INPUT
		IF RESULT == 0 && BASE:MASTER:TSP >= 1000
			BASE:MASTER:TSP -= 1000
			CALL 路人子画像リセット(RANDOM_CHARANUM)
			DELCHARA RANDOM_CHARANUM
			LOADCHARA TOSTR(LOCAL)
			PRINTFORML 召唤传送来了%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%。
			RETURN 1
		ELSEIF RESULT != 0
			;非0不执行任何操作
		ELSE
			PRINTW TSP不足，传送失败！
		ENDIF
	ENDIF
	RETURN -1
ELSEIF ARG && MOBGIRL_PLACE:LOCAL != 90
	PRINTFORMW 「哈！？　这样的雌性、我们这里没有呀！」
	PRINTFORML 
	RETURN -1
ENDIF
IF FIND_CHARADATA(TOSTR(LOCAL))
	CALL 路人子画像リセット(RANDOM_CHARANUM)
	DELCHARA RANDOM_CHARANUM
	LOADCHARA TOSTR(LOCAL)
	PRINTFORML \@ ARG ? 指名了 # 发现了 \@%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%
	IF MOBGIRL_FP:LOCAL != ""
		路人子一人称 = %MOBGIRL_FP:LOCAL%
		路人子二人称 = %MOBGIRL_SP:LOCAL%
	ENDIF
	IF CSTR:RANDOM_CHARANUM:路人子画像文字列_服_通常 == ""
		PRINTFORML 未设定外表。请选择类型。
		PRINTBUTTON "[町娘] ", "町娘"
		PRINTBUTTON "[人妻] ", "人妻"
		PRINTBUTTON "[妖精] ", "妖精"
		PRINTBUTTON "[女仆妖精] ", "女仆妖精"
		PRINTBUTTON "[僵尸妖精] ", "僵尸妖精"
		PRINTBUTTON "[河童] ", "河童"
		PRINTBUTTON "[白狼天狗] ", "白狼天狗"
		PRINTBUTTON "[因幡] ", "因幡"
		PRINTBUTTON "[玉兔] ", "玉兔"
		PRINTBUTTON "[鬼] ", "鬼"
		PRINTBUTTON "[妖怪狸] ", "妖怪狸"
		PRINTBUTTON "[妖怪狐] ", "妖怪狐"
		PRINTBUTTON "[妖怪猫] ", "妖怪猫"
		PRINTBUTTON "[随机] ", "随机"
		INPUTS
		CALL SET_MOB_IMAGE_STR(RANDOM_CHARANUM, RESULTS)
	ENDIF
	;过去に合成されて裸画像の笑颜に[装饰_照れ]が乘ってない场合に追记
	SIF STRFIND(CSTR:RANDOM_CHARANUM:路人子画像文字列_裸_笑颜, "装饰_照れ") == -1
		CSTR:RANDOM_CHARANUM:路人子画像文字列_裸_笑颜 += "[装饰9:装饰_照れ][乘色:朱色]"

	;过去（ver4.760以前）に保存されて种族のTALENT设定が适合しない场合に修正を行う
	SELECTCASE CAN_NANPA(CFLAG:MASTER:当前位置)
		;里
		CASE 1
			;以前の194 人类が现varでは194 幽灵になっている
			IF TALENT:RANDOM_CHARANUM:幽灵
				TALENT:RANDOM_CHARANUM:幽灵 = 0
				TALENT:RANDOM_CHARANUM:人类 = 1
			ENDIF
		;雾之湖、红魔馆、および地底の妖精たち
		CASE 2, 3, 5
			;以前の190 妖精が现verでは190 人类になっている
			IF TALENT:RANDOM_CHARANUM:人类
				TALENT:RANDOM_CHARANUM:人类 = 0
				TALENT:RANDOM_CHARANUM:妖精 = 1
			ENDIF
		;竹林
		CASE 4
			;因幡…妖兽が新设された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 6
				TALENT:RANDOM_CHARANUM:妖怪 = 6
		;冥界
		CASE 6
			;妖怪狸、狐、猫…妖兽が新设された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 6
				TALENT:RANDOM_CHARANUM:妖怪 = 6
		;妖怪之山山麓
		CASE 7
			;河童…河童が新设された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 4
				TALENT:RANDOM_CHARANUM:妖怪 = 4
		;妖怪之山山顶
		CASE 8
			;白狼天狗…以前の197 天狗が现verでは197 追加种族（巫女）になっている
			IF TALENT:RANDOM_CHARANUM:追加种族
				TALENT:RANDOM_CHARANUM:追加种族 = 0
				TALENT:RANDOM_CHARANUM:妖怪 = 5
			ENDIF
		;地底　鬼か妖精
		CASE 9
			;鬼…以前の195 鬼が现verでは195　付丧神になっている
			IF TALENT:RANDOM_CHARANUM:付丧神
				TALENT:RANDOM_CHARANUM:付丧神 = 0
				TALENT:RANDOM_CHARANUM:妖怪 = 2
			;妖精は同上
			ELSEIF TALENT:RANDOM_CHARANUM:人类
				TALENT:RANDOM_CHARANUM:人类 = 0
				TALENT:RANDOM_CHARANUM:妖精 = 1
			ENDIF
		;月　一応分けておく
		CASE 10
			;玉兔…妖兽が新设された
			SIF TALENT:RANDOM_CHARANUM:妖怪 != 6
				TALENT:RANDOM_CHARANUM:妖怪 = 6
	ENDSELECT

	IF FLAG:70	;时间停止中と风俗娘は反应无
	ELSEIF TALENT:RANDOM_CHARANUM:风俗娘
	ELSEIF CFLAG:RANDOM_CHARANUM:败犬 && (TALENT:RANDOM_CHARANUM:炮友 || TALENT:RANDOM_CHARANUM:思慕)
		PRINTFORMW 认出了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%，露出了懊恼又带着期待的表情，扭扭捏捏的…
	ELSEIF CFLAG:RANDOM_CHARANUM:败犬 && !TALENT:RANDOM_CHARANUM:恋慕 && !TALENT:RANDOM_CHARANUM:思慕 && !TALENT:RANDOM_CHARANUM:爱欲
		PRINTFORMW 发现了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%带着逞强的表情
	ELSEIF TALENT:RANDOM_CHARANUM:恋慕
		PRINTFORMW 认出了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%很开心地走了过来
	ELSEIF TALENT:RANDOM_CHARANUM:爱欲
		PRINTFORMW 认出了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%一看到你，就立刻过来，身体靠在你的胳膊上…
	ELSEIF ABL:RANDOM_CHARANUM:亲密 >= 5
		PRINTFORMW 发现了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%挥了挥手
	ENDIF
ELSE
	RETURN -1
ENDIF

@SAVE_MOBGIRL
#DIM CHOSEN_MOB
#DIMS MOBNAME

$LOOP
CALL PRINT_SAVED_GIRL_LIST
INPUT
SIF RESULT == 99
	RETURN -1
SIF !INRANGE(RESULT, 0, MAXMOB - 1)
	GOTO LOOP

CHOSEN_MOB = RESULT
IF FIND_CHARADATA(TOSTR(CHOSEN_MOB))
	CHKCHARADATA RESULTS:0
	STRFIND RESULTS:0, "：" 
	SUBSTRING RESULTS:0, 0, RESULT
	MOBNAME = %RESULTS:0%
	PRINTFORML %MOBNAME%的事忘掉,取而代之的是%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%的事吗？
	CALL ASK_YN
	IF !RESULT
		PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%干脆而薄情地忘掉了%MOBNAME%的事
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
MOBGIRL_PLACE:CHOSEN_MOB = CFLAG:RANDOM_CHARANUM:经常去的地方
SIF TALENT:RANDOM_CHARANUM:风俗娘
	MOBGIRL_PLACE:CHOSEN_MOB = 90
TALENT:RANDOM_CHARANUM:路人 = 2
CFLAG:RANDOM_CHARANUM:路人子セーブスロット = CHOSEN_MOB
MOBGIRL_FP:CHOSEN_MOB = %路人子一人称%
MOBGIRL_SP:CHOSEN_MOB = %路人子二人称%
SAVECHARA TOSTR(CHOSEN_MOB), CSTR:RANDOM_CHARANUM:路人子PROFILE１, RANDOM_CHARANUM
PRINTFORMW \@ TALENT:RANDOM_CHARANUM:风俗娘 ? 保持了对%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%的在意 # 记住了%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%的事 \@


;-------------------------------------------------
;路人子PROFILE２を更新する
;败犬でなければ、その日の过ごし方の样子でプロフィール２を追记
;败犬の场合は、陷落状态に合わせて更新
;-------------------------------------------------
@MOBGIRL_PROFILE2()
#DIM 回数

回数 = EXP:RANDOM_CHARANUM:Ｖ性交经验 + EXP:RANDOM_CHARANUM:Ａ性交经验

;败犬でない场合は、性的关系の进展で文本を构成
IF CFLAG:RANDOM_CHARANUM:败犬 == 0 
	;出会って日が浅い
	IF DAY:0 - PRINTDATEF_TO_DAY(CSTR:RANDOM_CHARANUM:路人子PROFILE１) <= 30
		CSTR:RANDOM_CHARANUM:路人子PROFILE２ = 刚认识的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
	ELSE
		CSTR:RANDOM_CHARANUM:路人子PROFILE２ = %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
	ENDIF
	;にゅーぷらん
	IF TALENT:RANDOM_CHARANUM:风俗娘
		SIF GROUPMATCH(TALENT:RANDOM_CHARANUM:风俗娘, 1, 2) && CFLAG:RANDOM_CHARANUM:1001 == 3
			TALENT:RANDOM_CHARANUM:风俗娘 += 2
		IF CFLAG:RANDOM_CHARANUM:1001  > 6
			LOCALS = 已经完全被情所困、甚至连工作和认真的区别都无法分清
		ELSEIF CFLAG:RANDOM_CHARANUM:1001  > 3
			LOCALS = 持有着顾客以上的感情、而且对于能够侍奉这件事感到了喜悦
		ELSE
			LOCALS = 因为被一个好顾客多次指名而十分开心
		ENDIF
	;允许无套
	ELSEIF CFLAG:RANDOM_CHARANUM:允许无套
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = 沉迷于恋爱、不顾丈夫和家庭、两人关系很亲密
		ELSE
			LOCALS = 作为雌性、本能地接受阴道内射精、甚至觉得生孩子也可以。
		ENDIF
	;合意ックス
	ELSEIF GETBIT(CFLAG:RANDOM_CHARANUM:既成事实 , 1) || TALENT:RANDOM_CHARANUM:合意误认
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = 似乎要把多余的情欲发泄出来
		ELSE
			LOCALS = 被吸引住了、原谅了自己的身体
		ENDIF
	;睡眠奸(一応)
	ELSEIF EXP:RANDOM_CHARANUM:睡眠奸经验
		LOCALS = 中计了、在睡眠之中的身体被好好爱护了
	;合意无ックス
	ELSEIF CFLAG:RANDOM_CHARANUM:ヤラせちゃった
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = 似乎无法抗拒丈夫所没有的魅力。
		ELSE
			LOCALS = 忘乎所以地发生了性行为
		ENDIF
	;屈服刻印
	ELSEIF MARK:RANDOM_CHARANUM:屈服刻印
		IF TALENT:RANDOM_CHARANUM:人妻 == 1
			LOCALS = 虽然不该这么做但仍然是以性的目光被看待着
		ELSE
			LOCALS = 似乎开始对性方面产生兴趣了。
		ENDIF
	;时奸刻印
	ELSEIF MARK:RANDOM_CHARANUM:时奸刻印
		LOCALS = 的注意、在时间停止的时候身体被开发了
	ELSE
		LOCALS = 的印象还过得去。
	ENDIF
	CSTR:RANDOM_CHARANUM:路人子PROFILE２ += LOCALS

;败犬の场合は、陷落强度に応じて文本を构成
ELSE
	IF TALENT:RANDOM_CHARANUM:恋慕
		CSTR:RANDOM_CHARANUM:路人子PROFILE２ = 弹幕游戏的胜负无所谓、现在从心底里恋慕着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
	ELSEIF TALENT:RANDOM_CHARANUM:爱欲
		IF GETBIT(TALENT:MASTER:性别, 1)
			IF 回数 >= 75
				CSTR:RANDOM_CHARANUM:路人子PROFILE２ = 身心完全屈服于%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的性能力、急切期待着再次被侵犯
			ELSE
				CSTR:RANDOM_CHARANUM:路人子PROFILE２ = 完全屈服于%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%、急切等待着再次被疼爱
			ENDIF
		ELSE
			CSTR:RANDOM_CHARANUM:路人子PROFILE２ = 曾经的威风不知到哪里去了、身心都被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的颜色所浸染
		ENDIF
	ELSEIF TALENT:RANDOM_CHARANUM:思慕
		CSTR:RANDOM_CHARANUM:路人子PROFILE２ = 不仅%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的腕力、好像也开始在意别人的态度了
	ELSEIF TALENT:RANDOM_CHARANUM:炮友
		CSTR:RANDOM_CHARANUM:路人子PROFILE２ = 被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%腾得七零八落、堕落为肉欲的俘虏
	ELSEIF MARK:RANDOM_CHARANUM:快乐刻印
		CSTR:RANDOM_CHARANUM:路人子PROFILE２ = 虽然嘴上否定了、但身体却老实地对欲望的快乐作出了反应
	ENDIF
ENDIF

;-------------------------------------------------
;PRINT_DATE_Fの●年目 ■の★日の文字列を
;DAY:0（总日数）と同じ日数に读み替える函数
;ARGS CSTR:RANDOM_CHARANUM:路人子PROFILE１を
;代入して、出会った日期を割り出す使用前提
;-------------------------------------------------
@PRINTDATEF_TO_DAY(ARGS)
#FUNCTION
#DIM YEAR
#DIM MOUTH
#DIMS 季
#DIM DAYS
#DIMS カット
#DIM TOTAL

SPLIT ARGS, "：", LOCALS
カット = %LOCALS:1%
SPLIT カット, "年目 ", LOCALS
カット = %LOCALS:1%
YEAR = TOINT(LOCALS:0)
SPLIT カット, "の月の", LOCALS
季 = %LOCALS:0%
カット = %LOCALS:1%
SPLIT カット, "日", LOCALS
DAYS = TOINT(LOCALS:0)
SELECTCASE 季
	CASE "春"
		MOUTH = 0
	CASE "夏"
		MOUTH = 31
	CASE "秋"
		MOUTH = 62
	CASE "冬"
		MOUTH = 93
ENDSELECT
TOTAL = (YEAR - 1) * 124 + MOUTH + DAYS
RETURNF TOTAL

