;-------------------------------------------------
;捏脸蛋
;日常系指令　レベル2
;-------------------------------------------------
@COM307
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:苦痛 = 150
ELSE
	TIME += 10

	SELECTCASE TFLAG:192
		;通常
		CASE 0
			LOCAL = RAND:110
			LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
			SIF LOCAL:1 > 109
				LOCAL:1 = 109
			IF LOCAL <= 9
				;大成功
				TFLAG:193 = 1
				TFLAG:98 = 2
			ELSEIF LOCAL >= LOCAL:1
				;失败
				TFLAG:193 = -1
				TFLAG:98 = -2
			ELSE
				;残りは成功
				TFLAG:193 = 0
				TFLAG:98 = 0
			ENDIF
		;强制成功or大成功
		CASE 1
			IF RAND:100 <= 9
				;大成功
				TFLAG:193 = 1
				TFLAG:98 = 2
			ELSE
				;残りは成功
				TFLAG:193 = 0
				TFLAG:98 = 0
			ENDIF
		;强制失败
		CASE -1
			TFLAG:193 = -1
			TFLAG:98 = -2
		;指令结束
		CASE -2
			TFLAG:193 = -1
			RETURN 0
	ENDSELECT

	;先制发生时は失败しない
	SIF TFLAG:102 == 3 && TFLAG:192 != -1
		TFLAG:193 = MAX(TFLAG:193,0)
		
	IF CFLAG:TARGET:睡眠
		DOWNBASE:MASTER:气力 += 50
	ELSE
		DOWNBASE:体力 += 20
		DOWNBASE:气力 += 50
		DOWNBASE:MASTER:气力 += 50
	ENDIF

;固定で获得するソース
SOURCE:欢乐 = 100
SOURCE:苦痛 = 150
SOURCE:反感 = 120

;ABL:亲密をみる
IF ABL:亲密 <= 3
	SOURCE:欢乐 += 100 + (ABL:亲密 * 20)
	SOURCE:情爱 += 150 + (ABL:亲密 * 30)
ELSEIF ABL:亲密 <= 5
	SOURCE:欢乐 += 300 + (ABL:亲密 * 40)
	SOURCE:情爱 += 350 + (ABL:亲密 * 30)
ELSEIF ABL:亲密 <= 8
	SOURCE:欢乐 += 600 + (ABL:亲密 * 50)
	SOURCE:情爱 += 500 + (ABL:亲密 * 40)
ELSEIF ABL:亲密 <= 11
	SOURCE:欢乐 += 800 + (ABL:亲密 * 70)
	SOURCE:情爱 += 700 + (ABL:亲密 * 60)
ELSE
	SOURCE:欢乐 += 1200 + (ABL:亲密 * 40)
	SOURCE:情爱 += 1500 + (ABL:亲密 * 20)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:欢乐 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:欢乐 += 200 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:欢乐 += 1000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:欢乐 < 0
	SOURCE:欢乐 = 0

;寝てる场合效果
IF CFLAG:睡眠 && SOURCE:欢乐 > 0
	SOURCE:欢乐 = SOURCE:欢乐 / 2
	SOURCE:情爱 = SOURCE:情爱 / 2
	SOURCE:反感 = SOURCE:反感 * 2
ENDIF

LOCAL = 0
;睡眠中は好感度变化无し
IF !CFLAG:睡眠
	;亲密が低いと好感度低下
	IF ABL:亲密 == 0
		LOCAL -= 3
	ELSEIF ABL:亲密 == 1
		LOCAL -= 2
	ELSEIF ABL:亲密 == 2
		LOCAL -= 1
	ENDIF
	;好感度が低いと好感度低下
	IF CFLAG:2 <= 50
		LOCAL -= 3
	ELSEIF CFLAG:2 <= 100
		LOCAL -= 2
	ELSEIF CFLAG:2 <= 250
		LOCAL -= 1
	ENDIF
ENDIF

;好感度变化
TFLAG:97 += LOCAL

;信赖度变化
;TFLAG:98 += 1

IF TFLAG:193 == -1
	TIMES SOURCE:欢乐 , 0.10
	TIMES SOURCE:情爱 , 0.50
	TIMES SOURCE:被动 , 0.50
	TIMES SOURCE:屈从 , 1.50
	TIMES SOURCE:反感 , 1.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:欢乐 , 1.00
	TIMES SOURCE:情爱 , 1.00
	TIMES SOURCE:被动 , 1.00
	TIMES SOURCE:屈从 , 1.00
ELSE
	TIMES SOURCE:欢乐 , 2.00
	TIMES SOURCE:情爱 , 2.00
	TIMES SOURCE:被动 , 2.00
	TIMES SOURCE:屈从 , 0.50
	TIMES SOURCE:反感 , 0.75
ENDIF

ENDIF
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE307
;实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(307)
	RETURN RESULT
SIF TARGET == 0
	RETURN 0
;时间停止中はスキップ
IF !FLAG:70
	;亲热强度1
	SIF (TCVAR:MASTER:亲热 < 1)
		RETURN 0
	;仕事中
	SIF CFLAG:行动 == 5
		RETURN 0
	;约会中
	SIF CHK_DATENOW(CFLAG:约会中) && CFLAG:当前位置 != OMANEKIBEYA()
		RETURN 0
ENDIF
;气力0
SIF BASE:MASTER:气力 <= 0
		RETURN 0
RETURN 1

