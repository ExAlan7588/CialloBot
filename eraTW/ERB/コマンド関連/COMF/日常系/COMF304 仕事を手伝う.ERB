;-------------------------------------------------
;帮忙干活
;日常系指令　レベル1
;-------------------------------------------------
@COM304
#DIM JOBTYPE

JOBTYPE = CFLAG:TARGET:职业种类

SIF FLAG:70 && JOBTYPE == JOB_会话
	RETURN 0

CALL JOB_EFFICIENCY(JOBTYPE)

IF FLAG:70
	IF BASE:工作量 <= 0
		PRINTFORML 已经完成了%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%交付的工作
		BASE:MASTER:TSP -= 10
		RETURN 0
	ELSE
		BASE:MASTER:TSP -= 200
	ENDIF
ELSE
	SELECTCASE JOBTYPE
		CASE JOB_清扫
			DOWNBASE:MASTER:体力 += 150
			DOWNBASE:MASTER:气力 += 100
			SIF TALENT:MASTER:清扫中毒
				DOWNBASE:MASTER:气力 -= 50
		CASE JOB_会话, JOB_お酒
			DOWNBASE:MASTER:体力 += 50
			DOWNBASE:MASTER:气力 += 150
		CASE JOB_战斗
			DOWNBASE:MASTER:体力 += 250
			DOWNBASE:MASTER:气力 += 50
		CASE JOB_教养
			DOWNBASE:MASTER:体力 += 50
			DOWNBASE:MASTER:气力 += 250
		CASE JOB_料理
			DOWNBASE:MASTER:体力 += 150
			DOWNBASE:MASTER:气力 += 150
		CASE JOB_音乐
			DOWNBASE:MASTER:体力 += 200
			DOWNBASE:MASTER:气力 += 100
		CASE JOB_游び
			DOWNBASE:MASTER:体力 += 200
			DOWNBASE:MASTER:气力 += 100
		CASE JOB_经营
			DOWNBASE:MASTER:体力 += 150
			DOWNBASE:MASTER:气力 += 150
		CASE JOB_家事
			DOWNBASE:MASTER:体力 += 150
			DOWNBASE:MASTER:气力 += 100
	ENDSELECT
	;所要时间
	IF JOBTYPE == JOB_宴会准备 || JOBTYPE == JOB_宴会の片づけ
		TIME += 30
	ELSE
		SELECTCASE TARGET
		CASE [[慧音]]
			;慧音の寺子屋授业善后工作手传い14时～14时30分まで
			;慧音の寺子屋授业善后工作手传い14时10分～14时30分まで
			;慧音の寺子屋授业善后工作手传い14时20分～14时30分まで
			;顺番的に时短处理が机能していなかったので修正
			IF INRANGE(TIME, CLOCK_TO_TIME(14, 20), CLOCK_TO_TIME(14, 30))
				TIME += 10
			ELSEIF INRANGE(TIME, CLOCK_TO_TIME(14, 10), CLOCK_TO_TIME(14, 30))
				TIME += 20
			ELSEIF INRANGE(TIME, CLOCK_TO_TIME(14, 00), CLOCK_TO_TIME(14, 30))
				TIME += 30
			ELSE
				TIME += 60
			ENDIF
		;艾伦の呼び込み手传い
		CASE [[艾伦]]
			IF TCVAR:TARGET:354 < 25
				TIME += 30
			ELSE
				TIME += 60
			ENDIF
		CASEELSE
			TIME += 60
		ENDSELECT
	ENDIF
	DOWNBASE:气力 += 10
	;信赖
	TFLAG:98 += 5
	TFLAG:情绪上升抑制 = 1
	;固定で获得するソース
	SOURCE:欢乐 = 300
	SIF JOBTYPE == 49
		SOURCE:欢乐 += 200
	
	;ABL:顺从をみる
	IF ABL:顺从 <= 1
		SOURCE:欢乐 += (ABL:顺从 * 50)
	ELSEIF ABL:顺从 <= 3
		SOURCE:欢乐 += 100 + (ABL:顺从 * 40)
	ELSEIF ABL:顺从 <= 5
		SOURCE:欢乐 += 200 + (ABL:顺从 * 50)
	ELSEIF ABL:顺从 <= 8
		SOURCE:欢乐 += 400 + (ABL:顺从 * 70)
	ELSEIF ABL:顺从 <= 11
		SOURCE:欢乐 += 500 + (ABL:顺从 * 60)
	ELSE
		SOURCE:欢乐 += 800 + (ABL:顺从 * 40)
	ENDIF

	;ABL:亲密チェック
	IF ABL:亲密 <= 1
		SOURCE:欢乐 += 70 + (ABL:亲密 * 50)
	ELSEIF ABL:亲密 <= 3
		SOURCE:欢乐 += 120 + (ABL:亲密 * 50)
	ELSEIF ABL:亲密 <= 5
		SOURCE:欢乐 += 200 + (ABL:亲密 * 50)
	ELSEIF ABL:亲密 <= 8
		SOURCE:欢乐 += 500 + (ABL:亲密 * 40)
	ELSEIF ABL:亲密 <= 11
		SOURCE:欢乐 += 800 + (ABL:亲密 * 40)
	ELSE
		SOURCE:欢乐 += 1200 + (ABL:亲密 * 30)
	ENDIF

	;好感度をみる
	IF CFLAG:2 <= 500
		SOURCE:欢乐 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:欢乐 += 500 + (CFLAG:2 - 500) / 3
	ELSE
		SOURCE:欢乐 += 2000 + (CFLAG:2 - 5000) / 5
	ENDIF
	SIF SOURCE:欢乐 < 0
		SOURCE:欢乐 = 0

	SOURCE:被动 = 230 + 80 * ABL:顺从
	SOURCE:征服 = 150 + 200 * ABL:施虐属性
ENDIF
SIF BASE:TARGET:工作量 <= 0
	CALL CHARA_JOBEND(TARGET)
RETURN 1


@JOB_EFFICIENCY(ARG)
#DIM ABILITY
#DIM WorkP

WorkP = 0
IF GROUPMATCH(ARG, JOB_清扫, JOB_会话, JOB_战斗, JOB_教养, JOB_料理, JOB_音乐, JOB_经营, JOB_お酒, JOB_家事, JOB_采集, JOB_钓り, JOB_调合)
	SELECTCASE ARG
	CASE JOB_清扫, JOB_家事
		ABILITY = ABL:MASTER:清扫技能
	CASE JOB_会话, JOB_经营, JOB_お酒
		ABILITY = ABL:MASTER:话术技能
	CASE JOB_战斗
		ABILITY = ABL:MASTER:战斗能力
	CASE JOB_教养
		ABILITY = ABL:MASTER:教养
	CASE JOB_料理
		ABILITY = ABL:MASTER:料理技能
	CASE JOB_音乐
		ABILITY = ABL:MASTER:音乐技能
	CASE JOB_采集
		ABILITY = TALENT:MASTER:采集Lv
	CASE JOB_钓り
		ABILITY = TALENT:MASTER:钓鱼Lv
	CASE JOB_调合
		ABILITY = TALENT:MASTER:调合Lv
	ENDSELECT
	SELECTCASE TFLAG:192
		;通常
		CASE 0
			LOCAL = RAND:100
			LOCAL:1 = 75 + ABILITY * 5
			IF LOCAL <= 10 + ABILITY * 2
				;技能次第で大成功
				TFLAG:193 = 1
				WorkP = (50 + (ABILITY) * 75) * 2
				TFLAG:98 = 2
			ELSEIF LOCAL >= LOCAL:1
				;10～1％で失败
				TFLAG:193 = -1
				WorkP = 0
				TFLAG:98 = -1
			ELSE
				;残りは成功
				TFLAG:193 = 0
				WorkP = 50 + (ABILITY) * 75
				TFLAG:98 = 0
			ENDIF
		;强制成功or大成功
		CASE 1
			IF RAND:100 <= 10 + ABILITY * 2
				;技能次第で大成功
				TFLAG:193 = 1
				WorkP = (50 + (ABILITY) * 75) * 2
				TFLAG:98 = 2
			ELSE
				;残りは成功
				TFLAG:193 = 0
				WorkP = 50 + (ABILITY) * 75
				TFLAG:98 = 0
			ENDIF
		;强制失败
		CASE -1
			TFLAG:193 = -1
			TFLAG:98 = -2
		;指令结束
		CASE -2
			TFLAG:193 = -1
			RETURN 0
	ENDSELECT
ELSEIF ARG == JOB_游び
	SELECTCASE TFLAG:192
		;通常
		CASE 0
			LOCAL = RAND:100
			LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
			SIF LOCAL:1 > 99
				LOCAL:1 = 99
			WorkP = 0
			IF LOCAL <= 9
				;10％で大成功
				TFLAG:193 = 1
				TFLAG:98 = 2
				EXP:MASTER:会话经验 ++
			ELSEIF LOCAL >= LOCAL:1
				;10～1％で失败
				TFLAG:193 = -1
				TFLAG:98 = -1
			ELSE
				;残りは成功
				TFLAG:193 = 0
				TFLAG:98 = 0
			ENDIF
		;强制成功or大成功
		CASE 1
			IF RAND:100 <= 9
				;10％で大成功
				TFLAG:193 = 1
				TFLAG:98 = 2
				EXP:MASTER:会话经验 ++
			ELSE
				;残りは成功
				TFLAG:193 = 0
				TFLAG:98 = 0
			ENDIF
		;强制失败
		CASE -1
			TFLAG:193 = -1
			TFLAG:98 = -2
		;指令结束
		CASE -2
			TFLAG:193 = -1
			RETURN 0
	ENDSELECT
ENDIF

SELECTCASE ARG
	CASE JOB_清扫
		SIF TFLAG:193 == 1
		EXP:MASTER:清扫经验 ++
		SIF !RAND:4
			EXP:MASTER:清扫经验 ++
	CASE JOB_会话, JOB_经营, JOB_お酒
		SIF TFLAG:193 == 1
		EXP:MASTER:会话经验 ++
		SIF !RAND:4
			EXP:MASTER:会话经验 ++
	CASE JOB_战斗
		SIF TFLAG:193 == 1
		EXP:MASTER:战斗经验 ++
		SIF !RAND:4
			EXP:MASTER:战斗经验 ++
	CASE JOB_教养
		SIF TFLAG:193 == 1
		EXP:MASTER:学习经验 ++
		SIF !RAND:4
			EXP:MASTER:学习经验 ++
	CASE JOB_料理
		SIF TFLAG:193 == 1
			EXP:MASTER:料理经验 ++
		EXP:MASTER:料理经验 ++
	CASE JOB_音乐
		SIF TFLAG:193 == 1
			EXP:MASTER:演奏经验 ++
		EXP:MASTER:演奏经验 ++
	CASE JOB_采集
		SIF TFLAG:193 == 1
			EXP:MASTER:采集经验 ++
		EXP:MASTER:采集经验 ++
	CASE JOB_钓り
		SIF TFLAG:193 == 1
			EXP:MASTER:钓鱼经验 ++
		EXP:MASTER:钓鱼经验 ++
	CASE JOB_调合
		SIF TFLAG:193 == 1
			EXP:MASTER:调合经验 ++
		EXP:MASTER:调合经验 ++
	;宴会片づけ
	CASE 50
		SIF TFLAG:193 == 1
			EXP:MASTER:清扫经验 ++
		SIF !RAND:4
			EXP:MASTER:清扫经验 ++
	CASE 53
		SIF TFLAG:193 == 1
			EXP:MASTER:调合经验 ++
		EXP:MASTER:调合经验 ++
ENDSELECT

;报酬倍率
LOCAL:4 = TCVAR:TARGET:工资

;働いた分工作量减少
LOCAL:3 = (WorkP + RAND:80 + RAND:80 + 10)

;当然时间停止中は会话系の手传いはできない
SIF FLAG:70 && GROUPMATCH(ARG, 41)
	LOCAL:3 = 0
SIF TCVAR:TARGET:没法帮忙 == 2 && FLAG:70
	LOCAL:3 /= 5
;调合知识
IF TARGET == [[铃仙]] && YOUBI_MATCH(DAY, "木")
	EXP:MASTER:调合经验 += 2
	SIF ABL:MASTER:教养 > RAND:5
		EXP:MASTER:调合经验 ++
ENDIF

;呼び込みは工作量少なめ（艾伦）
SIF TARGET == [[艾伦]] && CFLAG:职业种类 == JOB_经营
	LOCAL:3 /= 2
;みすちーは料理经验におまけ
SIF TARGET == [[米斯蒂亚]]
	EXP:MASTER:料理经验 ++
;TCVAR:315が2じゃなければ工作量减る
IF TCVAR:TARGET:没法帮忙 != 2
	BASE:工作量 = BASE:工作量 - LOCAL:3
ENDIF
IF BASE:工作量 < 0 && CFLAG:TARGET:20 > 0
	BASE:工作量 = 1
ELSEIF BASE:工作量 < 0
	BASE:工作量 = 0
ENDIF
;働いた分アナタの工作量を增加（报酬に影响）
TCVAR:MASTER:帮忙工作量 += LOCAL:3 * LOCAL:4
TCVAR:TARGET:帮忙工作量 += LOCAL:3
IF ARG != 46
	PRINTFORML 这次帮忙完成的工作量{LOCAL:3}
ENDIF

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE304
;帮忙干活实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(304)
	RETURN RESULT
;仕事中でない
SIF CFLAG:行动 != 5
	RETURN 0
;宴会参加者は别扱い
SIF GROUPMATCH(CFLAG:职业种类, JOB_宴会参加, JOB_醉いつぶれる)
	RETURN 0
SIF TCVAR:315 == 1
	RETURN 0
;仕事が终わってる
SIF BASE:工作量 <= 0
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;陪睡中
SIF CFLAG:陪睡中
	RETURN 0
;约会中
SIF CHK_DATENOW(CFLAG:约会中)
	RETURN 0
;气力0
SIF BASE:MASTER:气力 <= 0
	RETURN 0
RETURN 1

