;-------------------------------------------------
;聆听天之声
;日常系指令、レベル1
;-------------------------------------------------
@COM422
PRINTL [0] - 想找人
PRINTL [1] - 想做扫除
PRINTL [2] - 想找住在外面的人
;SIF STALKED
;	PRINTFORML [3] - 【追踪】对象变更（现在的对象：%NAME_OUTPUT_TRANSLATION("CALLNAME", STALKED)%）
PRINTL [100] - 返回
INPUT
IF RESULT == 0
	CALL HITOSAGASI()
ELSEIF RESULT == 1
	CALL OHEYA_CHECK
ELSEIF RESULT == 2
	CALL BROAD_HITOSAGASI
	TIME += 3
	RETURN 1
ELSEIF RESULT == 100
	RETURN -1
ENDIF
INPUTS
RESULT = TOINT(RESULTS)
IF RESULT == 0
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, 1, 19
		SIF STR:LOCAL == RESULTS
			RESULT = LOCAL
	NEXT
ENDIF

IF (RESULT >= 26 && RESULT <= 35) && ABL:MASTER:教养 < 1 && !INRANGE(CFLAG:MASTER:初始位置,26,35)
	PRINTFORMW 不熟悉的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%没有找到…
ELSE
	IF IN_HOME(RESULT) == 1
		SIF AT_HOME(MASTER) == 0
			GOTO MURIDESU
		CALL SKIP_MOVE(CFLAG:MASTER:当前位置, RESULT)
	ENDIF
ENDIF

SIF SKYSOUND == 0
	TIME += 10
RETURN 1
$MURIDESU
PRINTW 到这就不能再前进了…
TIME += 10
RETURN 1

@BROAD_HITOSAGASI
$KAKUNIN
FOR LOCAL,0,11
	SIF LOCAL == MAIN_MAP
		CONTINUE
	LOCAL:1 = 5000 + LOCAL
	PRINTFORMLC [{LOCAL}] - %STR:(LOCAL:1)%
	CALL 广域チェック(LOCAL,1)
	CALL 广域チェック(LOCAL,2)
	PRINTL
NEXT
DRAWLINE
PRINT [100] - 确认结束
$INPUT_LOOP
INPUT
IF RESULT == 100 || RESULT == MAIN_MAP
	RETURN -1
ELSEIF RESULT < 0
	CLEARLINE 1
	GOTO INPUT_LOOP
ELSEIF RESULT >= 11
	PRINTW 尚未制作
	CLEARLINE 2
	GOTO INPUT_LOOP
ENDIF
CALL 广域チェック(RESULT,2)
CALL ASK_YN("看看其他地方","确认结束")
SIF !RESULT
	GOTO KAKUNIN

@广域チェック(ARG,ARG:1)
VARSET LOCAL

FOR LOCAL,1,CHARANUM
	SIF AT_HOME(LOCAL) == 1
		CONTINUE
	SIF CFLAG:LOCAL:当前位置 == MAXROOM && CFLAG:LOCAL:睡眠
		CONTINUE
	FOR LOCAL:1,10,100,10
		IF CFLAG:LOCAL:当前位置 == ARG * 100 + LOCAL:1
			;その地域にいる角色を表示
			IF ARG:1 == 2
				SIF CFLAG:LOCAL:行动 == 5
					SETCOLOR 255, 100, 0
				PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)% 
				RESETCOLOR
			ENDIF
			LOCAL:2 ++
		ENDIF
	NEXT
NEXT
;その地域にいる人数
IF ARG:1 == 1
	IF LOCAL:2
		PRINTFORML {LOCAL:2}人
	ELSE
		PRINTL 谁也不在
	ENDIF
ENDIF

;聆听天之声
@COM_ABLE422
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(421)
	RETURN RESULT
;功德箱
SIF !天之声可(CFLAG:MASTER:当前位置)
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
RETURN 1