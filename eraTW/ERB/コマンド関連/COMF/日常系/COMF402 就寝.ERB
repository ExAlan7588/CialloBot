;-------------------------------------------------
;就寝
;日常系指令、レベル1
;-------------------------------------------------
@COM402
#DIM 劳动量
#DIM 收益

;魔改睡前自动扫地
CALL AUTO_SWEEP_BEFORE_SLEEP_ASK

IF FLAG:居家工作 && BASE:MASTER:气力 > 300 && BASE:MASTER:TSP > 0
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%在睡觉之前使用剩余的TSP做了些副业
	劳动量 = MIN(BASE:MASTER:气力, BASE:MASTER:TSP /2)
	收益 = 劳动量 * (5 + RAND:5) / 10
	MONEY += 收益
	PRINTFORMW 挣了%金额表示(收益)%
ENDIF
IF FLAG:自动结缘 && !FLAG:祈愿内容 && !CFLAG:陪睡中
	PRINTFORM 睡觉前
	IF MONEY >= 1000
		CALL GANKAKE_0(FLAG:自动结缘)
		MONEY -= 1000
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%向神祈祷能和%NAME_OUTPUT_TRANSLATION("CALLNAME", (FLAG:自动结缘))%变得更亲近
		SIF FLAG:祈愿内容 < 0
			PRINTFORMW ……但这个愿望被一股不思议的力量抹去了…
	ELSE
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%向神祈祷能和%NAME_OUTPUT_TRANSLATION("CALLNAME", (FLAG:自动结缘))%成为好友、但是没有香火钱
	ENDIF
ENDIF

CALL KIKENBIHOUMON

LOCAL:10 = 0
FOR LOCAL, 1, MAXCHILD + 1
	SIF CHILDNAME:TARGET:LOCAL == ""
		CONTINUE
	SIF CHILD_INDEPENDENT:TARGET:LOCAL
		CONTINUE
	SIF DAY - CHILD_BIRTHDAY:TARGET:LOCAL >= CHILD_蹒跚学步
		LOCAL:10 ++
NEXT

IF 儿童の睡眠时间(TARGET) && CFLAG:分娩经过日 >= CHILD_蹒跚学步 && LOCAL:10 && FLAG:儿童描写 && !INRANGE(TIME, 540, 840)
	PRINTFORMDL 一家人睡在了一起…
	CALL KOJO_MESSAGE_SEND("EVENT", 0, TARGET, 0, 0, "CHILD_RAISING_OYASUMI")
	TFLAG:193 = 1
	RESETCOLOR
ENDIF

CALL OYASUMI

RETURN 1

;就寝
@COM_ABLE402
;移动实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(402)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;私室设定
SIF CFLAG:MASTER:当前位置 != CFLAG:MASTER:311 && CFLAG:MASTER:当前位置 != OMANEKIBEYA() && CFLAG:陪睡中 == 0
	RETURN 0
;起床后12时间以上经过してないとダメ（デ背包中）
IF FLAG:1005 == 0
SIF NEMUKE() < 720
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
ENDIF
RETURN 1

@OYASUMI
IF TARGET > 0
	CFLAG:同行 = 0
	CFLAG:陪睡中 = 0
	CFLAG:MASTER:同行 = 0
	CFLAG:MASTER:陪睡中 = 0
ENDIF

IF CFLAG:MASTER:当前位置 == CFLAG:MASTER:311
	CALL SET_SLEEP(1,MASTER,0)
ELSE
	CALL SET_SLEEP(1,TARGET,1)
	SIF CFLAG:TARGET:衰弱
		CFLAG:TARGET:衰弱 = 0
ENDIF

CALL SET_SNEAK(0,TARGET,1)
;CALL SET_TOGETHER(0,TARGET,1)

CFLAG:MASTER:就寝时间 = DAY * 1440 + TIME
BEGIN AFTERTRAIN
