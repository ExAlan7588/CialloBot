;-------------------------------------------------
;接吻
;日常系指令　レベル2
;-------------------------------------------------
@COM312
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 10
	SOURCE:快Ｍ = ABL:PLAYER:舌 * 2 + 10
	EXP:MASTER:接吻经验 += 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	ENDIF
ELSE
	;甲斐性无しの场合、その场にいる全员が关系を公认してないと勇气がでない
	IF FLAG:甲斐性无
		FOR LOCAL, 1, GET_TARGETNUM() + 1
			SIF TARGET:LOCAL == TARGET:0
				CONTINUE
			IF GET_UWAKI_HISTORY(TARGET:LOCAL, TARGET:0) < 2
				DRAWLINE
				; PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%は%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%に口づけをしようとしたが
				PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%想亲吻%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%
				; PRINTFORMW 视界の端に%NAME_OUTPUT_TRANSLATION("CALLNAME", (TARGET:LOCAL))%の姿が映ると思わず颜を离してしまった…
				PRINTFORMW 但视野的边缘出现%NAME_OUTPUT_TRANSLATION("CALLNAME", (TARGET:LOCAL))%的身影时，%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%不由得将脸移开了
				RETURN -1
			ENDIF
		NEXT
	ENDIF
	TFLAG:理性削り = 1
	;拒绝判定
	IF REJECT_CHECK(SELECTCOM) && !CFLAG:睡眠
		TFLAG:193 = 99
		LOCAL = 0
		;固定获得ソース
		SOURCE:反感 = 500
		SOURCE:恐怖 = 300
		;好感度が低いと好感度低下
		IF CFLAG:2 <= 500
			LOCAL -= 50
		ELSEIF CFLAG:2 <= 1000
			LOCAL -= 30
		ELSEIF CFLAG:2 <= 1500
			LOCAL -= 10
		ENDIF
		;好感度变化
		TFLAG:97 += LOCAL
		TIME += 10
		RETURN 1
	ENDIF

	SELECTCASE TFLAG:192
		;通常
		CASE 0
			LOCAL = RAND:110
			LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
			SIF LOCAL:1 > 109
				LOCAL:1 = 109
			IF LOCAL <= 29
				;10％で大成功
				TFLAG:193 = 1
				IF CHK_DATENOW(CFLAG:TARGET:约会中)
					EXP:MASTER:约会经验 ++
					EXP:TARGET:约会经验 ++
				ENDIF
			ELSEIF LOCAL >= LOCAL:1
				;10～1％で失败
				TFLAG:193 = -1
			ELSE
				;残りは成功
				TFLAG:193 = 0
			ENDIF
		;强制成功or大成功
		CASE 1
			IF RAND:110 <= 9
				TFLAG:193 = 1
				IF CHK_DATENOW(CFLAG:TARGET:约会中)
					EXP:MASTER:约会经验 ++
					EXP:TARGET:约会经验 ++
				ENDIF
			ELSE
				TFLAG:193 = 0
			ENDIF
		;强制失败
		CASE -1
			TFLAG:193 = -1
		;指令结束
		CASE -2
			TFLAG:102 = 0
			TFLAG:193 = -1
			RETURN 0
	ENDSELECT

	;先制发生时は失败しない
	SIF TFLAG:102 == 3 && TFLAG:192 != -1
		TFLAG:193 = MAX(TFLAG:193,0)


	IF CFLAG:TARGET:陪睡中
		DOWNBASE:MASTER:气力 += 50
	ELSEIF CHK_DATENOW(CFLAG:TARGET:约会中)
		DOWNBASE:气力 += 30
		DOWNBASE:MASTER:气力 += 50
	ELSE
		DOWNBASE:气力 += 70
		DOWNBASE:MASTER:气力 += 100
	ENDIF

	;固定で获得するソース
	SOURCE:快Ｍ = ABL:PLAYER:舌 * 2 + 10
	SOURCE:PLAYER:快Ｍ = ABL:舌 * 2 + 10
	SOURCE:欢乐 = 150
	SOURCE:情爱 = 150
	SOURCE:露出 = 80
	SOURCE:反感 = 300
	SIF CFLAG:睡眠
		SOURCE:反感 = 200

	;ABL:亲密をみる
	IF ABL:亲密 <= 1
		SOURCE:情爱 += (ABL:亲密 * 30)
	ELSEIF ABL:亲密 <= 3
		SOURCE:欢乐 += 150 + (ABL:亲密 * 30)
		SOURCE:情爱 += 150 + (ABL:亲密 * 30)
		SOURCE:情欲 += 65 + (ABL:亲密 * 20)
	ELSEIF ABL:亲密 <= 5
		SOURCE:欢乐 += 300 + (ABL:亲密 * 40)
		SOURCE:情爱 += 280 + (ABL:亲密 * 30)
		SOURCE:情欲 += 130 + (ABL:亲密 * 26)
		SOURCE:露出 += 80 + (ABL:亲密 * 15)
	ELSEIF ABL:亲密 <= 8
		SOURCE:欢乐 += 450 + (ABL:亲密 * 70)
		SOURCE:情爱 += 380 + (ABL:亲密 * 40)
		SOURCE:情欲 += 170 + (ABL:亲密 * 30)
		SOURCE:露出 += 110 + (ABL:亲密 * 18)
	ELSEIF ABL:亲密 <= 11
		SOURCE:欢乐 += 500 + (ABL:亲密 * 85)
		SOURCE:情爱 += 480 + (ABL:亲密 * 50)
		SOURCE:情欲 += 230 + (ABL:亲密 * 35)
		SOURCE:露出 += 160 + (ABL:亲密 * 20)
	ELSE
		SOURCE:欢乐 += 800 + (ABL:亲密 * 80)
		SOURCE:情爱 += 760 + (ABL:亲密 * 45)
		SOURCE:情欲 += 350 + (ABL:亲密 * 30)
		SOURCE:露出 += 260 + (ABL:亲密 * 17)
	ENDIF

	;好感度をみる
	IF CFLAG:2 <= 500
		SOURCE:情爱 += CFLAG:2
	ELSEIF CFLAG:2 <= 5000
		SOURCE:情爱 += 400 + (CFLAG:2 - 500) / 5
		SOURCE:欢乐 += 400 + (CFLAG:2 - 500) / 4
	ELSE
		SOURCE:情爱 += 1200 + (CFLAG:2 - 5000) / 7
		SOURCE:欢乐 += 1200 + (CFLAG:2 - 5000) / 4
	ENDIF
	SIF SOURCE:情爱 < 0
		SOURCE:情爱 = 0

	LOCAL = 0
	;好感度が低いと好感度低下
	IF CFLAG:2 <= 100
		LOCAL -= 3
	ELSEIF CFLAG:2 <= 300
		LOCAL -= 2
	ELSEIF CFLAG:2 <= 500
		LOCAL -= 1
	ENDIF

SIF CFLAG:以身体为目的
	SOURCE:情爱 *= 3

	SOURCE:被动 = 150 + 300 * ABL:顺从
	SOURCE:征服 = 150 + 300 * ABL:施虐属性

	;好感度变化
	SIF !CFLAG:睡眠
		TFLAG:97 += LOCAL

	;二人きりだと效果UP、そうでないと露出以外ダウン=============
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:LOCAL:同室
			LOCAL:2 ++
	NEXT
	IF LOCAL:2 == 1
		TIMES SOURCE:欢乐 , 1.20
		TIMES SOURCE:情爱 , 1.20
		TIMES SOURCE:征服 , 1.20
		TIMES SOURCE:被动 , 1.20
	ELSE
		TIMES SOURCE:欢乐 , 0.70
		TIMES SOURCE:情爱 , 0.80
		TIMES SOURCE:征服 , 0.80
		TIMES SOURCE:被动 , 0.90
		TIMES SOURCE:露出 , 1.10
	ENDIF
	;情绪の影响=====================================================
	IF BASE:情绪 == MAXBASE:情绪
		TIMES SOURCE:欢乐 , 1.30
		TIMES SOURCE:情爱 , 1.30
		TIMES SOURCE:征服 , 1.20
		TIMES SOURCE:被动 , 1.20
		TIMES SOURCE:露出 , 1.30
	ELSEIF BASE:情绪 < 600 && BASE:情绪 > 200 && !TALENT:接吻魔
		TIMES SOURCE:欢乐 , 0.70
		TIMES SOURCE:情爱 , 0.75
		TIMES SOURCE:征服 , 0.70
		TIMES SOURCE:被动 , 0.60
		TIMES SOURCE:露出 , 0.50
	ELSEIF BASE:情绪 <= 200 && !TALENT:接吻魔
		TIMES SOURCE:欢乐 , 0.40
		TIMES SOURCE:情爱 , 0.40
		TIMES SOURCE:征服 , 0.40
		TIMES SOURCE:被动 , 0.40
		TIMES SOURCE:露出 , 0.35
	ENDIF
	;接吻魔===============================================================
	IF TALENT:接吻魔
		TIMES SOURCE:欢乐 , 1.40
		TIMES SOURCE:情爱 , 1.40
	ENDIF
	;=====================================================================
	IF TFLAG:193 == -1
		TIMES SOURCE:欢乐 , 0.10
		TIMES SOURCE:情爱 , 0.50
		TIMES SOURCE:征服 , 0.50
		TIMES SOURCE:被动 , 0.50
		TIMES SOURCE:露出 , 0.40
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:欢乐 , 1.00
		TIMES SOURCE:情爱 , 1.00
		TIMES SOURCE:征服 , 1.00
		TIMES SOURCE:被动 , 1.00
		TIMES SOURCE:露出 , 0.90
		TFLAG:情绪BOUNS = 50
	ELSE
		TIMES SOURCE:欢乐 , 2.00
		TIMES SOURCE:情爱 , 2.00
		TIMES SOURCE:征服 , 2.00
		TIMES SOURCE:被动 , 2.00
		TIMES SOURCE:露出 , 1.80
		TFLAG:情绪BOUNS = 100
	ENDIF
	TIME += 5
	SIF !CFLAG:睡眠
		EXP:接吻经验 += 1
	;SOURCE设定してると、SOURCE处理でCVABM経験はプラスされるのでコメントアウト
	;EXP:Ｍ经验 += 1
	;同上
	;EXP:MASTER:Ｍ经验 += 1
	EXP:MASTER:接吻经验 += 1
	IF CHK_DATENOW(CFLAG:TARGET:约会中)
		EXP:MASTER:约会经验 ++
		EXP:TARGET:约会经验 ++
	ENDIF
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 2
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	ENDIF
	;接吻したフラグ
	SIF !TALENT:恋慕 && !TALENT:恋人 && !TALENT:爱欲 && !TALENT:炮友
		TCVAR:314 = 1440 * DAY + TIME
ENDIF
TCVAR:ＦＫ丧失 = 1

RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE312
;接吻实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(312)
	RETURN RESULT
;时间停止中はスキップ
IF !FLAG:70
	;亲热强度1
	SIF (TCVAR:MASTER:亲热 < 1) && !CFLAG:睡眠
		RETURN 0
	;甲斐性なしはもう少し驯れ合い强度と既成事实が必要
	SIF FLAG:甲斐性无 && ((TCVAR:MASTER:亲热 < 2 && !CFLAG:睡眠) || !GETBIT(CFLAG:既成事实, 0))
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠 && !CFLAG:恶作剧 && !CFLAG:陪睡中
		RETURN 0
	;仕事中
	SIF CFLAG:行动 == 5
		RETURN 0
	;1时间间隔
	SIF TIME_PROGRESS(TCVAR:314) < 60 && CFLAG:诶嘿嘿 != 2
		RETURN 0
ENDIF
;气力0
SIF BASE:MASTER:气力 <= 0
	RETURN 0
;口使用中
SIF TFLAG:60 && MASTER_POSE(SET_M, SET_M) != TARGET
	RETURN 0
SIF GETBIT (TCVAR:26,2)
	RETURN 0
;六九式体势
SIF TEQUIP:六九式
	RETURN 0
RETURN 1

