;-------------------------------------------------
;劝酒
;日常系指令、レベル1
;-------------------------------------------------
@COM332
#DIM 酒ID
VARSET LOCALS

PRINTL 要敬什么酒？
CALL 酒一览表示(1)
IF RESULT
	;饮む酒の种类
	TFLAG:194 = RESULT
	ITEM:RESULT --
ELSE
	RETURN -1
ENDIF
COM_STR = %ITEMNAME:(TFLAG:194)%
酒ID = ALC_ID(COM_STR)

PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%倒入%COM_STR%并递给了%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%……


;暂定的に成功フラグを立てる
TFLAG:193 = 0

IF !TALENT:TARGET:恋慕 && CFLAG:TARGET:烂醉奸 == 2
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%已经不想再与%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%喝酒了的样子…
	SOURCE:反感 += 3000
	BASE:情绪 = 0
	BASE:愤怒 = 1500
;	TFLAG:193 = -2
	RETURN -1
ENDIF
IF BASE:酒气 > MAXBASE:酒气 * 2 / 3
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%看起来已经不想再喝了
;	TFLAG:193 = -1
	RETURN -1
ELSEIF (TALENT:TARGET:酒耐性 == -2 && CFLAG:TARGET:不会喝酒 < 5) || TFLAG:192 == -1
	SELECTCASE CFLAG:TARGET:不会喝酒
		CASE IS < -10
			LOCALS = %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%露出不高兴的样子
		CASE IS <= -3
			LOCALS = 曾经经历过讨厌感觉的%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%不想再喝酒的样子…
		CASEELSE
		IF TARGET == 55 || TFLAG:192 == -1
			LOCALS = %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%推托说自己不能喝酒
		ELSE
			LOCALS = 不会喝酒的%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%似乎不想再喝了的样子…
		ENDIF
	ENDSELECT
	PRINTFORML %LOCALS%
	PRINTFORML [0] 放弃
	PRINTFORML [1] 说服
	$LOOP
	INPUT
	IF RESULT == 0 || TARGET == 55 || TFLAG:192 == -1
	;ひじりんは宗教的信条なので绝对饮まない。口上侧で制御した场合もここ
		PRINTFORML 因为%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%完全没有要喝酒的意思、所以放弃了…
		TFLAG:193 = -5
		RETURN 0
	ELSEIF RESULT == 1
		IF (ABL:亲密 * 200 + ABL:MASTER:话术技能 * 100 + MIN(3,CFLAG:不会喝酒) * 300) * BASE:情绪 / MAXBASE:情绪 >= 1500
			PRINTFORML 由于%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的劝说%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%开始怯生生的喝起酒来…
			CFLAG:不会喝酒 += 1
			TFLAG:193 = 1
		ELSEIF CFLAG:不会喝酒 < -10
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%终于被惹火了
			SOURCE:反感 += 3000
			BASE:情绪 = 0
			BASE:愤怒 = 1500
			TFLAG:好感度减少 = 5
			TFLAG:193 = -2
			RETURN 1
		ELSE
			PRINTFORML 不管%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%怎么劝说、%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%还是不肯喝酒…
			SOURCE:反感 += 500
			BASE:情绪 /= 2
			TFLAG:好感度减少 = 1
			TFLAG:193 = -1
			RETURN 1
		ENDIF
	ELSE
		GOTO LOOP
	ENDIF
ENDIF

TRYCALLFORM EFFECT_酒データ{酒ID}(TARGET)
DOWNBASE:MASTER:气力 += 50
DOWNBASE:MASTER:体力 += 10

CALL DRUNKEN(MASTER,TFLAG:194)

CALL COM332_1

TIME += 30
RETURN 1

@COM332_1
#DIM 酒气增加量
#DIM 味补正
DOWNBASE:气力 += 10

;固定で获得するソース
SOURCE:欢乐 = 500

CALL DRUNKEN(TARGET,TFLAG:194)

;连续实行时は反感が发生
SIF PREVCOM == SELECTCOM
	SOURCE:反感 = 100

;ABL:顺从をみる
IF ABL:顺从 <= 1
	SOURCE:欢乐 += (ABL:顺从 * 30)
ELSEIF ABL:顺从 <= 3
	SOURCE:欢乐 += 400 + (ABL:顺从 * 30)
ELSEIF ABL:顺从 <= 5
	SOURCE:欢乐 += 600 + (ABL:顺从 * 30)
ELSEIF ABL:顺从 <= 8
	SOURCE:欢乐 += 800 + (ABL:顺从 * 50)
ELSEIF ABL:顺从 <= 11
	SOURCE:欢乐 += 1000 + (ABL:顺从 * 70)
ELSE
	SOURCE:欢乐 += 1500 + (ABL:顺从 * 40)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:欢乐 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:欢乐 += 700 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:欢乐 += 2500 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:欢乐 < 0
	SOURCE:欢乐 = 0

SOURCE:被动 = 200 + 100 * ABL:顺从
SOURCE:征服 = 200 + 100 * ABL:施虐属性

IF TFLAG:193 == -1
	TIMES SOURCE:欢乐 , 0.10
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:被动 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:欢乐 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:被动 , 1.00
ELSE
	TIMES SOURCE:欢乐 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:被动 , 2.00
ENDIF

;味による补正
味补正 = ALCOHOL_TASTE(TFLAG:194)
IF TALENT:TARGET:酒耐性 == -2 && ALCOHOL_TASTE(TFLAG:194) < 15
	IF CFLAG:TARGET:不会喝酒 < 3
		味补正 /= 2
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%喝了一小口就停下了
	ELSEIF CFLAG:TARGET:不会喝酒 < 5
		味补正 = 味补正 / 3 * 2
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%似乎不像以前那样抵触饮酒了
	ELSE
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%漂亮地喝光了杯里的酒
	ENDIF
ENDIF

SOURCE:欢乐 = SOURCE:欢乐 * 味补正 / 10
SOURCE:征服 = SOURCE:征服 * 味补正 / 10
SOURCE:被动 = SOURCE:被动 * 味补正 / 10

;信赖
TFLAG:98 = ALCOHOL_TASTE(TFLAG:194) / 10

IF ALCOHOL_TASTE(TFLAG:194) < 10
	PRINTFORMW 似乎不对%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的口味的样子…
	IF TALENT:TARGET:酒耐性 == -2
		CFLAG:TARGET:不会喝酒 --
		TFLAG:好感度减少 = 1
		TFLAG:98 = -1
	ENDIF
ELSEIF ALCOHOL_TASTE(TFLAG:194) >= 15
	SELECTCASE TFLAG:194
		CASE 513	;一夜奇稻田の特殊文本
			PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%说%ITEMNAME:(TFLAG:194)%
			SELECTCASE TALENT:TARGET:酒耐性
				CASE 3
					PRINTFORMW 很容易入口，像大海一样，咕嘟咕嘟地豪饮着……
				CASE 2
					PRINTFORMW 很容易入口，不断地喝下去……
				CASE 1
					PRINTFORMW 很容易入口，顺滑地喝了下去……
				CASEELSE
					PRINTFORMW 似乎很喜欢……
			ENDSELECT
		CASEELSE
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%对%ITEMNAME:(TFLAG:194)%很中意的样子…
	ENDSELECT
	SIF CFLAG:TARGET:不会喝酒 < 5
		CFLAG:TARGET:不会喝酒 ++
	CALL KIGEN_CHANGE(TARGET,40,1)
ENDIF

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE332

SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(332)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;陪睡中
SIF CFLAG:陪睡中
	RETURN 0
;妊娠自觉状态は不可
SIF CFLAG:妊娠自觉状态 == 1
	RETURN 0
;浴场
SIF BATHCHECK(CFLAG:MASTER:当前位置)
	RETURN 0
;约会中
SIF CHK_DATENOW(CFLAG:约会中) && CFLAG:当前位置 != OMANEKIBEYA()
	RETURN 0
;外出先で实行时は亲密3を要求
SIF !AT_HOME(MASTER) && ABL:亲密 < 3
	RETURN 0
;气力0
SIF BASE:MASTER:气力 <= 0
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
SIF TARGET < 1
	RETURN 0
RETURN 1

