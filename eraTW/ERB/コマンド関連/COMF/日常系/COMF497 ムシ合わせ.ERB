;ムシ合わせ
;NPCとのタイマンを前提として既存のを简素化したのを增筑したが、
;いっそ既存のにガッツリ手を突っ込んでNPCも扱える作りにした方がいいか？
;好きなキャラと一绪にNPC相手のタッグマッチとかできたらいいよね。

@COM497
#DIM 顺位
VARSET MUSHI_BATTLER
VARSET MUSHI_TEAM
VARSET MUSHI_POWER
VARSET BATTLERNAME
MUSHI_BATTLER:1 = MASTER
BATTLERNAME:1 '= @"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%"
;参加者は一人
MUSHI_CHARANUM = 2

NPC_KIRIFUDA_ID = 0
NPC_KIRIFUDA_X = 0
NPC_KIRIFUDA_G = 0

PRINTFORML 请选择对战对手
CALL ASK_M("虫管家1号",1,"虫管家2号",1,"天野川流星",1)
SELECTCASE RESULT
	CASE 0
		BATTLERNAME:2 = 虫管家1号
		NPC_LV = 1
	CASE 1
		BATTLERNAME:2 = 虫管家2号
		NPC_LV = 3
	CASE 2
		BATTLERNAME:2 = 天野川流星
		NPC_LV = 6
		NPC_KIRIFUDA_ID = 322
		NPC_KIRIFUDA_X = 1
		NPC_KIRIFUDA_G = 1
ENDSELECT
;仕样上、ランク4以上は强さに目茶苦茶幅がある
SELECTCASE NPC_LV
	CASE 1
		NPC_MBP = 1600 + RAND:1600
	CASE 2
		NPC_MBP = 3200 + RAND:2800
	CASE 3
		NPC_MBP = 6200 + RAND:6000
	CASE 4
		NPC_MBP = 12000 + RAND:18000
	CASE 5
		NPC_MBP = 30000 + RAND:120000
	CASE 6
		NPC_MBP = 150000 + RAND:350000
	CASEELSE
		NPC_MBP = 500000 + RAND:500000
ENDSELECT


PRINTFORML 与%BATTLERNAME:2%对战　
PRINTFORM %BATTLERNAME:2%的斗虫力：
CALL COLOR_STAMP(NPC_LV + 1, 0, "★", RANKCOLOR(NPC_LV + 1), 1)
PRINTL 
RESETCOLOR
PRINTL [1] 对战开始！
PRINTL [0] 果然还是算了
INPUT
SIF !RESULT
	RETURN -1
;1はMASTER

;本来キャラ番号が入るところ
;MUSHI_BATTLER:2 = 999

;セットアップ１
CALL CHARA_MUSHICAGE_SETTING(999, 2, 1)

;见せ合い＆ムシ选出
CALL NPC_MUSHI_BATTLE_SELECTION
;セットアップ２
CALL NPC_MUSHI_BATTLE_SETUP_2

;试合开始！
CALL MUSHI_BATTLE
;结果发表
CALL MUSHI_BATTLE_RESULT(1)

;时间经过
TIME += 30

RETURN 1

;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
;ムシバトル
@COM_ABLE497
;时间停止中
SIF FLAG:时间停止
	RETURN 0
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(497)
	RETURN RESULT
;昆虫采集套装がない
SIF !ITEM:昆虫采集套装
	RETURN 0
;;昆虫采集できる场所である
SIF !MUSHITORI_SPOT(CFLAG:MASTER:当前位置)
	RETURN 0
;;冬の间はおやすみ
;SIF GET_MONTH() == "冬の月"
;	RETURN 0
;虫を持ってない
SIF !MUSHI_CAGE:1
	RETURN 0
;陪睡中
SIF CFLAG:MASTER:陪睡中
	RETURN 0
;诶嘿嘿中
SIF CFLAG:MASTER:诶嘿嘿
	RETURN 0
;体力・气力不足
SIF BASE:MASTER:气力 < 500 || BASE:MASTER:体力 < 500
	RETURN 0
RETURN 1


;-------------------------------------------------
;ムシバトル：见せ合い
;-------------------------------------------------
@NPC_MUSHI_BATTLE_SELECTION

#DIM LPCOUNT
VARSET MUSHI_SELECT
VARSET MUSHI_REST
PRINTFORML ◆对战对手的虫笼◆
FOR LPCOUNT, -2, MUSHI_MAXCAGE + 2
	IF LPCOUNT == -2
		PRINTFORM ┌
	ELSEIF LPCOUNT == 0
		PRINTFORM ├
	ELSEIF LPCOUNT == MUSHI_MAXCAGE + 1
		PRINTFORM └
	ELSE
		PRINTFORM │
	ENDIF
	IF LPCOUNT == -2
		PRINTFORM %"─" * 20%┐
	ELSEIF LPCOUNT == -1
		LOCALS = %BATTLERNAME:2%
		PRINTFORM %LOCALS, 36, LEFT%
		SETCOLOR RANKCOLOR(NPC_LV + 1)
		PRINTFORM ★%TOFULL(TOSTR(NPC_LV))%
		RESETCOLOR
		PRINTFORM │
	ELSEIF LPCOUNT == MUSHI_MAXCAGE + 1
		PRINTFORM %"─" * 20%┘
	ELSEIF LPCOUNT == 0
		PRINTFORM %"─" * 20%┤
	ELSE
		CALL PRINT_MUSHINAME(MUSHI_NAME:(20 + LPCOUNT), MUSHI_CAGE:(20 + LPCOUNT), 40)
		PRINTFORM │
	ENDIF
	PRINTL 
NEXT
;フィールドタイプ设定
MUSHI_FIELD:99 = 0
WHILE !MUSHI_FIELD:99
	SETBIT MUSHI_FIELD:99, RAND:8
	SIF !(MUSHITORI_SPOT(CFLAG:MASTER:当前位置) &  MUSHI_FIELD:99)
		MUSHI_FIELD:99 = 0
WEND
PRINTFORM ＜场地类型：
CALL PRINT_FIELDTYPE(MUSHI_FIELD:99)
PRINTL ＞
PRINTFORML ◆%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的虫笼◆　请选出１～３匹虫参与战斗
DRAWLINE
;MASTERの选出に引き渡し
MUSHI_REST:1 = 0
MUSHI_MENTAL:1 = 1000
CALL MUSHI_BATTLE_SELECTION_MASTER

;-------------------------------------------------
;ムシバトル：セットアップ２：选出后
;-------------------------------------------------
@NPC_MUSHI_BATTLE_SETUP_2
#DIM LPCOUNT
#DIM SETNUM
#DIM KIRIHUDA
VARSET MUSHI_STATUS
VARSET MUSHI_MENTAL
;MASTERの设定
FOR LPCOUNT, 1, MUSHI_MAXSELECT + 1
	MUSHI_SELECT:(10 + LPCOUNT) = MUSHI_SELECT:LPCOUNT
	CALL MUSHI_PALAM(MUSHI_SELECT:LPCOUNT, MUSHI_POWER:1)
	CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(10 + LPCOUNT), 10 + LPCOUNT)
NEXT
;战斗初期设定
;MASTERのMENTALは1000固定
MUSHI_MENTAL:1 = 1000
MUSHI_STAND:1 = MUSHI_SELECT:11
MUSHI_STANDNUM:1 = 11
GETCOLOR
MUSHI_COLOR:1 = RESULT
;キャラの选出を决める
FOR LPCOUNT, 2, MUSHI_CHARANUM + 1
	SETNUM = LPCOUNT * 10
	IF NPC_KIRIFUDA_ID
	;切り札がいるなら切り札は确定（番号はランダム）
		KIRIHUDA = 1
		LOCAL = RAND:MUSHI_MAXSELECT + 1
		MUSHI_SELECT:(SETNUM + LOCAL) = MUSHI_CAGE:(SETNUM + 1)
		MUSHI_NAME:(SETNUM + LOCAL) = MUSHI_NAME:(SETNUM + 1)
		CALL MUSHI_PALAM(MUSHI_SELECT:(SETNUM + LOCAL), MUSHI_POWER:LPCOUNT)
		CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(SETNUM + LOCAL), SETNUM + LOCAL)
	ELSE
		KIRIHUDA = 0
	ENDIF
	;虫カゴからランダムで空いてる番号に入れていく,ただし切り札がいるなら1番は除外
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		IF !MUSHI_SELECT:(SETNUM + LOCAL)
			WHILE !MUSHI_SELECT:(SETNUM + LOCAL)
				LOCAL:1 = SETNUM + RAND:(MUSHI_MAXCAGE - KIRIHUDA) + 1 + KIRIHUDA
				LOCAL:2 = MUSHI_CAGE:(LOCAL:1)
				IF !MATCH(MUSHI_SELECT, LOCAL:2)
					MUSHI_SELECT:(SETNUM + LOCAL) = LOCAL:2
					MUSHI_NAME:(SETNUM + LOCAL) = MUSHI_NAME:(LOCAL:1)
				ENDIF
			WEND
			CALL MUSHI_PALAM(MUSHI_SELECT:(SETNUM + LOCAL), MUSHI_POWER:LPCOUNT)
			CALL MUSHI_FLAGSETTING(MUSHI_SELECT:(SETNUM + LOCAL), SETNUM + LOCAL)
		ENDIF
	NEXT
	;战斗初期设定
	FOR LOCAL, 1, MUSHI_MAXSELECT + 1
		SETBIT MUSHI_REST:LPCOUNT, LOCAL
	NEXT
	MUSHI_MENTAL:LPCOUNT = NPC_MBP
	MUSHI_STAND:LPCOUNT = MUSHI_SELECT:(SETNUM + 1)
	MUSHI_STANDNUM:LPCOUNT = SETNUM + 1
	;口上色の设定
	;色が变わってたら登录する
	IF LOCAL != RESULT
		MUSHI_COLOR:LPCOUNT = RESULT
	ELSE
		MUSHI_COLOR:LPCOUNT = LOCAL
	ENDIF
	RESETCOLOR
NEXT