;采伐
@COM442
#DIM CHARA
#DIM 木材种
#DIM 大きさ
#DIM 采伐パワー
#DIM 减少值
#DIM 采伐时间
#DIM 采集パワー
#DIM 采集物
#DIM 薪割り
#DIM 探索回数

;原木を抱えてるときは薪割りするか选择　薪割りしたら体力气力消费时间经过させる
IF ITEM:原木
	;依赖の报酬など、采伐以外で原木を入手してしまった场合の退避用
	SIF !FLAG:原木种类
		FLAG:原木种类 = MAX(RAND:12, 1)
	DRAWLINE
	PRINTFORML 必须先处理掉现在手头上的原木
	CALL WOOD_CHOPPING(FLAG:原木种类)
	IF ITEM:原木 == 0
		采伐パワー = TALENT:MASTER:采伐Lv * 20
		减少值 = 350 - 采伐パワー
		采伐时间 = 180 - 采伐パワー
		IF ITEM:电锯
			减少值 /= 2
			采伐时间 /= 2
		ENDIF
		IF FLAG:70
			BASE:MASTER:TSP -= 减少值
		ELSE
			DOWNBASE:MASTER:体力 = 减少值
			IF ITEM:电锯
				DOWNBASE:MASTER:气力 = 减少值 -50
			ELSE
				DOWNBASE:MASTER:气力 = 减少值 -25
			ENDIF
			TIME += 采伐时间
		ENDIF
		RETURN 1
	ELSE
		RETURN -1
	ENDIF
ENDIF
;夕方～夜の采伐は放弃
IF TIME:2 >= 5 && !FLAG:70
	DRAWLINE
	PRINTFORML 这个时间伐木的话太黑了…
	RETURN -1
ENDIF
;大雨、吹雪のときは放弃(天候パッチ)强风时も放弃
IF GROUPMATCH(天候, 5, 9) || GROUPMATCH(FLAG:异常气象, 9, 10) || WIND_VELOCITY >= 1
	DRAWLINE
	PRINTFORML 这个天气下采伐太危险了
	RETURN -1
ENDIF
;あからさまな人の敷地での盗伐は放弃
IF FLAG:催眠强度 <= 50
	SELECTCASE CFLAG:MASTER:当前位置
		CASE 25, 50
			IF ABL:[[灵梦]]:亲密 < 5
				DRAWLINE
				PRINTFORML 在这里擅自伐木的话总觉得不太妙
				RETURN -1
			ENDIF
		CASE 斜角的竹林, 433
			IF ABL:[[妹红]]:亲密 < 5 && ABL:[[帝]]:亲密 < 5 && ABL:[[辉夜]]:亲密 < 5 && ABL:[[永琳]]:亲密 < 5
				DRAWLINE
				PRINTFORML 在这里擅自伐木的话总觉得不太妙
				RETURN -1
			ENDIF
		CASE 绝景之丘, 805
			IF ABL:[[文]]:亲密 < 5 && ABL:[[果]]:亲密 < 5
				DRAWLINE
				PRINTFORML 在这里擅自伐木的话总觉得不太妙
				RETURN -1
			ENDIF
	ENDSELECT
ENDIF

TFLAG:情绪上升抑制 = 1

;先に采伐パワー算出
采伐パワー = 1 + (TALENT:MASTER:采伐Lv * 2) + ABL:MASTER:教养
;参加者判定
IF FLAG:约会的对象 && !FLAG:70
	CHARA = FLAG:约会的对象
ELSEIF TARGET && ABL:TARGET:亲密 >= 10 && !FLAG:70
	CHARA = TARGET
ELSE
	CHARA = 0
ENDIF
;参加者がいる场合は采伐パワー追加
SIF CHARA && !FLAG:70
	采伐パワー += (TALENT:TARGET:采伐Lv * 2) + ABL:TARGET:教养

探索回数 = 0

$LOOP_SELECT

REPEAT 5
	REUSELASTLINE  \@ CHARA ? 和%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%一起 #  \@捜索可以采伐的东西中.%"." * COUNT%
	TWAIT 150, 1
REND

;采伐候补の木材种とその大きさを决定
木材种 = WOOD_SPECIES(采伐可能(CFLAG:MASTER:当前位置))
大きさ = 0
IF RAND:5 == 0 && 木材种 <= 600 && 木材种 != 12
	大きさ ++
	SIF RAND:3 == 0
		大きさ ++
ENDIF

;采伐候补を文本出力
IF 木材种 == 646
	PRINTFORML 掰断小树枝一看、长出了带着芳香的树木…
ELSEIF 木材种 == 12
	PRINTFORML 竹子有规率地笔直生长…
ELSE
	PRINTFORML 这棵树的话怎么样呢…
ENDIF
DRAWLINE
SETCOLOR C_YELLOW
PRINTFORM 发现的\@ 木材种 == 12 ? 东西 # 树 \@：%WOOD_NAME(木材种)%
SELECTCASE 大きさ
	CASE 1
		PRINTFORM 的大木
	CASE 2
		PRINTFORM 的巨木
ENDSELECT
RESETCOLOR

;体力气力の减少值と采伐にかかる时间を算出
减少值 = WOOD_EFFORT(木材种, 大きさ) - (WOOD_EFFORT(木材种, 大きさ) * (采伐パワー / 70))
SIF ITEM:电锯
	减少值 -= SQRT(WOOD_EFFORT(木材种, 大きさ)) * 10
IF CHARA && !FLAG:70
	采伐时间 = 减少值 / (4 + TALENT:MASTER:采伐Lv + SQRT(TALENT:TARGET:采伐Lv))
ELSE
	采伐时间 = 减少值 / (4 + TALENT:MASTER:采伐Lv)
ENDIF

;采伐时间に応じて文本出力
SETCOLOR C_L_GRAY
SELECTCASE 采伐时间
	CASE IS <= 60 
		PRINTFORML 　（应该比较简单就可以采伐）
	CASE IS <= 120
		PRINTFORML 　（采伐的话会稍微费点功夫吧）
	CASE IS <= 180
		PRINTFORML 　（有点棘手啊…）
	CASE IS <= 240
		PRINTFORML 　（这个的话就比较辛苦了…）
	CASE IS <= 300
		PRINTFORML 　（这是得站稳了腰慢慢干的等级…）
	CASE IS <= 360
		PRINTFORML 　（会变成一个很大的工程…）
	CASEELSE
		PRINTFORML 　（以现在的水平的话还是太困难了…）
ENDSELECT
RESETCOLOR
PRINTL 
;体力・气力が实施后减少になる场合は、木材种选择に返回
IF (!FLAG:70 && (BASE:MASTER:体力 <= 减少值 || BASE:MASTER:气力 <= 减少值)) || (FLAG:70 && BASE:MASTER:TSP <= (采伐时间 * 10) + (探索回数 * 5))
	PRINTFORMW 想要采伐这个的话、现在的条件不太方便……还是找别的吧
	PRINTL 
	探索回数 ++
	;探索回数が10回を超えた场合は无限ループ除けのためにリターンする
	IF 探索回数 >= 10
		PRINTFORMW ……太投入的话可能会迷失其中的、这次就到此为止吧
		IF !FLAG:70
			TIME += 探索回数 * 5
		ELSE
			BASE:MASTER:TSP -= 探索回数 * 5
		ENDIF
		RETURN -1
	ENDIF
	GOTO LOOP_SELECT
ENDIF
PRINTFORML 要采伐这个吗？
CALL ASK_YN
IF !RESULT
	CLEARLINE 4
ELSE
	CLEARLINE 8
	探索回数 ++
	GOTO LOOP_SELECT
ENDIF
CALL DQPRINT("你砍着树ー", 1)
CALL DQPRINT("嘿~嘿~吼~", 1)
CALL DQPRINT("嘿~嘿~吼~", 1)
SIF 采伐时间 >= 540
	CALL DQPRINT("………………………", 1)
SIF 采伐时间 >= 480
	CALL DQPRINT("……………………", 1)
SIF 采伐时间 >= 420
	CALL DQPRINT("…………………", 1)
SIF 采伐时间 >= 360
	CALL DQPRINT("………………", 1)
SIF 采伐时间 >= 300
	CALL DQPRINT("……………", 1)
SIF 采伐时间 >= 240
	CALL DQPRINT("…………", 1)
CALL DQPRINT("………", 1)
CALL DQPRINT("……", 1)

;时间经过と体力消费
IF FLAG:70
	BASE:MASTER:TSP -= (采伐时间 * 10) + (探索回数 * 5)
ELSE
	DOWNBASE:MASTER:体力 = 减少值
	DOWNBASE:MASTER:气力 = 减少值
	TIME += 采伐时间 + (探索回数 * 10)
ENDIF

;経験追加处理
EXP:MASTER:采伐经验 += MAX((采伐时间 / 20), 1)
;试行回数が少ないだろうから确定
SIF ITEM:伐木心得
	EXP:MASTER:采伐经验 ++
;大きさ补正
SIF 大きさ
	EXP:MASTER:采伐经验 += (大きさ * 4)
;手传ってくれた角色の経験
IF CHARA
	EXP:CHARA:采伐经验 += MAX((采伐时间 / 20), 1)
	SIF 大きさ
		EXP:CHARA:采伐经验 += (大きさ * 4)
ENDIF

;约会经验
IF CHK_DATENOW(CFLAG:CHARA:约会中) && CHARA > 0
	EXP:MASTER:约会经验 ++
	EXP:CHARA:约会经验 ++
ENDIF
SETCOLOR 0x00FF00
;道具入手处理
SELECTCASE 木材种
	CASE 12, 646
		IF 木材种 == 12
			;竹だけはすぐに制材番号に变换 FLAG:原木种类には记录しない
			PRINTFORMDL 时间停止能力的应用、瞬 时 干 燥！
			PRINTFORMW 获得了竹制的材料
			LOG_SALES:12 ++
		ELSE
			;香木ほかの处理
			PRINTFORMW 获得了%WOOD_NAME(木材种)%
			SIF ITEM:木材种 < 99
				ITEM:木材种 ++
		ENDIF
	CASEELSE
		;伤んだ木は即座に木材に变换(未实装　貂ポ恶くなりそう)

		;普通の木に关してはまとめて
		ITEM:原木 ++
		FLAG:原木种类 = 木材种
		;大きさをFLAGに反映
		FLAG:原木种类 += 100 * 大きさ
		PRINTFORML 时间停止能力的应用、瞬 时 干 燥！
		PRINTFORMW 获得了%WOOD_NAME(FLAG:原木种类)%的原木

ENDSELECT
RESETCOLOR
;デ背包用
;IF FLAG:原木种类
;	PRINTFORMW 　　确认：%WOOD_NAME(FLAG:原木种类)%
;ENDIF
PRINTL 

;薪割り确认 薪割り时间・体力消费はサービス
IF FLAG:原木种类
	CALL WOOD_CHOPPING(FLAG:原木种类)
ENDIF

;オマケ采集物  采集Lvに依存  あくまでおまけなので控えめに
采集パワー = TALENT:MASTER:采集Lv * 10 + ABL:MASTER:教养 * 5 + TFLAG:幸运补正 * 3
SIF CHARA && !FLAG:70
	采集パワー += (10 + TALENT:CHARA:采集Lv * 10 + ABL:CHARA:教养 * 5) / 2

采集物 = 0
SELECTCASE RAND:(1 + 2 * SQRT(采集パワー))
	CASE 0 TO 6
		;失败
	CASE 7, 9, 11, 13, 15, 17, 19, 21, 23
		IF 采伐可能(CFLAG:MASTER:当前位置) == 2
			;竹林では何も手に入らない
		ELSEIF DAY:2 == 1 || DAY:2 == 2;春夏
			采集物 = 626;木莓
		ELSEIF DAY:2 == 3;秋
			SELECTCASE RAND:3;木莓、木通属、ぶどう
				CASE 0
					采集物 = 377
				CASE 1
					采集物 = 626
				CASEELSE
					采集物 = 628
			ENDSELECT
		ENDIF
		SIF 采集物
			PRINTFORMW 树上好像有什么东西…
	CASEELSE
		IF 采伐可能(CFLAG:MASTER:当前位置) == 2;春の竹林ではタケノコ
			SIF DAY:2 == 1
				采集物 = 629
		ELSEIF DAY:2 == 1 || DAY:2 == 3;春秋
			SELECTCASE RAND:2;普通的菌菇か山菜
				CASE 0
					采集物 = 600
				CASEELSE
					采集物 = 611
			ENDSELECT
		ELSEIF DAY:2 == 2;夏
			采集物 = 600;			普通的菌菇のみ
		ELSE
			SIF RAND:2 == 0;			冬はさらに50％の概率で冰之鳞
				采集物 = 609
		ENDIF
		SIF 采集物
			PRINTFORMW 地面上有什么东西…
ENDSELECT
IF 采集物
	SETCOLOR 0x00FF00
	PRINTFORMW 获得了%ITEMNAME:采集物%
	RESETCOLOR
	SIF ITEM:采集物 < 99
		ITEM:采集物 ++
	EXP:MASTER:采集经验 ++
	SIF CHARA 
		EXP:CHARA:采集经验 ++
ENDIF

;空腹时刻と参加者の疲劳度
TCVAR:MASTER:空腹时刻 -= 25
IF CHARA
	TCVAR:CHARA:空腹时刻 -= 25
	CALL ADD_TIRED(CHARA, POWER((大きさ + 1) + 1, 3) * 100)
ENDIF

RETURN 1

;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
;采伐
@COM_ABLE442
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(442)
	RETURN RESULT
SIF CFLAG:诶嘿嘿
	RETURN 0
;采伐可能场所でのみ
SIF !采伐可能(CFLAG:MASTER:当前位置)
	RETURN 0
;约会中は约会的对象とだけ
SIF FLAG:约会的对象 && TARGET != FLAG:约会的对象
	RETURN 0
;睡眠中
SIF CFLAG:MASTER:睡眠
	RETURN 0
;体力・气力不足
SIF BASE:MASTER:气力 < 800 || BASE:MASTER:体力 < 800
	RETURN 0
RETURN 1

;-----------------------------------------------------------
;当前位置から采伐スポットを割り出す函数
;-----------------------------------------------------------
@采伐可能(ARG)
#FUNCTION
IF !IN_HOME(ARG)
	SELECTCASE ARG
		CASE 妖精的大树
			RETURNF 1
		CASE 斜角的竹林
			RETURNF 2
		CASE 森林的入口
			RETURNF 3
		CASE 山麓树海
			RETURNF 4
		CASE 绝景之丘
			RETURNF 5
	ENDSELECT
ELSE
	SELECTCASE ARG
		CASE 25;镇守之森
			RETURNF 1
		CASE 433;斜角的竹林
			RETURNF 2
		CASE 507 ;菌菇群生地
			RETURNF 3
		CASE 702;山道・通往沼泽之道
			RETURNF 4
		CASE 805;山顶
			RETURNF 5
	ENDSELECT
ENDIF
RETURNF 0

;-----------------------------------------------------------
;采伐スポットから伐れる木材种を割り出す函数
;ARG 采伐可能
;-----------------------------------------------------------
@WOOD_SPECIES(ARG)
#FUNCTION
#DIM 候补

$LOOP

候补 = RAND:12
SELECTCASE ARG
	CASE 1;镇守之森、妖精的大树
		SIF GROUPMATCH(候补, 1, 2, 3, 6, 7, 8, 9)
			RETURNF 候补
	CASE 2;斜角的竹林
		;竹材に固定
		RETURNF 12
	CASE 3;魔法之森
		SIF 候补 == 0 && RAND:2 == 0
			RETURNF 646
		SIF GROUPMATCH(候补, 1, 2, 4, 6, 7, 8, 9)
			RETURNF 候补
	CASE 4;山麓树海
		SIF 候补 == 0 && RAND:2 == 0
			RETURNF 646
		SIF GROUPMATCH(候补, 1, 2, 3, 5, 7, 9, 10)
			RETURNF 候补
	CASE 5;绝景之丘 标高高め
		SIF GROUPMATCH(候补, 3, 5, 7, 9, 10, 11)
			RETURNF 候补
ENDSELECT
GOTO LOOP

;-----------------------------------------------------------
;原木・竹材の名称を返す函数
;ARG:0 …FLAG:原木种类　（木材种番号でも通る）
;ARG:1 =1 「の大木・の巨木」を文字列に反映しない
;      省略した场合は =0 (巨木・大木表示あり)
;RETURNF 1～ 普通の木 100～ 〇〇の大木 200～ 〇〇の巨木
;-----------------------------------------------------------
@WOOD_NAME(ARG:0, ARG:1 = 0)
#FUNCTIONS
#DIM 番号补正
#DIMS 仮名

IF ARG:0 <= 600
	番号补正 = ARG:0 % 100
ELSE
	番号补正 = ARG:0
ENDIF

SELECTCASE 番号补正
	CASE 646
		RETURNF "香木"
	CASEELSE
		仮名 = %WoodName:番号补正%
ENDSELECT

IF ARG:1 == 0
	SELECTCASE ARG:0
		CASE 101 TO 199
			仮名 += "の大木"
		CASE 201 TO 299
			仮名 += "の巨木"
	ENDSELECT
ENDIF
RETURNF 仮名
;-----------------------------------------------------------
;原木・竹材の采伐にかかる体力・气力を返す函数
;ARG:0 木材种番号	ARG:1 大きさ
;-----------------------------------------------------------
@WOOD_EFFORT(ARG:0, ARG:1)
#FUNCTION
#DIM ベース

SELECTCASE ARG:0
	CASE 646;香木
		ベース = 600
	CASEELSE
		ベース = WoodEffort:(ARG:0)
ENDSELECT

SELECTCASE ARG:1
	CASE 1;大木
		ベース += 700
	CASE 2;巨木
		ベース += 1000
ENDSELECT
RETURNF ベース

;-----------------------------------------------------------
;薪割りをするかどうかを确认して、木材を入手する
;原木を割る场合は体力・气力消费と时间の经过は别に记述をすること
;ARG 	FLAG:原木种类　で原木の薪割を行う
;		省略时は=0  一般薪割・原木无しでも可能・体力气力消费あり
;-----------------------------------------------------------
@WOOD_CHOPPING(ARG = 0)
#DIM 薪割り
#DIM 采伐パワー
#DIM 减少值
#DIM 采伐时间

IF ARG
	PRINTFORM 要把原木切割成木材吗？
	薪割り = (1 + ARG / 10) * 3 + RAND:5
; ELSE
; 	PRINTFORM 要砍成薪柴吗？
; 	薪割り = 3
ENDIF

IF (ITEM:木材 + 薪割り) > 99
	CALL COLORMESSAGE(@"　（木材已经多到拿不下了…）",C_GRAY,2)
ELSE
	PRINTW 
ENDIF
SIF ARG
	CALL COLORMESSAGE(@"　（注意：如果做成木材的话、就不能加工成制材了）",C_RED,1)
CALL ASK_YN
IF !RESULT
	PRINTFORMW 砍下来{薪割り}个薪柴
	IF ARG
		ITEM:原木 = 0
		FLAG:原木种类 = 0
	;一般薪割の场合は、体力气力时间消费
	ELSE
		采伐パワー = TALENT:MASTER:采伐Lv * 20
		减少值 = 350 - 采伐パワー
		采伐时间 = 180 - 采伐パワー
		IF ITEM:电锯
			减少值 /= 2
			采伐时间 /= 2
		ENDIF
		IF FLAG:70
			BASE:MASTER:TSP -= 减少值
		ELSE
			DOWNBASE:MASTER:体力 = 减少值
			IF ITEM:电锯
				DOWNBASE:MASTER:气力 = 减少值 - 50
			ELSE
				DOWNBASE:MASTER:气力 = 减少值 - 25
			ENDIF
			TIME += 采伐时间
		ENDIF
	ENDIF
	;木材数の增加处理
	ITEM:木材 = MIN(薪割り + ITEM:木材, 99)
ELSE
	IF ARG
		CLEARLINE 5
		PRINTFORMW 原木的话要么带去人间之里木材商那里、要么带去山里的制材所那里……
	ENDIF
ENDIF
PRINTL 

