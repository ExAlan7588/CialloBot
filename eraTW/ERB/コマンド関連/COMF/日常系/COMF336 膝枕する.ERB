;-------------------------------------------------
;膝枕
;日常系指令、レベル2
;-------------------------------------------------
@COM336
#DIM 恢复前体力
#DIM 恢复前气力
#DIM 恢复后体力
#DIM 恢复后气力
;膝枕
IF TIME_PROGRESS(TFLAG:膝枕した) < 45
	DRAWLINE
	PRINTFORMW 如果不动一下脚、过一段时间会发麻
	RETURN -1
ENDIF
IF TALENT:MASTER:体型 < TALENT:体型 - 1
	DRAWLINE
	PRINTFORMW 由于体格差别太大所以会很困难
	RETURN -1
ENDIF
LOCAL = ABL:亲密 * 10 + TALENT:恋慕 * 50 + TALENT:幼稚 * 30 - TALENT:羞耻心 * 20 - TALENT:自尊心 * 20
SIF BASE:体力 > MAXBASE:体力 * 4 / 5
	LOCAL -= 30
SIF BASE:体力 < MAXBASE:体力 * 2 / 5
	LOCAL += 30
SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = MAX(LOCAL,10)
		IF RAND:LOCAL > 120
			TFLAG:193 = 1
		ELSEIF RAND:LOCAL < 30
			TFLAG:193 = -1
		ELSE
			TFLAG:193 = 0
		ENDIF
	;强制成功or大成功
	CASE 1
		LOCAL = MAX(LOCAL,10)
		IF RAND:LOCAL > 120
			TFLAG:193 = 1
		ELSE
			TFLAG:193 = 0
		ENDIF
	;强制失败
	CASE -1
		TFLAG:193 = -1
	;指令结束
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT


DOWNBASE:气力 += 50

DOWNBASE:MASTER:气力 += 100


;固定で获得するソース
SOURCE:情爱 = 250
SOURCE:被动 = 70
SOURCE:反感 = 150
SIF TFLAG:193 == -1
	SOURCE:反感 += 150

;ABL:亲密をみる
IF ABL:亲密 <= 5
	SOURCE:情爱 += (ABL:亲密 * 25)
ELSEIF ABL:亲密 <= 8
	SOURCE:情爱 += 600 + (ABL:亲密 * 30)
	SOURCE:露出 += 200 + (ABL:欲望 * 15)
ELSEIF ABL:亲密 <= 11
	SOURCE:情爱 += 800 + (ABL:亲密 * 30)
	SOURCE:恭顺 += ABL:亲密 * 40
	SOURCE:露出 += 300 + (ABL:欲望 * 20)
ELSE
	SOURCE:情爱 += 1000 + (ABL:亲密 * 40)
	SOURCE:恭顺 += ABL:亲密 * 50
	SOURCE:露出 += 700 + (ABL:欲望 * 25)
ENDIF



SOURCE:被动 = 150 + 300 * ABL:顺从

IF TFLAG:193 == -1
	TIMES SOURCE:情爱 , 0.50
	TIMES SOURCE:被动 , 0.50
	TIMES SOURCE:露出 , 0.40
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:情爱 , 1.00
	TIMES SOURCE:被动 , 1.00
	TIMES SOURCE:露出 , 0.90
	TFLAG:情绪BOUNS = 30
ELSE
	TIMES SOURCE:情爱 , 1.50
	TIMES SOURCE:被动 , 2.00
	TIMES SOURCE:露出 , 1.80
	TIME += 20
	DOWNBASE:体力 -= 150
	DOWNBASE:气力 -= 300
	DOWNBASE:MASTER:气力 -= 50
	TFLAG:情绪BOUNS = 50
ENDIF
TIME += 20
CALL SET_DERAY(6,TARGET)
;膝枕フラグ
TFLAG:膝枕した = 1440 * DAY + TIME
RETURN 1


;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE336
;膝枕实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(336)
	RETURN RESULT
;亲热强度2
SIF TCVAR:MASTER:亲热 < 2
	RETURN 0
;室内のみ
SIF !(INROOM(CFLAG:MASTER:当前位置))
	RETURN 0
;澡堂场
SIF BATHROOM()
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;仕事中
SIF CFLAG:行动 == 5
	RETURN 0
;恶作剧中、陪睡中は无理
SIF CFLAG:恶作剧 || CFLAG:陪睡中
	RETURN 0
;气力0
SIF BASE:MASTER:气力 <= 0
	RETURN 0
;六九式体势
SIF TEQUIP:六九式
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
RETURN 1

