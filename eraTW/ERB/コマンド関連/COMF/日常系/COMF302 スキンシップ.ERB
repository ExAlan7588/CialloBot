;-------------------------------------------------
;身体接触
;日常系指令　レベル1
;-------------------------------------------------
@COM302

;魔改五重绝顶魔法EX模式触发开关
SIF Five_Orgasm_Magic >= 2
	CALL COM233

SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = RAND:100
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 99
			LOCAL:1 = 99
		IF LOCAL <= 9
			;10％で大成功
			TFLAG:193 = 1
			TFLAG:98 = 1
			IF CHK_DATENOW(CFLAG:TARGET:约会中)
				EXP:MASTER:约会经验 ++
				EXP:TARGET:约会经验 ++
			ENDIF
		ELSEIF LOCAL >= LOCAL:1
			;10～1％で失败
			TFLAG:193 = -1
			TFLAG:98 = -2
		ELSE
			;残りは成功
			TFLAG:193 = 0
		ENDIF
		;失败した场合でも、宴会の席では10%で成功
		SIF TFLAG:193 == -1 && RAND:100 <= 9 && CFLAG:350 == 49
			TFLAG:193 = 0
	;强制成功or大成功
	CASE 1
		LOCAL = RAND:100
		IF LOCAL <= 9
			;10％で大成功
			TFLAG:193 = 1
			TFLAG:98 = 1
			IF CHK_DATENOW(CFLAG:TARGET:约会中)
				EXP:MASTER:约会经验 ++
				EXP:TARGET:约会经验 ++
			ENDIF
		ELSE
			;残りは成功
			TFLAG:193 = 0
		ENDIF
	;强制失败
	CASE -1
		TFLAG:193 = -1
		TFLAG:98 = -1
	;指令结束
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT
;先制发生时は失败しない
SIF TCVAR:20
	TFLAG:193 = MAX(TFLAG:193,0)

DOWNBASE:气力 += 30
DOWNBASE:MASTER:气力 += 30


;固定で获得するソース
SOURCE:欢乐 = 100
SOURCE:情爱 = 100
SOURCE:露出 = 20
SOURCE:反感 = 50

;ABL:亲密をみる
IF ABL:亲密 <= 1
	SOURCE:情爱 += (ABL:亲密 * 30)
ELSEIF ABL:亲密 <= 3
	SOURCE:欢乐 += 100 + (ABL:亲密 * 40)
	SOURCE:情爱 += 200 + (ABL:亲密 * 30)
ELSEIF ABL:亲密 <= 5
	SOURCE:欢乐 += 400 + (ABL:亲密 * 50)
	SOURCE:情爱 += 400 + (ABL:亲密 * 30)
ELSEIF ABL:亲密 <= 8
	SOURCE:欢乐 += 700 + (ABL:亲密 * 60)
	SOURCE:情爱 += 600 + (ABL:亲密 * 40)
ELSEIF ABL:亲密 <= 11
	SOURCE:欢乐 += 1200 + (ABL:亲密 * 80)
	SOURCE:情爱 += 800 + (ABL:亲密 * 40)
ELSE
	SOURCE:欢乐 += 2000 + (ABL:亲密 * 50)
	SOURCE:情爱 += 1800 + (ABL:亲密 * 20)
ENDIF

;好感度をみる
IF CFLAG:2 <= 500
	SOURCE:欢乐 += CFLAG:2
ELSEIF CFLAG:2 <= 5000
	SOURCE:欢乐 += 500 + (CFLAG:2 - 500) / 3
ELSE
	SOURCE:欢乐 += 2000 + (CFLAG:2 - 5000) / 5
ENDIF
SIF SOURCE:欢乐 < 0
	SOURCE:欢乐 = 0

LOCAL = 0
;亲密が低いと好感度低下
IF ABL:亲密 == 0
	LOCAL -= 3
ELSEIF ABL:亲密 == 1
	LOCAL -= 2
ELSEIF ABL:亲密 == 2
	LOCAL -= 1
ENDIF
;好感度が低いと好感度低下
IF CFLAG:2 <= 50
	LOCAL -= 3
ELSEIF CFLAG:2 <= 100
	LOCAL -= 2
ELSEIF CFLAG:2 <= 250
	LOCAL -= 1
ENDIF


SOURCE:被动 = 100 + 200 * ABL:顺从
SOURCE:征服 = 100 + 200 * ABL:施虐属性

;好感度变化
TFLAG:97 += LOCAL

IF TFLAG:193 == -1
	TIMES SOURCE:欢乐 , 0.10
	TIMES SOURCE:情爱 , 0.50
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:被动 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:欢乐 , 1.00
	TIMES SOURCE:情爱 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:被动 , 1.00
	TFLAG:情绪BOUNS = 20
ELSE
	TIMES SOURCE:欢乐 , 2.00
	TIMES SOURCE:情爱 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:被动 , 2.00
	TFLAG:情绪BOUNS = 30
ENDIF
TIME += 10
IF CHK_DATENOW(CFLAG:TARGET:约会中)
	EXP:MASTER:约会经验 ++
	EXP:TARGET:约会经验 ++
ELSE
	;身体接触したフラグ
	TCVAR:303 = 1440 * DAY + TIME
ENDIF

RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE302
;身体接触实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(302)
	RETURN RESULT
;1时间间隔
SIF TIME_PROGRESS(TCVAR:303) < 60 && !CHK_DATENOW(CFLAG:约会中)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;陪睡中
SIF CFLAG:陪睡中
	RETURN 0
;仕事中
SIF CFLAG:行动 == 5
	RETURN 0
;气力0
SIF BASE:MASTER:气力 <= 0
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
RETURN 1

