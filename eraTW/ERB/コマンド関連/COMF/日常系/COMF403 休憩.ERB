;-------------------------------------------------
;休憩
;日常系指令、レベル1
;-------------------------------------------------
@COM403
#DIM 恢复前体力
#DIM 恢复前气力
#DIM 恢复后体力
#DIM 恢复后气力
#DIM 恢复率_体力, 4
#DIM 恢复率_气力, 4
#DIM 恢复率_精力, 4
#DIM Restore

恢复率_体力:0 = 100
恢复率_气力:0 = 200

恢复率_体力:1 = 125
恢复率_气力:1 = 225

恢复率_体力:2 = 150
恢复率_气力:2 = 250

恢复率_体力:3 = 175
恢复率_气力:3 = 275

恢复前体力 = BASE:MASTER:体力
恢复前气力 = BASE:MASTER:气力
IF NEMUKE() >= 960 && !CFLAG:MASTER:睡眠
	PRINTFORMW 本想小憩一下的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%就这样沉入了梦乡
	TFLAG:193 = -1
	CALL SET_SLEEP(1,MASTER,0)
	CFLAG:MASTER:就寝时间 = DAY * 1440 + TIME
	BEGIN AFTERTRAIN
ELSE
	IF CFLAG:MASTER:当前位置 == CFLAG:MASTER:初始位置 && ITEM:栎木沙发
		Restore = MIN(ITEM:栎木沙发,3)
	ELSE
		Restore = 0
	ENDIF
	;谁か连れてる时はこっち．
	;"午睡"からコピペしてるからこの分岐がおかしければごめんなさい
	IF TARGET > 0 && CFLAG:TARGET:6 > 0
		;吸血鬼または月光浴持ちで夜の间
		;または日光浴持ちで昼の间
		IF ((TALENT:吸血鬼 || TALENT:月光浴) && (TIME > 1140 || TIME < 360)) || (TALENT:日光浴 && (TIME > 360 && TIME < 1140))
			CALL RECOVER_PERMIL(TARGET,200,"体力",1)
			CALL RECOVER_PERMIL(TARGET,300,"气力",1)
		ELSEIF CHECK_CHARA(TARGET, "蓬莱人")
			CALL RECOVER_PERMIL(TARGET,250,"体力",1)
			CALL RECOVER_PERMIL(TARGET,350,"气力",1)
		ELSE
			CALL RECOVER_PERMIL(TARGET,恢复率_体力:Restore,"体力",1)
			CALL RECOVER_PERMIL(TARGET,恢复率_气力:Restore,"气力",1)
		ENDIF
	ENDIF
	IF CFLAG:MASTER:当前位置 == CFLAG:MASTER:初始位置 && ITEM:栎木沙发
		PRINTFORML 靠在沙发上、舒舒服服地躺着身子…
	ENDIF
	CALL RECOVER_PERMIL(MASTER,恢复率_体力:Restore,"体力",1)
	CALL RECOVER_PERMIL(MASTER,恢复率_气力:Restore,"气力",1)
ENDIF
TIME += 60
恢复后体力 = BASE:MASTER:体力
恢复后气力 = BASE:MASTER:气力
;DRAWLINE
;PRINTFORML 体力+{ABS(恢复后体力 - 恢复前体力)}（%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%）
;PRINTFORML 气力+{ABS(恢复后气力 - 恢复前气力)}（%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%）
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE403
;移动实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(403)
	RETURN RESULT
;停止中は不可
SIF FLAG:70 == 1
	RETURN 0
;自室、红魔馆、寺子屋、情侣酒店、待客室以外は不可
SIF CFLAG:MASTER:当前位置 != CFLAG:MASTER:311 && !休憩可(CFLAG:MASTER:当前位置)
	RETURN 0
;陪睡中
SIF CFLAG:陪睡中
	RETURN 0
;恶作剧中
SIF CFLAG:MASTER:恶作剧
	RETURN 0
SIF NEMUKE() >= 960
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
RETURN 1
