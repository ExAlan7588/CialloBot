;-------------------------------------------------
;换装
;日常系指令、レベル1
;-------------------------------------------------
@COM429
#DIM C_ID
#DIM ID_换装对象, 人物数量上限
#DIM 换装可能人数
#DIM L_ID ;使用列表のID
CALL MAKE_CHARA_LIST("可换装的角色列表", L_ID, ID_换装对象, 换装可能人数)
IF 换装可能人数 <= 1
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%换了衣服
	CALL 服装换装(MASTER)
	CALL CLOTHES_SETTING_TRAIN(MASTER)
	RETURN -1
ENDIF

;更衣对象の选择
;列表作成时に可否判定やってるのでここでは必要ないです
PRINTFORML 可以换衣服了
PRINTFORML 要给谁换衣服呢?
PRINTFORML 
CALL SIMPLE_CHARA_LIST(ID_换装对象, 换装可能人数)
;中止した
SIF RESULT == 999
	RETURN -1

;RESULTはすぐ腐るので
C_ID = ID_换装对象:(RESULT)
SIF FLAG:时间停止 == 1 && !(C_ID == MASTER)
	CFLAG:C_ID:时间停止换装 = 1
CALL 服装换装(C_ID)
CALL CLOTHES_SETTING_TRAIN(C_ID)
RETURN 0

SIF !FLAG:70
	TIME += 5
TFLAG:102 = 0

;CLOTHES_SETTING_TRAINに靴脱ぎ处理を移动させたので
;ここでCLOTHES_SETTING_TRAINすると靴を脱いでしまい、靴设定の保存ができません
;呼び出し元でCLOTHES_SETTING_TRAINすること
@服装换装(C_ID)
#DIM C_ID
#DIM LINESTART2

LINESTART = LINECOUNT

CALL SHOW_CLOTHES(C_ID, "现在的衣装")
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%换衣服中
PRINTFORML 
PRINTFORML [7] - 衣装确认
PRINTFORML [6] - 不穿衣服
PRINTFORML [5] - 指定喜爱的衣装
PRINTFORML [4] - 睡衣的变更
PRINTFORML [3] - 便装的变更
PRINTFORML [2] - 今天穿的衣服的变更
PRINTFORML [1] - 胖次
PRINTFORML 
PRINTFORML [0] - 返回
DO
	INPUT
	LINESTART2 = LINECOUNT
	SELECTCASE RESULT
	CASE 7
		CALL SHOW_CHARA_CLOTHES(C_ID, 0)
		CLEARLINE LINECOUNT - LINESTART
		RESTART
	CASE 6
	CLEARLINE LINECOUNT - LINESTART
	CALL SHOW_CLOTHES(C_ID, "套装")
		PRINTFORML 脱掉%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的衣服，要这么做吗？ 
		PRINTFORML [1] - 是
		PRINTFORML [0] - 否
		DO
			INPUT
			SELECTCASE RESULT
			CASE 1
			IF C_ID != 0
				PRINTFORML 用催眠术给%CALLNAME:C_ID%下了命令
				PRINTFORMW 从明天开始不准再穿衣服了
			ELSE
			PRINTFORMW 决定自己从明天开始不穿衣服了
			ENDIF
		    CALL CTRL_CLOTHES_SET(C_ID, "现在衣装の变更_全裸")
				PRINTFORML
				PRINTFORML 请问需要保留胖次吗？
			    PRINTFORML [1] - 是
		        PRINTFORML [0] - 否
		        INPUT
		        SELECTCASE RESULT
		        CASE 1
		        PRINTFORMW 那么胖次保留了
		        CASE 0
		        PRINTFORMW 那么明天开始%CALLNAME:C_ID%连胖次也不会穿了
		        CFLAG:C_ID:指定胖次 = 100
		        ENDSELECT  
				CALL DECIDE_CLOTHES(C_ID, "便装", "现在的衣装")
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
			CASEELSE
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			RESTART
		LOOP 1
	CASE 5
		$换装5
		CALL SHOW_CLOTHES(C_ID, "套装")
		PRINTFORML 设定%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的喜爱套装中
		PRINTFORML 
		PRINTFORML [4] - 指定现在的服装为套装
		PRINTFORML [3] - 穿设定好的套装
		PRINTFORML [2] - 穿和特定角色同款的衣服
		PRINTFORML [1] - 指定的复位
		PRINTFORML 
		PRINTFORML [0] - 放弃
		DO
			INPUT
			SELECTCASE RESULT
			CASE 4
				CALL DECIDE_CLOTHES(C_ID, "套装", "现在的衣装")
			CASE 3
				CALL COSPLAY_SET(C_ID, "套装")
			CASE 2
				CALL COSPLAY_CHARA(C_ID, "套装")
			CASE 1
				PRINTFORML 真的复位吗？
				PRINTFORML 
				PRINTFORML [1] - 复位
				PRINTFORML [0] - 放弃
				DO
					INPUT
					SELECTCASE RESULT
					CASE 1
						CALL CTRL_CLOTHES_SET(C_ID, "套装のリセット")
					CASE 0
						CLEARLINE LINECOUNT - LINESTART2
						GOTO 换装5
					CASEELSE
						CLEARLINE 1
						CONTINUE
					ENDSELECT
				LOOP 1
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
				RESTART
			CASEELSE
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			GOTO 换装5
		LOOP 1
	CASE 4
		IF C_ID == MASTER
			CLEARLINE LINECOUNT - LINESTART
			RESTART
		ENDIF
		$换装4
		CALL SHOW_CLOTHES(C_ID, "今天的睡衣")
		PRINTFORML 设定%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的睡衣中
		PRINTFORML 
		PRINTFORML [5] - 随你喜欢
		PRINTFORML [4] - 睡觉的服装
		PRINTFORML [3] - 指定睡衣
		PRINTFORML [2] - 裸睡
		PRINTFORML [1] - 只穿内裤就睡觉
		PRINTFORML 
		PRINTFORML [0] - 放弃
		DO
			INPUT
			SELECTCASE RESULT
			CASE 5
				CFLAG:C_ID:睡衣指定 = 0
			CASE 4
				SIF CFLAG:C_ID:套装
					CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_套装")
			CASE 3
				CALL COSPLAY_SELECT(C_ID, "睡衣", "睡衣セット")
			CASE 2
				CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_全裸")
			CASE 1
				CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_针织衫")
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
				RESTART
			CASEELSE
				CLEARLINE 1
				CONTINUE
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			GOTO 换装4
		LOOP 1
	CASE 3
		$换装3
		CALL SHOW_CLOTHES(C_ID, "便服")
		PRINTFORML 设定%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的便装中
		PRINTFORML 
		PRINTFORML [4] - 指定现在的服装（为发生而需要而被重写）
		PRINTFORML [3] - 穿设定好的套装
		PRINTFORML [2] - 穿和特定角色同款的衣服
		PRINTFORML [1] - 由本人决定
		PRINTFORML 
		PRINTFORML [0] - 放弃
		DO
			INPUT
			SELECTCASE RESULT
			CASE 4
				CALL DECIDE_CLOTHES(C_ID, "便装", "现在的衣装")
			CASE 3
				CALL COSPLAY_SET(C_ID, "便装")
			CASE 2
				CALL COSPLAY_CHARA(C_ID, "便装")
			CASE 1
				PRINTFORMW 便服的设定交给本人了
				CFLAG:C_ID:基本服装SET = 0
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
				RESTART
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			GOTO 换装3
		LOOP 1
	CASE 2
		$换装2
		;#;PRINTFORML CFLAG:C_ID:衣物暂时更换{CFLAG:C_ID:衣物暂时更换}
		CALL SHOW_CLOTHES(C_ID, "现在的衣装")
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的现在穿的衣服设定中
		PRINTFORML 
		PRINTFORML [4] - 把衣服换成服装
		PRINTFORML [3] - 穿设定好的套装
		PRINTFORML [2] - 穿和特定角色同款的衣服
		PRINTFORML [1] - 回到今天的默认
		PRINTFORML 
		PRINTFORML [0] - 放弃
		DO
			INPUT
			SELECTCASE RESULT
			CASE 4
				IF !CFLAG:C_ID:套装
					CLEARLINE LINECOUNT - LINESTART2
					GOTO 换装2
				ENDIF
				CALL DECIDE_CLOTHES(C_ID, "现在的衣装", "套装")
			CASE 3
				CALL COSPLAY_SET(C_ID, "现在的衣装")
			CASE 2
				CALL COSPLAY_CHARA(C_ID, "现在的衣装")
			CASE 1
				PRINTFORMW 把今天的服装改了
				CALL CTRL_CLOTHES_SET(C_ID, "现在衣装の变更_便装")
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
				RESTART
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			GOTO 换装2
		LOOP 1
	CASE 1
		$换装1
		PRINTFORML 斟酌%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的胖次中
		PRINTFORML   现在穿着的胖次：%GET_STR(C_ID, "下半身内衣_ずらし可能", EQUIP:C_ID:下半身内衣２, "名前")%
		SELECTCASE CFLAG:C_ID:指定胖次
		CASE 0
			PRINTFORML 　现在的胖次设定：无指定
		CASE 100
			PRINTFORML 　现在的胖次设定：指定不穿
		CASEELSE
			PRINTFORML 　现在的胖次设定：%GET_STR(C_ID, "下半身内衣_ずらし可能", CFLAG:C_ID:指定胖次, "名前")%
		ENDSELECT
		PRINTFORML 
		PRINTFORML 
		SIF C_ID == MASTER
			PRINTFORML [3] - 脱掉正在穿的胖次
		PRINTFORML [2] - 现在的内裤设定为喜欢的胖次
		PRINTFORML [1] - 解除喜欢的胖次
		PRINTFORML 
		PRINTFORML [0] - 放弃
		DO
			INPUT
			SELECTCASE RESULT
			CASE 3
				PRINTFORMW 脱掉正在穿的胖次
				EQUIP:C_ID:下半身内衣２ = 100
			CASE 2
				PRINTFORMW 指定喜欢的胖次
				CFLAG:C_ID:指定胖次 = EQUIP:C_ID:下半身内衣２
				SIF !CFLAG:C_ID:指定胖次
					CFLAG:C_ID:指定胖次 = 100
			CASE 1
				PRINTFORMW 解除喜欢的胖次了
				CFLAG:C_ID:指定胖次 = 0
			CASE 0
				CLEARLINE LINECOUNT - LINESTART
				RESTART
			ENDSELECT
			CLEARLINE LINECOUNT - LINESTART2
			GOTO 换装1
		LOOP 1
	CASE 0
		RETURN
	CASEELSE
		CLEARLINE 1
		CONTINUE
	ENDSELECT
LOOP 1



;模倣させる对象角色の选择
@COSPLAY_CHARA(C_ID, TYPE)
#DIM  C_ID
#DIMS TYPE
#DIM  COS_CHARA
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的%TYPE%设定中
PRINTFORML 和谁同款呢？
CALL SHOW_CHARA_LIST("可以穿同款衣服的角色列表", "体力气力")
COS_CHARA = RESULT
IF GROUPMATCH(COS_CHARA, 0, 999)
	PRINTFORMW 服装的设定中断了
	RETURN
ELSEIF COS_CHARA == C_ID
	SELECTCASE TYPE
	CASE "套装"
		PRINTFORMW 服装的设定中断了
	CASE "便装"
		PRINTFORMW 便服的设定交给本人了
		CFLAG:C_ID:基本服装SET = 0
	CASE "现在的衣装"
		PRINTFORMW 把今天的服装改了
		CALL CTRL_CLOTHES_SET(C_ID, "现在衣装の变更_便装")
	ENDSELECT
	RETURN
ENDIF
CALL DECIDE_CLOTHES(C_ID, TYPE, "角色便装", COS_CHARA)
SIF RESULT
	RESTART


;衣装セットのカテゴリ选择
@COSPLAY_SET(C_ID, TYPE)
#DIM  C_ID
#DIM  SET_ID
#DIMS TYPE
#DIMS CATEGORY
CALL SHOW_CLOTHES(C_ID, TYPE)
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的%TYPE%设定中
PRINTFORML 服装的种类选择
PRINTFORML 
PRINTFORML [3] - 基本套装
PRINTFORML [2] - cosplay服装
PRINTFORML [1] - 角色特殊服装集
PRINTFORML 
PRINTFORML [0] - 放弃
DO
	INPUT
	SELECTCASE RESULT
	CASE 3
		CATEGORY '= "基本套装"
	CASE 2
		CATEGORY '= "cosplay服装"
	CASE 1
		CATEGORY '= "角色特殊服装集"
	CASE 0
		RETURN
	CASEELSE
		REUSELASTLINE 无效输入
		CONTINUE
	ENDSELECT
	BREAK
LOOP 1 
CALL COSPLAY_SELECT(C_ID, TYPE, CATEGORY)
SIF !RESULT
	RESTART


;个々の衣装セットの选择
@COSPLAY_SELECT(C_ID, TYPE, CATEGORY)
#DIM  C_ID
#DIM  SET_ID
#DIMS TYPE
#DIMS CATEGORY
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的%TYPE%设定中（%CATEGORY%）
PRINTFORML 选择使用的服装套装
PRINTFORML 
CALL CLASS_LIST("衣装セット", CATEGORY)
PRINTFORML 
PRINTFORML [0] - 放弃
DO
	INPUT
	SET_ID = RESULT
	SELECTCASE SET_ID
	CASE 0
		RETURN
	CASEELSE
		SIF GET_INT(0, "衣装セット" , SET_ID, CATEGORY)
			BREAK
		REUSELASTLINE 无效输入
		CONTINUE
	ENDSELECT
LOOP 1 
CALL DECIDE_CLOTHES(C_ID, TYPE, "衣装セット", SET_ID)
SIF RESULT
	RESTART



;衣装决定の最终确认
@DECIDE_CLOTHES(C_ID, COS_SAVE, COS_LOAD, SET_ID)
#DIM  C_ID     ;设定する角色
#DIMS COS_SAVE ;衣装の保存先
#DIMS COS_LOAD ;衣装の参照元
#DIM  SET_ID   ;参照衣装等のID
;#; PRINTFORML C_ID{C_ID}, COS_SAVE%COS_SAVE%, COS_LOAD%COS_LOAD%, SET_ID{SET_ID}
PRINTFORMW 最终确认
CALL SHOW_CLOTHES(C_ID, COS_LOAD, SET_ID)
PRINTFORML 
PRINTFORML 这套服装可以么？
PRINTFORML 
PRINTFORML [1] - 决定
PRINTFORML [0] - 再考虑考虑
PRINTFORML 
PRINTFORML [9] - 放弃
DO
	INPUT
	SELECTCASE RESULT
	CASE 1
		BREAK
	CASE 0
		RETURN 1
	CASE 9
		RETURN
	CASEELSE
		REUSELASTLINE 无效输入
		CONTINUE
	ENDSELECT
LOOP 1


SELECTCASE COS_LOAD
CASE "现在的衣装"
	SELECTCASE COS_SAVE
	CASE "便装"
		PRINTFORMW 现在穿的服装以后每天都会穿了
		CFLAG:C_ID:基本服装SET = 100
		CALL CTRL_CLOTHES_SET(C_ID, "现在衣装の保存_おきにいり")
	CASE "套装"
		PRINTFORMW 现在穿的衣服放进指定了
		CALL CTRL_CLOTHES_SET(C_ID, "现在衣装の保存_おきにいり")
	ENDSELECT
	
CASE "衣装セット"
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的%COS_SAVE%指定为%GET_STR(C_ID, "衣装セット", SET_ID, "名前")%
	SELECTCASE COS_SAVE
	CASE "现在的衣装"
		CALL CTRL_CLOTHES_SET(C_ID, "现在衣装の变更_衣装セット", SET_ID)
	CASE "便装"
		CALL CTRL_CLOTHES_SET(C_ID, "便装の变更_衣装セット", SET_ID)
	CASE "套装"
		CALL CTRL_CLOTHES_SET(C_ID, "套装の变更_衣装セット", SET_ID)
	CASE "睡衣"
		CALL CTRL_CLOTHES_SET(C_ID, "睡衣的变更_衣装セット", SET_ID)
	ENDSELECT

CASE "角色便装"
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%穿上和%NAME_OUTPUT_TRANSLATION("CALLNAME", SET_ID)%同样的衣服了
	SELECTCASE COS_SAVE
	CASE "现在的衣装"
		CALL CTRL_CLOTHES_SET(C_ID, "现在衣装の变更_角色衣装", SET_ID, "便装")
	CASE "便装"
		CALL CTRL_CLOTHES_SET(C_ID, "便装の变更_角色衣装", SET_ID, "便装")
	CASE "套装"
		CALL CTRL_CLOTHES_SET(C_ID, "套装の变更_角色衣装", SET_ID, "便装")
	ENDSELECT

ENDSELECT



;换装
@COM_ABLE429
;移动实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(429)
	RETURN RESULT
;私室设定
SIF (CFLAG:MASTER:当前位置 != CFLAG:MASTER:311) && (CFLAG:当前位置 != CFLAG:311)
	RETURN 0
SIF !GETBIT(CFLAG:TARGET:既成事实, 1) && TARGET
	RETURN 0
SIF CFLAG:诶嘿嘿
	RETURN 0
RETURN 1
