;-------------------------------------------------
;祈愿
;日常系コマンド、レベル1
;-------------------------------------------------
@COM408
#DIM C_ID
PRINTFORM 当前住在
SETCOLOR 219,90,107
SIF CFLAG:MASTER:当前位置 > P60大客厅
	PRINTFORM {CFLAG:MASTER:当前位置-60}号房-
PRINTFORM %NAME_FROM_PLACE(CFLAG:MASTER:当前位置)%
RESETCOLOR
PRINTFORM 的角色,
SIF CFLAG:MASTER:初始位置 == CFLAG:MASTER:当前位置
	PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%也住在这里,
PRINTFORML 点击可以\@CFLAG:MASTER:当前位置==59 ? 释放她们 #让她们搬出去\@...

FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:神社在住 == CFLAG:MASTER:当前位置
		PRINTFORMLC [{LOCAL,3}] %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL),12,LEFT%
NEXT
PRINTL
DRAWLINEFORM －
;SIF TALENT:MASTER:魔王
;	PRINTFORML 尊敬的魔王大人,邀请谁呢...
FOR LOCAL,1,CHARANUM
	SIF !CFLAG:LOCAL:出禁 && !TALENT:LOCAL:路人 && !CFLAG:LOCAL:神社在住
		PRINTFORMLC [{LOCAL,3}] %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL),12,LEFT%
NEXT
PRINTL 
DRAWLINEFORM － 
SIF CFLAG:MASTER:初始位置 != CFLAG:MASTER:当前位置
	PRINTFORMLC [200] - 我也要住在这里
PRINTFORMLC [233] - 还是算了
INPUT
SETCOLOR 219,90,10
IF RESULT == 200
	CFLAG:MASTER:初始位置 = CFLAG:MASTER:当前位置
	PRINTFORML 把家搬到了{CFLAG:MASTER:当前位置}%NAME_FROM_PLACE(CFLAG:MASTER:当前位置)%...
	SIF CFLAG:MASTER:初始位置 == 60
		PRINTFORML 是大卧室的床不够软吗?居然要睡大厅!!!
ENDIF

IF !INRANGE(RESULT,1,CHARANUM)
	RESETCOLOR
	RETURN -1
ELSE
	C_ID = RESULT
	IF CFLAG:C_ID:神社在住 > 0
		CFLAG:C_ID:神社在住 = 0
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID),10,RIGHT%离开了神社{CFLAG:MASTER:当前位置}%NAME_FROM_PLACE(CFLAG:MASTER:当前位置)%...
	ELSE

			IF !CFLAG:C_ID:睡眠
				CFLAG:C_ID:当前位置 = CFLAG:MASTER:当前位置
				PRINTFORML 受到了邀请,%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%很高兴就赶来了...
			ENDIF
			CFLAG:C_ID:神社在住 = CFLAG:MASTER:当前位置
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID),10,RIGHT%把家搬到了{CFLAG:MASTER:当前位置}%NAME_FROM_PLACE(CFLAG:MASTER:当前位置)%...
			SIF CFLAG:MASTER:当前位置 == P60大客厅
				PRINTFORML 居然忍心让%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%睡大厅???你这个人渣!!!

	ENDIF
ENDIF
RESETCOLOR
RESTART


@COM_ABLE408
SIF !TFLAG:100
	RETURN 0
SIF !AT_HOME(MASTER)
	RETURN 0
;SIF !INRANGE(CFLAG:MASTER:当前位置, 58, 72)
;	RETURN 0
SIF !GROUPMATCH(CFLAG:MASTER:当前位置,P58大主卧,P60大客厅,P61地の间,P62水の间,P63火の间,P64风の间,P65光の间,P66暗の间)
	RETURN 0
SIF CFLAG:MASTER:诶嘿嘿
	RETURN 0
RETURN 1
