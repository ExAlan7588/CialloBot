;-------------------------------------------------
;摸屁股
;日常系指令　レベル1
;-------------------------------------------------
@COM310
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 10
ELSE
	LOCAL = RAND:110
	LOCAL:1 = MIN(109, 90 + GET_SUCCESS_RATE() / 5)
	CALL KOJO_FORCED_FAILURE(LOCAL, LOCAL:1)
	SIF TFLAG:193 == -2
		RETURN -1
	DOWNBASE:气力 += 50
	DOWNBASE:MASTER:气力 += 50
	TFLAG:理性削り = 1
	;信赖
	TFLAG:98 = -3

	;固定で获得するソース
	SOURCE:欢乐 = 100
	SOURCE:情爱 = 100
	SOURCE:露出 = 200
	SOURCE:反感 = 500
	SOURCE:情欲 = 100
	SOURCE:被动 = 100

	;ABL:亲密をみる
	IF ABL:亲密 <= 5
		SOURCE:反感 -= (ABL:亲密 * 5)

	ELSEIF ABL:亲密 <= 7
		SOURCE:欢乐 += 400 + (ABL:亲密 * 5)
		SOURCE:情爱 += 400 + (ABL:亲密 * 10)
		SOURCE:反感 -= (ABL:亲密 * 10)
		TFLAG:193 = RAND:2 + 2
	ELSEIF ABL:亲密 <= 9
		SOURCE:欢乐 += 700 + (ABL:亲密 * 10)
		SOURCE:情爱 += 600 + (ABL:亲密 * 15)
		SOURCE:反感 -= (ABL:亲密 * 15)
		SOURCE:情欲 += 200 * ABL:欲望
		SOURCE:被动 += 200 * ABL:顺从
		TFLAG:193 = RAND:2 + 2
	ELSEIF !TALENT:恋慕
		SOURCE:欢乐 += 1200 + (ABL:亲密 * 20)
		SOURCE:情爱 += 800 + (ABL:亲密 * 25)
		SOURCE:反感 -= (ABL:亲密 * 20)
		SOURCE:情欲 += 250 * ABL:欲望
		SOURCE:被动 += 250 * ABL:顺从
		TFLAG:193 = RAND:2 + 2
	ELSE
		SOURCE:欢乐 += 2000 + (ABL:亲密 * 20)
		SOURCE:情爱 += 1800 + (ABL:亲密 * 30)
		SOURCE:情欲 += 1000 + 250 * ABL:欲望
		SOURCE:被动 += 1000 + 250 * ABL:顺从
		SOURCE:反感 -= (ABL:亲密 * 50)
		TFLAG:193 = RAND:2 + 3
	ENDIF
	SIF SOURCE:反感 < 0
		SOURCE:反感 = 0

	SIF SOURCE:欢乐 < 0
		SOURCE:欢乐 = 0
	;先制发生时は失败しない
	SIF TFLAG:102 == 3 && TFLAG:192 != -1
		TFLAG:193 = MAX(TFLAG:193,2)

	LOCAL = 0
	;亲密が低いと好感度低下
	IF ABL:亲密 <= 5
		LOCAL -= 10
	ELSEIF ABL:亲密 <= 7
		LOCAL -= 5
	ELSEIF ABL:亲密 <= 9
		LOCAL -= 3
	ELSEIF TALENT:恋慕
		LOCAL -= 1
	ENDIF
	;好感度变化
	TFLAG:97 += LOCAL
	IF HITOKE(1)
		TIMES SOURCE:露出 , 2.00
		TIMES SOURCE:反感 , 2.00
	ENDIF
	IF TFLAG:193 == 1
		TIMES SOURCE:欢乐 , 0.10
		TIMES SOURCE:情爱 , 0.50
		TIMES SOURCE:被动 , 0.50
		TIMES SOURCE:情欲 , 0.50
		TIMES SOURCE:反感 , 1.50
	ELSEIF TFLAG:193 == 2
		TIMES SOURCE:欢乐 , 1.00
		TIMES SOURCE:情爱 , 1.00
		TIMES SOURCE:被动 , 1.00
		TIMES SOURCE:情欲 , 1.00
	ELSEIF TFLAG:193 == 3
		TIMES SOURCE:欢乐 , 1.50
		TIMES SOURCE:情爱 , 1.50
		TIMES SOURCE:被动 , 1.50
		TIMES SOURCE:情欲 , 1.50
	ELSE
		TIMES SOURCE:欢乐 , 2.00
		TIMES SOURCE:情爱 , 2.00
		TIMES SOURCE:被动 , 2.00
		TIMES SOURCE:情欲 , 2.00
		TIMES SOURCE:反感 , 0.75
	ENDIF
	TIME += 10
ENDIF
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE310
;摸屁股实行判定
SIF !TFLAG:100
	RETURN 0
SIF !TARGET
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(310)
	RETURN RESULT
;时间停止中はスキップ
IF !FLAG:70
	SIF CFLAG:睡眠 == 0 && GET_TARGETNUM() == 0
		RETURN 0
	;睡眠中
	SIF CFLAG:睡眠 && !CFLAG:恶作剧
		RETURN 0
	;仕事中
	SIF CFLAG:行动 == 5 && !(TARGET == [[美宵]])
		RETURN 0
	;陪睡中
	SIF CFLAG:陪睡中
		RETURN 0
	;约会中
	SIF CHK_DATENOW(CFLAG:约会中) && CFLAG:当前位置 != OMANEKIBEYA()
		RETURN 0
	;甲斐性无しにそんな度胸はない
	SIF FLAG:甲斐性无 && !CFLAG:诶嘿嘿
		RETURN 0
ENDIF
;气力0
SIF BASE:MASTER:气力 <= 0
	RETURN 0
;腕使用中
SIF TFLAG:61
	RETURN 0
SIF GETBIT (TCVAR:26,1)
	RETURN 0
RETURN 1

@KOJO_FORCED_FAILURE(ARG,ARG:1)
SELECTCASE TFLAG:192
	;通常は概率判定
	CASE 0
		IF ARG <= 9
			;大成功
			TFLAG:193 = 3
		ELSEIF ARG >= ARG:1
			;失败
			TFLAG:193 = 1
		ELSE
			;残りは成功
			TFLAG:193 = 2
		ENDIF
	;强制失败
	CASEELSE
		TFLAG:193 = TFLAG:192
ENDSELECT
