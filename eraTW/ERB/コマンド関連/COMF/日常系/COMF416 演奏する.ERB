;-------------------------------------------------
;演奏
;日常系指令、レベル1
;-------------------------------------------------
@COM416
#DIM DYNAMIC NOW_TARGET
#DIM DYNAMIC NUM_TARGET
#DIM DYNAMIC MUSIC_BONUS
#DIM DYNAMIC BAND_BONUS
#DIM DYNAMIC DANCE_BONUS
#DIM OHINERI
#DIM INTERRUPTED
#DIM OMAKE, 20 = 40,120,195,212,213,214,215,216,500,510,520,521,600,601,610,611,612,636,642,706
#DIM CHOSEN_OMAKE

TFLAG:使用乐器 = 0
IF 宴会场所在判定(MASTER)
;IF CFLAG:MASTER:当前位置 == TFLAG:宴会场 && MASTER != TARGET && DAY >= FLAG:开始日 && TIME >= FLAG:33 && FLAG:参加人数
	IF ABL:MASTER:音乐技能 < 3
		PRINTL 对现在的音乐技能是否能达到宴会演奏的相応水平还不是很有自信……
		RETURN -1
	ELSEIF ABL:MASTER:音乐技能 <= ABL:TARGET:战斗能力 - TALENT:TARGET:心情
		PRINTFORML 如果演奏不好的话会被%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%\@ TALENT:TARGET:心情 < 0 ? 生气地 # \@扔石头的
	ELSEIF ABL:MASTER:音乐技能 < 5
		PRINTL 演奏不好的话大概会有石头飞过来吧
	ELSE
		PRINTL 要用哪种乐器给宴会助兴呢
	ENDIF
ELSE
	PRINTL 要使用什么乐器呢……
ENDIF
{
	CALL ASK_M(
		"返回", 1,
		ITEMNAME:30, MUSIC_ABLE(30),
		ITEMNAME:31, MUSIC_ABLE(31),
		ITEMNAME:32, MUSIC_ABLE(32),
		ITEMNAME:33, MUSIC_ABLE(33),
		ITEMNAME:34, MUSIC_ABLE(34)
	)
}
TFLAG:使用乐器 = RESULT
SIF ITEM:演奏指南书 && !RAND:2
	EXP:MASTER:演奏经验 += 2
SIF !TFLAG:使用乐器
	RETURN -1
INTERRUPTED = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:当前位置 != CFLAG:MASTER:当前位置
		CONTINUE
	SIF CFLAG:LOCAL:行动 != 5
		CONTINUE
	IF !GROUPMATCH(CFLAG:LOCAL:职业种类, JOB_音乐, JOB_游び, JOB_宴会参加, JOB_醉いつぶれる)
		PRINTFORML 「吵死了！」
		CALL THROW_STONE(LOCAL)
	ELSEIF CFLAG:LOCAL:职业种类 == JOB_宴会参加 && !ACCOMPANY_ABLE(LOCAL)
		EXP:MASTER:演奏经验 ++
		SIF ABL:LOCAL:战斗能力 >= 4
			EXP:MASTER:演奏经验 ++
		IF ABL:MASTER:音乐技能 + TALENT:LOCAL:心情 >= ABL:LOCAL:战斗能力
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%%TEXTR("陶醉于你的演奏/双眼闪着光/称赞了你的演奏")%
			OHINERI = MAX(RAND:(ABL:LOCAL:战斗能力 + 1), 1)
			MONEY:2 += OHINERI
			PRINTPLAIN *叮啷*
			CALL COLORMESSAGE(@"被投了{OHINERI}个筹码过来！",C_YELLOW,1)
			IF !RAND:7
				IF !RAND:2
					;落空了くじ
					CHOSEN_OMAKE = 131
				ELSE
					CHOSEN_OMAKE = OMAKE:(RAND:20)
				ENDIF
				CALL COLORMESSAGE(@"%ITEMNAME:(CHOSEN_OMAKE)%被扔了过来！",C_YELLOW,1)
				ITEM:CHOSEN_OMAKE ++
			ENDIF
		ELSE
			PRINTFORML 「%TEXTR("吵死了！/滚蛋！/真烂！")%」
			CALL THROW_STONE(LOCAL)
		ENDIF
	ENDIF
	SIF RESULT == -1
		INTERRUPTED ++
NEXT
IF INTERRUPTED
	PRINTFORML 「真是严格的客人啊」
	CALL COLORMESSAGE(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%中断了演奏",C_YELLOW,1)
	RETURN -1
ENDIF

SELECTCASE TFLAG:192
	;通常
	CASE 0
		LOCAL = RAND:100
		LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
		SIF LOCAL:1 > 99
			LOCAL:1 = 99
		IF LOCAL <= (9 + ABL:MASTER:音乐技能)
			;10％で大成功
			EXP:MASTER:演奏经验 ++
			EXP:MASTER:演奏经验 += 5
			TFLAG:193 = 1
		ELSEIF LOCAL >= LOCAL:1
			;10～1％で失败
			TFLAG:193 = -1
		ELSE
			;残りは成功
			EXP:MASTER:演奏经验 ++
			TFLAG:193 = 0
		ENDIF
	;强制成功or大成功
	CASE 1
		IF RAND:100 <= (9 + ABL:MASTER:音乐技能)
			;10％で大成功
			EXP:MASTER:演奏经验 ++
			EXP:MASTER:演奏经验 += 5
			TFLAG:193 = 1
		ELSE
			;残りは成功
			EXP:MASTER:演奏经验 ++
			TFLAG:193 = 0
		ENDIF
	;强制失败
	CASE -1
		TFLAG:193 = -1
	;指令结束
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT


DOWNBASE:MASTER:体力 = 300
DOWNBASE:MASTER:气力 = 300

;その场に居るTARGET全员で回す
NOW_TARGET = TARGET
NUM_TARGET = GET_TARGETNUM()
CVARSET TCVAR, GETNUM(TCVAR, "演奏参加"), 0

FOR LOCAL, 1, NUM_TARGET + 1
	SIF !SHIRAHU(TARGET:LOCAL)
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM416_1
	SELECTCASE TCVAR:演奏参加
	CASE 1, 2
		MUSIC_BONUS += ABL:音乐技能 + 2 * TALENT:音感
	CASE 3
		DANCE_BONUS += ABL:音乐技能
	ENDSELECT
NEXT

BAND_BONUS = CMATCH(TCVAR:演奏参加, 1, 1) + CMATCH(TCVAR:演奏参加, 2, 1)
IF TFLAG:193 < 0
	FOR LOCAL, 1, NUM_TARGET + 1
		SOURCE:(TARGET:LOCAL):反感 += MUSIC_BONUS * ABL:(TARGET:LOCAL):音乐技能
		SIF TCVAR:(TARGET:LOCAL):演奏参加
			SOURCE:(TARGET:LOCAL):郁闷 += 3 * MUSIC_BONUS * ABL:音乐技能
		SOURCE:(TARGET:LOCAL):反感 = SOURCE:(TARGET:LOCAL):反感 * (2 + BAND_BONUS + DANCE_BONUS) / 2
		SOURCE:(TARGET:LOCAL):郁闷 = SOURCE:(TARGET:LOCAL):郁闷 * (2 + BAND_BONUS + DANCE_BONUS) / 2
		SOURCE:(TARGET:LOCAL):欢乐 = SOURCE:(TARGET:LOCAL):欢乐 * 2 / (2 + BAND_BONUS + DANCE_BONUS)
		SOURCE:(TARGET:LOCAL):征服 = SOURCE:(TARGET:LOCAL):征服 * 2 / (2 + BAND_BONUS + DANCE_BONUS)
		;この手の个别处理は无いほうが良いが思いついてしまったものは仕方がない
		;露娜萨
		SIF TCVAR:[[露娜萨]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):郁闷, 1.1
		SIF TCVAR:[[舞]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):郁闷, 0.9
		SIF TCVAR:[[里乃]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):反感, 0.9
	NEXT
ELSE
	BAND_BONUS += TFLAG:193
	BAND_BONUS += MAX(SQRT(MUSIC_BONUS) - 1, 0)
	FOR LOCAL, 1, NUM_TARGET + 1
		SIF !SHIRAHU(TARGET:LOCAL)
			CONTINUE
		SOURCE:(TARGET:LOCAL):欢乐 += MUSIC_BONUS * 15
		SOURCE:(TARGET:LOCAL):征服 += MUSIC_BONUS * 10
		SOURCE:(TARGET:LOCAL):欢乐 = MAX(0, SOURCE:(TARGET:LOCAL):欢乐 * (2 + BAND_BONUS + DANCE_BONUS) / 2)
		SOURCE:(TARGET:LOCAL):征服 = MAX(0, SOURCE:(TARGET:LOCAL):征服 * (2 + BAND_BONUS + DANCE_BONUS) / 2)
		;梅露兰
		SIF TCVAR:[[梅露兰]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):欢乐, 1.1
		;雷鼓
		SIF TCVAR:[[雷鼓]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):征服, 1.1
		;心
		SIF TCVAR:[[心]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):欢乐, 1.2
		SIF TCVAR:[[心]]:演奏参加
			TIMES SOURCE:(TARGET:LOCAL):征服, 1.2
	NEXT
ENDIF

TFLAG:好感度BOUNS = BAND_BONUS * 50
TARGET = NOW_TARGET

TIME += 30
RETURN 1


@COM416_1

DOWNBASE:气力 += 100

;欢乐强度
SOURCE:欢乐 = 700 + ABL:MASTER:音乐技能 * 300 + ABL:亲密 * 50
;征服强度
SOURCE:征服 = 100 * (ABL:MASTER:音乐技能 + 1) + ABL:亲密 * 30

IF TFLAG:193 == -1
	TIMES SOURCE:欢乐 , 0.10
	TIMES SOURCE:征服 , 0.50
ELSEIF TFLAG:193 == 0
	TIMES SOURCE:欢乐 , 1.00
	TIMES SOURCE:征服 , 1.00
ELSE
	TIMES SOURCE:欢乐 , 2.00
	TIMES SOURCE:征服 , 2.00
ENDIF
;音乐技能で欢乐がさらに增减
IF ABL:MASTER:音乐技能 >= 5
	TIMES SOURCE:欢乐 , 2.00
ELSEIF ABL:MASTER:音乐技能 == 4
	TIMES SOURCE:欢乐 , 1.50
ELSEIF ABL:MASTER:音乐技能 == 3
	TIMES SOURCE:欢乐 , 1.00
ELSEIF ABL:MASTER:音乐技能 == 2
	TIMES SOURCE:欢乐 , 0.50
ELSE
	TIMES SOURCE:欢乐 , 0.10
ENDIF
;[音乐知识]を持っている场合、欢乐增加
SIF TALENT:MASTER:音乐知识
	TIMES SOURCE:欢乐 , 1.50
;音感のある无でもう少し增减
IF TALENT:MASTER:音感 == 2
	TIMES SOURCE:欢乐 , 2.00
ELSEIF TALENT:MASTER:音感 == 1
	TIMES SOURCE:欢乐 , 1.50
ELSEIF TALENT:MASTER:音感 == -1
	TIMES SOURCE:欢乐, 0.50
ENDIF

SIF TARGET <= 0
	RETURN 1
SELECTCASE TALENT:TARGET:音乐知识
;乐器持ち角色、プリリバ三姊妹と九十九姊妹と雷鼓がTARGET
CASE 1
	EXP:MASTER:演奏经验 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日时 = TIME
		DOWNBASE:气力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:欢乐 , 2.00
		TCVAR:演奏参加 = 1
		EXP:演奏经验 ++
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%开始演奏着乐器！
		IF ABL:MASTER:音乐技能 >= ABL:TARGET:音乐技能
			EXP:演奏经验 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
;歌唱角色、米斯蒂亚と响子、艾伦がTARGET
CASE 2
	EXP:MASTER:演奏经验 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日时 = TIME
		DOWNBASE:气力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:欢乐 , 2.00
		TCVAR:演奏参加 = 2
		EXP:歌唱经验 ++
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%配合着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的旋律唱起了歌！
		IF ABL:MASTER:音乐技能 >= ABL:TARGET:音乐技能
			EXP:歌唱经验 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
;舞蹈角色、心、舞、里乃
CASE 3
	EXP:MASTER:演奏经验 ++
	IF ACCOMPANY_ABLE(TARGET)
		TCVAR:演奏日时 = TIME
		DOWNBASE:气力 += 200
		DOWNBASE:体力 += 100
		TIMES SOURCE:欢乐 , 2.00
		TCVAR:演奏参加 = 3
		EXP:舞蹈经验 ++
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%配合着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的旋律跳起了舞！
		IF ABL:MASTER:音乐技能 >= ABL:TARGET:音乐技能
			EXP:舞蹈经验 ++
			TIMES SOURCE:征服 , 1.50
		ENDIF
	ENDIF
CASEELSE
	SIF ABL:MASTER:音乐技能 <= ABL:TARGET:音乐技能 || TALENT:TARGET:音乐知识
		EXP:MASTER:演奏经验 ++
ENDSELECT
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE416
;演奏实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(416)
	RETURN RESULT
	
;いずれの乐器（30～34）も使えない
LOCAL:1 = 0
FOR LOCAL,30,35
	SIF MUSIC_ABLE(LOCAL)
		LOCAL:1 ++
NEXT
SIF !LOCAL:1
	RETURN 0

;乐器三种のいずれかを持っていても13,24,30澡堂と14,40厕所では演奏できない
;SIF CFLAG:MASTER:当前位置 == 13 || CFLAG:MASTER:当前位置 == 14 || CFLAG:MASTER:当前位置 == 24 || CFLAG:MASTER:当前位置 == 30 || CFLAG:MASTER:当前位置 == 39 || CFLAG:MASTER:当前位置 == 40
SIF (IN_TOILET(CFLAG:MASTER:当前位置) || BATHCHECK(CFLAG:MASTER:当前位置)) && GET_MAPID(CFLAG:MASTER:当前位置) == MAIN_MAP
	RETURN 0
;气力0
SIF BASE:MASTER:气力 <= 0
	RETURN 0
SIF CFLAG:诶嘿嘿 == 2
	RETURN 0
;睡眠中
;いっそ强行起床させるという案もあったり
SIF CFLAG:睡眠 && !CFLAG:陪睡中 || CFLAG:陪睡中
	RETURN 0
;仕事中、かつ、宴会参加者ではない
SIF CFLAG:行动 == 5 && CFLAG:职业种类 != JOB_宴会参加
	RETURN 0
;停止中は不可
SIF FLAG:70
	RETURN 0
RETURN 1

@MUSIC_ABLE(ARG)
#FUNCTION
SIF !ITEM:ARG
	RETURNF 0
SELECTCASE ARG
;电子琴、吉他、ペット
	CASE 30,32,33
		RETURNF 1
;钢琴
;博丽神社起居室、红魔馆の广场と废洋馆、地灵殿の广场、寺子屋に设置しているという形です。
	CASE 31
		SIF GROUPMATCH(CFLAG:MASTER:当前位置,GET_MAP_REPLACEMENT(9),GET_MAP_REPLACEMENT(222),GET_MAP_REPLACEMENT(309),GET_MAP_REPLACEMENT(335))
			RETURNF 1
;バイオリン
	CASE 34
		SIF ABL:MASTER:音乐技能 >= 2
			RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT
RETURNF 0

@ACCOMPANY_ABLE(ARG)
#FUNCTION
SIF BASE:ARG:气力 < 200
	RETURNF 0
SIF TIME_PROGRESS(TCVAR:ARG:演奏日时) < 90
	RETURNF 0
SIF ABL:MASTER:音乐技能 < 3
	RETURNF 0
SIF !SHIRAHU(ARG)
	RETURNF 0
SIF CFLAG:ARG:行动 == 5
	RETURNF 0

RETURNF 1

@THROW_STONE(ARG)
#DIM LISTENER_P
CALL COLORMESSAGE(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%扔了石头过来",C_YELLOW,1)
LISTENER_P = ABL:ARG:战斗能力
IF RAND:(ABL:MASTER:战斗能力 * (2 + CFLAG:ARG:弹幕胜负胜利) + 1) > LISTENER_P
	CALL COLORMESSAGE(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%华丽地回避了攻击",C_YELLOW,1)
ELSE
	CALL RECOVER(MASTER, (LISTENER_P + 1) * (-150), "体力", @"%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%の投石")
	RETURN -1
ENDIF
@演奏集合
#DIM 集合, 人物数量上限, 3 ;集合:X:Y
#DIM 人数                      ;同一Mapにいる人数、移动できない角色も表示され、人数に加算されている
#DIM 入力值                    ;RESULT取得用
#DIM C_ID
#DIM NOW_TARGET
SIF CFLAG:MASTER:当前位置 == OMANEKIBEYA()
	RETURN
SIF !AT_HOME(MASTER)
	RETURN
SIF CFLAG:MASTER:当前位置 == CFLAG:MASTER:初始位置
	RETURN
SIF PRIVATEROOM:(CFLAG:MASTER:当前位置 % 100)
	RETURN
CALL SYUGOS_GETDATA(人数, 集合)
CALL SYUGOS_COST(人数, 集合)
SIF TARGET
	NOW_TARGET = TARGET
FOR LOCAL, 0, 人数
	C_ID = 集合:LOCAL:0
	SIF GETBIT(集合:LOCAL:1, 0)
		CONTINUE
	;移动回数が多いと除外
	SIF 睡眠时间(C_ID)
		CONTINUE
	SIF 集合:LOCAL:2 > 3
		CONTINUE
	SIF CFLAG:C_ID:勃然大怒
		CONTINUE
	;IF RAND:(7-ABL:MASTER:音乐技能) == 0
	;	CFLAG:C_ID:当前位置 = CFLAG:MASTER:当前位置
	;	CFLAG:C_ID:同室 = 1
	;	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID,12)%听说%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%正在演奏乐器,于是赶过来旁听了
	;ENDIF

	CFLAG:C_ID:当前位置 = CFLAG:MASTER:当前位置
	CFLAG:C_ID:同室 = 1
	;自由行动の中断
	Activity_Duration:C_ID = 0
	Activity_Type:C_ID = 0
	;PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%过来了
	;PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%.
NEXT
CALL TARGETSET_CHACK
;SIF NOW_TARGET
;	TARGET = NOW_TARGET

;PRINTFORML 听说%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%正在演奏乐器,于是赶过来了...
PRINTFORML 搞不好会吸引到附近的人,so...是时候表演真正的技术了!!!
