;设下陷阱／陷阱の样子を见る
;角色の技能や経験依存ではなく、プレイヤーの眼力で猎物がいそうかどうかを判定する感じのデザイン。
;昆虫诱捕装置と兼用
@COM448
;どっちも可能なら选择
IF HUNTING_SPOT(CFLAG:MASTER:当前位置) && MUSHITORI_SPOT(CFLAG:MASTER:当前位置) && ITEM:昆虫采集套装 && (!MUSHI_TRAP || GROUPMATCH(CFLAG:MASTER:当前位置, GET_MUSHIDATA(MUSHI_TRAP, "场所")))
	IF GROUPMATCH(CFLAG:MASTER:当前位置, TRAP_POINT:1, TRAP_POINT:2, TRAP_POINT:3)
		PRINTL 　[ 1] 看看狩猎陷阱的样子
	ELSE
		PRINTL 　[ 1] 设置狩猎陷阱
	ENDIF
	IF GROUPMATCH(CFLAG:MASTER:当前位置, GET_MUSHIDATA(MUSHI_TRAP, "场所"))
		PRINTL 　[ 2] 看看昆虫诱捕装置的样子
	ELSE
		PRINTL 　[ 2] 设置昆虫诱捕装置
	ENDIF
	PRINTL 　[ 0] 中止
	INPUT
	IF RESULT == 1
		CALL TRAP_HUNTING
	ELSEIF RESULT == 2
		CALL MUSHI_TRAP
	ELSE
		RETURN -1
	ENDIF
ELSEIF HUNTING_SPOT(CFLAG:MASTER:当前位置)
	CALL TRAP_HUNTING
ELSEIF MUSHITORI_SPOT(CFLAG:MASTER:当前位置)
	CALL MUSHI_TRAP
ELSE
	RETURN -1
ENDIF
RETURN RESULT

@TRAP_HUNTING
#DIM CHARA
#DIM Hunt_Power
#DIM Serch
#DIM 生息密度
#DIM 探索回数
#DIM 削除行数
#DIM リザルト
#DIM 猎物

猎物 = 0
探索回数 = 0

;夕方～夜の狩猎は放弃
IF TIME:2 >= 5 && !FLAG:70
	DRAWLINE
	PRINTFORML 这个时间开始狩猎的话很快就会天黑了…
	RETURN -1
ENDIF
;大雨、吹雪のときは放弃(天候パッチ)强风时も放弃
IF GROUPMATCH(天候, 5, 9) || GROUPMATCH(FLAG:异常气象, 9, 10) || WIND_VELOCITY >= 1
	DRAWLINE
	PRINTFORML 在这种天候下诱捕太危险了
	RETURN -1
ENDIF
;あからさまな人の敷地での密猎は放弃
SELECTCASE CFLAG:MASTER:当前位置
	CASE 25, 50
		IF ABL:[[灵梦]]:亲密 < 5
			DRAWLINE
			PRINTFORML 在这里偷猎感觉会不太好
			RETURN -1
		ENDIF
	CASE 斜角的竹林, 433
		IF ABL:[[妹红]]:亲密 < 5 && ABL:[[帝]]:亲密 < 5 && ABL:[[辉夜]]:亲密 < 5 && ABL:[[永琳]]:亲密 < 5
			DRAWLINE
			PRINTFORML 在这里偷猎感觉会不太好
			RETURN -1
		ENDIF
	CASE 绝景之丘, 805
		IF ABL:[[文]]:亲密 < 5 && ABL:[[果]]:亲密 < 5 && ABL:[[龙]]:亲密 < 5
			DRAWLINE
			PRINTFORML 在这里偷猎感觉会不太好
			RETURN -1
		ENDIF
ENDSELECT

;参加者判定
IF FLAG:约会的对象 && !FLAG:70
	CHARA = FLAG:约会的对象
ELSEIF TARGET && ABL:TARGET:亲密 >= 5 && SHIRAHU(TARGET) && CFLAG:TARGET:行动 != 5
	CHARA = TARGET
ELSE
	CHARA = 0
ENDIF

TFLAG:情绪上升抑制 = 1

;◆陷阱を仕挂けてある场合は样子を见る
IF GROUPMATCH(CFLAG:MASTER:当前位置, TRAP_POINT:1, TRAP_POINT:2, TRAP_POINT:3)
	;①陷阱が破损している场合
	IF TRAP_DURABILITY:(TP_TO_PLACE()) == 0
		PRINTFORML 陷阱坏掉了…
		IF DAY:0 - TRAP_CAPTUREDATA:(TP_TO_PLACE()) >= 30
			PRINTFORMW 似乎已经被猎物触发过、但时间经过太久了…
		ELSEIF TRAP_CAPTUREDATA:(TP_TO_PLACE())
			PRINTFORMW 似乎已经被猎物触发过、然后被逃掉了…
		ELSE
			PRINTFORMW 如果没有机会的话、或许将其回收并重新设置会比较好
		ENDIF
		;变量をリセット
		CALL TRAP_V_RESET(TP_TO_PLACE())
		TFLAG:193 = -1
		
	;②猎物がかかっている场合
	ELSEIF TRAP_CAPTURE:(TP_TO_PLACE())
		猎物 = TRAP_CAPTURE:(TP_TO_PLACE())
		CAPTURE_RECORD:猎物 ++ 
		;兽の各种变量をセット
		CALL CRITTER_DATA(猎物)
		PRINTL 
		PRINTFORMW ………！
		PRINTFORMW 猎物被抓住了！
		PRINTL 
		DRAWLINE
		PRINTFORML 　【被捕获的猎物】
		PRINTFORM 　　
		CALL SPOIL_TEXT(SPOIL_ENHANCEMENT(猎物), 0, 1)
		PRINTFORML 　　%CritterIntro%
		DRAWLINE
		PRINTW 
		
		;一部角色だと逃がしてあげようよ、に
		IF SPOIL_RELEASE(CHARA, 猎物) == 1 && !FLAG:70
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%的眼睛看向这边、似乎想要说些什么…
			PRINTFORM 这样下去可不行、%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%将

			$RELEASE
			CALL SPOIL_TEXT(SPOIL_ENHANCEMENT(猎物), 2, 0)
			PRINTFORML 放走了
			
			LOCALS = %ITEMNAME:(TRAP_TYPE:TP_TO_PLACE())%
			CALL GET_ITEM(LOCALS, 1, 2)
			猎物 = 0
			;变量をリセット
			CALL TRAP_V_RESET(TP_TO_PLACE())
		ELSE
			PRINTFORML 要将猎物解体吗？
			CALL ASK_YN("解体", "放走")
			IF !RESULT
				CLEARLINE 4
			ELSE
				GOTO RELEASE
			ENDIF
			IF FLAG:70
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%用时间停止将猎物固定・并解体了…
			ELSEIF CHARA
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%和%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%协力、将猎物固定住・进行了解体…
				
			ELSE
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%将猎物固定住・进行了解体…
			ENDIF
			LOCALS = %ITEMNAME:(TRAP_TYPE:TP_TO_PLACE())%
			CALL GET_ITEM(LOCALS, 1, 1)
		ENDIF
	;③まだ何もかかっていないよ
	ELSE
		PRINTFORML 设下的陷阱好像还没有被触发
		IF TRAP_DURABILITY:(TP_TO_PLACE()) < 21
			PRINTFORML 検察陷阱的时候、零件出现了稍微的松动…
		ELSEIF TRAP_DURABILITY:(TP_TO_PLACE()) < 11
			PRINTFORML 検察陷阱的时候、发现已经损坏很多了…
		ENDIF
		PRINTFORML 要回收陷阱吗？
		PRINTL 
		$INPUT_LOOP02
		PRINTFORML 　[0]保持原样
		PRINTFORML 　[1]将陷阱回收
		INPUT
		SELECTCASE RESULT
			CASE 0
				RETURN -1
			CASE 1
				PRINTFORML 决定将陷阱回收
				;陷阱を道具に戻す
				LOCALS = %ITEMNAME:(TRAP_TYPE:TP_TO_PLACE())%
				CALL GET_ITEM(LOCALS, 1, 2)
				;变量をリセット
				CALL TRAP_V_RESET(TP_TO_PLACE())
			CASEELSE
				CLEARLINE 3
				GOTO INPUT_LOOP02
		ENDSELECT
	ENDIF
;◆设下陷阱
ELSE
	;陷阱を持っていなければリターン
	IF !ITEM:诱捕用品【小型】 && !ITEM:诱捕用品【中型】 && !ITEM:诱捕用品【大型】
		DRAWLINE
		PRINTFORML 需要准备好陷阱才行
		RETURN -1
	;３ポイントに仕挂けてある
	ELSEIF TRAP_POINT:1 && TRAP_POINT:2 && TRAP_POINT:3
		DRAWLINE
		PRINTFORML 已经在３个地方设置好陷阱了、已经足够了
		RETURN -1
	ELSE
		;①先に各种系数を算出
		;基礎パワー １～１１０程度
		Hunt_Power = MAX((ABL:MASTER:教养 * 2) + (ABL:MASTER:战斗能力 * 2) + (TFLAG:幸运补正 * 10) + (CFLAG:MASTER:今日的运势 / 20), 1)

		;参加者がいる场合は狩猎パワー追加　２人のほうが单纯に目が２倍なので、良い结果を引きやすい
		SIF CHARA && !FLAG:70
		Hunt_Power += MAX((ABL:CHARA:教养 * 2) + (ABL:CHARA:战斗能力 * 2) + (FLAG:每日变更事件 / 20), 1)

		$LOOP_SELECT
	
		探索回数 ++
		;奥地に踏み込んでいくイメージで
		SIF 探索回数 != 1
			Hunt_Power += RAND:16
		
		;②探索回数が10回を超えた场合は无限ループ除けのためにリターンする
		IF 探索回数 >= 10
			PRINTFORMW ……太投入的话可能会迷失其中的、这次就到此为止吧
			GOTO AFTER
		ENDIF

		REPEAT 5
			REUSELASTLINE  \@ CHARA ? 和%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)% #  \@捜索野兽痕迹中.%"." * COUNT%
			TWAIT 150, 1
		REND

		;③狩猎パワーからサーチ值を固定・生息密度をセットし、フィールドサインの文本（ヒント）を流す
		Serch = RAND:Hunt_Power
		IF Serch
			生息密度 = Serch / 10
		ELSE
			生息密度 = 0
		ENDIF
		PRINTL 
		PRINTFORML ◇现在所在地的情况◇
		PRINT 　
		CALL FIELDSIGN(Serch)
		DRAWLINE
		;④どの设下陷阱か选择
		PRINTFORML 在这里设置陷阱吗？
		PRINTL 
		$INPUT_LOOP01
		削除行数 = 0
		FOR LOCAL, 132, 135
			削除行数 ++
			IF ITEM:LOCAL == 0
				SETCOLOR C_GRAY
				PRINTPLAINFORM 　[{LOCAL}]%ITEMNAME:LOCAL%
			ELSE
				PRINTFORM 　[{LOCAL}]%ITEMNAME:LOCAL%
			ENDIF
			SELECTCASE LOCAL
				CASE 132
					PRINTFORML …入门者向的廉价品、耐久度低。用于捕获小型～中型兽
				CASE 133
					PRINTFORML …从入门者到专家、耐久度一般。小型～大型兽都能捕获
				CASE 134
					PRINTFORML …面向目标远大的熟练猎师、优秀的耐久性。用于捕获中型～大型兽
				CASEELSE
					PRINTFORML 
			ENDSELECT
			RESETCOLOR
		NEXT
		PRINTL 
		PRINTFORML 　[998]继续探索
		PRINTFORML 　[999]停止诱捕
		削除行数 += 3
		
		INPUT
		
		リザルト = RESULT
		SELECTCASE リザルト
			CASE 132 TO 134
				IF ITEM:リザルト == 0
					CLEARLINE 削除行数 + 1
					GOTO INPUT_LOOP01
				;⑤设下陷阱处理
				ELSE
					CALL TRAP_FLAVORTEXT(リザルト)
					PRINTFORML 设置了%ITEMNAME:リザルト%　过段时间再来看看样子吧
					TFLAG:193 = 0
					ITEM:リザルト -= 1
					FOR LOCAL, 1, 4
						;空いてる一番若い番号に、各种变量を插入
						IF TRAP_POINT:LOCAL == 0
							TRAP_POINT:LOCAL = CFLAG:MASTER:当前位置
							TRAP_SETUPDATA:LOCAL = DAY:0
							TRAP_TYPE:LOCAL = リザルト
							TRAP_ALERTNESS:LOCAL = 75
							TRAP_DENSITY:LOCAL = 生息密度
							;陷阱に応じて耐久度をセット
							SELECTCASE リザルト
								CASE 132
									TRAP_DURABILITY:LOCAL = 25
								CASE 133
									TRAP_DURABILITY:LOCAL = 40
								CASE 134
									TRAP_DURABILITY:LOCAL = 55
								CASEELSE
									TRAP_DURABILITY:LOCAL = 30
							ENDSELECT
							BREAK
						ENDIF
					NEXT
				ENDIF
			CASE 998
				CLEARLINE 削除行数 + 8
				GOTO LOOP_SELECT
			CASE 999
				PRINTL 
			CASEELSE
				CLEARLINE 削除行数 + 1
				GOTO INPUT_LOOP01
		ENDSELECT
	ENDIF
ENDIF

$AFTER
;◆陷阱を仕挂けた or 猎物を解体した（成功扱い） or 陷阱が坏れていた（失败扱い）
;时间经过と体力消费、空腹时刻と参加者の疲劳度
IF FLAG:70
	;狩り成功
	IF 猎物
		BASE:MASTER:TSP -= CritterEffort * 3
	;猎物を逃がしてやったか、陷阱を回收した
	ELSEIF 探索回数 == 0 && 猎物 == 0
		BASE:MASTER:TSP -= 150
	ELSE
		;陷阱を仕挂けた
		IF TP_TO_PLACE()
			BASE:MASTER:TSP -= (探索回数 * 6) + 180
		;探索しただけ
		ELSE
			BASE:MASTER:TSP -= 探索回数 * 6
		ENDIF
	ENDIF
ELSE
	IF 猎物
		DOWNBASE:MASTER:体力 = MAX((CritterEffort * 3) - (ABL:MASTER:战斗能力 * 10), 40)
		DOWNBASE:MASTER:气力 = CritterEffort * 2
		TIME += CritterEffort
		TCVAR:MASTER:空腹时刻 -= 25
		IF CHARA
			TCVAR:CHARA:空腹时刻 -= 25
			CALL ADD_TIRED(CHARA, CritterEffort)
		ENDIF
	ELSEIF 探索回数 == 0 && 猎物 == 0
		DOWNBASE:MASTER:体力 = 100
		DOWNBASE:MASTER:气力 = 50
		TIME += 45
		TCVAR:MASTER:空腹时刻 -= 10
		IF CHARA
			TCVAR:CHARA:空腹时刻 -= 10
			CALL ADD_TIRED(CHARA, 50)
		ENDIF
	ELSE
		IF TP_TO_PLACE()
			DOWNBASE:MASTER:体力 = 120 + 探索回数 * 5
			DOWNBASE:MASTER:气力 = 60 + 探索回数 * 3
			TIME += 30 + 探索回数 * 6
		ELSE
			DOWNBASE:MASTER:体力 = 探索回数 * 5
			DOWNBASE:MASTER:气力 = 探索回数 * 3
			TIME += 探索回数 * 6
		ENDIF
	ENDIF
ENDIF

;约会经验
IF CHK_DATENOW(CFLAG:CHARA:约会中) && CHARA > 0
	EXP:MASTER:约会经验 ++
	EXP:CHARA:约会经验 ++
ENDIF

;道具入手处理
IF 猎物
	TFLAG:193 = 1
	TFLAG:194 = 猎物
	LOCALS = %ITEMNAME:(CritterMeat)%
	CALL GET_ITEM(LOCALS, (CritterMeat_Value + SPOIL_ENHANCEMENT(猎物)), 2)
	;陷阱の各种变量リセット
	CALL TRAP_V_RESET(TP_TO_PLACE())
ENDIF
PRINTL 

;参加者のソース、陷阱が坏れていると半减
IF CHARA > 0
	;固定で获得するソース
	SOURCE:CHARA:欢乐 = 400
	SOURCE:CHARA:情爱 = 100
	
	;ABL:顺从をみる
	IF ABL:顺从 <= 1
		SOURCE:CHARA:欢乐 += (ABL:顺从 * 40)
		SOURCE:CHARA:情爱 += (ABL:亲密 * 20)
	ELSEIF ABL:顺从 <= 3
		SOURCE:CHARA:欢乐 += 200 + (ABL:顺从 * 40)
		SOURCE:CHARA:情爱 += 50 + (ABL:亲密 * 30)
	ELSEIF ABL:顺从 <= 5
		SOURCE:CHARA:欢乐 += 500 + (ABL:顺从 * 40)
		SOURCE:CHARA:情爱 += 200 + (ABL:亲密 * 30)
	ELSEIF ABL:顺从 <= 8
		SOURCE:CHARA:欢乐 += 750 + (ABL:顺从 * 60)
		SOURCE:CHARA:情爱 += 300 + (ABL:亲密 * 40)
	ELSEIF ABL:顺从 <= 11
		SOURCE:CHARA:欢乐 += 1000 + (ABL:顺从 * 60)
		SOURCE:CHARA:情爱 += 600 + (ABL:亲密 * 40)
	ELSE
		SOURCE:CHARA:欢乐 += 1600 + (ABL:顺从 * 30)
		SOURCE:CHARA:情爱 += 1000 + (ABL:亲密 * 20)
	ENDIF
	SIF CHARA != FLAG:约会的对象 
		SOURCE:CHARA:情爱 = 0
	
	;好感度はみない
	
	SOURCE:CHARA:被动 = 200 + 100 * ABL:顺从
	SOURCE:CHARA:征服 = 200 + 100 * ABL:施虐属性
	
	;猎物をみる
	IF 猎物
		;死亡蠕虫。达成感はあるが、食べるのこれ？
		IF 猎物 == 23
			TIMES SOURCE:CHARA:欢乐 , 0.70
			TIMES SOURCE:CHARA:被动 , 0.70
			TIMES SOURCE:CHARA:征服 , 0.70
			SOURCE:CHARA:达成 = 300
		ELSEIF STRCOUNT(CritterTribe, "希少/")
			TIMES SOURCE:CHARA:欢乐 , 2.25
			TIMES SOURCE:CHARA:被动 , 2.25
			TIMES SOURCE:CHARA:征服 , 2.00
			SOURCE:CHARA:达成 = 350
		;亚洲黑熊。ショウブあった！
		ELSEIF 猎物 == 19
			TIMES SOURCE:CHARA:欢乐 , 2.00
			TIMES SOURCE:CHARA:被动 , 2.00
			TIMES SOURCE:CHARA:征服 , 1.75
			SOURCE:CHARA:达成 = 300
		ELSEIF STRCOUNT(CritterTribe, "谜/")
			TIMES SOURCE:CHARA:欢乐 , 1.00
			TIMES SOURCE:CHARA:被动 , 1.00
			TIMES SOURCE:CHARA:征服 , 1.00
			SOURCE:CHARA:达成 = 80
		ELSEIF STRCOUNT(CritterTribe, "小型/")
			TIMES SOURCE:CHARA:欢乐 , 1.10
			TIMES SOURCE:CHARA:被动 , 1.10
			TIMES SOURCE:CHARA:征服 , 1.10
			SOURCE:CHARA:达成 = 115
		ELSEIF STRCOUNT(CritterTribe, "中型/")
			TIMES SOURCE:CHARA:欢乐 , 1.25
			TIMES SOURCE:CHARA:被动 , 1.25
			TIMES SOURCE:CHARA:征服 , 1.15
			SOURCE:CHARA:达成 = 125
		ELSEIF STRCOUNT(CritterTribe, "大型/")
			TIMES SOURCE:CHARA:欢乐 , 1.50
			TIMES SOURCE:CHARA:被动 , 1.50
			TIMES SOURCE:CHARA:征服 , 1.25
			SOURCE:CHARA:达成 = 150
		ENDIF
	;陷阱が坏れていた
	ELSEIF TFLAG:193 == -1
		TIMES SOURCE:CHARA:欢乐 , 0.50
		TIMES SOURCE:CHARA:被动 , 0.50
		TIMES SOURCE:CHARA:征服 , 0.50
	;陷阱をしかけた
	ELSEIF TP_TO_PLACE()
		SOURCE:CHARA:达成 = 70
	;解体せず逃がしたか、陷阱を回收した
	ELSEIF 探索回数 == 0 && 猎物 == 0
		TIMES SOURCE:CHARA:欢乐 , 1.10
		TIMES SOURCE:CHARA:被动 , 1.10
		TIMES SOURCE:CHARA:征服 , 1.05
		SOURCE:CHARA:达成 = 110
	ENDIF
	;猎物の角色に応じた固有反应（上书き）

	;兽关联の变量をリセット
	CALL CRITTER_V_RESET
ENDIF
RETURN 1

;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
;设下陷阱
@COM_ABLE448
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(448)
	RETURN RESULT
SIF CFLAG:诶嘿嘿
	RETURN 0
;诱捕可能な场所or昆虫采集可能な场所でのみ
SIF !HUNTING_SPOT(CFLAG:MASTER:当前位置) && !MUSHITORI_SPOT(CFLAG:MASTER:当前位置)
	RETURN 0
IF MUSHITORI_SPOT(CFLAG:MASTER:当前位置) && !HUNTING_SPOT(CFLAG:MASTER:当前位置)
	;昆虫诱捕装置は昆虫采集套装がないとダメ
	SIF !ITEM:昆虫采集套装
		RETURN 0
	;昆虫诱捕装置を仕挂けてたら仕挂けた场所でしかダメ
	SIF MUSHI_TRAP && CFLAG:MASTER:当前位置 != GET_MUSHIDATA(MUSHI_TRAP, "场所")
		RETURN 0
ENDIF
;约会中は约会的对象とだけ
SIF FLAG:约会的对象 && TARGET != FLAG:约会的对象
	RETURN 0
;睡眠中
SIF CFLAG:MASTER:睡眠
	RETURN 0
;体力・气力不足
SIF BASE:MASTER:气力 < 800 || BASE:MASTER:体力 < 800
	RETURN 0
RETURN 1

;-----------------------------------------------------------
;当前位置から诱捕スポットを割り出す函数
;ARG 基本はCFLAG:MASTER:当前位置
;	 陷阱发动判定时はTRAP_POINT:X
;-----------------------------------------------------------
@HUNTING_SPOT(ARG)
#FUNCTION
IF !IN_HOME(ARG)
	SELECTCASE ARG
		CASE 50
			RETURNF 1
		CASE 斜角的竹林
			RETURNF 2
		CASE 森林的入口
			RETURNF 3
		CASE 山麓树海
			RETURNF 4
		CASE 绝景之丘
			RETURNF 5
		CASE 幻想风穴
			RETURNF 6
	ENDSELECT
ELSE
	SELECTCASE ARG
		CASE P25镇守之森
			RETURNF 1
		CASE P433斜角的竹林
			RETURNF 2
		CASE P507菌菇群生地
			RETURNF 3
		CASE P702山道・通往沼泽之道
			RETURNF 4
		CASE P805顶上
			RETURNF 5
		CASE P948竖井底部
			RETURNF 6
	ENDSELECT
ENDIF
RETURNF 0

;-----------------------------------------------------------
;土地番号からスポット名を当てはめる函数
;ARG 基本はTRAP_POINT:X
;-----------------------------------------------------------
@猎场名(ARG)
#FUNCTIONS
IF !IN_HOME(ARG)
	SELECTCASE ARG
		CASE 50
			LOCALS = 博丽神社的【妖精的大树】周边
		CASE 斜角的竹林
			LOCALS = 永远亭的【斜角的竹林】周边
		CASE 森林的入口
			LOCALS = 魔法之森的【森林的入口】周边
		CASE 山麓树海
			LOCALS = 妖怪之山・山麓的【山麓树海】周边
		CASE 绝景之丘
			LOCALS = 妖怪之山・山顶的【绝景之丘】周边
		CASE 幻想风穴
			LOCALS = 地底的【幻想风穴】周边
		CASEELSE
			LOCALS = エラーサブ
	ENDSELECT
ELSE
	SELECTCASE ARG
		CASE P25镇守之森
			LOCALS = 据点的【镇守之森】周边
		CASE P433斜角的竹林
			LOCALS = 据点的【斜角的竹林】周边
		CASE P507菌菇群生地
			LOCALS = 据点的【菌菇群生地】周边
		CASE P702山道・通往沼泽之道
			LOCALS = 据点的【山道・通往沼泽之道】周边
		CASE P805顶上
			LOCALS = 据点的【山顶】周边
		CASE P948竖井底部
			LOCALS = 据点的【竖井底部】周边
		CASEELSE
			LOCALS = エラーメイン
	ENDSELECT
ENDIF
RETURNF LOCALS
;-----------------------------------------------------------
;诱捕スポットに生息する兽を割り出す函数
;ARG:0 HUNTING_SPOT
;ARG:1 陷阱道具番号 132小 133中 134大
;-----------------------------------------------------------
@CRITTER_SPECIES(HUNTING_SPOT, 陷阱番号)
#DIM C_ID
#DIM HUNTING_SPOT
#DIM 陷阱番号
#DIM 季节

;季节异变に关する补正
季节 = DAY:2
SIF GatheringSeason
	季节 = GatheringSeason

$LOOP

C_ID = 1 + RAND:60
;变量を读み込み
CALL CRITTER_V_RESET
CALL CRITTER_DATA(C_ID)
;无名（未登录）ならリターン
SIF !STRLENS(CritterName)
	GOTO LOOP
;タグ上の无登录はリターン
SIF !STRCOUNT(CritterTribe, "兽/") && !STRCOUNT(CritterTribe, "鸟/")
	GOTO LOOP
;[希少][猛禽]は１／２で再抽选に
SIF (STRCOUNT(CritterTribe, "希少/") || STRCOUNT(CritterTribe, "猛禽/")) && RAND:2
	GOTO LOOP
;地底以外で冬眠しているものは弹く
SIF 季节 == 4 && STRCOUNT(CritterTribe, "冬眠/") && HUNTING_SPOT != 6
	GOTO LOOP
;夏鸟、冬鸟两持ちの对応
IF (DAY:2 == 2 && STRCOUNT(CritterTribe, "夏鸟/")) || (DAY:2 == 4 && STRCOUNT(CritterTribe, "冬鸟/"))

;夏以外の夏鸟を弹く（渡ってきてるので１日限定の季节异变は避ける）
ELSEIF DAY:2 != 2 && STRCOUNT(CritterTribe, "夏鸟/")
	GOTO LOOP
;冬以外の冬鸟を弹く
ELSEIF DAY:2 != 4 && STRCOUNT(CritterTribe, "冬鸟/")
	GOTO LOOP
ENDIF
;陷阱に応じて捕获できる猎物のレンジを变える  小…基本の肉の量が2以下  中…肉の量が2～6  大…肉の量が4以上
SELECTCASE 陷阱番号
	CASE 132
		SIF CritterMeat_Value > 3
			GOTO LOOP
	CASE 133
		SIF CritterMeat_Value > 6 || CritterMeat_Value == 1
			GOTO LOOP
	CASE 134
		SIF CritterMeat_Value < 4
			GOTO LOOP
ENDSELECT
SELECTCASE HUNTING_SPOT
	CASE 1;镇守之森、妖精的大树…普通の山の兽と山鸟たち ユニークは槌之子、矢部大角鹿、ニホンオオカミ
		SIF STRCOUNT(CritterTribe, "博/")
			RETURN C_ID
	CASE 2;斜角的竹林…兔祭り。大型兽は少ない ユニークは槌之子
		SIF RAND:3 == 0
			RETURN 13
		SIF STRCOUNT(CritterTribe, "竹/")
			RETURN C_ID
	CASE 3;魔法之森…多样、ゲテモノも少々 ユニークは玛坦戈
		SIF STRCOUNT(CritterTribe, "魔/")
			RETURN C_ID
	CASE 4;山麓树海…多样、ユニークはニホンオオカミ、ニホンカワウソ
		SIF STRCOUNT(CritterTribe, "泽/")
			RETURN C_ID
	CASE 5;绝景之丘…小型兽が少し减る、ユニークは花泉盛牛
		SIF STRCOUNT(CritterTribe, "山/")
			RETURN C_ID
	CASE 6;幻想风穴、竖井底部…ゲテモノが取れる ユニークは死亡蠕虫
		SIF STRCOUNT(CritterTribe, "底/")
			RETURN C_ID
	CASEELSE
		;エラーによる无限ループ除け
		RETURN C_ID
ENDSELECT
;兽关联の变量をリセット
CALL CRITTER_V_RESET
GOTO LOOP

;-----------------------------------------------------------
;フィールドサインの文本を返す函数
;季节、诱捕スポットを参照し文本を变える
;-----------------------------------------------------------
@FIELDSIGN(Serch)
#DIM Serch
#DIM 季节

;季节异变に关する补正
季节 = DAY:2
SIF GatheringSeason
	季节 = GatheringSeason


SELECTCASE Serch
	CASE 0 TO 19
		SETCOLOR 0x00D787
		;地底だけ特殊
		IF HUNTING_SPOT(CFLAG:MASTER:当前位置) == 6
			PRINTDATAL
				DATAFORM 没有找到什么特别的线索…
				DATAFORM 在一条不起眼的踏痕上继续前进着…
				DATAFORM 从岩石表面渗出的水形成了一条细细支流…
				DATAFORM 干燥的粪便零星地散布着…
				DATAFORM 少见的土壤之上、生长着苔藓
				DATAFORM 缓慢地爬过一堵没有生命迹象的岩壁…
				DATAFORM 鲜有光亮的世界、情景没有什么变化…
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM 没有找到什么特别的线索…
				DATAFORM 在一条不起眼的小道上继续前进着…
				DATAFORM \@ 季节 == 4 ? 绮丽的雪原绵延着… # 适宜步行的草原绵延着… \@
				DATAFORM \@ 季节 == 4 ? 透过没有叶子的树枝的间隙可以看见天空… # 被枝叶遮挡住、什么景色都看不见… \@
				DATAFORM 一条小溪泽流过…
				DATAFORM \@ 季节 == 4 ? 被雪覆盖的树林延续着… # 长满青苔的森林延续着… \@
				DATAFORM 干燥的粪便零星地散布着…
				DATAFORM 碎掉的落叶覆盖了些许足迹…
				DATAFORM \@ 季节 == 4 ? 在雪上隐约看见了足迹形状的凹陷… # 似乎发现了掩埋在枝叶下足迹… \@
				DATAFORM \@ 季节 == 4 ? 在光滑的雪面上发现了不自然的凹痕… # 地面有被翻开的痕迹、但已经干了… \@
				DATAFORM 隐约看到了被擦蹭在树木上的毛发…
				DATAFORM \@ 季节 == 3 ? 被啃过的蘑菇四散在周围… # 一些被啃掉枝叶的灌木正在枯萎着… \@
				DATAFORM \@ 季节 == 4 ? 冷风吹拂、叩打着肌肤… # 树间吹来的风、揺动着树梢呼啸而过… \@
				DATAFORM 开始起雾了…迷失了方向…
			ENDDATA
		ENDIF
	CASE 20 TO 39
		SETCOLOR 0x5EE555
		IF HUNTING_SPOT(CFLAG:MASTER:当前位置) == 6
			PRINTDATAL
				DATAFORM 落下的粪便…非常干燥且不成形状
				DATAFORM 岩石上、有干燥的泥土足迹…
				DATAFORM 少量的水聚集在一起、形成了像水洼样的存在…
				DATAFORM 岩石上、看起来像是有什么已经干涸的东西粘在了表面
				DATAFORM 有一些被啃食过的苔藓的痕迹
				DATAFORM 隐约看到岩石上有被摩擦下的毛发的痕迹…
				DATAFORM 被啃过的蘑菇？四散着…
				DATAFORM 巨大的岩盘挡住了去路…爬上去会很费力
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM 落下的粪便…非常干燥且不成形状
				DATAFORM 和周围相比、稀薄草木上的踏迹断断续续地延续着…
				DATAFORM \@ 季节 == 4 ? 追踪者和被追踪者、在雪上留下了二重的足迹… # 发现了脚印和里面被踩碎的落叶… \@
				DATAFORM \@ 季节 == 4 ? 可以零星看到树皮被啃食、剥离的痕迹… # 藪丛中有泥坑的痕迹…似乎已经干了 \@
				DATAFORM \@ 季节 == 4 ? 发现了风化的雪上足迹… # 发现了轮廓十分模糊的足迹… \@
				DATAFORM \@ 季节 == 4 ? 也许是被深雪困住了、有一处积雪凹陷的地方 # 地面是有零星的被挖开的迹象… \@
				DATAFORM \@ 季节 == 3 ? 有一些被咬过的蘑菇、但看起来已经坏掉了… # 灌木的叶子被吃得散落一地。咬痕开始枯萎了… \@
				DATAFORM 石头上的苔藓、星星点点地开始剥落枯萎…
				DATAFORM 依靠着一个小小的立足点、爬上了难以通过的陡峭斜坡…
				DATAFORM 树枝被折断并固定在了一起…是人为的吗？还是？
				DATAFORM 被草丛中的隐秘的凹陷绊住了、差点摔倒…
			ENDDATA
		ENDIF
	CASE 40 TO 59
		SETCOLOR 0xA3F030
		IF HUNTING_SPOT(CFLAG:MASTER:当前位置) == 6
			PRINTDATAL
				DATAFORM 落在地上的粪便…已经开始有些干燥了、但还保持着形状
				DATAFORM 岩石上、泥土足迹还有些粘稠…好像是交通要道
				DATAFORM 是清澈的泉水…这真是太好了
				DATAFORM 岩石上、有什么粘稠的东西被移动过
				DATAFORM 苔藓已经被剥落了…感觉还没有枯死太久
				DATAFORM 岩石和岩石的间隙、有一个像洞穴一样的空间…
				DATAFORM 有一些被咬过的蘑菇、但看起来已经坏掉了…
				DATAFORM 从下面看、高耸的岩石使得登攀十分困难…
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM 落在地上的粪便…已经开始有些干燥了、但还保持着形状
				DATAFORM 和周围相比、少许落叶和草木上的踏迹断断续续地延续着…
				DATAFORM \@ 季节 == 4 ? 雪上的为弱的足迹、朝向山脊稜线延续着… # 发现了积水的足迹… \@
				DATAFORM \@ 季节 == 4 ? 见到了残剩的枝叶被啃食的痕迹… # 残留着泥浊的水坑…似乎被当作泥坑使用了 \@
				DATAFORM \@ 季节 == 4 ? 雪地上残留着看起来是躺下的痕迹… # 残留着踏过草木的弯曲的痕迹… \@
				DATAFORM \@ 季节 == 4 ? 仿佛两只兽在嬉戏一样的、混乱的雪地 # 地面有被翻开的痕迹、许多蚂蟻慌慌张张地跑来跑去… \@
				DATAFORM 石头上的苔藓有被剥落的痕迹…被剥落的地方快要干枯了
				DATAFORM 爬上承认兽影的岩石、俯瞰下面的场景…真可怕
				DATAFORM 不知是何者的远吠、在山野间回荡着…
			ENDDATA
		ENDIF
	CASE 60 TO 79
		SETCOLOR 0xE7FB0C
		IF HUNTING_SPOT(CFLAG:MASTER:当前位置) == 6
			PRINTDATAL
				DATAFORM 落在地上的粪便…看起来比较新鲜
				DATAFORM 岩石之上、有着无数的泥土足迹
				DATAFORM 岩盘上呈带状广散着白色物质…像岩塩一样
				DATAFORM 岩石上、像是残留着是那么东西的粘液一样
				DATAFORM 苔藓园被不自然地啃食掉了…是最近发生的
				DATAFORM 从某处传来了石头碰撞的声音…
				DATAFORM 被咬成虫食状的蘑菇、保留着原有的形状被折断了…
				DATAFORM 大概距离地上很近吧、有一点光射了进来…
				DATAFORM 咯吱咯吱的烦人的刮擦黑板声从头上响来…
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM 落在地上的粪便…看起来比较新鲜
				DATAFORM 仿佛闻到了和枝叶摩擦的沙沙声…
				DATAFORM \@ 季节 == 4 ? 雪上的足迹、向树林延伸…好像往复了好几次 # 可以清晰地见到足迹通向了藪丛中… \@
				DATAFORM \@ 季节 == 4 ? 风突然停止了…这个地形似乎提供了良好的避风港 # 藪丛中形成了一个泥场…而且还没有被废弃 \@
				DATAFORM \@ 季节 == 4 ? 找到了轮廓清晰的、雪上的足迹… # 发现了较为明显的足迹… \@
				DATAFORM \@ 季节 == 4 ? 发现了就像推着雪行走一样的、崭新的雪面凹痕… # 见到了地面上到处都是崭新的被挖掘的痕迹… \@
				DATAFORM 石头上的苔藓有被剥落的痕迹……由于仍然是绿色的、所以看起来还没过多久
				DATAFORM 感觉到有视线于是转过身去、\@ 季节 == 4 ? 结果只是被雪的反射光刺痛了眼睛… # 结果只看到了密密麻麻的树梢和覆盖其上的树叶… \@
				DATAFORM 清晰细密尖锐的鸣叫声、就像彼此交替呼唤着一样响了起来…
				DATAFORM 抬头和回头都只是山和树木…似乎已经进入了人迹未踏的原生林
				DATAFORM 绕到了一颗参天大树的旁边……树干上的空洞、大到足够可以吞込一个人
				DATAFORM 完全没有声音、一条瀑布被秘密地饮込了潭中…
			ENDDATA
		ENDIF
	CASEELSE
		SETCOLOR C_YELLOW
		IF HUNTING_SPOT(CFLAG:MASTER:当前位置) == 6
			PRINTDATAL
				DATAFORM 落在地上的粪便粪便呈固体状…似乎很新鲜
				DATAFORM 岩石之上、泥土足迹清晰地残留着…看起来是新的
				DATAFORM 少见的土壤之上、足迹清晰地残留着…
				DATAFORM 岩石上、一层厚厚的粘液覆盖着…似乎是嚼碎的苔藓之类的
				DATAFORM 沿着墙壁、来到了一个有类似树根的东西密密麻麻地降下的空间…感觉相当的不妙
				DATAFORM 嘎啦一声、岩石从头上掉落下去了…危险、危险
				DATAFORM 有被吃得散落的蘑菇的残骸…还是崭新的
				DATAFORM 通过岩石的裂缝、来到了一个地表光透入的不思议的空间…一点点的绿意盎然
				DATAFORM 听到了地鸣一样的、低沉悠长的咆哮…或者说感觉到了
				DATAFORM 岩石的缝隙中、吱吱作响的摩擦声到处都能闻到
			ENDDATA
		ELSE
			PRINTDATAL
				DATAFORM 落在地上的粪便粪便呈固体状…似乎很新鲜
				DATAFORM 树林的中间、可以见到清晰的兽道
				DATAFORM 枝叶摩擦的沙沙声从各个角度都能闻到…
				DATAFORM 有被铺置好的常青藤和草的痕迹…
				DATAFORM 地面有被平整成圆圈状的痕迹…
				DATAFORM \@ 季节 == 4 ? 发现了可能是兽群行动而产生的、雪地上的多重足迹… # 多次发现了有清晰轮廓的足迹… \@
				DATAFORM \@ 季节 == 4 ? 雪很薄、足迹集中在容易通过的场所… # 藪丛中有泥场…从泥的拨撒方式来看、刚刚还被使用过 \@
				DATAFORM \@ 季节 == 4 ? 见到了树皮被剥下后的样子…还是崭新的 # 有绿色的枝叶被咬断的痕迹…还是崭新的 \@
				DATAFORM \@ 季节 == 4 ? 这里的雪被挖空、地面露出来了… # 有地面被挖开的痕迹…还是潮湿的。好像是刚刚挖掘的 \@
				DATAFORM 石头上的苔藓、被剥落下来了…还是绿色的、是最近才被剥下的
				DATAFORM \@ 季节 == 4 ? 在洼地一样的地方、雪静静地吹着…奇妙的平静 # 在茂密的森林中、有一片异常开阔的、平静的草坪… \@
				DATAFORM 空气中弥漫着山中的寂静气息、有点害怕、汗毛竖起来了…
				DATAFORM 闻到了有谁在说悄悄话的「声音」…在这样的深山中？　不会吧…
				DATAFORM 从云层之间、从谷底、都听到了与风的声音明显不同的、呻吟般的「呼喊声」…
			ENDDATA
		ENDIF
ENDSELECT
RESETCOLOR
PRINTL 

;-----------------------------------------------------------
;陷阱介绍のフレーバー文本を流す
;介绍は６行で固定
;-----------------------------------------------------------
@TRAP_FLAVORTEXT(リザルト)
#DIM リザルト

SELECTCASE リザルト
	CASE 132
		PRINTDATAL
			DATALIST
				DATAFORM 　　┌───────────────────────┐
				DATAFORM 　　│■箱陷阱　　　　　　　　　　　　　　　　　　　　│
				DATAFORM 　　│　诱使猎物进入箱中、进入之后　　　　　　　　　│
				DATAFORM 　　│　侵入口的扉便会关闭、将猎物困住　　　　　　　│
				DATAFORM 　　│　体积小易于携带、易于设置　　　　　　　　　　│
				DATAFORM 　　└───────────────────────┘
			ENDLIST
		ENDDATA
	CASE 133
		PRINTDATAL
			DATALIST
				DATAFORM 　　┌───────────────────────┐
				DATAFORM 　　│■套索陷阱　　　　　　　　　　　　　　　　　　　│
				DATAFORM 　　│　金属丝的一端捆绑在树木或其他物体上　　　　　│
				DATAFORM 　　│　另一端是一个环、　　　　　　　　　　　　　　│
				DATAFORM 　　│　当猎物的腿进入环内时、便会收缩并将其拘束　　│
				DATAFORM 　　└───────────────────────┘
			ENDLIST
			DATALIST
				DATAFORM 　　┌───────────────────────┐
				DATAFORM 　　│■户板落下　　　　　　　　　　　　　　　　　　│
				DATAFORM 　　│　户板的一侧放置于地面、另一侧则搭起支柱　　　│
				DATAFORM 　　│　在户板下方撒诱饵来引诱猎物　　　　　　　　　│
				DATAFORM 　　│　被饵引诱的猎物、会被户板压住而被捕获　　　　│
				DATAFORM 　　└───────────────────────┘
			ENDLIST
		ENDDATA
	CASE 134
		PRINTDATAL
			DATALIST
				DATAFORM 　　┌───────────────────────┐
				DATAFORM 　　│■捕兽夹　　　　　　　　　　　　　　　　　　　│
				DATAFORM 　　│　当脚踩中陷阱的中央的踏板时、金属板会　　　　　│
				DATAFORM 　　│　弹起并用力夹住脚部的陷阱　　　　　　　　　　　│
				DATAFORM 　　│　在现代日本是被禁止使用的　　　　　　　　　　│
				DATAFORM 　　└───────────────────────┘
			ENDLIST
			DATALIST
				DATAFORM 　　┌───────────────────────┐
				DATAFORM 　　│■机关弩　　　　　　　　　　　　　　　　　　　│
				DATAFORM 　　│　放置好的石弓的扳机上系着一条线、绑在　　　　│
				DATAFORM 　　│　附近的树木上　当路过的　　　　　　　　　　　│
				DATAFORM 　　│　猎物触碰到张力线时、自动发射箭矢　　　　　　│
				DATAFORM 　　└───────────────────────┘
			ENDLIST
		ENDDATA
ENDSELECT
CALL DQPRINT("…………", 1)
CALL DQPRINT("………", 1)
CALL DQPRINT("……", 1)

;-----------------------------------------------------------
;かかった猎物のステータス补正をする描写函数
;同一日では(幸运补正が变わらない限り)何度参照しても同じ数を返す
;ARG TRAP_CAPTURE:Xから引く　猎物のID
;RETURN 0 补正无し  1 少し太った  2 まるまる太った 3 旬で脂がのっている
;-----------------------------------------------------------
@SPOIL_ENHANCEMENT(ARG)
#FUNCTION
#DIM 季节

;季节异变に关する补正
季节 = DAY:2
SIF GatheringSeason
	季节 = GatheringSeason

;旬のものを先に判定
SELECTCASE ARG
	CASE 16, 17, 19, 27;鹿、髭羚、亚洲黑熊、矢部大角鹿は秋
		SIF 季节 == 3
			RETURNF 3
	CASE 15;野猪は冬
		SIF 季节 == 4
			RETURNF 3
ENDSELECT

RANDOMIZE DAY:0
SELECTCASE MAX(RAND:100 + (TFLAG:幸运补正 * 10), 0)
	CASE 0 TO 40
		RETURNF 0
	CASE 41 TO 80
		RETURNF 1
	CASEELSE
		RETURNF 2
ENDSELECT

;-----------------------------------------------------------
;かかった猎物名を补正文本とともに描写
;上の函数を利用
;STYLE =  0,L  = 1,W =  =2, 改行无し
;FONT = 0,无 = 1,太字
;-----------------------------------------------------------
@SPOIL_TEXT(猎物, STYLE = 0, FONT = 0)
#DIM 猎物
#DIM STYLE
#DIM FONT

SELECTCASE SPOIL_ENHANCEMENT(TRAP_CAPTURE:TP_TO_PLACE())
	CASE 0
		LOCALS = %CritterName%
	CASE 1
		LOCALS = 大%CritterName%
	CASE 2
		LOCALS = 肥胖的%CritterName%
	CASE 3
		LOCALS = 非常肥的%CritterName%
	CASEELSE
		LOCALS = %CritterName%
ENDSELECT

SIF FONT
	FONTSTYLE 1

SELECTCASE STYLE
	CASE 1
		SETCOLOR C_YELLOW
		PRINTFORMW %LOCALS%
	CASE 2
		PRINTFORM  %LOCALS%
	CASEELSE
		SETCOLOR C_YELLOW
		PRINTFORML %LOCALS%
ENDSELECT
FONTREGULAR
RESETCOLOR

;-----------------------------------------------------------
;かかった猎物と同行角色から、强制莉莉スの判定をする函数
;RETURNF 1　で强制莉莉ス
;-----------------------------------------------------------
@SPOIL_RELEASE(CHARA, 猎物)
#FUNCTION
#DIM CHARA
#DIM 猎物

SELECTCASE CHARA
	CASE [[蕾米莉亚]], [[芙兰]]
		SIF 猎物 == 21;狐蝠
			RETURNF 1
	CASE [[橙]], [[阿燐]], [[星]], [[三花]]
		SIF 猎物 == 12;野猫
			RETURNF 1
	CASE [[蓝]], [[典]]
		SIF 猎物 == 10;狐
			RETURNF 1
	CASE [[莉格露]], [[山女]], [[拉尔瓦]], [[百百世]]
		SIF 猎物 == 23;死亡蠕虫
			RETURNF 1
	CASE [[米斯蒂亚]]
		SIF 猎物 == 50 || 猎物 == 51 || 猎物 == 52 || 猎物 == 53 || 猎物 == 54;鸟类
			RETURNF 1
	CASE [[纳兹琳]]
		SIF 猎物 == 1 || 猎物 == 5 || 猎物 == 14;老鼠、松鼠、海狸鼠
			RETURNF 1
	CASE [[铃仙]], [[帝]], [[铃仙二号]], [[清兰]], [[铃瑚]]
		SIF 猎物 == 13;兔
			RETURNF 1
	CASE [[影狼]], [[椛]]
		SIF 猎物 == 11 || 猎物 == 29;野狗、ニホンオオカミ
			RETURNF 1
	CASE [[猯藏]]
		SIF 猎物 == 7;貉
			RETURNF 1
	CASE [[润美]]
		SIF 猎物 == 28;花泉盛牛
			RETURNF 1
	CASE [[早鬼]]
		SIF 猎物 == 29;ニホンオオカミ
			RETURNF 1
	CASE [[八千慧]]
		SIF 猎物 == 30;ニホンカワウソ
			RETURNF 1
ENDSELECT
RETURNF 0

;-----------------------------------------------------------
;现地点がTRAP_POINT:X　のどこかを返す函数
;-----------------------------------------------------------
@TP_TO_PLACE()
#FUNCTION

FOR LOCAL, 1, 4
	SIF CFLAG:MASTER:当前位置 == TRAP_POINT:LOCAL
		RETURNF LOCAL
NEXT
RETURNF 0

;-----------------------------------------------------------
;陷阱关联の变量の全リセット
;-----------------------------------------------------------
@TRAP_V_ALLRESET

VARSET TRAP_SETUPDATA
VARSET TRAP_TYPE
VARSET TRAP_ALERTNESS
VARSET TRAP_DURABILITY
VARSET TRAP_DENSITY
VARSET TRAP_CAPTURE
VARSET TRAP_CAPTUREDATA
VARSET TRAP_POINT

;-----------------------------------------------------------
;指定した陷阱关联の变量の全リセット
;ARG TP_TO_PLACE()
;-----------------------------------------------------------
@TRAP_V_RESET(ARG)

TRAP_SETUPDATA:(ARG) = 0
TRAP_TYPE:(ARG) = 0
TRAP_ALERTNESS:(ARG) = 0
TRAP_DURABILITY:(ARG) = 0
TRAP_DENSITY:(ARG) = 0
TRAP_CAPTURE:(ARG) = 0
TRAP_CAPTUREDATA:(ARG) = 0
TRAP_POINT:(ARG) = 0

;-----------------------------------------------------------
;兽关联の变量のリセット
;-----------------------------------------------------------
@CRITTER_V_RESET

VARSET CritterName
VARSET CritterMeat
VARSET CritterMeat_Value
VARSET CritterEffort
VARSET CritterIntro
VARSET CritterHint
VARSET CritterTribe

;-----------------------------------------------------------
;引っ越し时の陷阱の设置ポイントの张替えを行う函数
;-----------------------------------------------------------
@TRAP_MOVING
#DIM トラップ番号

FOR トラップ番号, 1, 4
	;メイン→サブMapへの张替え
	SELECTCASE TRAP_POINT:トラップ番号
		CASE 25
			SIF MAIN_MAP != 0
				TRAP_POINT:トラップ番号 = 50
		CASE 433
			SIF MAIN_MAP != 4
				TRAP_POINT:トラップ番号 = 430
		CASE 507
			SIF MAIN_MAP != 5
				TRAP_POINT:トラップ番号 = 510
		CASE 702
			SIF MAIN_MAP != 7
				TRAP_POINT:トラップ番号 = 710
		CASE 805
			SIF MAIN_MAP != 8
				TRAP_POINT:トラップ番号 = 820
		CASE 948
			SIF MAIN_MAP != 9
				TRAP_POINT:トラップ番号 = 910
	;サブ→メインMapへの张替え
		CASE 50
			SIF MAIN_MAP == 0
				TRAP_POINT:トラップ番号 = 25
		CASE 430
			SIF MAIN_MAP == 4
				TRAP_POINT:トラップ番号 = 433
		CASE 510
			SIF MAIN_MAP == 5
				TRAP_POINT:トラップ番号 = 507
		CASE 710
			SIF MAIN_MAP == 7
				TRAP_POINT:トラップ番号 = 702
		CASE 820
			SIF MAIN_MAP == 8
				TRAP_POINT:トラップ番号 = 805
		CASE 910
			SIF MAIN_MAP == 9
				TRAP_POINT:トラップ番号 = 948
	ENDSELECT
NEXT

;昆虫诱捕装置
;设置した场所に引っ越した
IF MAIN_MAP == GET_MUSHIDATA(MUSHI_TRAP, "场所") / 100
	SELECTCASE GET_MUSHIDATA(MUSHI_TRAP, "场所")
	;樱花道的鸟居,境内
	CASE 10, 20
		CALL MUSHI_TRAP_MOVING(2)
	;妖精的大树
	CASE 50
		CALL MUSHI_TRAP_MOVING(25)
	CASE 倒垂的柳树下, 命莲寺境内
		CALL MUSHI_TRAP_MOVING(102)
	CASE 墓地
		CALL MUSHI_TRAP_MOVING(121)
	CASE 广场
		CALL MUSHI_TRAP_MOVING(202)
	CASE 长屋街
		CALL MUSHI_TRAP_MOVING(201)
	CASE 雾之湖
		CALL MUSHI_TRAP_MOVING(347)
	CASE 废洋馆
		CALL MUSHI_TRAP_MOVING(343)
	CASE 正门
		CALL MUSHI_TRAP_MOVING(302)
	CASE 竹林入口
		CALL MUSHI_TRAP_MOVING(433)
	CASE 迷失的小道
		CALL MUSHI_TRAP_MOVING(434)
	CASE 斜角的竹林
		CALL MUSHI_TRAP_MOVING(434)
	CASE 兔的洞穴
		CALL MUSHI_TRAP_MOVING(431)
	CASE 无名之丘
		CALL MUSHI_TRAP_MOVING(436)
	CASE 太阳花田
		CALL MUSHI_TRAP_MOVING(438)
	CASE 森林的入口
		CALL MUSHI_TRAP_MOVING(501)
	CASE 古木的大树
		CALL MUSHI_TRAP_MOVING(540)
	CASE 再思之道
		CALL MUSHI_TRAP_MOVING(509)
	CASE 无缘冢
		CALL MUSHI_TRAP_MOVING(506)
	CASE 中有之道
		CALL MUSHI_TRAP_MOVING(601)
	CASE 三途之川
		CALL MUSHI_TRAP_MOVING(602)
	CASE 彼岸, 地狱
		CALL MUSHI_TRAP_MOVING(604)
	CASE 白玉楼庭院
		CALL MUSHI_TRAP_MOVING(621)
	CASE 山麓树海
		CALL MUSHI_TRAP_MOVING(701)
	CASE 玄武之泽
		CALL MUSHI_TRAP_MOVING(703)
	CASE 大蛤蟆之池
		CALL MUSHI_TRAP_MOVING(708)
	CASE 绝景之丘
		CALL MUSHI_TRAP_MOVING(805)
	CASE 山之湖
		CALL MUSHI_TRAP_MOVING(814)
	CASE 守矢神社境内
		CALL MUSHI_TRAP_MOVING(811)
	CASE 幻想风穴
		CALL MUSHI_TRAP_MOVING(948)
	CASE 地狱的深道
		CALL MUSHI_TRAP_MOVING(945)
	CASE 旧地狱街道
		CALL MUSHI_TRAP_MOVING(939)
	CASE 地灵殿
		CALL MUSHI_TRAP_MOVING(901)
	CASE 宁静海
		CALL MUSHI_TRAP_MOVING(916)
	ENDSELECT
;设置した场所から引っ越した
ELSEIF !MUSHITORI_SPOT(GET_MUSHIDATA(MUSHI_TRAP, "场所"))
	CALL MUSHI_TRAP_MOVING(GET_MAP_REPLACEMENT(GET_MUSHIDATA(MUSHI_TRAP, "场所")))
ENDIF


;-----------------------------------------------------------
;陷阱の日数经过による变化について变量に记入を行う
;-----------------------------------------------------------
@TRAP_PROGRESS
#DIM トラップ番号
#DIM 期待值
#DIM 生息密度

FOR トラップ番号, 1, 4
	IF TRAP_POINT:トラップ番号
		;未捕获の场合
		IF TRAP_CAPTURE:トラップ番号 == 0 && TRAP_DENSITY:トラップ番号 >= 1
			;陷阱发动判定 生息密度の回数繰り返し
			FOR LOCAL:1, 1, TRAP_DENSITY:トラップ番号 + 1
				;期待值 1～…100 - 陷阱警戒度 + 幸运补正*10(-50～+50) + 今日的运势/20（0～50）+ RAND:(陷阱设置からの经过日数 * 10)
				期待值 = MAX(100 - TRAP_ALERTNESS:トラップ番号 + (TFLAG:幸运补正 * 10) + (CFLAG:MASTER:今日的运势 / 20) + RAND:((1 + DAY:0 - TRAP_SETUPDATA:トラップ番号) * 8), 1)
				DEBUGPRINTFORML 陷阱{トラップ番号}　猎物{LOCAL:1}　期待值…{期待值}
				IF RAND:期待值 > 50
					CALL CRITTER_SPECIES(HUNTING_SPOT(TRAP_POINT:トラップ番号), TRAP_TYPE:トラップ番号)
					TRAP_CAPTURE:トラップ番号 = RESULT
					TRAP_CAPTUREDATA:トラップ番号 = DAY:0
					BREAK
				ENDIF
			NEXT
		ENDIF
		;时间经过に伴う陷阱の警戒度の变动　一日中奖了－２
		IF TRAP_ALERTNESS:トラップ番号
			TRAP_ALERTNESS:トラップ番号 = MAX(TRAP_ALERTNESS:トラップ番号 - 2, 0)
			
			;天候によってさらに低下
			SIF !GROUPMATCH(TIME:5, 0, 1, 2, 3) && TRAP_ALERTNESS:トラップ番号 > 0
				TRAP_ALERTNESS:トラップ番号 --
		ENDIF
		;时间经过に伴う陷阱の耐久度の变动
		IF TRAP_DURABILITY:トラップ番号 > 0
			TRAP_DURABILITY:トラップ番号 --
			;恶劣天气でさらに低下
			SIF GROUPMATCH(TIME:5, 5, 9) && TRAP_DURABILITY:トラップ番号 > 0
				TRAP_DURABILITY:トラップ番号 --
			;猎物がかかっているとさらに低下
			IF TRAP_CAPTURE:トラップ番号 && TRAP_DURABILITY:トラップ番号 > 0
				CALL CRITTER_DATA(TRAP_CAPTURE:トラップ番号)
				TRAP_DURABILITY:トラップ番号 --
				;大型の猎物がかかっているとさらに低下
				SIF CritterMeat_Value >= 5 && TRAP_DURABILITY:トラップ番号 > 0
					TRAP_DURABILITY:トラップ番号 --
				CALL CRITTER_V_RESET
			ENDIF
		ENDIF
		;兽の生息密度の变动
		TRAP_DENSITY:トラップ番号 = MAX(TRAP_DENSITY:トラップ番号 + RAND:4 - RAND:3, 0)
		
		DEBUGPRINTFORML 　陷阱{トラップ番号}：{TRAP_CAPTURE:トラップ番号}
		DEBUGPRINTFORML 　　 　警戒度：{TRAP_ALERTNESS:トラップ番号}
		DEBUGPRINTFORML 　　 　耐久度：{TRAP_DURABILITY:トラップ番号}
		DEBUGPRINTFORML 　　 生息密度：{TRAP_DENSITY:トラップ番号}
	ENDIF
NEXT


;-------------------------------------------------
;昆虫诱捕装置
;-------------------------------------------------
@MUSHI_TRAP
;昆虫诱捕装置はMASTERの一人作业なので约会经验やTARGETへのソースは无い
;昆虫诱捕装置の样子を见る
IF GROUPMATCH(CFLAG:MASTER:当前位置, GET_MUSHIDATA(MUSHI_TRAP, "场所"))
	IF !GET_MUSHIDATA(MUSHI_TRAP, "ムシ番号")
		PRINTFORML 　设置的诱饵：%ITEMNAME:(GET_MUSHIDATA(MUSHI_TRAP, "エサ番号"))%
		PRINTFORML 　　诱饵质量：%"☆" * MUSHI_ESA_DATA(GET_MUSHIDATA(MUSHI_TRAP, "エサ番号"))%
		PRINTFORML 　　诱饵类型：%RESULTS:10%
		DRAWLINE
		PRINTFORML 好像还没有虫子来
		PRINTFORML 　[ 0]保持原样
		PRINTFORML 　[ 1]破弃陷阱
		INPUT
		IF RESULT
			MUSHI_TRAP = 0
			TFLAG:193 = 11
			IF FLAG:时间停止
				BASE:MASTER:TSP -= 20
			ELSE
				TIME += 5
			ENDIF
		ELSE
			RETURN -1
		ENDIF
	ELSE
		PRINTL 
		PRINTFORMW ………！
		PRINTFORMW 上面覆盖了昆虫！
		PRINTL 
		MUSHI_TRAP += DAY * 1000000000000 
		CALL SHOW_MUSHI_DATA(MUSHI_TRAP)
		DRAWLINE
		PRINTFORML 用昆虫诱捕装置捕获了%MUSHI_NAME%！
		CALL MUSHI_CAPTURE(MUSHI_TRAP)
		MUSHI_TRAP = 0
		TFLAG:193 = 12
		EXP:MASTER:采集经验 ++
		IF FLAG:时间停止
			BASE:MASTER:TSP -= 20
		ELSE
			TIME += 5
		ENDIF
	ENDIF
;昆虫诱捕装置を仕挂ける
ELSE
	PRINTL ◆请为昆虫诱捕装置选择诱饵（消耗）
	DRAWLINE
	FOR LOCAL, 0, 800
		SIF ITEMNAME:LOCAL == ""
			CONTINUE
		SIF !ITEM:LOCAL
			CONTINUE
		SIF MUSHI_ESA_DATA(LOCAL)
			PRINTFORMC %ITEMNAME:LOCAL%({ITEM:LOCAL})[{LOCAL, 3}]
	NEXT
	PRINTL 
	DRAWLINE
	PRINTC 果然还是放弃[999]
	INPUT
	IF INRANGE(RESULT, 0, 800) && ITEM:RESULT
		ITEM:RESULT --
		MUSHI_TRAP = RESULT * 1000000000000 + CFLAG:MASTER:当前位置 * 100000000
		TFLAG:193 = 10
		IF FLAG:时间停止
			BASE:MASTER:TSP -= 50
		ELSE
			DOWNBASE:MASTER:体力 = 50
			DOWNBASE:MASTER:气力 = 50
			TIME += 20
		ENDIF
	ELSE
		RETURN -1
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;昆虫诱捕装置でムシを捕まえる函数
;@MUSHITORIを参考に
;-------------------------------------------------
@MUSHI_TRAP_PROGRESS
#DIM パワー
#DIM 初期レアリティ指定
#DIM レアリティ指定
#DIM ランク
#DIM Ｇ型
#DIM Ｘ型
#DIM MUSHI_ID
#DIM 捕まえたムシ
IF MUSHI_TRAP && !GET_MUSHIDATA(MUSHI_TRAP, "ムシ番号")
	パワー = LIMIT(5 + MUSHI_ESA_DATA(GET_MUSHIDATA(MUSHI_TRAP, "エサ番号")), 1, 10)
	;捕获判定
	SIF RAND:パワー < 2
		RETURN
	;特定好物と好物で2回回す
	初期レアリティ指定 = 0
	FOR LOCAL, 1, 3
		SIF !初期レアリティ指定
			初期レアリティ指定 = RAND:パワー
		レアリティ指定 = 初期レアリティ指定
		Ｇ型 = 0
		Ｘ型 = 0
		MUSHI_ID = 0
		WHILE レアリティ指定 >= 0
			;1回目は特定好物で优先的に拾う
			IF LOCAL == 1
				CALL DRAW_MUSHI("昆虫诱捕装置特定好物", GET_MUSHIDATA(MUSHI_TRAP, "场所"), レアリティ指定)
			ELSE
				CALL DRAW_MUSHI("昆虫诱捕装置", GET_MUSHIDATA(MUSHI_TRAP, "场所"), レアリティ指定)
			ENDIF
			IF RESULT
				捕まえたムシ = RESULT
				MUSHI_CAPTURE ++
				;ランク判定
				ランク = MIN(RAND:(20 * MUSHI_ESA_DATA(GET_MUSHIDATA(MUSHI_TRAP, "エサ番号"))), 99)
				SIF RAND:100 < (パワー / 2)
					Ｇ型 = 1
				SIF RAND:100 < (パワー / 2)
					Ｘ型 = 1
				;ID设定,日期は实际に捕まえた时
				MUSHI_ID = MUSHI_CAPTURE * 1000000000000 + GET_MUSHIDATA(MUSHI_TRAP, "场所") * 100000000 + ランク * 10000 + MUSHI_RARE * 1000 + 捕まえたムシ
				SIF Ｇ型
					MUSHI_ID += 1000000
				SIF Ｘ型
					MUSHI_ID += 10000000
				MUSHI_TRAP = MUSHI_ID
				RETURN
			ELSE
				レアリティ指定 --
			ENDIF
		WEND
	NEXT
ENDIF

;-------------------------------------------------
;昆虫诱捕装置を引っ越す函数
;ARGが引越し先
;-------------------------------------------------
@MUSHI_TRAP_MOVING(ARG)
MUSHI_TRAP -= GET_MUSHIDATA(MUSHI_TRAP, "场所") * 100000000
MUSHI_TRAP += ARG * 100000000
