;-------------------------------------------------
;读日记
;日常系指令
;-------------------------------------------------
@COM406
#DIM 同栖
#DIM OWNER

IF TARGET > 0 && CFLAG:TARGET:初始位置 == CFLAG:MASTER:初始位置
	同栖 = 1
ELSE
	同栖 = 0
ENDIF
OWNER = COM406_CHECK_OWNER()

LOCAL = 50 + ABL:MASTER:话术技能 * 5 + ABL:TARGET:亲密 * 2
SIF BASE:TARGET:酒气 > MAXBASE:TARGET:酒气 / 2
	LOCAL -= 15

SELECTCASE TFLAG:192
	;通常
	CASE 0
		SELECTCASE RAND:LOCAL
			CASE IS < 20
				TFLAG:193 = 1
			CASE 20 TO 50
				TFLAG:193 = 2
			CASEELSE
				TFLAG:193 = 3
		ENDSELECT
	;强制成功or大成功
	CASE 1
		SELECTCASE RAND:LOCAL
			CASE IS < 50
				TFLAG:193 = 2
			CASEELSE
				TFLAG:193 = 3
		ENDSELECT
	;强制失败
	CASE -1
		TFLAG:193 = 1
	;指令结束
	CASE -2
		TFLAG:193 = -1
		RETURN 0
ENDSELECT

TFLAG:194 = OWNER

IF CFLAG:MASTER:当前位置 == CFLAG:MASTER:初始位置
	PRINTFORML 书桌上放着\@ 同栖 ? 两本 # 一本 \@日记。
	PRINTFORML 要做什么？
	VARSET LOCALS, "？？？？"
	IF TARGET > 0
		LOCALS:0 = 让%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%读日记
		LOCALS:1 = 读%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的日记
	ENDIF
	{
	CALL ASK_M("写日记",1,"读日记",1,@"%LOCALS:0%", TARGET > 0,
	@"%LOCALS:1%", 同栖 && (!SHIRAHU(TARGET) || TALENT:TARGET:恋慕 ),
	"保存日记簿",1, "读取日记簿",1,
	"什么也不做",1)
	}
	SELECTCASE RESULT
		CASE 0
			IF STRCOUNT(SAVESTR:1,"//") >= 100
				PRINTFORML 这本日记簿已经写满了。
				PRINTFORMW 更换另一个日记簿吧。
			ELSE
				PRINTFORML 请直接输入日记内容。
				PRINTFORML 日期由系统自动记录、故不需要手动输入。
				PRINTL
				CALL MASTER_DIARY_INPUT
				SIF RESULTS != ""
					CALL MASTER_DIARY_WRITE,RESULTS
				PRINTL
			ENDIF
		CASE 1
			CALL READ_MASTER_DIARY
		CASE 2
			IF ABL:TARGET:亲密 >= 5
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%拿起了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的日记…
				CALL SHARE_MASTER_DIARY
			ELSE
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%似乎不感兴趣…
			ENDIF
		CASE 3
			CALL CHARA_DIARY_COM(TARGET)
		CASE 4
			CALL WRITE_MASTER_DIARY_FILE
		CASE 5
			CALL READ_MASTER_DIARY_FILE
		CASE 6
			RETURN -1
	ENDSELECT
ELSEIF TALENT:OWNER:恋慕 && CFLAG:MASTER:当前位置 != CFLAG:MASTER:初始位置
	PRINTFORML 枕头旁边放置着日记本。
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", OWNER)%似乎愿意分享日记的内容。
	PRINTFORML 该怎么做呢？
	CALL ASK_YN("读日记","不读")
	IF !RESULT
		CALL CHARA_DIARY_COM(OWNER)
	ELSE
		RETURN -1
	ENDIF
ELSEIF CFLAG:OWNER:当前位置 != CFLAG:MASTER:当前位置 && TARGET && !(FLAG:70 || !SHIRAHU(TARGET))
	PRINTFORML 枕头下放置着%NAME_OUTPUT_TRANSLATION("CALLNAME", OWNER)%的日记本。
	PRINTFORML 然而…%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%就在旁边…
	PRINTFORML 该怎么做呢？
	CALL ASK_YN("一起偷看日记","不读")
	IF !RESULT && TFLAG:193 > 0
		CALL CHARA_DIARY_COM(OWNER)
	ELSE
		PRINTFORML 还是算了吧…
		RETURN -1
	ENDIF
ELSE
	PRINTFORML 枕头下面放置着日记本。
	PRINTFORML 怎么做？
	CALL ASK_YN("偷看","不读")
	IF !RESULT
		CALL CHARA_DIARY_COM(OWNER)
	ELSE
		RETURN -1
	ENDIF
ENDIF

IF !FLAG:70
	TIME += 5
ELSE
	BASE:MASTER:TSP -= 10
ENDIF

IF SHIRAHU(TARGET) && TARGET > 0
	;固定で获得するソース
	SOURCE:欢乐 = 200

	;ABL:亲密をみる
	IF ABL:亲密 <= 1
		SOURCE:欢乐 += (ABL:亲密 * 30)
	ELSEIF ABL:亲密 <= 3
		SOURCE:欢乐 += 200 + (ABL:亲密 * 30)
	ELSEIF ABL:亲密 <= 5
		SOURCE:欢乐 += 500 + (ABL:亲密 * 30)
	ELSEIF ABL:亲密 <= 8
		SOURCE:欢乐 += 750 + (ABL:亲密 * 50)
	ELSEIF ABL:亲密 <= 10
		SOURCE:欢乐 += 1000 + (ABL:亲密 * 50)
	ELSE
		SOURCE:欢乐 += 1600 + (ABL:亲密 * 30)
	ENDIF


	SOURCE:欢乐 = LIMIT(SOURCE:欢乐,0,5000)


	SOURCE:被动 = 100 + 100 * ABL:顺从
	SOURCE:征服 = 100 + 100 * ABL:施虐属性

	IF TFLAG:193 == -1 || TFLAG:193 == -2
		TIMES SOURCE:欢乐 , 0.10
		TIMES SOURCE:征服 , 0.50
		TIMES SOURCE:被动 , 0.50
	ELSEIF TFLAG:193 == 0
		TIMES SOURCE:欢乐 , 1.00
		TIMES SOURCE:征服 , 1.00
		TIMES SOURCE:被动 , 1.00
	ELSE
		TIMES SOURCE:欢乐 , 2.00
		TIMES SOURCE:征服 , 2.00
		TIMES SOURCE:被动 , 2.00
	ENDIF
	SELECTCASE ABL:MASTER:话术技能
		CASE 0
			TIMES SOURCE:欢乐 , 0.20
			TIMES SOURCE:征服 , 0.20
			TIMES SOURCE:被动 , 0.20
		CASE 1
			TIMES SOURCE:欢乐 , 0.40
			TIMES SOURCE:征服 , 0.40
			TIMES SOURCE:被动 , 0.40
		CASE 2
			TIMES SOURCE:欢乐 , 0.70
			TIMES SOURCE:征服 , 0.70
			TIMES SOURCE:被动 , 0.70
		CASE 3
			TIMES SOURCE:欢乐 , 1.00
			TIMES SOURCE:征服 , 1.00
			TIMES SOURCE:被动 , 1.00
		CASE 4
			TIMES SOURCE:欢乐 , 1.20
			TIMES SOURCE:征服 , 1.20
			TIMES SOURCE:被动 , 1.20
		CASEELSE
			TIMES SOURCE:欢乐 , 1.50
			TIMES SOURCE:征服 , 1.50
			TIMES SOURCE:被动 , 1.50
	ENDSELECT
ENDIF
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE406
#DIM OWNER
OWNER = COM406_CHECK_OWNER()

;移动实行判定
SIF !TFLAG:100
	RETURN 0
	
;恶作剧かウフフ中
SIF CFLAG:MASTER:恶作剧
	RETURN 0

SIF CFLAG:MASTER:诶嘿嘿
	RETURN 0

;据点にいる时か待客室のみ
SIF !AT_HOME(MASTER) && CFLAG:MASTER:当前位置 != OMANEKIBEYA()
	RETURN 0

SIF CFLAG:MASTER:当前位置 != CFLAG:MASTER:初始位置 && !OWNER
	RETURN 0

SIF !TALENT:OWNER:恋慕 && ABL:MASTER:教养 < ABL:OWNER:教养 && OWNER
	RETURN 0

SIF !TALENT:OWNER:恋慕 && CFLAG:OWNER:当前位置 == CFLAG:OWNER:初始位置 && !(FLAG:70 || !SHIRAHU(OWNER)) && OWNER
	RETURN 0

SIF !TALENT:TARGET:恋慕 && !(FLAG:70 || !SHIRAHU(TARGET)) && TARGET
	RETURN 0

RETURN 1

@MESSAGE_COM406
#DIM OWNER
OWNER = COM406_CHECK_OWNER()

IF (!TALENT:OWNER:恋慕 && !(FLAG:70 || !SHIRAHU(OWNER))) && !(CFLAG:MASTER:当前位置 == CFLAG:MASTER:初始位置)
	PRINTFORML 读完之后、把日记本轻轻放回原处。
ELSE
ENDIF

;日记の着者チェッカー
@COM406_CHECK_OWNER
#FUNCTION

IF TARGET > 0 && CFLAG:TARGET:初始位置 == CFLAG:MASTER:初始位置 && CFLAG:MASTER:当前位置 == CFLAG:MASTER:初始位置
	RETURNF TARGET
ELSEIF TARGET == CFLAG:MASTER:招待 && CFLAG:MASTER:当前位置 == OMANEKIBEYA()
	RETURNF CFLAG:MASTER:招待
ELSE
	RETURNF PRIVATEROOM:(CFLAG:MASTER:当前位置 % 100)
ENDIF
