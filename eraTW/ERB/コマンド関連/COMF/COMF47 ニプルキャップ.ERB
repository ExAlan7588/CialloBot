;-------------------------------------------------
;乳头夹
;道具使用指令、レベル2
;-------------------------------------------------
@COM47
;着脱の处理
TEQUIP:乳头夹 = !TEQUIP:乳头夹

;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = PLAYER
;侍奉快乐フラグ
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 80
	SOURCE:快Ｂ = 200

ELSE

	DOWNBASE:体力 += 10
	DOWNBASE:气力 += 80

	SOURCE:快Ｂ = 200
	SOURCE:露出 = 20
	SOURCE:偏离正轨 = 30
	SOURCE:反感 = 30


	;-------------------------------------------------
	;特殊ソース处理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑郁ソース追加
	SIF TALENT:自己爱 < 0 || TALENT:抵抗
		SOURCE:郁闷 += 30
ENDIF
;-------------------------------------------------
;経験の处理
;-------------------------------------------------
;同性爱　レズなら返回值1ゲイなら返回值2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
EXP:PLAYER:道具使用经验 ++

RETURN 1
;-------------------------------------------------
;装着继续の处理
;-------------------------------------------------
@EQUIP_COM47(ARG)
;乳头夹装备中
PRINTL ＜乳头夹装备中＞
;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 20
	SOURCE:ARG:快Ｂ += 100

ELSE

	DOWNBASE:ARG:体力 += 5
	DOWNBASE:ARG:气力 += 20

	SOURCE:ARG:快Ｂ += 100
	SOURCE:ARG:露出 += 10
	SOURCE:ARG:偏离正轨 += 15
	SOURCE:ARG:反感 += 15
ENDIF
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE47
;乳头夹实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(47)
	RETURN RESULT
;解除是つでも可能
SIF TEQUIP:16
	RETURN 1
;道具を持っているか、道具无设定でないとダメ
SIF ITEM:10 == 0 && NOITEM == 0
	RETURN 0
;上半身着衣
SIF TEQUIP:1 > 1 && TEQUIP:3 == 0
	RETURN 0
;搾乳器使用中は不可
SIF TEQUIP:17
	RETURN 0
;乳房电极装着中はダメ
SIF TEQUIP:24
	RETURN 0
;ASSIの场合、技巧3以上じゃないと使えない。
IF ASSIPLAY
	SIF ABL:ASSI:技巧 < 3
		RETURN 0
ENDIF
;淋浴中はダメ
SIF TEQUIP:32
	RETURN 0
RETURN 1

