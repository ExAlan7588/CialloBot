;-------------------------------------------------
;骑乘位肛交
;性交系指令、レベル2
;-------------------------------------------------
@COM66
;前回の调教が3Pのとき3Pへ
IF PREVCOM == 71
	CALL COM_ABLE71
	SIF RESULT
		JUMP COM71
ENDIF
;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = TARGET

;侍奉快乐フラグ
TCVAR:108 = 1

CSTR:PLAYER:10 += @"{TARGET}/"
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛门 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛门侍奉 10=道具 11=触手 12=Ａ调教 13=Ｖ调教）
TCVAR:PLAYER:2 = 2
TCVAR:PLAYER:童贞丧失 = 1
TCVAR:Ａ丧失发生 = 1
;强度
TCVAR:PLAYER:4 = 2200

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛门 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛门侍奉 10=道具 11=触手 12=Ａ调教 13=Ｖ调教）
TCVAR:TARGET:2 = 12
;强度
TCVAR:TARGET:4 = 1300

DOWNBASE:PLAYER:体力 += 20
DOWNBASE:PLAYER:气力 += 40

;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 200
	SOURCE:PLAYER:快Ｃ = 400

	SOURCE:快Ａ = 500
	SOURCE:苦痛 = 400

ELSE

	DOWNBASE:体力 += 90
	DOWNBASE:气力 += 50
	SOURCE:PLAYER:快Ｃ = 400

	SOURCE:快Ａ = 500
	SOURCE:情爱 = 250
	SOURCE:性行动 = 250
	SOURCE:达成 = 250
	SOURCE:苦痛 = 800
	SOURCE:露出 = 120
	SOURCE:偏离正轨 = 700
	SOURCE:反感 = 900

	;上のほうで计算した污れデータ
	SOURCE:不洁 = Y*10 + 60

	;-------------------------------------------------
	;特殊ソース处理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑郁ソース追加
	SIF TALENT:自己爱 < 0 || TALENT:抵抗
		SOURCE:郁闷 += 900
ENDIF
;Aへの苦痛は先に处理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;污れの处理
;-------------------------------------------------
;射精に关系なく行われる处理はここから
;奴隷のＡ⇔调教者のＰ的污垢が移动
CALL STAIN_肛门Ｐ插入

;-------------------------------------------------
;経験の处理
;-------------------------------------------------
;A経験
EXP:Ａ经验 ++

;性交经验

EXP:PLAYER:插入经验 ++
;同性爱　レズなら返回值1ゲイなら返回值2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	ENDIF
		EXP:PLAYER:时奸经验 += 1
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 7
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 7
	ENDIF
	EXP:Ａ性交经验 ++
	;爱情经验
	SIF !ASSIPLAY
		EXP:爱情经验 += TALENT:恋慕
ENDIF
RETURN 1

@CAN_COM66(ARG)
IF FLAG:70 == 1
	SKIPDISP ARG
	PRINTL 用骑乘位侵犯肛门
ELSE
	SKIPDISP ARG
	PRINTL 骑乘位肛交
	;-------------------------------------------------
	;实行判定处理
	;-------------------------------------------------
	A = 0
	S = 0

	;全部の命令に共通の要素を考虑
	;(顺从が高いと命令に从いやすいなど)
	CALL COM_ORDER

	;ABL:欲望
	IF ABL:欲望
		SIF S
			PRINT  + 
		A += ABL:欲望 * 3
		PRINT 欲望
		PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
		S = 1
	ENDIF
	;ABL:Ａ感觉
	IF ABL:Ａ感觉
		SIF S
			PRINT  + 
		A += ABL:Ａ感觉 * 2
		PRINT Ａ感觉
		PRINTV 'LV,ABL:Ａ感觉,'(,ABL:Ａ感觉 * 2,')
		S = 1
	ENDIF
	;ABL:侍奉精神
	IF ABL:侍奉精神
		SIF S
			PRINT  + 
		A += ABL:侍奉精神 * 4
		PRINT 侍奉精神
		PRINTV 'LV,ABL:侍奉精神,'(,ABL:侍奉精神 * 4,')
		S = 1
	ENDIF
	IF MARK:快乐刻印
		SIF S
			PRINT  + 
		A += MARK:快乐刻印 * 3
		PRINT 快乐刻印
		PRINTV 'LV,MARK:快乐刻印,'(,MARK:快乐刻印 * 3,')
		S = 1
	ENDIF

	;PALAM:润滑（の不足）
	IF PALAM:润滑 < PALAMLV:3
		PRINT  - 
		A -= 5
		PRINT 润滑不足
		PRINTV '(,5,')
		S = 1
	ENDIF
	;PALAM:情欲
	IF GETPALAMLV(PALAM:情欲,5)
		SIF S
			PRINT  + 
		A += GETPALAMLV(PALAM:情欲,5) * 3
		PRINT 情欲
		PRINTV 'LV,GETPALAMLV(PALAM:情欲,5),'(,GETPALAMLV(PALAM:情欲,5) * 3,')
		S = 1
	ENDIF
	;害羞
	IF TALENT:羞耻心 > 0
		PRINT  - 
		A -= 2
		PRINT 害羞
		PRINTV '(,2,')
		S = 1
	ENDIF
	;否定快感（淫乱、娼妇で无效）
	IF TALENT:快感应答 < 0 && !TALENT:淫乱
		PRINT  - 
		A -= 5
		PRINT 否定快感
		PRINTV '(,5,')
		S = 1
	ENDIF
	;讨厌男人（恋慕、亲爱で无效）
	IF OTOKOGIRAI(TARGET)
		PRINT  - 
		A -= 12
		PRINT 讨厌男人
		PRINTV '(,12,')
		S = 1
	ENDIF
	;淫乱
	IF TALENT:淫乱 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 15
		PRINT 淫乱
		PRINTV '(,15,')
		S = 15
	ENDIF
	;娼妇
	IF TALENT:淫乱 > 1 && ASSIPLAY == 0
		SIF S
			PRINT  + 
		A += 5
		PRINT 娼妇
		PRINTV '(,5,')
		S = 5
	ENDIF


	;A経験
	IF EXP:Ａ经验 < EXPLV:2
		PRINT  - 
		A -= 30
		PRINT Ａ经验
		PRINT 不足
		PRINTV '(,30,')
		S = 1
	ENDIF

	;调教者が扶她
	IF (TALENT:PLAYER:2 & 3) == 3
		SIF S
			PRINT  + 
		A += 4
		PRINT 扶她
		PRINTV '(,4,')
		S = 1
	ENDIF

	;媚药
	IF TCVAR:媚药
		SIF S
			PRINT  + 
		A += 6
		PRINT 媚药
		PRINTV '(,6,')
		S = 1
	ENDIF

	Y = 0
	;爱液的污垢
	SIF STAIN:PLAYER:2 & 1
		Y += 1
	;精液的污垢
	SIF STAIN:PLAYER:2 & 4
		Y += 3
	;肛门的污垢
	SIF STAIN:PLAYER:2 & 8
		Y += 7
	SIF STAIN:PLAYER:2 & 16
		Y += 15
	Y = Y * (2 - TALENT:污臭耐性) / 2

	;有污垢
	IF Y
		PRINT  - 
		A -= Y
		PRINT 有污垢
		;污臭钝感
		IF TALENT:污臭耐性 > 0
			PRINT 、污臭钝感
		ELSEIF TALENT:污臭耐性 < 0
			PRINT 、污臭敏感
		ENDIF
		PRINTV '(,Y,')
		S = 1
	ENDIF

	;合计を表示(45以上で实行)
	PRINT  = 
	PRINTV A

	V = 45
	SIF A < V
		PRINT  < 
	SIF A == V
		PRINT  = 
	SIF A > V
		PRINT  > 
	PRINT 实行值
	PRINTV V

	WAIT
	SKIPDISP 0
	;实行できない
	SIF A < V
		RETURN 0
	IF TALENT:妊娠 && (CFLAG:903 <= 30 || CFLAG:903 >= 71)
		PRINTFORMW 因为肚子有孩子现在还是别对%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%乱来比较好吧
		RETURN 0
	ENDIF
ENDIF
SKIPDISP 0
RETURN 1


;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE66
;骑乘位肛交实行判定
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(66)
	RETURN RESULT
;毒人形だと肛门がついてないのでダメ
SIF TALENT:196 == 99
	RETURN 0
;睡眠奸中
SIF CFLAG:恶作剧 > 1
	RETURN 0
;调教者が男人、扶她の场合はある程度勃起てないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:PLAYER:勃起 < 1000
	RETURN 0
;肛用振动棒插入中はダメ
SIF TEQUIP:14
	RETURN 0
;后庭拉珠使用中は不可
SIF TEQUIP:15
	RETURN 0
;普通の浣肠使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;电极使用中は不可
SIF TEQUIP:23
	RETURN 0
;时间停止中はスキップ
IF FLAG:70 != 1
	;一定のＡ经验が必要
	SIF EXP:2 < EXPLV:3
		RETURN 0
	;绳使用中は不可
	SIF TEQUIP:绳
		RETURN 0
ENDIF
;体格差がある场合は主人が禁断的知识持ちじゃないとダメ
SIF 插入不可(TARGET) == 1 && !TALENT:PLAYER:禁断的知识 && !TALENT:PLAYER:禁断的知识
	RETURN 0
;さらに体格差があると経験が必要
SIF 插入不可(TARGET) == 2 && (!TALENT:PLAYER:禁断的知识 || EXP:Ａ经验 < 50)
	RETURN 0
SIF 插入不可(TARGET) == 3
	RETURN 0
;调教者が男人か半人半妖か扶她じゃないとダメ
SIF !(TALENT:PLAYER:2 & 2)
	RETURN 0


;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;浴室PLAY中は气垫床が无いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;润滑不足で、顺从3以下・百合属性3以下のASSIの场合はできない（施虐狂ならＯＫ）
IF PALAM:润滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:顺从 <= 3 || ABL:ASSI:技巧 <= 3) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
;ベッドルームor浴室ルームで气垫床がないとダメ
SIF !BEDROOM(CFLAG:PLAYER:当前位置) && !(BATHROOM() && ITEM:20)
	RETURN 0
RETURN 1

