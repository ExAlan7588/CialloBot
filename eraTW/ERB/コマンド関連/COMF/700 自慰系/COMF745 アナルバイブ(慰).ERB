;-------------------------------------------------
;肛用振动棒
;道具使用指令、レベル2
;-------------------------------------------------
@COM745
;着脱の处理
TEQUIP:PLAYER:肛用振动棒 = !TEQUIP:PLAYER:肛用振动棒 
;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = PLAYER

;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
DOWNBASE:PLAYER:体力 += 60
DOWNBASE:PLAYER:气力 += 150

SOURCE:PLAYER:快Ａ = 250
SOURCE:PLAYER:苦痛 = 250
SOURCE:PLAYER:露出 = 50
SOURCE:PLAYER:屈从 = 50
SOURCE:PLAYER:偏离正轨 = 50
SOURCE:PLAYER:反感 = 200
;-------------------------------------------------
;特殊ソース处理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑郁ソース追加
SIF TALENT:PLAYER:自己爱 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:郁闷 += 70

;Aへの苦痛は先に处理しておく
CALL PAIN_CHECK_A(PLAYER)
;-------------------------------------------------
;污れの处理
;-------------------------------------------------
STAIN:PLAYER:肠内 |= STAIN:PLAYER:Ａ
STAIN:PLAYER:Ａ |= STAIN:PLAYER:肠内
;-------------------------------------------------
;経験の处理
;-------------------------------------------------
SIF !FLAG:时间停止
	TIME += 5
;Ａ经验
EXP:PLAYER:Ａ经验 ++

;自慰经验
CFLAG:PLAYER:自慰过了 ++

EXP:PLAYER:道具使用经验 ++

RETURN 1
;-------------------------------------------------
;装着继续の处理
;-------------------------------------------------
@EQUIP_COM745(PLAYER)
;肛用振动棒插入中
PRINTL ＜肛用振动棒插入中＞

DOWNBASE:PLAYER:体力 += 30
DOWNBASE:PLAYER:气力 += 80

SOURCE:PLAYER:快Ａ += 125
SOURCE:PLAYER:苦痛 += 100
SOURCE:PLAYER:露出 += 25
SOURCE:PLAYER:偏离正轨 += 25
SOURCE:PLAYER:反感 += 40

;-------------------------------------------------
;特殊ソース处理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑郁ソース追加
SIF TALENT:PLAYER:自己爱 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:郁闷 += 35

;Aへの苦痛は先に处理しておく
;EXP:Ａ经验をみる
LOCAL = 100
LOCAL:1 = 40
IF EXP:PLAYER:Ａ经验 < EXPLV:1
	TIMES LOCAL , 3.00
	TIMES LOCAL:1 , 3.00
ELSEIF EXP:PLAYER:Ａ经验 < EXPLV:2
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 2.00
ELSEIF EXP:PLAYER:Ａ经验 < EXPLV:3
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.50
ELSEIF EXP:PLAYER:Ａ经验 < EXPLV:4
	TIMES LOCAL , 0.60
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:PLAYER:Ａ经验 < EXPLV:5
	TIMES LOCAL , 0.40
ELSEIF EXP:PLAYER:Ａ经验 >= EXPLV:5
	TIMES LOCAL , 0.20
ENDIF

;PALAM:润滑をみる
IF PALAM:PLAYER:润滑 < PALAMLV:1
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 2.00
ELSEIF PALAM:PLAYER:润滑 < PALAMLV:2
	TIMES LOCAL , 1.00
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:PLAYER:润滑 < PALAMLV:3
	TIMES LOCAL , 0.60
ELSEIF PALAM:PLAYER:润滑 < PALAMLV:4
	TIMES LOCAL , 0.30
ELSEIF PALAM:PLAYER:润滑 >= PALAMLV:4
	TIMES LOCAL , 0.20
ENDIF
SOURCE:PLAYER:苦痛 += LOCAL
SOURCE:PLAYER:反感 += LOCAL:1

;-------------------------------------------------
;経験の处理
;-------------------------------------------------
;自慰实行时は経験＋１
SIF EXP_UP(22,PLAYER)
	EXP:PLAYER:Ａ经验 ++

RETURN 1

;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
@COM_ABLE745
;肛用振动棒实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(745)
	RETURN RESULT
;解除是つでも可能
SIF TEQUIP:14
	RETURN 1
;道具を持っているか、道具无设定でないとダメ
SIF ITEM:5 == 0 && NOITEM == 0
	RETURN 0
;润滑不足で、顺从3以下・百合属性3以下のASSIの场合肛用振动棒はできない（施虐狂ならＯＫ）
IF PALAM:润滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:顺从 <= 3 || ABL:ASSI:百合属性 <= 3) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;浣肠＋肛塞使用中はダメ
SIF TEQUIP:21
	RETURN 0
;后庭拉珠使用中は不可
SIF TEQUIP:15
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;电极使用中は不可
SIF TEQUIP:23
	RETURN 0
RETURN 1

