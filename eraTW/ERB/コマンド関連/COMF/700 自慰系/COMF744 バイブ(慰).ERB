;-------------------------------------------------
;振动棒
;道具使用指令、レベル2
;-------------------------------------------------
@COM744
;着脱の处理
TEQUIP:PLAYER:振动棒 = !TEQUIP:PLAYER:振动棒
;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = PLAYER

;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
DOWNBASE:PLAYER:体力 += 30
DOWNBASE:PLAYER:气力 += 100

SOURCE:PLAYER:快Ｖ = 250
SOURCE:PLAYER:苦痛 = 50
SOURCE:PLAYER:露出 = 20
SOURCE:PLAYER:偏离正轨 = 40
SOURCE:PLAYER:反感 = 40


;-------------------------------------------------
;特殊ソース处理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑郁ソース追加
SIF TALENT:PLAYER:自己爱 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:郁闷 += 40

;处女だった场合は、苦痛のソースと反抗のソースを追加する
IF TALENT:PLAYER:处女 == 1
	SOURCE:PLAYER:苦痛 += 2000
	SOURCE:PLAYER:反感 += 3000
ENDIF

;再生处女だった场合は、苦痛のソースと反抗のソースを追加する
IF TALENT:PLAYER:处女 == 2
	SOURCE:PLAYER:苦痛 += 500
	SOURCE:PLAYER:反感 += 1000
ENDIF

;Vへの苦痛は先に处理しておく
CALL PAIN_CHECK_V(PLAYER)

;-------------------------------------------------
;経験の处理
;-------------------------------------------------
SIF !FLAG:时间停止
	TIME += 5
;插入时はＶ经验+
EXP:PLAYER:Ｖ经验 ++

;异常经验
IF TALENT:PLAYER:处女 == 1 && !GETBIT(CFLAG:PLAYER:3,3)
	EXP:PLAYER:异常经验 ++
	SETBIT CFLAG:PLAYER:3,3
ENDIF

;自慰经验
CFLAG:PLAYER:自慰过了 ++

EXP:PLAYER:道具使用经验 ++

RETURN 1

;-------------------------------------------------
;装着继续の处理
;-------------------------------------------------
@EQUIP_COM744(PLAYER)
;振动棒插入中
PRINTL ＜振动棒插入中＞

DOWNBASE:PLAYER:体力 += 10
DOWNBASE:PLAYER:气力 += 50

SOURCE:PLAYER:快Ｖ += 125
SOURCE:PLAYER:露出 += 10
SOURCE:PLAYER:偏离正轨 += 20

;-------------------------------------------------
;特殊ソース处理
;-------------------------------------------------
;圧抑、抵抗持ちには、抑郁ソース追加
SIF TALENT:PLAYER:自己爱 < 0 || TALENT:PLAYER:抵抗
	SOURCE:PLAYER:郁闷 += 20

;Vへの苦痛は先に处理しておく
;EXP:Ｖ经验をみる（处女はありえない）
LOCAL = 25
LOCAL:1 = 20
IF EXP:PLAYER:Ｖ经验 < EXPLV:2
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:PLAYER:Ｖ经验 < EXPLV:3
	TIMES LOCAL , 0.40
ELSEIF EXP:PLAYER:Ｖ经验 < EXPLV:4
	TIMES LOCAL , 0.10
ELSEIF EXP:PLAYER:Ｖ经验 < EXPLV:5
	TIMES LOCAL , 0.00
ELSEIF EXP:PLAYER:Ｖ经验 >= EXPLV:5
	TIMES LOCAL , 0.00
ENDIF

;PALAM:润滑をみる
IF PALAM:PLAYER:润滑 < PALAMLV:1
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:PLAYER:润滑 < PALAMLV:2
	TIMES LOCAL , 0.70
	TIMES LOCAL:1 , 1.20
ELSEIF PALAM:PLAYER:润滑 < PALAMLV:3
	TIMES LOCAL , 0.40
ELSEIF PALAM:PLAYER:润滑 < PALAMLV:4
	TIMES LOCAL , 0.10
ELSEIF PALAM:PLAYER:润滑 >= PALAMLV:4
	TIMES LOCAL , 0.10
ENDIF
SOURCE:PLAYER:苦痛 += LOCAL
SOURCE:PLAYER:反感 += LOCAL:1

;-------------------------------------------------
;経験の处理
;-------------------------------------------------
;自慰实行时は経験＋１
SIF EXP_UP(22,PLAYER)
	EXP:PLAYER:Ｖ经验 += 1

RETURN 1


;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
@COM_ABLE744
;振动棒实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(744)
	RETURN RESULT
;解除是つでも可能
SIF TEQUIP:13
	RETURN 1
;男人だとダメ
SIF !(TALENT:2 & 1)
	RETURN 0
;道具を持っているか、道具无设定でないとダメ
SIF ITEM:4 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;处女で、顺从4以下・百合属性4以下のASSIの场合振动棒はできない（施虐狂ならＯＫ）
IF TALENT:处女 && ASSIPLAY
	SIF (ABL:ASSI:顺从 <= 4 || ABL:ASSI:百合属性 <= 4) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
;润滑不足で、顺从3以下・百合属性3以下のASSIの场合振动棒はできない（施虐狂ならＯＫ）
IF PALAM:润滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:顺从 <= 3 || ABL:ASSI:百合属性 <= 3) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
RETURN 1

