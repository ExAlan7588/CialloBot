;-------------------------------------------------
;自慰结束
;-------------------------------------------------
@COM751
;CALL ENDUFUFU(PLAYER)
IF FLAG:70 == 1
	;时间停止中は服装セットしない
ELSEIF CFLAG:PLAYER:恶作剧 > 1
	;はだけ中は服装セットしない
ELSEIF LOCK:(CFLAG:PLAYER:311) && CFLAG:PLAYER:恶作剧 < 2
	;恶作剧Lv1以下なら睡衣セット
	CALL CTRL_CLOTHES_SET(PLAYER, "现在衣装の变更_睡衣")
ELSE
	;恶作剧以外なら便装セット
	CALL CTRL_CLOTHES_SET(PLAYER, "现在衣装の变更_便装")
	CFLAG:PLAYER:睡衣 = 0
ENDIF

;服装をTEQUIPへ（↓でやってるので削除）

;ウフフ时系装备リセット
;TEQUIP:ARG:10-TEQUIP:ARG:149を初期化する
;PLAYER:1をカウントに使用のはやめよう
VARSET TEQUIP:(PLAYER):0, 0, 10, 150

;射精关系リセット
IF TFLAG:102 < 2
	BASE:PLAYER:射精 = 0
	TCVAR:PLAYER:要去了 = 0
ENDIF
CFLAG:现在忙得抽不出手 = 0
TEQUIP:PLAYER:50 = -1
TEQUIP:PLAYER:51 = -1
TEQUIP:PLAYER:上半身裸露状态 = 0
TEQUIP:PLAYER:下半身挪开状态 = 0
TCVAR:PLAYER:强行 = 0
IF !FLAG:70
	TFLAG:运搬 = 0
	IF CFLAG:PLAYER:口内精液
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", PLAYER)%把嘴里残留着的精液给\@ ABL:PLAYER:精液中毒 > 2 ? 喝掉了 # 吐出来了 \@
		CFLAG:PLAYER:口内精液 = 0
	ENDIF
	IF CFLAG:PLAYER:脸上精液 || CFLAG:PLAYER:手上精液
		PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", PLAYER)%
		IF CFLAG:PLAYER:脸上精液 && !CFLAG:PLAYER:手上精液
			PRINTFORM 把粘在脸上的
		ELSEIF !CFLAG:PLAYER:脸上精液 && CFLAG:PLAYER:手上精液
			PRINTFORM 把粘在手上的
		ELSE
			PRINTFORM 把粘在脸上和手上的
		ENDIF
		PRINTFORM 精液\@ ABL:PLAYER:精液中毒 > 2 ? 含进嘴里 # 擦掉了 \@
		CFLAG:PLAYER:脸上精液 = 0
		CFLAG:PLAYER:手上精液 = 0
	ENDIF
ENDIF
TEQUIP:PLAYER:吸乳头自慰 = 0
TEQUIP:PLAYER:揉胸自慰 = 0
TFLAG:106 = 0
;はだけフラグをオフ
TEQUIP:PLAYER:2 = 0
TEQUIP:PLAYER:上半身裸露状态 = 0
CALL CLOTHES_SETTING_TRAIN(PLAYER)
;上衣脱衣完毕フラグをオフ
TEQUIP:PLAYER:5 = 0
SIF !FLAG:时间停止
	TIME += 5
;自慰经验
SIF CFLAG:PLAYER:自慰过了 > 0
	EXP:PLAYER:自慰经验 ++
CFLAG:PLAYER:自慰过了 = 0
RETURN 1

;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
@COM_ABLE751
;自慰结束实行判定
SIF !TFLAG:100
	RETURN 0
SIF TARGET > 0 && FLAG:70 && CFLAG:PLAYER:现在忙得抽不出手
	RETURN 1
;批量管理
SIF GLOBAL_COMABLE(751)
	RETURN RESULT
RETURN 1

