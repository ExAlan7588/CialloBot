;-------------------------------------------------
;肛用振动棒
;道具使用指令、レベル2
;-------------------------------------------------
@COM45
;着脱の处理
TEQUIP:肛用振动棒 = !TEQUIP:肛用振动棒 
;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = PLAYER
;侍奉快乐フラグ
TCVAR:PLAYER:108 = 1

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛门 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛门侍奉 10=道具 11=触手 12=Ａ调教 13=Ｖ调教）
TCVAR:TARGET:2 = 12
;强度
TCVAR:TARGET:4 = 900
TCVAR:Ａ丧失发生 = 1
;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 150
	SOURCE:快Ａ = 250
	SOURCE:苦痛 = 130

ELSE

	DOWNBASE:体力 += 60
	DOWNBASE:气力 += 150

	SOURCE:快Ａ = 250
	SOURCE:苦痛 = 250
	SOURCE:露出 = 50
	SOURCE:屈从 = 50
	SOURCE:偏离正轨 = 50
	SOURCE:反感 = 200
	;-------------------------------------------------
	;特殊ソース处理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑郁ソース追加
	SIF TALENT:自己爱 < 0 || TALENT:抵抗
		SOURCE:郁闷 += 70
ENDIF
;Aへの苦痛は先に处理しておく
CALL PAIN_CHECK_A(TARGET)

;-------------------------------------------------
;污れの处理
;-------------------------------------------------
STAIN:7 |= STAIN:4
STAIN:4 |= STAIN:7
;-------------------------------------------------
;経験の处理
;-------------------------------------------------
;Ａ经验
EXP:Ａ经验 ++
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ELSE
	;同性爱　レズなら返回值1ゲイなら返回值2
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	ENDIF
ENDIF
EXP:PLAYER:道具使用经验 ++

RETURN 1
;-------------------------------------------------
;装着继续の处理
;-------------------------------------------------
@EQUIP_COM45(ARG)
;肛用振动棒插入中
PRINTL ＜肛用振动棒插入中＞

CSTR:TARGET:10 += @"{PLAYER}/"
TCVAR:TARGET:2 = 12
TCVAR:TARGET:4 = 450

IF FLAG:70 == 1
	BASE:MASTER:TSP -= 80
	SOURCE:ARG:快Ａ += 125

ELSE


	DOWNBASE:ARG:体力 += 30
	DOWNBASE:ARG:气力 += 80

	SOURCE:ARG:快Ａ += 125
	SOURCE:ARG:苦痛 += 100
	SOURCE:ARG:露出 += 25
	SOURCE:ARG:偏离正轨 += 25
	SOURCE:ARG:反感 += 40

	;-------------------------------------------------
	;特殊ソース处理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑郁ソース追加
	SIF TALENT:ARG:自己爱 < 0 || TALENT:ARG:抵抗
		SOURCE:ARG:郁闷 += 35

	;Aへの苦痛は先に处理しておく
	;EXP:Ａ经验をみる
	LOCAL = 100
	LOCAL:1 = 40
	IF EXP:ARG:Ａ经验 < EXPLV:1
		TIMES LOCAL , 3.00
		TIMES LOCAL:1 , 3.00
	ELSEIF EXP:ARG:Ａ经验 < EXPLV:2
		TIMES LOCAL , 1.20
		TIMES LOCAL:1 , 2.00
	ELSEIF EXP:ARG:Ａ经验 < EXPLV:3
		TIMES LOCAL , 0.80
		TIMES LOCAL:1 , 1.50
	ELSEIF EXP:ARG:Ａ经验 < EXPLV:4
		TIMES LOCAL , 0.60
		TIMES LOCAL:1 , 1.20
	ELSEIF EXP:ARG:Ａ经验 < EXPLV:5
		TIMES LOCAL , 0.40
	ELSEIF EXP:ARG:Ａ经验 >= EXPLV:5
		TIMES LOCAL , 0.20
	ENDIF

	;PALAM:润滑をみる
	IF PALAM:ARG:润滑 < PALAMLV:1
		TIMES LOCAL , 1.20
		TIMES LOCAL:1 , 2.00
	ELSEIF PALAM:ARG:润滑 < PALAMLV:2
		TIMES LOCAL , 1.00
		TIMES LOCAL:1 , 1.50
	ELSEIF PALAM:ARG:润滑 < PALAMLV:3
		TIMES LOCAL , 0.60
	ELSEIF PALAM:ARG:润滑 < PALAMLV:4
		TIMES LOCAL , 0.30
	ELSEIF PALAM:ARG:润滑 >= PALAMLV:4
		TIMES LOCAL , 0.20
	ENDIF
	SOURCE:ARG:苦痛 += LOCAL
	SOURCE:ARG:反感 += LOCAL:1

	;-------------------------------------------------
	;経験の处理
	;-------------------------------------------------
	;自慰实行时は経験＋１
	SIF EXP_UP(22,TARGET)
		EXP:ARG:Ａ经验 ++
ENDIF
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE45
;肛用振动棒实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(45)
	RETURN RESULT
;毒人形だと肛门がついてないのでダメ
SIF TALENT:196 == 99
	RETURN 0
;解除是つでも可能
SIF TEQUIP:14
	RETURN 1
;道具を持っているか、道具无设定でないとダメ
SIF ITEM:5 == 0 && NOITEM == 0
	RETURN 0
;润滑不足で、顺从3以下・百合属性3以下のASSIの场合肛用振动棒はできない（施虐狂ならＯＫ）
IF PALAM:润滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:顺从 <= 3 || ABL:ASSI:百合属性 <= 3) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF

;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
;浣肠＋肛塞使用中はダメ
SIF TEQUIP:21
	RETURN 0
;后庭拉珠使用中は不可
SIF TEQUIP:15
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;电极使用中は不可
SIF TEQUIP:23
	RETURN 0
RETURN 1

