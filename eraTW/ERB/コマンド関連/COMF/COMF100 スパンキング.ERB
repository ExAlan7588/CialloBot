;-------------------------------------------------
;打屁股
;SM系指令、レベル1
;-------------------------------------------------
@COM100
;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = PLAYER

;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 60
	SOURCE:苦痛 = 450

ELSE

	DOWNBASE:体力 += 60
	DOWNBASE:气力 += 20

	SOURCE:苦痛 = 450
	SOURCE:恐怖 = 400
	SOURCE:偏离正轨 = 50
	SOURCE:反感 = 500
	IF TALENT:幼稚
		SOURCE:苦痛 += 350
		SOURCE:恐怖 += 200
		SOURCE:偏离正轨 += 50
	ENDIF
ENDIF
;-------------------------------------------------
;特殊ソース处理
;-------------------------------------------------
;苦痛のパラメータによる苦痛のソース增加は先に处理しておく
SOURCE:苦痛 = SOURCE:苦痛 * (10 + 5 * MAX(GETPALAMLV(PALAM:苦痛,5) - 1, 0)) / 10
;∨とＡに快乐补正が入るようにTCVARを立てる
TCVAR:指令Ｖ补正 = 2 * (ABL:受虐属性 - 1)
TCVAR:指令Ａ补正 = 4 * (ABL:受虐属性 - 1)
;-------------------------------------------------
;経験の处理
;-------------------------------------------------
;同性爱　レズなら返回值1ゲイなら返回值2
IF FLAG:70 == 1
	IF HETEROSEX(TARGET,PLAYER)
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	ENDIF
ELSE
	IF HETEROSEX(TARGET,PLAYER)
		EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 2
		EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	ENDIF
ENDIF

EXP:PLAYER:嗜虐快乐经验 += 1

RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE100
;打屁股实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(100)
	RETURN RESULT
;顺从3未满のASSIの场合打屁股はできない（施虐狂ならＯＫ）
IF ASSIPLAY
	SIF ABL:ASSI:顺从 < 3 && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
;睡眠奸中
SIF CFLAG:恶作剧 > 1
	RETURN 0
;下半身着衣
SIF TEQUIP:0 & 8
	RETURN 0
RETURN 1

