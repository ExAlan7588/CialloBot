;木材商
@COM623
#DIM 制材判定
#DIM 卖值
#DIM ループ数
#DIM リザルト保管
#DIM 候补番号
#DIM 候补个数
#DIM 卖出个数
#DIM ProcessingCharge
ループ数 = 0

;开店时间・闭店时间
IF INRANGE(TIME:0, 540, 1080) == 0
	PRINTFORMW 好像是非营业时间
	RETURN -1
ENDIF


;初回イベント
IF FLAG:最后一次访问木材商日期 == 0
	;初回イベント前は、原木持ちでなければ店にも入れない
	IF ITEM:原木 == 0
		PRINTFORMW 来这里也没什么用…先去伐点木材再来卖吧
		RETURN -1
	ENDIF
	;依赖の报酬など、采伐以外で原木を入手してしまった场合の退避用
	SIF !FLAG:原木种类
		FLAG:原木种类 = MAX(RAND:12, 1)
	PRINTFORMW 「欢迎光临、我是这里的店主。这位客人是来卖原木的吗？」
	PRINTFORMW 「那么、让我看一下您带来的原木吧」
	PRINTFORMW 「………」
	PRINTFORMW 「喔、品质很高啊……」
	PRINTFORMW 「……诶？　如果有这个意思的话、每天都能带过来？　因为人间之里的外面要多少有多少？」
	PRINTFORMW 「…………」
	PRINTFORMW 「……看来这位客人不是泛泛之辈呀」
	PRINTFORMW 「好吧。那么采伐而来的原木、就由我买下来吧」
	PRINTFORMW 「当然、品质越高的价格就越高、反之则只能低价收购」
	PRINTFORMW 「如果需要制作家具之类的制材的话、也可以把原木代为加工成制材。当然、是要收取人工费的」
	PRINTFORMW 「那么、就让我検查一下这次的木头吧」
	;卖值はここで设定、基本的に中途中止はしない
	卖值 = WOOD_VALUE(FLAG:原木种类)
	SETCOLOR 0x00FF00
	PRINTFORMD 「
	PRINTFORM %WOOD_NAME(FLAG:原木种类)%
	PRINTFORMD 啊……那就以
	PRINTFORM %金额表示(卖值)%
	RESETCOLOR
	PRINTFORMW 买下怎么样」
	PRINTFORMW 　　所持金: %金额表示(MONEY)% → %金额表示(MONEY + 卖值)%
	PRINTFORMW 「那么这样就成交了。期待您下次带来的商品」
	PRINTFORMW 「哪有、敢进入妖精和妖怪嚣张跋扈的山林里伐木的勇士、一共也没有几人。我可是很期待的哟？」
	PRINTW 
	MONEY += 卖值
	ITEM:原木 = 0
	FLAG:原木种类 = 0
	ループ数 = 1
ENDIF

$LOOP_SELECT

IF ループ数
	PRINTL 
	PRINTFORMW 「那么、还有什么别的事吗？」
ELSE
	;平常来店
	PRINTL 
	IF DAY:0 - FLAG:最后一次访问木材商日期 > 28
		PRINTFORMW 「喔、是你啊。我还以为你已经从樵夫的工作里金斧洗手了」
	ELSEIF DAY:0 - FLAG:最后一次访问木材商日期 > 14
		PRINTFORMW 「喔、好久不见了」
	ELSEIF FLAG:最后一次访问木材商日期 == DAY:0
		PRINTFORMW 「什么、忘了东西？　真是个粗心的家伙啊」
	ELSE
		PRINTFORMW 「喔、今日有何贵干？」
	ENDIF
ENDIF

ループ数 ++
FLAG:最后一次访问木材商日期 = DAY:0

PRINTFORML 　　所持金: %金额表示(MONEY)%
DRAWLINE

CALL ASK_M("原木卖出・制材", ITEM:原木, "卖木材", ITEM:木材, "卖制材", 4, "确认交易内容", 5, "从店里离开", 6)
リザルト保管 = RESULT
ProcessingCharge = LUMBERING(FLAG:原木种类, "木材商")
CLEARLINE 12
SELECTCASE リザルト保管
	;原木を卖る　制材してほしい
	CASE 0
		;依赖の报酬など、采伐以外で原木を入手してしまった场合の退避用
		SIF !FLAG:原木种类
			FLAG:原木种类 = MAX(RAND:12, 1)
		SETCOLOR 0x00FF00
		PRINTFORML 「%WOOD_NAME(FLAG:原木种类)%啊……」
		卖值 = WOOD_VALUE(FLAG:原木种类)
		PRINTFORMD 「买下来的话价格是
		PRINTFORM %金额表示(卖值)%
		PRINTFORMD 、制材的话加工费是
		PRINTFORM %金额表示(ProcessingCharge)%
		RESETCOLOR
		PRINTFORMW 」
		CALL ASK_M("卖了", 1, "制材", MONEY > ProcessingCharge,"还是算了", 1)
		SELECTCASE RESULT
			CASE 0
				PRINTFORMW 　所持金: \\ → %金额表示(MONEY + 卖值)%
				PRINTFORMW 「谢谢惠顾。那么、期待您下次带来的商品」
				PRINTL 
				MONEY += 卖值
			CASE 1
				PRINTL 
				PRINTFORMW 　所持金: %金额表示(MONEY)% → %金额表示(MONEY - ProcessingCharge)%
				PRINT 　
				CALL PAY_MONEY_YEN(ProcessingCharge, 2)
				PRINTL 
				PRINTFORMW 「好的。那么这边就开始作业了」
				CALL LogRefine("木材商")
			CASE 2
				PRINTFORMW 「嗯？还有什么别的打算吗？」
				PRINTL 
				GOTO LOOP_SELECT
		ENDSELECT
		ITEM:原木 = 0
		FLAG:原木种类 = 0
	;木材を卖る
	CASE 1
		候补个数 = ITEM:木材
		候补番号 = 610
		PRINTFORM 「%ITEMNAME:候补番号%的话　%金额表示(ITEMPRICE:候补番号 / 2)%买下、怎么样？」
		CALL COM623_SELL(候补个数)
		SELECTCASE RESULT
			CASE 0
				PRINTL 
				GOTO LOOP_SELECT
			CASE 1 TO 候补个数
				CLEARLINE 2
				卖出个数 = RESULT
				PRINTFORML 「那么就%ITEMNAME:候补番号%{卖出个数}个、总计%金额表示((ITEMPRICE:(候补番号) / 2 * 卖出个数))%」
				PRINTFORMW 　所持金: %金额表示(MONEY)% → %金额表示(MONEY + (ITEMPRICE:(候补番号) / 2 * 卖出个数))%
				MONEY += (ITEMPRICE:(候补番号) / 2 * 卖出个数)
				ITEM:候补番号 -= 卖出个数
				PRINTL 
				GOTO LOOP_SELECT
		ENDSELECT
	;制材を卖る
	CASE 2
		制材判定 = 0
		FOR LOCAL, 1, 50
			SIF LOG_SALES:LOCAL
				BREAK
			制材判定 ++
		NEXT
		IF 制材判定 > 48
			PRINTFORMW 未持有制材
			PRINTL 
			GOTO LOOP_SELECT
		ENDIF
		;PRINTFORMW 「制材的采购吗。让我看看」
		PRINTFORMW 「要卖掉手上的制材吗。让我看看」
		DRAWLINE
		FOR LOCAL, 1, 50
			SIF LOG_SALES:LOCAL == 0
				CONTINUE
			PRINTFORM 　
			SIF LOCAL < 10
				PRINTFORM  
			PRINTFORM [{LOCAL}] 
			PRINTFORM %WOOD_NAME(LOCAL), 16, LEFT%
			PRINTFORM 制材数： 
			SELECTCASE LOG_SALES:LOCAL
				CASE IS < 10
					PRINTFORM     
				CASE IS < 100
					PRINTFORM    
				CASE IS < 1000
					PRINTFORM   
				CASE IS < 10000
					PRINTFORM  
			ENDSELECT
			PRINTFORML {LOG_SALES:LOCAL}
		NEXT
		PRINTFORML 　 [0] 放弃

		$INPUT_LOOP03
		INPUT
		
		リザルト保管 = RESULT
		IF リザルト保管 && LOG_SALES:リザルト保管
			卖值 = LUMBERING(リザルト保管, "木材商", "制材", 1)
			候补个数 = LOG_SALES:リザルト保管
			SETCOLOR 0x00FF00
			PRINTFORMD 「
			PRINTFORM %WOOD_NAME(リザルト保管)%
			PRINTFORMD 的制材的话、这边以
			PRINTFORM %金额表示(卖值)%
			RESETCOLOR
			PRINTFORML 买下、怎么样？」
			CALL COM623_SELL(候补个数)
			SELECTCASE RESULT
				CASE 0
					PRINTL 
					GOTO LOOP_SELECT
				CASE 1 TO 候补个数
					CLEARLINE 2
					卖出个数 = RESULT
					PRINTFORML 「那么%WOOD_NAME(リザルト保管)%的制材{卖出个数}个、总计%金额表示(卖值 * 卖出个数)%」
					PRINTFORMW 　所持金: %金额表示(MONEY)% → %金额表示(MONEY + (卖值 * 卖出个数))%
					MONEY += 卖值 * 卖出个数
					LOG_SALES:リザルト保管 -= 卖出个数
					PRINTL 
			ENDSELECT
		ELSEIF リザルト保管 == 0
			PRINTL 
			GOTO LOOP_SELECT
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP03
		ENDIF
	;取引内容を确认する
	CASE 3
		PRINTFORMW 「正在收购原木、竹材、制材」
		PRINTFORMW 「如果没有的话、端材・木材之类的也会收购。嘛、当然也不值几个钱就是了」
		PRINTFORMW 「如果如果的话我们还承包原木的制材。但是、会根据原木的种类・大小收取加工费」
		PRINTFORMW 「顺便一提、先在这里加工成制材、再在这里把制材卖掉就是纯粹的浪费钱。如果是打算义务捐赠的话倒是非常欢迎」
		PRINTFORMW 「……什么、想要家具？　那不是我们的业务范围。找个家具师傅吧」
		PRINTL 
	CASE 4
		PRINTFORMW 「那么、回头再见」
		PRINTL 
		TIME += ループ数 * 5
		RETURN -1
ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
;木材商
@COM_ABLE623
;批量管理
SIF GLOBAL_COMABLE(623)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:当前位置 != 商业街
		RETURN 0
ELSE
	;西大街
	SIF CFLAG:MASTER:当前位置 != 206
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;时间停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;原木・制材の卖出价格を设定する函数
;何回か见る场合もあるので、その日の中では固定值出力
;ARG:0		FLAG:原木种类
;Type		卖出价格  省略时は原木
;-------------------------------------------------
@WOOD_VALUE(ARG:0, Type)
#FUNCTION
#DIM Species
#DIM 卖值ベース
#DIM 口八丁补正
#DIMS Type

Species = ARG % 100

IF Type == "制材"
	卖值ベース = WoodValue_Plank:Species
ELSE
	卖值ベース = WoodValue_Log:Species
ENDIF

;日替わり变动
卖值ベース += FLAG:每日变更事件 * 2

;口八丁补正  日期で乱数を固定
RANDOMIZE DAY:0
口八丁补正 = RAND:(1 + ABL:MASTER:话术技能 * 5 + ABL:MASTER:教养)

SELECTCASE 口八丁补正
	CASE 0 TO 10

	CASE 11 TO 20
		TIMES 卖值ベース, 1.05
	CASE 21 TO 30
		TIMES 卖值ベース, 1.07
	CASEELSE
		TIMES 卖值ベース, 1.10
ENDSELECT

;原木の时のみ、巨木・大木补正
IF Type != "制材"
	IF ARG > 200
		卖值ベース *= 5
	ELSEIF ARG > 100
		卖值ベース *= 3
	ENDIF
ENDIF
RETURNF 卖值ベース


;-------------------------------------------------
;FLAG:原木种类から、巨木・大木を无视して木材种番号を取り出す函数
;ARG…FLAG:原木种类
;-------------------------------------------------
@WOOD_NUM(ARG)
#FUNCTION

RETURNF ARG % 100

;-------------------------------------------------
;制材コスト、制材数を割り出す函数
;复数回选べるので、固定值を出す
;ARG	FLAG:原木种类
;Type	价格ないし制材数
;Sale	卖出标志，为1时返回卖出价格，为0时返回加工价格
;-------------------------------------------------
@LUMBERING(ARG, Craft, Type, Sale)
#FUNCTION
#DIM 费用ベース
#DIMS DYNAMIC Craft
#DIMS DYNAMIC Type
#DIM DYNAMIC Sale

IF Type == "QTY"
	RETURNF 1 + RAND:4 + (ARG / 10) * 2
ENDIF

IF Type == "制材"
	费用ベース = WOOD_VALUE(ARG, "制材")
ELSE
	费用ベース = WOOD_VALUE(ARG)
ENDIF

IF Sale == 1
	SELECTCASE Craft
		CASE "木材商"
			RETURNF 费用ベース
		CASE "制材所"
			THROW ERROR! ILLEGAL Craft STRING WHEN Sale's VALUE IS 1!
	ENDSELECT
ELSEIF Sale == 0
	SELECTCASE Craft
		CASE "木材商"
			RETURNF 费用ベース * 3
		CASE "制材所"
			RETURNF 费用ベース
	ENDSELECT
ENDIF
SIF !GROUPMATCH(Sale, 0, 1)
	THROW ERROR! ILLEGAL Sale VALUE!
RETURNF 0

@COM623_SELL(候补个数)
#DIM 候补个数
SETCOLOR 0x777777
PRINTFORML 　要卖多少个？(输入1～{候补个数}、0为返回)
RESETCOLOR
PRINTL 
PRINT 　
PRINTBUTTON "[0] 放弃", 0
PRINT     
PRINTBUTTON "[1] 1个", 1
PRINT     
IF 候补个数 >= 5
	PRINTBUTTON "[5] 5个", 5
	PRINT     
ENDIF
IF 候补个数 >= 10
	PRINTBUTTON "[10] 10个", 10
	PRINT    
ENDIF
INPUT
SIF RESULT > 候补个数
	RETURN 0
SIF RESULT <= 0
	RETURN 0
RETURN RESULT
