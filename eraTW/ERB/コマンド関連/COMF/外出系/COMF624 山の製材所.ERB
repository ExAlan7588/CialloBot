;山の制材所
@COM624
#DIM 卖值
#DIM ループ数
#DIM リザルト保管

ループ数 = 0

;开店时间・闭店时间
IF INRANGE(TIME:0, 540, 1080) == 0
	PRINTFORMW 好像是非营业时间
	RETURN -1
ENDIF

;初回イベント
IF FLAG:最后一次访问制材所日期 == 0
	;初回イベント前は、原木持ちでなければ店にも入れない
	IF ITEM:原木 == 0
		PRINTFORMW 来这里也没什么用…先去伐点木材再来卖吧
		RETURN -1
	ENDIF
	;依赖の报酬など、采伐以外で原木を入手してしまった场合の退避用
	SIF !FLAG:原木种类
		FLAG:原木种类 = MAX(RAND:12, 1)
	
	PRINTFORMW 「初次见面、盟友」
	SIF FLAG:扮演 == 0
		PRINTFORMW 「我听过你的传闻哟ー。什么非常活跃的、奇妙的外来人什么的」
	SIF ABL:[[荷取]]:亲密 >= 3
		PRINTFORMW 「好像也有河童的同伴受你照顾了呢。很好嘛……嗯哼嗯哼、善良又值得信赖的家伙」
	PRINTFORMW 「那么、有什么事呢？　诶、想要把原木变成制材、吗。可以哟ー、工钱大概是这样――」
	PRINTFORMW 滋滋、河童把说明用的操纵面板调了出来……
	PRINTFORMW 这个、不是比人间之里的行情便宜了很多吗？
	PRINTFORMW 「那是当然啦。这可是工程机械的全自动制材、总不至于落后于人力吧」
	PRINTFORMW 「……话是这么说、在寺庙的建立和神社的修复完成之后就没什么订单了、好不容易才开发出来的机械也闲置在这里了」
	PRINTFORMW 「对了。如果您今后也能多多光临的话、今天的这第一次就是免费服务了、意下如何？」
	PRINTFORMW …这个、不就是单纯的炫耀自己的机械吗、不过我还是把这句话给咽了回去、决定承蒙这个好意
	PRINTFORMW 「就这么决定了。那么请把原木放在这里把」
	PRINTFORMW 「那么、开关・ON！　呼呼呼、咆哮吧我的机魂！　见识一下河童技术力的精华吧！」
	PRINTFORMW 姑且不提迷之情绪高涨的河童、至少工程机械它是致密有高速地完成了制材
	PRINTFORMW …………
	PRINTFORMW ……
	CALL LogRefine("制材所")
	PRINTL 
	PRINTFORMW 「完成！　怎么样！」
	PRINTFORMW 看着满脸得意的河童、只好坦率地表达了感谢
	PRINTFORMW 「嘿嘿、果然这个时候配这个眼镜太好了。那么、今后也请多关照了ー」
	PRINTL 
	ループ数 = 1
ENDIF

$LOOP_SELECT

IF ループ数
	PRINTL 
	PRINTFORMW 「撒撒、下次也要把原木带过来哟」
ELSE
	;平常来店
	PRINTL 
	IF FLAG:最后一次访问制材所日期 == DAY:0
		PRINTFORMW 「哦哟？　怎么了？」
	ELSE
		PRINTFORM 「呀、
		SELECTCASE TIME:0
			CASE 540 TO 600
				PRINTFORM 早上好
			CASE 601 TO 1020
				PRINTFORM 你好
			CASEELSE
				PRINTFORM 晚上好
		ENDSELECT
		PRINTFORM 盟友。　
		IF DAY:0 - FLAG:最后一次访问制材所日期 > 28
			PRINTFORMW 最近好久没有过来露个脸了、还有点担心来着」
		ELSEIF DAY:0 - FLAG:最后一次访问制材所日期 > 14
			PRINTFORMW 好久不见了」
		ELSE
			PRINTFORMW 今天有什么事呢？」
		ENDIF
	ENDIF
ENDIF

ループ数 ++
FLAG:最后一次访问制材所日期 = DAY:0

PRINTFORML 　　所持金: %TOSTR(MONEY,"#,###")%　所持筹码: %TOSTR(MONEY:2,"#,###")%
DRAWLINE

CALL ASK_M("想要制材", ITEM:原木, "确认交易内容", 1, "从店里离开", 2)
CLEARLINE 7

SELECTCASE RESULT
	;制材してほしい
	CASE 0
		;依赖の报酬など、采伐以外で原木を入手してしまった场合の退避用
		SIF !FLAG:原木种类
			FLAG:原木种类 = MAX(RAND:12, 1)

		SETCOLOR 0x00FF00
		PRINTFORMD 「
		PRINTFORM %WOOD_NAME(FLAG:原木种类)%
		PRINTFORMD 的制材对吧。工钱是
		PRINTFORM %金额表示(LUMBERING(FLAG:原木种类, "制材所"))%
		PRINTFORMD 左右吧。啊、
		PRINTFORM %TOSTR(LUMBERING_CMCOST(FLAG:原木种类),"#,###")%筹码
		RESETCOLOR
		PRINTFORMW 的话也可以哟？」
		CALL ASK_M("\\来支付", MONEY >= LUMBERING(FLAG:原木种类, "制材所"), "用筹码来支付", MONEY:2 >= LUMBERING_CMCOST(FLAG:原木种类), "还是算了", 2)

		リザルト保管 = RESULT
		IF リザルト保管 == 0 || リザルト保管 == 1
			IF リザルト保管 == 0
				PRINTFORMW 　所持金: %金额表示(MONEY)% → %金额表示(MONEY - LUMBERING(FLAG:原木种类, "制材所"))%
				PRINT 　
				CALL PAY_MONEY_YEN(LUMBERING(FLAG:原木种类, "制材所"), 2)
			ELSE
				PRINTFORMW 　所持筹码: %TOSTR(MONEY:2,"#,###")% → %TOSTR(MONEY:2 - LUMBERING_CMCOST(FLAG:原木种类),"#,###")%
				PRINT 　
				CALL PAY_MONEY_CM(LUMBERING_CMCOST(FLAG:原木种类), 2)
			ENDIF
			PRINTL 
			PRINTDATAW
				DATAFORM 「谢谢惠顾。那么、开关・ON！　呼呼呼、咆哮吧我的机魂！　见识一下河童技术力的精华吧！」
				DATAFORM 「谢谢惠顾。把原木放在这里、接下来就是全自动作业了！」
				DATAFORM 「谢谢惠顾。那么、让你见识一下现代化工厂・自动化生产的神髄！」
			ENDDATA
			CALL LogRefine("制材所")
			PRINTL 
		ELSEIF リザルト保管 == 2
			PRINTFORMW 「唔、不接受砍价的交渉的ー」
			PRINTL 
			GOTO LOOP_SELECT
		ENDIF
	;取引内容を确认する
	CASE 1
		PRINTFORMW 「原木的制材、给你便宜点哟ー」
		PRINTFORMW 「想要卖出点原木和制材？　我们山里这边已经物资供给过剩了、去卖到人间之里的话比较好吧？」
		PRINTFORMW 「你问我会不会造家具？　唔ー。我造机器已经造腻了、不想再造个家具机器了」
		PRINTFORMW 「这种复杂的工作、我觉得还是去拜托人类的工匠比较好」
		PRINTFORMW 「如果真让我来做的话、可能会比较随心所欲、做出一个带有各种不需要的奇怪机能的河童式家具出来吧ー」
		PRINTFORMW 「…那也挺有意思的？　嘿嘿、嘛、那就有机会的话ー」
		PRINTL 
	CASE 2
		PRINTFORMW 「那么、回头见」
		PRINTL 
		TIME += ループ数 * 5
		RETURN 1
ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
;山の制材所
@COM_ABLE624
;批量管理
SIF GLOBAL_COMABLE(624)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:当前位置 != 玄武之泽
		RETURN 0
ELSE
	;山麓が据点の场合は、河童的宣传站
	SIF CFLAG:MASTER:当前位置 != 704
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;时间停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;制材コストを筹码に变换する函数
;ARG …FLAG:原木种类
;-------------------------------------------------
@LUMBERING_CMCOST(ARG)
#FUNCTION
#DIM 费用ベース

费用ベース = LUMBERING(ARG, "制材所")

;元值の幅がそんなに广くないので、一律で
RETURNF 费用ベース / 300

;-------------------------------------------------
;制材结果の表示など
;-----------------------------------------------
@LogRefine(Craft)
#DIMS Craft
LOCAL = LUMBERING(FLAG:原木种类, Craft, "QTY")
SETCOLOR 0x00FF00
PRINTFORMW 　%WOOD_NAME(FLAG:原木种类, 1)%的制材: {LOG_SALES:WOOD_NUM(FLAG:原木种类)} → {LOG_SALES:WOOD_NUM(FLAG:原木种类) + LOCAL}
LOG_SALES:WOOD_NUM(FLAG:原木种类) += LOCAL
ITEM:原木 = 0
FLAG:原木种类 = 0
RESETCOLOR
