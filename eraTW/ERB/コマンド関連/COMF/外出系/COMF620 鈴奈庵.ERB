;借书屋
@COM620
#DIM CHARISMA
#DIM ItemId

$C_LOOP
DRAWLINE
IF !CHARISMA
	PRINTFORML 所持金:%金额表示(MONEY)%
	PRINTFORML 「借书每本一律%金额表示(3000)%」
ELSE
	PRINTFORML 所持筹码:{MONEY:2}
	PRINTL 「每本书的借书费用都是10筹码。」
ENDIF
DRAWLINE
FOR LOCAL,1, BookTitles + 1
	ItemId = LOCAL + 100
	PRINTFORM [{LOCAL}] %ITEMNAME:ItemId%
	IF ITEM:ItemId
		PRINTFORML （被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%借着 返还时间{FLAG:借书返还期限-1}日）
	ELSEIF GETBIT (FLAG:1011,LOCAL)
		PRINTL （借出中）
	ELSE
		PRINTL 　
	ENDIF
NEXT

IF !CHARISMA
	PRINTFORML [100] 筹码付款
ELSE
	PRINTFORML [100] 现金支付
ENDIF
DRAWLINE
PRINTL [0] 放弃

$LOOP
INPUT

IF RESULT == 100
	IF !CHARISMA
		LOCALS = 「筹码付款？？？？」/「嗯……那么就10个筹码？可以么……」
		CALL BOOK_RENTAL_SALESTALK(LOCALS)
		IF MONEY:2 < 10
			PRINTFORMW 筹码不足
			GOTO C_LOOP
		ENDIF
	ELSE
		LOCALS = 「啊、我用钱支付吧」
		CALL BOOK_RENTAL_SALESTALK(LOCALS)
	ENDIF
	PRINTFORML 
	CHARISMA = !CHARISMA
	GOTO C_LOOP
ELSE
	IF !CHARISMA && MONEY < 3000
		PRINTW 钱不够…
		RETURN -1
	ENDIF
	IF INRANGE(RESULT, 1, BookTitles)
		RESULT:1 = RESULT
		IF GETBIT (FLAG:1011,RESULT:1)
			ItemId = RESULT:1 + 100
			IF ITEM:ItemId
				IF FLAG:借书返还期限 == 8
					LOCALS = 「您不是正在借着吗？」
					CALL BOOK_RENTAL_SALESTALK(LOCALS)
					RETURN -1
				ENDIF
				
				LOCALS = 「那本书您现在正借着呢,可是......」/「您要支付借书费用,将借书期限延长一周吗?」
				CALL BOOK_RENTAL_SALESTALK(LOCALS)
				CALL ASK_YN
				IF !RESULT
					IF !CHARISMA
						MONEY -= 3000
					ELSE
						MONEY:2 -= 10
					ENDIF
					FLAG:借书返还期限 = 8
					PRINTFORML 将%ITEMNAME:(100 + RESULT:1)%的借阅期限延长了一周。
					LOCALS = 「非常感谢」/「您似乎很喜欢这本书呢。」
					CALL BOOK_RENTAL_SALESTALK(LOCALS)
				ELSE
					RETURN -1
				ENDIF
			ELSE
				LOCALS = 「正在外借中呢」
				CALL BOOK_RENTAL_SALESTALK(LOCALS)
			ENDIF
			RETURN -1
		ELSE
			FOR LOCAL,1, BookTitles + 1
				ItemId = LOCAL + 100
				IF ITEM:ItemId
					LOCALS = 「不能同时借二册以上」/「我需要再收取借书费用,您能先归还当前借的书,然后再借这本书?」
					CALL BOOK_RENTAL_SALESTALK(LOCALS)
					CALL ASK_YN
					IF !RESULT
						ITEM:ItemId = 0
						CLEARBIT FLAG:1011,LOCAL
					ELSE
						RETURN -1
					ENDIF
				ENDIF
			NEXT
			SETBIT FLAG:1011,RESULT:1
			IF !CHARISMA
				MONEY -= 3000
			ELSE
				MONEY:2 -= 10
			ENDIF
			ITEM:(100 + RESULT:1) = 1
			FLAG:借书返还期限 = 8
			TFLAG:193 = 1
			PRINTFORML %ITEMNAME:(100 + RESULT:1)%借走了
			LOCALS = 「非常感谢」/「还书期限是一周哦」
			CALL BOOK_RENTAL_SALESTALK(LOCALS)
			RETURN 1
		ENDIF
	ELSEIF RESULT == 0
		RETURN -1
	ELSE
		GOTO LOOP
	ENDIF
ENDIF
TIME += 30

;借书屋
@COM_ABLE620
;批量管理
SIF GLOBAL_COMABLE(620)
	RETURN RESULT
;借书屋ではない场所
SIF !BOOK_RENTAL(CFLAG:MASTER:当前位置)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;时间停止中
SIF FLAG:70
	RETURN 0
RETURN 1

;借书屋を判定する函数
@BOOK_RENTAL(ARG)
#FUNCTION
;铃奈庵
SIF ARG == GET_MAP_REPLACEMENT(213)
	RETURNF 1
RETURNF 0

@BOOKLEND
FOR LOCAL,1,BookTitles + 1
	SIF LOCAL == 6	;[纸芝居]をスルー
		CONTINUE
	SIF !ITEM:(100 + LOCAL) && !RAND:3
		INVERTBIT FLAG:1011,LOCAL
NEXT
SIF FLAG:借书返还期限
	FLAG:借书返还期限 -= 1
IF FLAG:借书返还期限 == 1
	FOR LOCAL,1,BookTitles + 1
		ITEM:(100 + LOCAL) = 0
	NEXT
	PRINTL 因为还书时限到了、把借来的书归还了。
ENDIF

@BOOK_RENTAL_SALESTALK(ARGS)
IF CFLAG:[[小铃]]:当前位置 == CFLAG:PLAYER:当前位置
	CALL SPTALK, [[小铃]], "笑颜", 0, @"%ARGS%", 1
	SIF TARGET != [[小铃]]
		RESETCOLOR
ELSE
	CALL SPTALK, 0, "笑颜", 12, @"%ARGS%", 1
ENDIF
