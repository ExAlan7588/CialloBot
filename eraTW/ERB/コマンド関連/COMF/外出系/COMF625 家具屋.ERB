;家具屋
@COM625
#DIM 卖值
#DIM ループ数
#DIM リザルト
#DIM 资材保管
#DIM 削除行数
#DIM MaterialQTY
#DIMS DiscountedPrice

ループ数 = 0

;开店时间・闭店时间
IF INRANGE(TIME:0, 480, 1020) == 0
	PRINTFORMW 好像是非营业时间
	RETURN -1
ENDIF

;初回イベント
IF FLAG:最后一次访问家具屋日期 == 0
	PRINTL 
	PRINTFORMW 写着家具屋的看板映入眼帘、进入玄关…
	PRINTFORMW 「……什么啊、你那副憨脸。啊？」
	PRINTFORMW 「啊？　是啊、这里是家具屋。什么？　没有摆出来卖的东西？」
	PRINTFORMW 「蠢货玩意。不管是衣柜还是什么别的东西、我这里都是接到订单了之后再开始干活的。现在就只是单纯地闲着摸鱼而已」
	PRINTFORMW 「……所以呢？　你有什么想要的东西吗」
	PRINTFORMW 「……啊？　看了实物之后再决定？」
	PRINTFORMW 「只是过来口嗨两句的话你还是给我回去吧……虽然想这么说、没办法。我看看啊、上一个工作的账本――」
	PRINTFORMW 　这个男人、砰、这么甩出来的账本上面、用一丝不苟的字体写着订单的事项和费用
	PRINTFORMW 　……呜哇、太贵了吧！？
	PRINTFORMW 「这是专业的实力和严谨的态度保证出来的上品、根本不可能是便宜的妖艳贱货好吧」
	PRINTFORMW 　价格上能不能再有点别的什么让步、这么问了问…
	PRINTFORMW 「你只要坐在金块上面、然后一边数数一边往这扔金条、从一数到十的话我就接受你放的屁」
	PRINTFORMW 「……话是这么说。只要能准备好『制材』的材料的话、我就把相应的原料价格给你扣掉」
	PRINTFORMW 　那样的话、嗯、大概价格会是……呼、这样的话至少还是付得起的水平、总算松了口气
	PRINTFORMW 「哼。嘛、准备好了再过来下订单吧。我会给你做出最好的逸品的」
	PRINTL 
	ループ数 = 1
ENDIF

SIF FLAG:作成中家具
	FLAG:家具作成残り = MAX(FLAG:家具作成残り-((DAY:0 - FLAG:最后一次访问家具屋日期)*2),0)

$LOOP_SELECT

IF ループ数
	PRINTL 
	IF FLAG:家具作成残り
		PRINTFORMW 「让开让开、爷可是很忙的！」
	ELSE
		PRINTFORMW 「什么啊、还有什么别的事吗」
	ENDIF
ELSE
	;平常来店
	PRINTL 
	;家具发注中
	IF FLAG:家具作成残り >= 14
		PRINTFORMW 「什么嘛、是你啊。猴什么急、图纸都还没开始画呢」
	ELSEIF FLAG:家具作成残り >= 9
		PRINTFORMW 「什么嘛、是你啊。如果是问定的家具的话、还得再摸段时间」
	ELSEIF FLAG:家具作成残り >= 5
		PRINTFORMW 「什么嘛、是你啊。定的家具已经摸了不少了、放心吧」
	ELSEIF FLAG:家具作成残り >= 2
		PRINTFORMW 「喔、定的家具就快做好了！」
	ELSEIF FLAG:作成中家具 && FLAG:家具作成残り == 0
		;完成日
		IF FLAG:作成中家具 == 902
			PRINTFORMW 「噢哟、终于来了。　诶？　明明是在旁边看着澡堂完成的、为什么还要特地来店里一趟？」
			PRINTFORMW 「因为要交付确认手续书之类的乱七八糟的东西。这种东西不弄好我不舒服、万一留下什么隐患还非常恶心」
		ELSE
			PRINTFORMW 「哟。久等了。这就是你定的东西」
		ENDIF
		IF FLAG:作成中家具 == 907
			PRINTFORMW 「咿呀……嘛、毕竟我也不是做扫帚的专家、还是稍微费了点功夫的」
		ELSEIF ITEM:(FLAG:作成中家具) == 2
			PRINTFORMW 「咿呀……久违地做了个让自己满意的工作。实在是、尽了职人的冥利啦」
		ELSEIF ITEM:(FLAG:作成中家具) == 1 
			PRINTFORMW 「呼、已经把想要在意的地方全都做好了……嘛、大概就是这个样子了吧」
			PRINTFORMW 「虽然作为制作方来说是很满足了、但是可能和使用方的满足有些微妙的区别、不过两者都能兼顾才叫职人就是了」
		ELSE
			PRINTFORMW 「如果有想要的家具了再叫我。啊、如果对现在正在用的家具不满意的话也叫我」
		ENDIF
		PRINTL 
		SETCOLOR 0x00FF00
		PRINTFORM 获得了%ITEMNAME:(FLAG:作成中家具)%
		IF ITEM:(FLAG:作成中家具) == 0
			PRINTFORMW ！
		ELSE
			PRINTFORMW 的改进版本、用起来更舒服了！
		ENDIF
		RESETCOLOR
		PRINTL 
		ITEM:(FLAG:作成中家具) ++
		FLAG:作成中家具 = 0
	ELSE
		IF DAY:0 - FLAG:最后一次访问家具屋日期 > 28
			PRINTFORMW 「什么嘛、是你啊。终于打算来下单了？」
		ELSEIF FLAG:最后一次访问家具屋日期 == DAY:0
			PRINTFORMW 「什么嘛。没有搭理你的功………啊、我是很闲怎么了、有意见吗」
		ELSE
			PRINTFORMW 「什么嘛、是你啊。有想要的家具吗？」
		ENDIF
	ENDIF
ENDIF

ループ数 ++
FLAG:最后一次访问家具屋日期 = DAY:0

PRINTFORM 　　所持金: %TOSTR(MONEY,"#,###")%
IF FLAG:作成中家具
	PRINTFORML 　　%ITEMNAME:(FLAG:作成中家具)%正在制作中　完工：还有{FLAG:家具作成残り}日
ELSE
	PRINTL 
ENDIF
DRAWLINE

CALL ASK_M("想要制作家具", !FLAG:作成中家具, "确认交易内容", 1, "从店里离开", 2)

SELECTCASE RESULT
	;家具を作ってほしい
	CASE 0
		PRINTFORMW 「如果是说你需要的家具的话……嘛、大概是这样？」

		$INPUT_LOOP01
		;列表の呼び出し
		削除行数 = 0
		FOR LOCAL, 902, 908
			削除行数 ++
			IF ITEM:LOCAL == 3
				PRINTPLAINFORM 　[{LOCAL}]
				LOCALS = %ITEMNAME:LOCAL%Lv{ITEM:LOCAL}
				PRINTPLAINFORM %LOCALS, 21, LEFT%
			ELSE
				PRINTFORM 　
				PRINTFORM [{LOCAL}]
				IF ITEM:LOCAL == 0
					;未所持
					PRINTFORM %ITEMNAME:LOCAL, 21, LEFT%
				ELSE
					;所持
					LOCALS = %ITEMNAME:LOCAL%Lv{ITEM:LOCAL}
					PRINTFORM %LOCALS, 21, LEFT%
				ENDIF
			ENDIF
			CALL FURNITURE_EXPLANATION(LOCAL)
		NEXT
		PRINTFORML 　[999]果然还是算了
		削除行数 ++
		
		INPUT
		
		リザルト = RESULT
		MaterialQTY = FURNITURE(リザルト, "轻减上限", ITEM:リザルト)
		SELECTCASE リザルト
			CASE 902 TO 907
				;Lv3の时は选择できない
				IF ITEM:リザルト == 3
					CLEARLINE 削除行数 + 1
					GOTO INPUT_LOOP01
				ENDIF

				;注文家具の请求を作成
				DRAWLINE
				PRINTFORML %ITEMNAME:リザルト%的\@ ITEM:リザルト == 0? 订购# 改修\@
				;所持制材量での值引きの表记
				;Lv1→2の时は半额
				IF ITEM:リザルト == 1
					PRINTFORM 　费用　　　　：%金额表示((ITEMPRICE:リザルト / 2))% → 
				ELSE
					PRINTFORM 　费用　　　　：%金额表示(ITEMPRICE:リザルト)%  → 
				ENDIF
				DiscountedPrice = %金额表示(FURNITURE(リザルト, 0, ITEM:リザルト))%
				
				IF FURNITURE(リザルト, 0, ITEM:リザルト) == FURNITURE(リザルト, "最低额", ITEM:リザルト)
					SETCOLOR C_RED
				ELSEIF ITEM:リザルト == 1 && FURNITURE(リザルト, 0, ITEM:リザルト) < ITEMPRICE:リザルト / 2
					SETCOLOR C_YELLOW
				ELSEIF FURNITURE(リザルト, 0, ITEM:リザルト) < ITEMPRICE:リザルト
					SETCOLOR C_YELLOW
				ENDIF
				PRINTFORML %DiscountedPrice% \@ FURNITURE(リザルト, 0, ITEM:リザルト) == FURNITURE(リザルト, "最低额", ITEM:リザルト)? 最安值！# \@
				RESETCOLOR

				;价格轻减に必要な资材量と现在所持量の比较
				PRINTFORM 　价格轻减条件：准备%WOOD_NAME(FURNITURE(リザルト, "资材番号"))%的制材
				PRINTFORM 准备好{MaterialQTY}
				SIF LOG_SALES:FURNITURE(リザルト, "资材番号") >= MaterialQTY
					SETCOLOR C_RED
				PRINTFORML （{LOG_SALES:FURNITURE(リザルト, "资材番号")}／{MaterialQTY}）
				RESETCOLOR
			CASE 999
				PRINTL 
				GOTO LOOP_SELECT
			CASEELSE
				CLEARLINE 1
				GOTO INPUT_LOOP01
		ENDSELECT
		PRINTL 
		IF ITEM:リザルト == 0
			CALL ASK_M("做这个家具", MONEY >= FURNITURE(リザルト, 0), "算了", 1)
		ELSE
			CALL ASK_M("改造这个家具", MONEY >= FURNITURE(リザルト, 0, ITEM:リザルト) && ITEM:リザルト <= 2, "算了", 1)
		ENDIF
		
		IF RESULT
			CLEARLINE 9 + 削除行数
			GOTO INPUT_LOOP01
		ELSE
			;费用の支支付
			PRINTFORMW 　所持金: %金额表示(MONEY)% → %金额表示(MONEY - FURNITURE(リザルト, 0, ITEM:リザルト))%
			PRINTFORM 　
			CALL PAY_MONEY_YEN(FURNITURE(リザルト, 0, ITEM:リザルト))

			;制材等の消费  轻减上限量以上に持っていれば上限量分减らし、上限に达していなければ全部消费
			;现在的制材の量を退避してから计算
			资材保管 = LOG_SALES:FURNITURE(リザルト, "资材番号")
			LOG_SALES:FURNITURE(リザルト, "资材番号") = MAX(0, LOG_SALES:FURNITURE(リザルト, "资材番号") - MaterialQTY)
			;文章部分
			PRINTFORMW 　%WOOD_NAME(FURNITURE(リザルト, "资材番号"))%的制材：{资材保管} → {LOG_SALES:FURNITURE(リザルト, "资材番号")}
			PRINTL 
			IF ITEM:リザルト
				PRINTFORMW 「什么嘛、对之前的家具不满吗？　那正好。{FURNITURE(リザルト, "期间", ITEM:リザルト)}天后再到店里来、会让你大吃一惊的」
			ELSE
				PRINTFORMW 「好、那么契约成立了。完工是在{FURNITURE(リザルト, "期间", ITEM:リザルト)}天后、那时候再到店里来」
			ENDIF
			FLAG:家具作成残り = FURNITURE(リザルト, "期间", ITEM:リザルト)
			FLAG:作成中家具 = リザルト
		ENDIF
	;取引内容を确认する
	CASE 1
		PRINTFORMW 「啊？　想确认一下？」
		PRINTFORMW 「这里是家具屋、除了做家具还能干什么」
		PRINTFORMW 「准备好材料的制材的话、会给你点优惠」
		PRINTFORMW 「虽然是废话、下完订单之后把家具需要一定的时间」
		PRINTFORMW 「顺便一提、在制作期间因为要专心、所以不接受其他的订单」
		PRINTFORMW 「啊、如果对做好的家具不满的话、直接给我说。虽然会收点钱、但是会把它改进得更好的」
		PRINTL 
	CASE 2
		PRINTFORMW 「没事的话就拜拜了您呐」
		PRINTL 
		TIME += ループ数 * 5
		RETURN -1

ENDSELECT
GOTO LOOP_SELECT

;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
;家具屋
@COM_ABLE625
;批量管理
SIF GLOBAL_COMABLE(625)
	RETURN RESULT

IF !AT_HOME(MASTER)
	SIF CFLAG:MASTER:当前位置 != 长屋街
		RETURN 0
ELSE
	;长屋前
	SIF CFLAG:MASTER:当前位置 != 214
		RETURN 0
ENDIF
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;时间停止中
SIF FLAG:70
	RETURN 0
RETURN 1


;-------------------------------------------------
;注文できる家具列表に付随する说明文
;ARG 家具の道具番号
;-------------------------------------------------
@FURNITURE_EXPLANATION(ARG)

IF ITEM:ARG
	PRINTFORM 设置场所：
	SELECTCASE ARG
		CASE 902
			LOCALS = %NAME_FROM_PLACE(NEAR_BATH(1))%
		CASE 904, 907
			LOCALS = %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%带在身上
		CASEELSE
			LOCALS = %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的私室
	ENDSELECT
	PRINTFORMD %LOCALS, 26, LEFT%
ELSE
	PRINTFORM 　　　　　　　　　　　　　　　　　　
ENDIF

SIF ITEM:ARG == 0
	SETCOLOR C_GRAY

SELECTCASE ARG
	CASE 902
		PRINTFORM …在设置的场所入浴的话可以增加体力・气力恢复
	CASE 903
		PRINTFORM …增加睡眠时的体力・气力・精力恢复
	CASE 904
		PRINTFORM …能做出更好的料理
	CASE 905
		PRINTFORM …提高在私室里学习或者调合的效率
	CASE 906
		PRINTFORM …增加私室里休憩的体力・气力・精力恢复
	CASE 907
		PRINTFORM …提升扫除的效率
	CASEELSE
		PRINT 
ENDSELECT
IF ITEM:ARG == 3
	SETCOLOR C_ORANGE
	PRINTFORML ▲▲
ELSEIF ITEM:ARG == 2
	SETCOLOR C_YELLOW
	PRINTFORML ▲
ELSE
	PRINTL 
ENDIF
RESETCOLOR

;-------------------------------------------------
;家具注文で使用する各种数值をまとめた函数
;现状は902～907
;F_ID …家具の道具番号
;ARGS 0= 补正价格(省略时)  1= 资材による轻减上限数
;      2= 家具に对応する资材番号  3= 轻减最低额  
;      4= 发注后の作成期间   を返す
;EnhanceTime 0= 初发注(省略时)  1= 强化１  2= 强化２
;        主にITEM:家具番号を入れて利用
;-------------------------------------------------
@FURNITURE(F_ID, ARGS, EnhanceTime = 0)
#FUNCTION
#DIM F_ID
#DIM EnhanceTime
#DIM DiscountRate
#DIM MaxDiscount
#DIM 资材番号
#DIM 轻减上限
#DIM 最低额
#DIM 补正价格
#DIM 期间

;手持制材との调整价格を表记
SELECTCASE F_ID
	;木浴缸
	CASE 902
		资材番号 = 2;ヒノキ
		轻减上限 = 50
		最低额 = 250000
		期间 = 15
	;桐木床
	CASE 903
		资材番号 = 8;キリ
		轻减上限 = 30
		最低额 = 200000
		期间 = 10
	;银杏案板
	CASE 904
		资材番号 = 6;イチョウ
		轻减上限 = 10
		最低额 = 50000
		期间 = 5
	;榉木桌椅
	CASE 905
		资材番号 = 7;ケヤキ
		轻减上限 = 30
		最低额 = 200000
		期间 = 10
	;栎木沙发
	CASE 906
		资材番号 = 9;ナラ
		轻减上限 = 30
		最低额 = 200000
		期间 = 10
	;特制竹箒
	CASE 907
		资材番号 = 12;竹
		轻减上限 = 30
		最低额 = 30000
		期间 = 10
ENDSELECT

;Lv1→Lv2时は半分  Lv2→Lv3时は最初と同じで
MaxDiscount = ITEMPRICE:F_ID - 最低额
IF EnhanceTime == 1
	轻减上限 /= 2
	最低额 /= 2
	期间 /= 2
	MaxDiscount /= 2
ENDIF

SIF ARGS == "资材番号"
	RETURNF 资材番号
SIF ARGS == "轻减上限"
	RETURNF 轻减上限
SIF ARGS == "最低额"
	RETURNF 最低额
SIF ARGS == "期间"
	RETURNF 期间
	
DiscountRate = 100 * MIN(LOG_SALES:资材番号, 轻减上限) / 轻减上限

IF EnhanceTime == 1
	补正价格 = (ITEMPRICE:F_ID / 2) - MaxDiscount * DiscountRate / 100
ELSE
	补正价格 = ITEMPRICE:F_ID - MaxDiscount * DiscountRate / 100
ENDIF
RETURNF 补正价格
