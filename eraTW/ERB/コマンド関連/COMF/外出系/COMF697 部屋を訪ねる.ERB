;-------------------------------------------------
;访问房间
;-------------------------------------------------
@COM697
#DIM 家主
#DIM 嫉妒
#DIM 浮气
#DIM 房间的污垢
FOR LOCAL,1,CHARANUM
	IF CFLAG:LOCAL:自宅位置 == CFLAG:MASTER:当前位置
		SIF CFLAG:LOCAL:出禁
			CONTINUE
		SIF LOCAL == TARGET
			CONTINUE
;修正住别墅的妹纸 错误的出现在原住地
		SIF CFLAG:LOCAL:神社在住
			CONTINUE
		SIF CFLAG:LOCAL:被掌握把柄
			SETCOLOR C_AQUA
		SIF CFLAG:LOCAL:当前位置 != SUKIMA() || !VISIT(LOCAL) || CFLAG:LOCAL:据点来访 || BASE:LOCAL:体力 < 1000
			SETCOLOR C_GRAY
		PRINTFORML [{LOCAL}] %OMANEKI_PLACE(LOCAL)%
		RESETCOLOR
	ENDIF
NEXT
DRAWLINE
PRINTL [999] 放弃
INPUT
SIF RESULT == 999
	RETURN -1
SIF CFLAG:RESULT:自宅位置 != CFLAG:MASTER:当前位置
	RETURN -1

家主 = RESULT

;别の子と约会中
IF FLAG:约会的对象
	PRINTFORMW 虽然想进入%OMANEKI_PLACE(家主)%、却被%NAME_OUTPUT_TRANSLATION("CALLNAME", (FLAG:约会的对象))%死死地用眼神睨视着
	RETURN -1
ENDIF

;见られてないかチェック
浮气 = 0
嫉妒 = 0
FOR LOCAL,1,CHARANUM
	IF LOCAL != 家主 && CFLAG:LOCAL:当前位置 == CFLAG:MASTER:当前位置 && !TALENT:MASTER:左拥右抱
		SIF LOCAL == 60
			嫉妒 = 60
		SIF TALENT:LOCAL:恋慕 && CFLAG:LOCAL:信赖度 < 1000
			浮气 = LOCAL
	ENDIF
NEXT
;帕露西が出现し4人も住んでいる地灵殿はかなり难易度が高い
;连动角色も駆けつけてくるので二股してると大变
IF 嫉妒 || 浮气
	PRINTFORM 虽然想进入%OMANEKI_PLACE(家主)%、
	IF 嫉妒
		PRINTFORMW 却被面露恶鬼般表情的%NAME_OUTPUT_TRANSLATION("CALLNAME", 嫉妒)%给干涉了
	ELSE
		PRINTFORMW 却被面露惊讶之色的%NAME_OUTPUT_TRANSLATION("CALLNAME", 浮气)%给叫住了
	ENDIF
	RETURN -1
ENDIF

IF TCVAR:家主:到家访问
	PRINTFORML 今天还是算了吧
	RETURN -1
ENDIF

SELECTCASE RAND:10
	CASE IS <= 1
		TFLAG:193 = 1
	CASE 2 TO 4
	;多忙
		TFLAG:193 = 2
	CASE 5,6,7
	;稍微だけ
		TFLAG:193 = 3
	CASE 8,9
	;上げてもらえる
		TFLAG:193 = 4
ENDSELECT
SIF !TALENT:家主:恋慕 && !TALENT:家主:思慕 && TFLAG:193 > 2
	TFLAG:193 --

SIF CFLAG:家主:当前位置 != SUKIMA() || !VISIT(家主) || CFLAG:家主:据点来访 || BASE:家主:体力 < 1000
	TFLAG:193 = 1

PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%拜访了%OMANEKI_PLACE(家主)%
TCVAR:家主:到家访问 = 1
[IF_DEBUG]
IF FLAG:テストプレイ用自慰暴露强制发生
	CALL ONABARE(家主)
	GOTO OWARI
ENDIF
[ENDIF]

CALL IRAI_VISIT_HOME(家主)
SIF RESULT
	GOTO OWARI


IF DAY:2 == 3 && (DAY:3 == 13 || DAY:3 == 15) && TFLAG:193 > 1
	CFLAG:家主:最后见面的日子 = DAY
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%出来了
	PRINTFORMW 「牛头鱼、三角鱼!」%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%祈求着丰收
	IF ABL:家主:教养 >= 5 || 家主 == 77 || 家主 == 78
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%给了我点心！
		;现状实装されている点心を随机に
		SELECTCASE RAND:5
			CASE 0
				ITEM:120 ++
			CASE 1
				ITEM:212 ++
			CASE 2
				ITEM:213 ++
			CASE 3
				ITEM:216 ++
			CASEELSE
				ITEM:376 ++
		ENDSELECT
	ELSE
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%一副讶异的表情…看来不知道什么是“秘密”
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%沉默的把门闭上了
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%有些伤心
	ENDIF
	TIME += 10
	RETURN 1
ENDIF

IF TFLAG:193 > 1 && CFLAG:家主:积攒度 > 300
	IF RAND:(CFLAG:家主:积攒度 + TFLAG:幸运补正 * 50) >= RAND:700
		CALL ONABARE(家主)
		GOTO OWARI
	ELSEIF ITEM:兔符「开运大纹」
		PRINTL 咦、开运大纹的样子…？
		CALL ASK_YN("取出","置之不理")
		IF !RESULT
			ITEM:兔符「开运大纹」 --
			CALL COLORMESSAGE(@"兔符「开运大纹」发动！",C_YELLOW,2)
			CALL ONABARE(家主)
			GOTO OWARI
		ENDIF
	ENDIF
ENDIF
SELECTCASE TFLAG:193
	CASE 1
		IF CFLAG:MASTER:当前位置 == CFLAG:家主:当前位置
			PRINTFORMW 被眼前的%NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%、说道不要擅自进来
		ELSE
			PRINTFORMW 没有回应…　出门了或者在睡觉吧
		ENDIF
	CASE 2
		CFLAG:家主:最后见面的日子 = DAY
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%出来了
		IF !TALENT:家主:恋慕 && !TALENT:家主:思慕
			PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%有些为难、
		ELSE
			PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%感到有些抱歉、
		ENDIF
		PRINTFORMW 说道今天没有时间请下次再说
		PRINTFORM 试着问了问明天能否见面、
		IF !RAND:2 && !GETBIT (TFLAG:一日一回,5)
			PRINTFORMW 被告知明天打算要去%GET_MAPNAME(MAIN_MAP)%
			CFLAG:家主:翌日来访 = 1
			SETBIT TFLAG:一日一回,5
		ELSE
			PRINTFORMW 被告知还不能确定下来
		ENDIF
		CALL COM697_1(家主)
		TARGET = 家主
		TIME += 5
		RETURN 1

	CASE 3
		CFLAG:家主:最后见面的日子 = DAY
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%出来了
		PRINTFORML 对%NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%说因为想见她所以来了、哪怕只是一会儿也好
		PRINTFORMW …
		PRINTFORMW ……
		PRINTFORMW ………
		PRINTFORMW 和%NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%谈笑了一段时间
		PRINTFORMW 离开的时候、问了问明天能不能见面
		IF !RAND:2 && !GETBIT (TFLAG:一日一回,5)
			PRINTFORMW 被告知明天打算要去%GET_MAPNAME(MAIN_MAP)%
			CFLAG:家主:翌日来访 = 1
			SETBIT TFLAG:一日一回,5
		ELSE
			PRINTFORMW 被告知还不能确定下来
		ENDIF
		CALL COM697_1(家主)
		TARGET = 家主
		TIME += 30
		RETURN 1

	CASE 4
		CFLAG:家主:最后见面的日子 = DAY
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%出来了
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%好像很开心的样子…
		CALL ASK_YN("邀请约会","打扰了")
		IF !RESULT || !TALENT:家主:恋慕
			IF !RESULT
				PRINTFORMW 邀请%NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%去约会了
			ELSE
				PRINTFORML 虽然%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%提出希望能来打扰，但是被拒绝了
				PRINTFORMW 取而代之的是%NAME_OUTPUT_TRANSLATION("CALLNAME", 家主)%提议约会，%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%毫不犹豫地点了点头
			ENDIF
			CFLAG:MASTER:同行 = 60
			CFLAG:家主:同行 = 60
			CFLAG:家主:当前位置 = CFLAG:MASTER:当前位置
			CFLAG:家主:约会中 = CFLAG:MASTER:当前位置 / 100
			CFLAG:家主:据点来访 = 1
			FLAG:约会的对象 = 家主
			TFLAG:约会前好感度 = CFLAG:家主:2
		ELSE
			;约会处理入れてないのに约会中だけ入れるのはバグの元になりかねないので一旦オミット
			;CFLAG:家主:约会中 = CFLAG:MASTER:当前位置 / 100
			CALL OMANEKI_ENTER(家主)
		ENDIF
ENDSELECT

$OWARI
TFLAG:102 = 0
RETURN 0

@COM697_1(ARG)
#DIM 欢乐UP
#DIM 被动UP
#DIM 征服UP

;会话のコンパチ
TFLAG:98 = 5
;固定で获得するソース
欢乐UP = 200
被动UP = 0
征服UP = 0
;ABL:亲密をみる
IF ABL:ARG:亲密 <= 1
	欢乐UP += (ABL:ARG:顺从 * 40)
ELSEIF ABL:ARG:亲密 <= 3
	欢乐UP += 200 + (ABL:ARG:顺从 * 40)
ELSEIF ABL:ARG:亲密 <= 5
	欢乐UP += 500 + (ABL:ARG:顺从 * 40)
ELSEIF ABL:ARG:亲密 <= 8
	欢乐UP += 750 + (ABL:ARG:顺从 * 60)
ELSEIF ABL:ARG:亲密 <= 10
	欢乐UP += 1000 + (ABL:ARG:顺从 * 60)
ELSE
	欢乐UP += 1600 + (ABL:ARG:顺从 * 30)
ENDIF

;好感度をみる
IF CFLAG:ARG:2 <= 1000
	欢乐UP += CFLAG:ARG:2
ELSEIF CFLAG:2 <= 5000
	欢乐UP += 500 + (CFLAG:ARG:2 - 500) / 3
ELSE
	欢乐UP += 2000 + (CFLAG:ARG:2 - 5000) / 5
ENDIF
SIF 欢乐UP < 0
	欢乐UP = 0


被动UP = 100 + 100 * ABL:ARG:顺从
征服UP = 100 + 100 * ABL:ARG:施虐属性

SELECTCASE ABL:MASTER:话术技能
	CASE 0
		TIMES 欢乐UP , 0.20
		TIMES 征服UP , 0.20
		TIMES 被动UP , 0.20
	CASE 1
		TIMES 欢乐UP , 0.40
		TIMES 征服UP , 0.40
		TIMES 被动UP , 0.40
	CASE 2
		TIMES 欢乐UP , 0.70
		TIMES 征服UP , 0.70
		TIMES 被动UP , 0.70
	CASE 3
		TIMES 欢乐UP , 1.00
		TIMES 征服UP , 1.00
		TIMES 被动UP , 1.00
	CASE 4
		TIMES 欢乐UP , 1.20
		TIMES 征服UP , 1.20
		TIMES 被动UP , 1.20
	CASEELSE
		TIMES 欢乐UP , 1.50
		TIMES 征服UP , 1.50
		TIMES 被动UP , 1.50
ENDSELECT

IF TFLAG:193 == 3
	TIMES 欢乐UP , 3.50
	TIMES 征服UP , 3.50
	TIMES 被动UP , 3.50
	TFLAG:好感度BOUNS = 200
ENDIF
SOURCE:ARG:欢乐 = 欢乐UP
SOURCE:ARG:征服 = 征服UP
SOURCE:ARG:被动 = 被动UP

CALL FAVOR_CALC(ARG)
CALL TRUST_CALC(ARG)
TFLAG:好感度BOUNS = 0

;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
;访问房间
@COM_ABLE697
;批量管理
SIF GLOBAL_COMABLE(697)
	RETURN RESULT
SIF AT_HOME(MASTER)
	RETURN 0
SIF CFLAG:MASTER:诶嘿嘿
	RETURN 0
;时间停止中
SIF FLAG:70
	RETURN 0
;潜伏モード中は不可
SIF GETBIT(FLAG:潜伏,0)
	RETURN 0
LOCAL:1 = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:出禁
		CONTINUE
	SIF CFLAG:LOCAL:自宅位置 == CFLAG:MASTER:当前位置 && !CFLAG:LOCAL:神社在住 && LOCAL != TARGET; && (TALENT:LOCAL:思慕 || TALENT:LOCAL:恋慕) 
		LOCAL:1 ++
NEXT

SIF !LOCAL:1
	RETURN 0
RETURN 1
