@COM618
#DIM 买うもの
;购入条件设定
FOR LOCAL, 0, 1000
	CALL ITEMDATA(LOCAL, "SHOP:花屋")
	IF RESULT
		CALL ITEMDATA(LOCAL, "SALES")
		ITEMSALES:LOCAL = RESULT
	ENDIF
NEXT
DRAWLINE
CALL LIST_ITEM("跳蛋")
CALL LIST_ITEM("野菜")
CALL LIST_ITEM("园艺用品")
DRAWLINE
PRINTFORML 请选择要购买的道具（所持金:%金额表示(MONEY)%）
DRAWLINE
CALL SHOW_SHOPPING("花屋")
DRAWLINE
PRINTL [999] - 返回
INPUT
IF RESULT == 999
	TIME += 10
	RETURN -1
ELSEIF RESULT == GETNUM(ITEM, "园艺用品一式") && ITEMSALES:RESULT >= 0
	CALL ITEMDATA(RESULT, "购入")
	SIF RESULT
		RESTART
ELSE
	买うもの = RESULT
	CALL ITEMDATA(买うもの, "SHOP:花屋")
	IF RESULT && ITEMSALES:买うもの >= 1 && ITEM:买うもの < 所持数制限
		CALL ITEMDATA(买うもの, "购入")
		SIF !RESULT
			RESTART
	ELSE
		RESTART
	ENDIF
ENDIF
TIME += 10

;-------------------------------------------------
;实行可能判定
;-------------------------------------------------
;花屋
@COM_ABLE618
;批量管理
SIF GLOBAL_COMABLE(618)
	RETURN RESULT

;花屋ではない场所
SIF !FLOWER_SHOP(CFLAG:MASTER:当前位置)
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;时间停止中
SIF FLAG:70
	RETURN 0
RETURN 1

;花屋を判定する函数
@FLOWER_SHOP(ARG)
#FUNCTION
;花屋
SIF ARG == GET_MAP_REPLACEMENT(215)
	RETURNF 1
RETURNF 0
