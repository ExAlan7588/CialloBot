;-------------------------------------------------
;乱牡丹
;性交系派生指令
;-------------------------------------------------
@SCOM4
;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = PLAYER
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛门 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛门侍奉 10=道具 11=触手）

LOCALS = {TARGET}/
CSTR:PLAYER:10 += LOCALS
TCVAR:PLAYER:2 = 1
;强度
TCVAR:PLAYER:4 = 2300

;侍奉快乐フラグ
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
;消费体力、气力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 60
	SOURCE:快Ｃ = 300
	SOURCE:快Ｖ = 400
	SOURCE:快Ｂ = 300
	SOURCE:苦痛 = 200

	;处女だった场合は、苦痛のソースと反抗のソースを追加する
	IF TALENT:处女 == 1
		SOURCE:苦痛 += 1000
	ENDIF

	;再生处女だった场合は、苦痛のソースと反抗のソースを追加する
	IF TALENT:处女 == 2
		SOURCE:苦痛 += 250
	ENDIF

ELSE

	DOWNBASE:体力 += 50
	DOWNBASE:气力 += 100

	SOURCE:快Ｃ = 300
	SOURCE:快Ｖ = 400
	SOURCE:快Ｂ = 300
	SOURCE:情爱 = 400
	SOURCE:苦痛 = 400
	SOURCE:露出 = 600
	SOURCE:偏离正轨 = 700
	SOURCE:反感 = 1200

	;上のほうで计算した污れデータ
	SOURCE:不洁 = Y*10 + 60


	;-------------------------------------------------
	;特殊ソース处理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑郁ソース追加
	SIF TALENT:自己爱 < 0 || TALENT:抵抗
		SOURCE:郁闷 += 1000

	;贞操重视持ちで恋慕、亲爱持ち以外には常に反抗のソースを追加する（性交系のみ）
	SIF TALENT:贞操 > 0 && !TALENT:恋慕
		SOURCE:反感 += 1000

	;处女だった场合は、苦痛のソースと反抗のソースを追加する
	IF TALENT:处女 == 1
		SOURCE:苦痛 += 2000
		SOURCE:反感 += 3000
	ENDIF

	;再生处女だった场合は、苦痛のソースと反抗のソースを追加する
	IF TALENT:处女 == 2
		SOURCE:苦痛 += 500
		SOURCE:反感 += 1000
	ENDIF
ENDIF
;Vへの苦痛は先に处理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;污れの处理
;-------------------------------------------------
;射精に关系なく行われる处理はここから
;膣内に破瓜之血的污垢を付加
SIF TALENT:处女  &&TALENT:处女 != -1
	STAIN:6 |= 64
;奴隷の膣内⇔调教者のＰ的污垢が移动
STAIN:6 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:6

;-------------------------------------------------
;経験の处理
;-------------------------------------------------
;性交经验
EXP:PLAYER:插入经验 ++

;同性爱　レズなら返回值1ゲイなら返回值2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF

IF FLAG:70 == 1
	EXP:MASTER:时奸经验 += 1
ELSE
	EXP:Ｖ性交经验 += 1
	;爱情经验
	SIF !ASSIPLAY
		EXP:爱情经验 += 2 * TALENT:恋慕
ENDIF
RETURN 1

@CAN_SCOM4(ARG)
;-------------------------------------------------
;实行判定处理
;-------------------------------------------------
IF FLAG:70 == 1

ELSE

	;绳で缚られている场合は无视（绳が无い时のみ实行判定を行う）
	IF !TEQUIP:绳
		SKIPDISP ARG
		PRINTL 乱牡丹
		A = 0
		S = 0
		;全部の命令に共通の要素を考虑
		;(顺从が高いと命令に从いやすいなど)
		CALL COM_ORDER

		;ABL:欲望
		IF ABL:欲望
			SIF S
				PRINT  + 
			A += ABL:欲望 * 3
			PRINT 欲望
			PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 3,')
			S = 1
		ENDIF
		;ABL:Ｖ感觉
		IF ABL:Ｖ感觉
			SIF S
				PRINT  + 
			A += ABL:Ｖ感觉 * 2
			PRINT Ｖ感觉
			PRINTV 'LV,ABL:Ｖ感觉,'(,ABL:Ｖ感觉 * 2,')
			S = 1
		ENDIF
		;快乐刻印
		IF MARK:快乐刻印
			SIF S
				PRINT  + 
			A += MARK:快乐刻印 * 3
			PRINT 快乐刻印
			PRINTV 'LV,MARK:快乐刻印,'(,MARK:快乐刻印 * 3,')
			S = 1
		ENDIF
		;PALAM:润滑（の不足）
		IF PALAM:润滑 < PALAMLV:3
			PRINT  - 
			A -= 5
			PRINT 润滑不足
			PRINTV '(,5,')
			S = 1
		ENDIF
		;PALAM:情欲
		IF GETPALAMLV(PALAM:情欲,5)
			SIF S
				PRINT  + 
			A += GETPALAMLV(PALAM:情欲,5) * 3
			PRINT 情欲
			PRINTV 'LV,GETPALAMLV(PALAM:情欲,5),'(,GETPALAMLV(PALAM:情欲,5) * 3,')
			S = 1
		ENDIF
		;害羞
		IF TALENT:羞耻心 > 0
			PRINT  - 
			A -= 2
			PRINT 害羞
			PRINTV '(,2,')
			S = 1
		ENDIF
		;否定快感
		IF TALENT:快感应答 < 0 && !TALENT:恋慕
			PRINT  - 
			A -= 5
			PRINT 否定快感
			PRINTV '(,5,')
			S = 1
		ENDIF
		;讨厌男人（恋慕、亲爱で无效）
		IF OTOKOGIRAI(TARGET)
			PRINT  - 
			A -= 12
			PRINT 讨厌男人
			PRINTV '(,12,')
			S = 1
		ENDIF
		;恋慕
		IF TALENT:恋慕 && ASSIPLAY == 0
			SIF S
				PRINT  + 
			A += 15
			PRINT 恋慕
			PRINTV '(,15,')
			S = 15
		ENDIF
		;亲爱
		IF TALENT:恋慕 > 1 && ASSIPLAY == 0
			SIF S
				PRINT  + 
			A += 5
			PRINT 亲爱
			PRINTV '(,5,')
			S = 5
		ENDIF
		;处女
		LOCAL = MAX(0,40 - EXP:Ｖ经验)
		IF TALENT:处女 && TALENT:处女 != -1 && LOCAL
			PRINT  - 
			A -= LOCAL
			PRINT 处女
			PRINTV '(,LOCAL,')
			S = 1
		;V経験
		ELSEIF EXP:Ｖ经验 < EXPLV:2
			PRINT  - 
			A -= 15
			PRINT Ｖ经验
			PRINT 不足
			PRINTV '(,15,')
			S = 1
		ENDIF

		;调教者が扶她
		IF (TALENT:PLAYER:2 & 3) == 3
			SIF S
				PRINT  + 
			A += 4
			PRINT 扶她
			PRINTV '(,4,')
			S = 1
		ENDIF

		;媚药
		IF TCVAR:媚药
			SIF S
				PRINT  + 
			A += 6
			PRINT 媚药
			PRINTV '(,6,')
			S = 1
		ENDIF

		Y = 0
		;爱液的污垢
		SIF STAIN:PLAYER:2 & 1
			Y += 1
		;精液的污垢
		SIF STAIN:PLAYER:2 & 4
			Y += 3
		;肛门的污垢
		SIF STAIN:PLAYER:2 & 8
			Y += 7
		SIF STAIN:PLAYER:2 & 16
			Y += 15
		Y = Y * (2 - TALENT:污臭耐性) / 2

		;有污垢
		IF Y
			PRINT  - 
			A -= Y
			PRINT 有污垢
			;污臭钝感
			IF TALENT:污臭耐性 > 0
				PRINT 、污臭钝感
			ELSEIF TALENT:污臭耐性 < 0
				PRINT 、污臭敏感
			ENDIF
			PRINTV '(,Y,')
			S = 1
		ENDIF

		;合计を表示(47以上で实行)
		PRINT  = 
		PRINTV A

		V = 47
		SIF A < V
			PRINT  < 
		SIF A == V
			PRINT  = 
		SIF A > V
			PRINT  > 
		PRINT 实行值
		PRINTV V

	ENDIF

	WAIT
	SKIPDISP 0
	;实行できない
	SIF A < V
		RETURN 0
ENDIF
RETURN 1


;-------------------------------------------------
;选择可能判定
;-------------------------------------------------
@COM_ABLE504
;乱牡丹实行判定
SIF !TFLAG:100
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(504)
	RETURN RESULT
;调教者が男人、扶她の场合はある程度勃起てないとダメ
SIF (TALENT:PLAYER:2 & 2) && BASE:MASTER:勃起 < 1000
	RETURN 0
;羞耻PLAY中じゃないとダメ
SIF TEQUIP:30 == 0
	RETURN 0
;振动棒插入中はダメ
SIF TEQUIP:13
	RETURN 0
;男人だとダメ
SIF  !(TALENT:2 & 1)
	RETURN 0
;全裸でないとダメ（袜子、特殊は除く）
SIF TEQUIP:0 || TEQUIP:1
	RETURN 0
;调教者が男人、半人半妖、扶她、もしくは穿戴式假阴茎がないとダメ
SIF !(TALENT:PLAYER:2 & 2) && ITEM:PBAND == 0
	RETURN 0
;润滑不足で、顺从3以下・百合属性3以下のASSIの场合乱牡丹はできない（施虐狂ならＯＫ）
IF PALAM:润滑 < PALAMLV:2 && ASSIPLAY
	SIF (ABL:ASSI:顺从 <= 3 || ABL:ASSI:百合属性 <= 3) && TALENT:ASSI:施虐狂 == 0
		RETURN 0
ENDIF
RETURN 1

