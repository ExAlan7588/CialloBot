;-------------------------------------------------
;插入子宫口玩弄
;系指令、レベル
;-------------------------------------------------
@SCOM7
#DIM 苦痛补正值

;-------------------------------------------------
;COM别FLAGの设定
;-------------------------------------------------
;行为者
TCVAR:116 = PLAYER
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛门 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛门侍奉 10=道具 11=触手）

LOCALS = {TARGET}/
CSTR:PLAYER:10 += LOCALS
TCVAR:PLAYER:2 = 1
;强度
TCVAR:PLAYER:4 = 2000 + TEQUIP:TARGET:子宫 * 250

CSTR:TARGET:10 += @"{PLAYER}/"
;TCVAR:2 射精部位フラグ（-1=避孕套 1=膣内 2=肛门 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛门侍奉 10=道具 11=触手 12=Ａ调教 13=Ｖ调教）
TCVAR:TARGET:2 = 13
;强度
TCVAR:TARGET:4 = 1600

SIF EXP:TARGET:子宫口开发经验 >= 50
	TCVAR:PLAYER:4 += MIN(EXP:TARGET:子宫口开发经验 * 5,1500)


;ベッドがないと立位に
SIF !BEDROOM(CFLAG:PLAYER:当前位置) && !(BATHROOM() && ITEM:20)
	TFLAG:193 = 1

;侍奉快乐フラグ
TCVAR:PLAYER:108 = 1

;-------------------------------------------------
;ソース追加处理
;-------------------------------------------------
SOURCE:快Ｖ = 500 + TEQUIP:TARGET:子宫 * 200
SOURCE:苦痛 = MAX(600 - EXP:TARGET:子宫口开发经验 * 2, 400)
SIF EXP:TARGET:子宫口开发经验 > 50
	SOURCE:苦痛 /= 2

;子宫口开发经验で快感追加
SELECTCASE EXP:TARGET:子宫口开发经验
	CASE IS > 300
		SOURCE:快Ｖ += 200
	CASE IS > 150
		SOURCE:快Ｖ += 100
	CASE IS > 50
	CASEELSE
		SOURCE:快Ｖ /= 2
ENDSELECT


;消费体力、气力
IF FLAG:70 == 1
	BASE:MASTER:TSP -= 60
	SOURCE:MASTER:快Ｃ = 500

ELSE
	SOURCE:MASTER:快Ｃ = 500

	DOWNBASE:体力 += 60
	DOWNBASE:气力 += 120
	SOURCE:情爱 = 200 + TEQUIP:TARGET:子宫 * 100
	SOURCE:露出 = 120
	SOURCE:反感 = 500
	;-------------------------------------------------
	;特殊ソース处理
	;-------------------------------------------------
	;圧抑、抵抗持ちには、抑郁ソース追加
	SIF TALENT:自己爱 < 0 || TALENT:抵抗
		SOURCE:郁闷 += 500

	;贞操重视持ちで恋慕、亲爱持ち以外には常に反抗のソースを追加する（性交系のみ）
	SIF TALENT:贞操 > 0 && !TALENT:恋慕
		SOURCE:反感 += 1000
ENDIF

;Vへの苦痛は先に处理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;污れの处理
;-------------------------------------------------
;射精に关系なく行われる处理はここから
;膣内に破瓜之血的污垢を付加
SIF TALENT:处女 && TALENT:处女 != -1
	STAIN:6 |= 64
;奴隷の膣内⇔调教者のＰ的污垢が移动
STAIN:6 |= STAIN:PLAYER:2
STAIN:PLAYER:2 |= STAIN:6

;-------------------------------------------------
;経験の处理
;-------------------------------------------------
EXP:PLAYER:插入经验 ++
;同性爱　レズなら返回值1ゲイなら返回值2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF

EXP:子宫口开发经验 ++
SIF ABL:MASTER:腰 >= 3
	EXP:子宫口开发经验 ++
IF FLAG:70 == 1
	EXP:MASTER:时奸经验 += 1
ELSEIF CFLAG:MASTER:恶作剧 == 2
	EXP:MASTER:睡眠奸经验 += 1
	EXP:TARGET:睡眠奸经验 += 1
ELSE
	EXP:Ｖ性交经验 ++
	;爱情经验
	SIF !ASSIPLAY
		EXP:爱情经验 += TALENT:恋慕
ENDIF
RETURN 1

;-------------------------------------------------
;实行判定
;-------------------------------------------------
@COM_ABLE73
;插入子宫口玩弄
SIF !TFLAG:100
	RETURN 0
SIF !MASTER_POSE(SET_V,SET_P,1)
	RETURN 0
;批量管理
SIF GLOBAL_COMABLE(73)
	RETURN RESULT
;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
SIF TEQUIP:TARGET:50 != MASTER
	RETURN 0
;体格差がある场合は主人が禁断的知识持ちじゃないとダメ
SIF 插入不可(TARGET) == 1 && !TALENT:PLAYER:禁断的知识
	RETURN 0
;さらに体格差があると経験が必要
SIF 插入不可(TARGET) == 2 && (!TALENT:PLAYER:禁断的知识 || EXP:Ｖ经验 < 50)
	RETURN 0
SIF 插入不可(TARGET) == 3
	RETURN 0
RETURN 1

;-------------------------------------------------
;选择可能判定
;-------------------------------------------------
@COM_ABLE507
;插入子宫口玩弄
SIF !TFLAG:100
	RETURN 0
;下半身着衣
SIF TEQUIP:0 & 4
	RETURN 0
RETURN 1

