;----------------------------------
;隐密中フラグセット CFLAG:330SET
;呼び出し时の整合性を判定し参数として渡された值をフラグにセットする
;ARG=SET值 ARG:1=セット对象 ARG:2=MASTER连动
;----------------------------------
@SET_SNEAK(ARG,ARG:1,ARG:2)
SELECTCASE ARG
	;FLAG_OFF
	CASE 0
		;整合性チェック
		CFLAG:(ARG:1):隐密中 = 0
		IF ARG:2 == 1
			CFLAG:MASTER:隐密中 = 0
		ELSEIF ARG:2 == 2
			SIF CFLAG:(ARG:1):当前位置 == CFLAG:MASTER:当前位置
				CFLAG:MASTER:隐密中 = 0
		ENDIF
	;FLAG_ON
	CASE 1
		;整合性チェック
		;TARGETとMASTERが同室かつ睡眠中でないとセットは出来ない
		IF CFLAG:(ARG:1):当前位置 == CFLAG:MASTER:当前位置 && CFLAG:(ARG:1):睡眠
			CFLAG:(ARG:1):隐密中 = 1
			SIF ARG:2 == 1
				CFLAG:MASTER:隐密中 = 1
		ELSE
		ENDIF
ENDSELECT

;----------------------------------
;恶作剧フラグセット CFLAG:331SET
;呼び出し时の整合性を判定し参数として渡された值をフラグにセットする
;ARG=SET值 ARG:1=セット对象 ARG:2=MASTER连动ON
;----------------------------------
@SET_PRANK(ARG,ARG:1,ARG:2)
SELECTCASE ARG
	;FLAG_OFF
	CASE 0
		;整合性チェック
		CFLAG:(ARG:1):恶作剧 = 0
		IF ARG:2 == 1
			CFLAG:MASTER:恶作剧 = 0
		ELSEIF ARG:2 == 2
			SIF CFLAG:(ARG:1):当前位置 == CFLAG:MASTER:当前位置
				CFLAG:MASTER:恶作剧 = 0
		ENDIF
	;FLAG_ON
	CASE 1
		;整合性チェック
		;TARGETとMASTERが同室・隐密中・睡眠中でないとセットは出来ない
		IF CFLAG:(ARG:1):当前位置 == CFLAG:MASTER:当前位置 && CFLAG:(ARG:1):隐密中 == CFLAG:MASTER:隐密中 && CFLAG:(ARG:1):睡眠
			CFLAG:(ARG:1):恶作剧 = 1
			SIF ARG:2 == 1
				CFLAG:MASTER:恶作剧 = 1
		ELSE

		ENDIF
	;过激な恶作剧FLAG_ON
	CASE 2
		;整合性チェック
		;TARGETとMASTERが同室・隐密中・睡眠中・既に恶作剧FLAG_ONでないとセットは出来ない
		IF CFLAG:(ARG:1):当前位置 == CFLAG:MASTER:当前位置 && CFLAG:(ARG:1):隐密中 == CFLAG:MASTER:隐密中 && CFLAG:(ARG:1):恶作剧 > 0 && CFLAG:(ARG:1):睡眠
			CFLAG:(ARG:1):恶作剧 = 2
			SIF ARG:2 == 1
				CFLAG:MASTER:恶作剧 = 2
		ELSE

		ENDIF
ENDSELECT

;----------------------------------
;添い寝フラグセット CFLAG:334SET
;呼び出し时の整合性を判定し参数として渡された值をフラグにセットする
;ARG=SET值 ARG:1=セット对象 ARG:2=MASTER连动ON (1=无条件に起きる 2=同室なら起きる)
;----------------------------------
@SET_TOGETHER(ARG,ARG:1,ARG:2)
SELECTCASE ARG
	;FLAG_OFF
	CASE 0
		;整合性チェック
		CFLAG:(ARG:1):陪睡中 = 0
		IF ARG:2 == 1
			CFLAG:MASTER:陪睡中 = 0
		ELSEIF ARG:2 == 2
			SIF CFLAG:(ARG:1):当前位置 == CFLAG:MASTER:当前位置
				CFLAG:MASTER:陪睡中 = 0
		ENDIF
	;FLAG_ON
	CASE 1
		;整合性チェック
		;TARGETとMASTERが同室・睡眠中でないとセットは出来ない
		IF CFLAG:(ARG:1):当前位置 == CFLAG:MASTER:当前位置 && CFLAG:(ARG:1):睡眠
			CFLAG:(ARG:1):陪睡中 = 1
			SIF ARG:2 == 1
				CFLAG:MASTER:陪睡中 = 1
		ELSE
		ENDIF
ENDSELECT

;----------------------------------
;睡眠中フラグセット CFLAG:313SET
;呼び出し时の整合性を判定し参数として渡された值をフラグにセットする
;ARG=SET值 ARG:1=セット对象 ARG:2=MASTER连动ON
;----------------------------------
@SET_SLEEP(ARG,ARG:1,ARG:2)
SELECTCASE ARG
	;FLAG_OFF
	CASE 0
		;整合性チェック
		CFLAG:(ARG:1):睡眠 = 0
		;睡眠フラグでMASTER连动は不使用だろうけど… とか言ってたら添い寝で使用ことに…
		IF ARG:2 == 1
			CFLAG:MASTER:睡眠 = 0
		ELSEIF ARG:2 == 2
			SIF CFLAG:(ARG:1):当前位置 == CFLAG:MASTER:当前位置
				CFLAG:MASTER:睡眠 = 0
		ENDIF
		SETBIT CFLAG:(ARG:1):更衣实行, 0
		CLEARBIT CFLAG:(ARG:1):更衣实行, 1
		;疲劳と睡眠深度をリセット
		TCVAR:ARG:疲劳度   = 0
		TCVAR:ARG:睡眠深度 = 0
	;FLAG_ON
	CASE 1
		;整合性チェック
		;延迟中はセット出来ない
		IF !CFLAG:(ARG:1):延迟 || ARG:1 == MASTER
			CFLAG:(ARG:1):睡眠 = 1
			CFLAG:(ARG:1):约会中 = MAIN_MAP
			;MASTER连动は使わなn（ry
			SIF ARG:2 == 1
				CFLAG:MASTER:睡眠 = 1
		;衰弱时は延迟中も寝る
		ELSEIF CFLAG:(ARG:1):衰弱
			CFLAG:(ARG:1):睡眠 = 1
			CFLAG:(ARG:1):约会中 = MAIN_MAP
			;MASTER连动は使わなn（ry
			SIF ARG:2 == 1
				CFLAG:MASTER:睡眠 = 1
		ENDIF
		IF CFLAG:(ARG:1):睡眠 == 1 && GETBIT(CFLAG:(ARG:1):更衣实行, 0)
			TCVAR:(ARG:1):睡眠深度 = 1200 * (TCVAR:(ARG:1):睡眠药强度 + 1)
			TCVAR:(ARG:1):睡眠深度 += TCVAR:(ARG:1):疲劳度 / 3
		ENDIF
		SETBIT CFLAG:(ARG:1):更衣实行, 1
		CLEARBIT CFLAG:(ARG:1):更衣实行, 0
		IF CFLAG:(ARG:1):延迟 || !睡眠时间(ARG:1) || (CFLAG:(ARG:1):衰弱 && CFLAG:(ARG:1):当前位置 != CFLAG:(ARG:1):初始位置)
			SETBIT CFLAG:(ARG:1):更衣实行, 0
			CLEARBIT CFLAG:(ARG:1):更衣实行, 1
		ENDIF
		SIF ARG:1 == MASTER
			CALL BUFF_RESET
ENDSELECT

;睡眠关联处理としてここに追加
;----------------------------------
;疲劳度产出函数
;DOWNBASEから疲劳度を算出する
;√0避けのために作成
;ARG:RETURNF
;_10:_1
;_25:_2
;_50:10
;100:30
;125:36
;150:45
;200:89
;DOWNBASEには体力ならx12、气力ならx8の系数がつき、实际の疲劳度加算值は
;体力100消费で1314、体力200消费で3718
;气力100消费で715、气力200消费で2023
;（电卓で计算したので误差が出ます）
;恶劣天气で训练すると「死ぬほど疲れる」
;----------------------------------
@CULC_TIRED(ARG)
#FUNCTION
SIF ARG < 10
	RETURNF 0
LOCAL = ARG / 10
RETURNF LOCAL * SQRT(LOCAL)

;----------------------------------
;疲劳度加算函数 TCVAR:151ADD
;主にMASTER避けと睡眠判定のために作成
;----------------------------------
@ADD_TIRED(CHARA, VALUE)
#DIM CHARA
#DIM VALUE
SIF CHARA == MASTER || CFLAG:CHARA:睡眠
	RETURN
TCVAR:CHARA:疲劳度 += VALUE


;-----------------------------------------------------------
;约会フラグセットという名のお出挂けセット函数
;（现在はCFLAG:约会中は当前位置のMAPIDとして使われている）
;呼び出し时の整合性を判定し、P_IDへの各种お出挂けフラグを设定する
;P_ID              外出目的地のMAPID,MAXHOME以上だと回家
;C_ID              同行する角色ID
;所要时间          道中にかかる时间
;时间停止ジャンプ  时间停止でお出挂け（FLAG:时间停止を不使用）
;-----------------------------------------------------------
@SET_DATE(P_ID, C_ID, 所要时间, 时间停止ジャンプ)
#DIM P_ID
#DIM C_ID
#DIM 所要时间
#DIM 时间停止ジャンプ
#DIM 现在地MAPID
;お出挂け结束
IF P_ID >= MAXHOME
	;整合性チェック
	IF CHK_DATENOW(CFLAG:C_ID:约会中)
		CFLAG:MASTER:约会中 = MAIN_MAP
		FLAG:约会的对象 = 0
		TFLAG:约会前好感度 = 0
		CFLAG:MASTER:当前位置 = GET_ENTRANCE(MAIN_MAP)
		TFLAG:约会道中 = 0
		IF C_ID
			CFLAG:C_ID:约会中 = MAIN_MAP
			CFLAG:C_ID:当前位置 = GET_ENTRANCE(MAIN_MAP)
		ENDIF
	ENDIF
;お出挂け开始
ELSE
	;整合性チェック
	SIF BASE:MASTER:气力 <= 0
		RETURN
	IF C_ID
		SIF CFLAG:C_ID:衰弱
			RETURN
		SIF BASE:C_ID:气力 <= 0
			RETURN
	ENDIF
	CFLAG:MASTER:约会中 = P_ID
	现在地MAPID = GET_MAPID(CFLAG:MASTER:当前位置)
	;C_IDと约会
	IF C_ID
		FLAG:约会的对象 = C_ID
		CFLAG:C_ID:约会中 = P_ID
		TFLAG:约会前好感度 = CFLAG:C_ID:好感度
		;人里约会専用处理
		IF P_ID == 2
			CALL EX_COSTUME(C_ID, "人里约会")
			SIF RESULT
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%换上了适合人间之里的衣服
		ENDIF
		CFLAG:MASTER:当前位置 = ROAD_TO(P_ID)
		CFLAG:C_ID:当前位置 = ROAD_TO(P_ID)
		;开始时は+1000する
		TFLAG:约会道中 = 所要时间 + 1000
	;一人でお出挂け
	ELSE
		CFLAG:MASTER:当前位置 = GET_ENTRANCE(P_ID)
		IF 时间停止ジャンプ
			BASE:MASTER:TSP -= 所要时间 * 3
		ELSE
			TIME += 所要时间
		ENDIF
	ENDIF
	CALL HappenToChild
	CALL PICK_RUNINTO(P_ID)
ENDIF

;----------------------------------
;行动抑止时间セット CFLAG:313SET
;现在的场所ににセットする
;ARG=呼び出し种类 ARG:1=セット对象
;----------------------------------
@SET_DERAY(ARG,ARG:1)
IF !CFLAG:(ARG:1):延迟
	;呼び出し形式により处理变更 1=场所による延迟 2以降=イベントによる延迟
	SELECTCASE ARG
		;特定の场所に入ったら延迟
		CASE 1
			SELECTCASE CFLAG:(ARG:1):当前位置
				;厨房は料理するので延迟发生
				CASE 11
					IF ABL:(ARG:1):料理技能 >= 3
						CFLAG:(ARG:1):延迟 = 30
					ELSEIF ABL:(ARG:1):料理技能 > 1 && ABL:(ARG:1):料理技能 < 3
						CFLAG:(ARG:1):延迟 = 60
					ELSE
						CFLAG:(ARG:1):延迟 = 120
					ENDIF
				;お澡堂に入ると延迟发生
				CASE 12
					CFLAG:(ARG:1):延迟 = 60
			ENDSELECT
		;朝起きたら延迟（朝は气分がまとまらないしネ）
		CASE 2
			CFLAG:(ARG:1):延迟 = 60
		;工作结束后は延迟
		CASE 3
			CFLAG:(ARG:1):延迟 = 20
		;添い寝やったら顽张って起きてくれる（衰弱时以外）
		CASE 4
			SIF !CFLAG:(ARG:1):衰弱
				CFLAG:(ARG:1):延迟 = 60
		;被逆推なだめたら延迟
		CASE 5
			CFLAG:(ARG:1):延迟 = 40
		;膝枕时は延迟
		CASE 6
			CFLAG:(ARG:1):延迟 = 10
		CASE 99
			;Deray Clear to Do nothing
	ENDSELECT
ELSE
	SELECTCASE ARG
		;延迟をクリア
		CASE 99
			CFLAG:(ARG:1):延迟 = 0
	ENDSELECT
ENDIF
