;-----------------------------------------------------------
;狩った鸟兽列表の表示
;-----------------------------------------------------------
@SHOW_COLLECTION_HUNTED
#DIM 选んだ兽
#DIM 全种数
#DIM 狩った种数
#DIM 狩った总数
#DIM 消去行数
#DIM 改行

VARSET LOCAL
全种数 = 0
狩った种数 = 0
狩った总数 = 0
消去行数 = 0
改行 = 0

CLEARLINE 1
PRINTFORML 至今为止狩猎到的鸟兽的列表
DRAWLINE
FOR LOCAL, 1, 61
	CALL CRITTER_DATA(LOCAL)
	;兽が无名でない
	IF CritterName != ""
		全种数 ++
		SIF LOCAL < 10
			PRINTFORM  
		IF CAPTURE_RECORD:LOCAL
			狩った种数 ++
			PRINTFORM [{LOCAL}] %CritterName, 20, LEFT%
			SETCOLOR C_L_GRAY
			PRINTFORM （狩猎头数：
			SELECTCASE CAPTURE_RECORD:LOCAL
				CASE IS < 10
					PRINTFORM     
				CASE IS < 100
					PRINTFORM    
				CASE IS < 1000
					PRINTFORM   
				CASE IS < 10000
					PRINTFORM  
			ENDSELECT
			狩った总数 += CAPTURE_RECORD:LOCAL
			IF STRCOUNT(CritterTribe, "鸟/")
				PRINTFORM {CAPTURE_RECORD:LOCAL}羽 ）
			ELSE
				PRINTFORM {CAPTURE_RECORD:LOCAL}匹 ）
			ENDIF
		ELSE
			PRINTFORM [{LOCAL}] ????????????                                
		ENDIF
		改行 ++
		IF 改行 == 2
			PRINTL 
			改行 = 0
		ELSE
			PRINT 　　　
		ENDIF
	ENDIF
	RESETCOLOR
	;兽关联の变量リセット
	CALL CRITTER_V_RESET
NEXT
PRINTL 
SETCOLOR 0x00FF00
PRINTFORM 狩猎鸟兽种类：{狩った种数}／{全种数}
IF 狩った总数
	PRINTFORML 　狩猎鸟兽总数：{狩った总数}头・羽
ELSE
	PRINTL 
ENDIF
RESETCOLOR
DRAWLINE
PRINT [0] - 返回

$LOOP
INPUT

选んだ兽 = RESULT
SELECTCASE 选んだ兽
	CASE 1 TO 61
		CALL CRITTER_DATA(选んだ兽)
		IF CritterName != ""
			CLEARLINE 1
			CALL PRINT_CRITTERINFO(选んだ兽)
			IF CAPTURE_RECORD:选んだ兽
				消去行数 = 5
			ELSE
				消去行数 = 2
			ENDIF
			CLEARLINE 消去行数
			GOTO LOOP
		ELSE
			CLEARLINE 1
			GOTO LOOP
		ENDIF
	CASE 0
		RETURN
		;兽关联の变量リセット
		CALL CRITTER_V_RESET
	CASEELSE
		CLEARLINE 1
		GOTO LOOP
ENDSELECT

;-----------------------------------------------------------
;选择した兽の说明／ヒント表记
;ARG …选んだ兽番号
;-----------------------------------------------------------
@PRINT_CRITTERINFO(ARG)

DRAWLINE
IF CAPTURE_RECORD:ARG
	FONTSTYLE 1
	PRINTPLAINFORM [{ARG}] %CritterName%
	FONTREGULAR
	IF STRCOUNT(CritterTribe, "鸟/")
		PRINTFORML （狩猎数：{CAPTURE_RECORD:ARG}羽 ）
	ELSE
		PRINTFORML （狩猎数：{CAPTURE_RECORD:ARG}头 ）
	ENDIF
	IF ABL:MASTER:教养 < 3
		CALL COLORMESSAGE(@"　　（提高教养Lv、便能得知分布域・猎期…）",C_L_GRAY,1)
	ELSE
		PRINTFORML 　　%CritterHint%
	ENDIF
	PRINTFORML 　　◇平均收获物： %ITEMNAME:CritterMeat% × {CritterMeat_Value} ◇
	PRINTFORML 　　%CritterIntro%
ELSE
	PRINTPLAINFORM [{ARG}] ?????????????
	PRINTFORML 　　%CritterHint%
ENDIF
WAIT

