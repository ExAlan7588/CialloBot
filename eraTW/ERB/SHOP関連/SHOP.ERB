;FileName_SHOP.ERB -------------------------------- Rev1.00
;起床前メニュー画面表示
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;----------------------------------------------------------
@SHOW_SHOP
;起床前画面表示中フラグ
TFLAG:100 = 0
;---------------------
;路人店员画像を一度だけ作成。スマートなやり方ではない。もっといい场所はないだろうか
;SIF !SPRITECREATED("颜绘_服_通常_MOB_0")
;	CALL 路人店员画像作成
;SPTALKで呼ばれたとき、画像がなければ作成にしました
;---------------------
;起床时间自动补正、最低睡眠时间は4时间
LOCAL:1 = CFLAG:MASTER:就寝时间 - DAY * 1440
IF (TIME:3 >= LOCAL:1 && (TIME:3 - LOCAL:1) < 240) || (TIME:3 < 240 && (1440 + TIME:3 - LOCAL:1) < 240)
	PRINTL 睡眠时间太短了。至少也要睡四个小时
	CALL 最低睡眠时间设定()
	PRINTFORMW 起床时间设定为（%时刻表示(TIME:3)%）了
ENDIF
;---------------------
DRAWLINE
PRINTFORM {DAY}日目(
CALL COLOR_YOUBI
PRINTFORML )　所持金:%金额表示(MONEY)%
PRINTL

CALL BASE_BAR(MASTER, "体力", 28, "绿")
PRINTFORM 　　
CALL BASE_BAR(MASTER, "气力", 28, "青", "「改行」")
CALL BASE_BAR(MASTER, "TSP", 28, "水色")
PRINTFORM 　　
SIF TALENT:MASTER:2 & 2
	CALL BASE_BAR(MASTER, "精力", 28, "黄")
PRINTFORML 

;PRINT 体力
;CALL PRINT_COLORBAR, BASE:MASTER:0,MAXBASE:MASTER:0,28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("赤"), RESULT:1
;PRINTFORML  ({BASE:MASTER:体力,5}/{MAXBASE:MASTER:体力,5})  
;PRINT 气力
;CALL PRINT_COLORBAR, BASE:MASTER:1,MAXBASE:MASTER:1,28, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("绿"), RESULT:1
;PRINTFORML  ({BASE:MASTER:1,5}/{MAXBASE:MASTER:1,5})  
PRINTFORML 
PRINTBUTTON "[自宅位置]", 120
PRINTFORM ：%GET_MAPNAME(MAIN_MAP)%
IF FLAG:气绝中断
	PRINTFORML （气绝中）	
ELSE
	PRINTFORML 
ENDIF
PRINTBUTTON "[起床时间]", 2400
PRINTFORML ：%时刻表示(TIME:3)%
;CALL INFO_CLOCK_IMAGE(TIME:3, 500, "left")
PRINTFORML 
CALL TW_TIPS
PRINTFORML 

SETCOLOR 103,228,126
PRINTL 【主菜单】
RESETCOLOR
PRINTLC [100] - 睁开眼睛
SIF FLAG:气绝中断
	SETCOLOR C_GRAY
PRINTLC [102] - 居住环境设定
RESETCOLOR
IF CFLAG:MASTER:当前位置 != OMANEKIBEYA()
	SIF FLAG:气绝中断
		SETCOLOR C_GRAY
	PRINTLC [120] - 搬家
	RESETCOLOR
ENDIF
PRINTL
PRINTL
IF EATDLC == 1
	PRINTL
	PRINTLC [667] - 宴会
	PRINTL
ENDIF
PRINTLC [101] - 能力表示
SIF FLAG:气绝中断
	SETCOLOR C_GRAY
PRINTLC [103] - 隙间商店
PRINTLC [104] - 所持道具一览
PRINTL
IF FLAG:700
	SIF FLAG:气绝中断
		SETCOLOR C_GRAY
	PRINTLC [105] - 收集
	RESETCOLOR
ENDIF
PRINTLC [106] - 入手的乐谱
PRINTLC [107] - 收到的礼物
PRINTL
PRINTLC [108] - 钓鱼列表
PRINTLC [109] - 狩猎鸟兽列表
PRINTLC [110] - 捕虫列表
PRINTFORML 
PRINTLC [111] - 固有料理列表
PRINTL 
PRINTL 
SIF FLAG:气绝中断
	SETCOLOR C_GRAY
PRINTLC [150] - 实绩解除
PRINTLC [151] - 农家设定
SIF CFLAG:MASTER:儿童人数
	PRINTLC [152] - 儿童一览
RESETCOLOR
PRINTL
PRINTL

SETCOLOR 103,228,126
PRINTL 【系统菜单】
RESETCOLOR
SIF FLAG:气绝中断
	SETCOLOR C_GRAY
PRINTLC [160] - 重置委托
RESETCOLOR
PRINTLC [161] - 重置服装
PRINTL
PRINTLC [400] - OPTION
PRINTLC [410] - 采集列表
PRINTLC [411] - 出现场所列表

PRINTL 
PRINTLC [420] - 单独指导
RESETCOLOR

PRINTL
PRINTL

PRINTLC [200] - SAVE
PRINTLC [300] - LOAD
SIF SAVESTR:本体バージョン != eraTW_Version
	SETCOLOR C_RED
PRINTLC [500] - UPDATE
;魔改内容
PRINTL
PRINTLC [501] - 单独口上UPDATE
;魔改完成
RESETCOLOR
PRINTL

TRYCALLFORM IS_DEBUGGERR
IF RESULT == 1
	SETCOLOR 241,158,194
	PRINTLC [666] - 幻想乡之主
	RESETCOLOR
	SIF !MONEY:2
		PRINTL
ENDIF

SIF MONEY:2
	PRINTFORML [600] - 卖出筹码
	
SETCOLOR 0xFFFCAA
DEBUGPRINTFORML [777] - 主动调用函数
RESETCOLOR 


;统计谁有口上
CALL createKojoShopButtonTargetList
;显示
CALL showKojoShopButton
PRINTL
SETCOLOR 252,160,18
PRINTLC [987] - 重置角色&换口上
RESETCOLOR
PRINTL

DRAWLINE

;FileName_SHOP.ERB -------------------------------- Rev1.00
;起床前メニューRESULT处理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;----------------------------------------------------------
@USERSHOP
;起床
CFLAG:1:205 = 5
EQUIP:1:5 = 5
IF RESULT == 100
	BEGIN TRAIN
	RETURN 1
;住环境设定
ELSEIF RESULT == 102 && !FLAG:气绝中断
	CALL SCHEDULE
;自宅引っ越し
ELSEIF RESULT == 120 && CFLAG:MASTER:当前位置 != OMANEKIBEYA()&& !FLAG:气绝中断
	CALL SET_MAINHOME
;能力表示
ELSEIF RESULT == 101
	DEBUGCLEAR
	CALL SHOW_CHARA_LIST("现有的角色列表", "体力气力", "「情报表示」「条件变更」")
;	CALL SHOW_CHARADATA
;通信贩卖
ELSEIF RESULT == 103 && !FLAG:气绝中断
	CALL ITEM_BUY
;所持道具一览
ELSEIF RESULT == 104
	CALL SHOW_ALLITEM
;收藏
ELSEIF RESULT == 105 && FLAG:700 && !FLAG:气绝中断
	CALL SHOW_COLLECTION
;手に入れた乐谱
ELSEIF RESULT == 106
	CALL SHOW_COLLECTION_SCORE
;获得的礼物
ELSEIF RESULT == 107
	CALL SHOW_COLLECTION_GIFT
;钓った鱼列表
ELSEIF RESULT == 108
	CALL SHOW_COLLECTION_FISH
;狩った鸟兽列表
ELSEIF RESULT == 109
	CALL SHOW_COLLECTION_HUNTED
;捕った虫列表
ELSEIF RESULT == 110
	CALL SHOW_COLLECTION_MUSHI
ELSEIF RESULT == 111
	CALL SHOW_SP_DISH_LIST
;实绩解除
ELSEIF RESULT == 150 && !FLAG:气绝中断
	CALL BONUS
;农家设定
ELSEIF RESULT == 151 && !FLAG:气绝中断
	CALL RESTART_NOUMIN
;儿童一览
ELSEIF RESULT == 152 && CFLAG:MASTER:儿童人数
	CALL SHOW_ALLCHILDLIST
;依赖リセット
ELSEIF RESULT == 160 && !FLAG:气绝中断
	CALL IRAI_RESET
;复原衣服
ELSEIF RESULT == 161
	CALL RESET_ALL_CLOTHES
;OPTION
ELSEIF RESULT == 400
	CALL OPTION
;采集列表
ELSEIF RESULT == 410
	CALL SHOW_GATHERING_LIST
;出现场所列表
ELSEIF RESULT == 411
	CALL SHOW_APPEAR_LIST
;UPDATE
ELSEIF RESULT == 500
	CALL UPDATE
;魔改内容-口上单独UPDATE
ELSEIF RESULT == 501
	CALL UPDATE_AND_SELECTION_OF_KOJO_MODDING
;魔改完成
;单独指导
ELSEIF RESULT == 420
	CALL TUTORIAL_MAIN
;SAVE
ELSEIF RESULT == 200
	CALL Real_Time_Save(1001)
;LOAD
ELSEIF RESULT == 300
	CALL Real_Time_Save(1002)
;筹码卖出
ELSEIF RESULT == 600
	CALL CM_BALANCE

ELSEIF RESULT == 667
	CALL MY_ENKAI_SETTING
ELSEIF RESULT == 666
	TRYCALLFORM IS_DEBUGGERR
	IF RESULT == 1
		CALL DEBUG_MODE
	ENDIF

;DEBUG调用函数
ELSEIF RESULT == 777
	CALL DEBUG_CALL_FUNCTION

;换指定角色口上跟重置角色
ELSEIF RESULT == 987
	CALL ALTER_RESET_CHARA_MAIN()

;起床时间设定
ELSEIF RESULT == 2400
	CALL 起床时间设定()
ELSEIF RESULT == 797
	CALL choiceButtonTarget()
ELSEIF RESULT == 798
	IF ShopButtonTargetId > 0
		ShopButtonTargetId --
		ShopButtonTarget = ShopButtonTargetList:ShopButtonTargetId
	ENDIF
ELSEIF RESULT == 799
	IF ShopButtonTargetId + 1 < ShopButtonTargetNum
		ShopButtonTargetId ++
		ShopButtonTarget = ShopButtonTargetList:ShopButtonTargetId
	ENDIF
ELSEIF RESULT >= 800 && RESULT <= 809
	CALL useKojoShopButton(RESULT - 800)
ENDIF


@TW_TIPS
SETCOLOR C_YELLOW
PRINTPLAIN 温馨提示:
PRINTDATA
	DATAFORM 某些指令只能在特定场所使用。
	DATAFORM 点击名字旁边的【★】、就会使用【追踪】指令、向该角色所在的场所移动。
	DATAFORM 根据所在地图的不同、可能本地区不存在家庭菜园或者战斗训练的场所。如果需要的话可以考虑搬家。
	DATAFORM 时间停止能力的TSP是越用越长的哦。
	DATAFORM 失物去向可以去请教纳兹琳哦、她很擅长的(＾＿＾)。
	DATAFORM 酒不但系统商城有、酒屋也能买到哦。
	DATAFORM 神签和占卜的一些结果会干涉现实呢。如果抽中的话・红魔CASINO・采集之类都在影响范围内。
	DATAFORM 下雪对庄稼的生长不利。
	DATAFORM 筹码是红魔馆红魔CASINO的硬通货。
	DATAFORM 大部分情况下、用筹码会比现金付款便宜。
	DATAFORM 大家都会有各自擅长的事情、多发现哦。
	DATAFORM 战斗能力和教养越低、好感度就越难提升。
	DATAFORM 大部分的祈求、神明总是能实现的。
	DATAFORM 可以在人里广场猎艳的哦。
	DATAFORM 随机人物只有在第一天才能够上涨能力。
	DATAFORM 尽管选择了忘却了随机人物的话就不会再次登场、但也只有忘却的场合才能进行进行妊娠判定。
	DATAFORM 随机角色再次登场的情况下、会失去能力上升补正、而且不会怀孕。
	DATAFORM 不能同时出现两个以上的随机人物。
	DATAFORM 随机角色的情报会以dat文件的形式进行保存。
	DATAFORM 随机角色只会在第一次见到的那个地图出现。
	DATAFORM 禁欲可是很苦的哦、还是应该诚实面对自己的身体、但求心之所安。
	DATAFORM 有些人的生活节奏昼夜颠倒呢、想追就要委屈下自己了、把起床时间调整一下呗。
	DATAFORM 指示不显示了？可能是你把它过滤了吧。
	DATAFORM 无志气的话很多种类的命令都无法使用、不过更容易被推。
	DATAFORM 角色的个人工作都是需要专业素质的、素质不够乱帮忙的话容易失败哦。
	DATAFORM 做料理必须准备食材哟。
	DATAFORM 把水倒进酒虫的瓶子里就能收获酒了。
	DATAFORM 跳蛋是可以在家庭菜园种出来的哟。
	DATAFORM 污臭耐性越高越难扫除。
	DATAFORM 在红魔CASINO有很多小游戏可以玩、玩多了会员等级还会上升。
	DATAFORM 时奸刻印与同等级的屈服刻印、具有相同的快乐效果。让傲娇们口嫌体正直起来也是一种有效的手段哦。
	DATAFORM 随着日期变换、收藏隙间不但会补充、还会增加持有数上限、所以请使劲的用吧。
	DATAFORM 有的地方需要你与地主关系亲密才能进入呢。
	DATAFORM 学习与训练是苦闷的、但是如果能跟其他人一起做就很有效率了。
	DATAFORM 挑逗模式能抑制绝顶、等待一些钝感的部位一起进入高潮、是专为多重绝顶而设的重要功能
	DATAFORM 到铃奈庵借书能得到书所对应的技能经验。
	DATAFORM 使用奇迹之石能提高角色欲望、甚至会当街把你推倒、但是亲密和信赖也就很难上升了。
	DATAFORM 游戏一年可以用起床选项的（败者食尘）开始你的二周目、会继承一周目的一部分能力。
	DATAFORM 多重绝顶因此而上升的额外好感度、会在射精时追加入BOUNS里一同计算。
	DATAFORM 禁欲3日就能得到素质【浓厚精液】来使精液得到强化、射精之后第二天【浓厚精液】会消失。
	DATAFORM 禁欲太久的话会梦遗哦。
	DATAFORM 料理得到的评价在80点以上就会使人心情舒畅、反之35点以下会让人反胃。
	DATAFORM 付丧神的情欲极难上升、因此妊娠率及其低下、但是会随着玩家的道具经验上升而慢慢恢复正常生育能力。
	DATAFORM 信赖度太低的话好感度会进入瓶颈。而在约会中这种制约会消失。
	DATAFORM 育儿中时欲求不满度上升了就很麻烦。那么孩子睡觉或者上学的时候…
	DATAFORM 就算没有拥有调合套装、也有若干可以进行调合的场所。
	DATAFORM 调合成功率除了调合Lv之外还受到教养的影响。
	DATAFORM 有些素材不在特定季节就无法采集到。
	DATAFORM 采集到高级素材的话就可以提升采集等级。
	DATAFORM 请不要在正在工作的人的面前演奏。
	DATAFORM 宴会中进行演奏的话会有一些特别的收获、但如果水平不够的话反而会受到冷淡的对待。
	DATAFORM 进行缘结的祈愿的话、第二天被祈愿的角色就会来访。
	DATAFORM 工作预定之类的可以在能力画面或者日历里确认。
	DATAFORM 在有些角色的能力画面上、可设定使用另一系画像。
	DATAFORM 点击目标角色可以直接切换立绘与颜绘。
	DATAFORM 能从部分角色手中买到素材
	DATAFORM 【妖精】喜欢「自然」的礼物。
	DATAFORM 【幼稚】喜欢「漫画角色」的礼物。
	DATAFORM 「符卡」的礼物、除了没有战斗能力的孩子以外都很喜欢。
	DATAFORM 【妖怪】【幽灵】一般是不喜欢「巫术」或「法具」的礼物的。
	DATAFORM 「粗恶」的礼物没有人会喜欢吧。
	DATAFORM 除了妖怪以外、大多人都不喜欢「妖术」「骨」「血」「毒」的礼物。
	DATAFORM 在假期路边摆摊卖卖东西・更容易盈利。
	DATAFORM 蔬菜在地底能卖出高价。
	DATAFORM 鱼在山顶能卖出高价。
	DATAFORM 【丰盛】的料理饱腹度高，【简易】的料理饱腹度低。
	DATAFORM 时间停止解除时根据高潮次数会有不同表现哦
	DATAFORM 慧音老师在寺子屋上课时如果欲求不满的话……
	DATAFORM 阎王地狱审判时如果欲求不满的话……
	DATAFORM 当Ta犯困时就和心爱的Ta依偎在一起吧……陪Ta睡一会
	DATAFORM 仕事·宴会时关注一下喜欢你的对方哦
	DATAFORM 剧毒身躯的媚毒浸润效果大大增强了哦
	DATAFORM 带着Ta一起去钓鱼吧
	DATAFORM 孕期短了很多哦……这是异变！不过现已结束，但也许可以再次引发……？
	DATAFORM 孩子成长的飞快……这是异变！不过现已结束，但也许可以再次引发……？
	DATAFORM 母乳体质获取似乎简单了很多……如果想拥有的话、不妨在哺乳期多培养一下
	DATAFORM 理性之壁的冲突更频繁了呢
	DATAFORM 每日结算显示的项目似乎更多了呢
	DATAFORM 时停能力似乎比以前更强了呢、要合理利用哦
	DATAFORM 忍着不色色、积蓄太久似乎也不是什么坏事呢
	DATAFORM 看到其他角色深夜发电时就会心一笑吧
	DATAFORM 路人初见时似乎在意的事情更多了呢
	DATAFORM 发光的牛子！能发光的牛子真的很特别
	DATAFORM 似乎有人喜欢让少女们漏尿……漏多了的话……
	DATAFORM 清纯的交往很难达成呢、请不要急于求成、明显不那么清纯的行为千万不要做哦
	DATAFORM 博丽神社的签升级了呢……今天是否幸运呢？
	DATAFORM 似乎学会催眠了呢……要用它做坏事么？还是简单恶作剧一下适可而止呢？
	DATAFORM 听说每一个少女灵魂深处都有能觉醒的部分呢……觉醒后的少女听说无论是行为还是态度都会变化呢
	DATAFORM 记录对方情报似乎更详细了呢……这些情报都是哪来的呢？你竟然随时能看到包括自己在内的各种情报呢
	DATAFORM 观察对方的状态时能看到的东西似乎更多了呢……各种情报的信息涌入脑海
	DATAFORM 奇迹之石不知怎么回事，现在有了新用处呢。试着找找吧
	DATAFORM 少女的喘息好色……不知为何更色了
	DATAFORM 极端天气出门要小心哦
	DATAFORM 可以多和少女们贴贴哦
	DATAFORM 如果欲求不满较低，则表示满足。没有必要一直做到体力耗尽。
;DATAFORM 我整合的版本都会发贴吧的哦，有BUG反馈或者想要新功能就在那里找我吧。喂，打破第四面墙了！
ENDDATA
PRINTFORML 

;虫列表を参考に
@SHOW_ALLCHILDLIST
#DIM list_chara_has_child, 人物数量上限
#DIM chara_has_child_num
#DIM 对象
#DIM printing_page
#DIM printing_chara
;キャラ数が增えた场合に变更する
#DIM CONST NEXT_PAGE = 999
#DIM CONST PREV_PAGE = 998
#DIM CONST MAX_LINE = 20
#DIM leave_line_count_last

;消さずに残す最后の行
;このあと CLEARLINE LINECOUNT - leave_line_count_last でこの时记录した行よりも后の行を全てCLEARする
leave_line_count_last = LINECOUNT

;儿童がいるキャラのリストを作成する
VARSET list_chara_has_child
VARSET chara_has_child_num
FOR 对象, 1, CHARANUM
	SIF !CFLAG:对象:儿童人数
		CONTINUE
	list_chara_has_child:chara_has_child_num = 对象
	chara_has_child_num ++
NEXT
printing_page = 1

WHILE 1
	CLEARLINE LINECOUNT - leave_line_count_last
	DRAWLINE
	PRINTFORML ◆儿童列表 PAGE.{printing_page} 总计{CFLAG:MASTER:儿童人数}人
	STRLENFORM {PREV_PAGE}
	PRINTFORML %" " * (RESULT + 5)%母亲角色%" " * 4%儿童的人数・性别
	DRAWLINE

	FOR printing_chara, (printing_page - 1)*MAX_LINE, MAX_LINE + (printing_page - 1)*MAX_LINE
		IF printing_chara >= chara_has_child_num
			PRINTL
			CONTINUE
		ENDIF
		PRINTFORM [{list_chara_has_child:printing_chara, 3}] %NAME_OUTPUT_TRANSLATION("CALLNAME", (list_chara_has_child:printing_chara)), 16, LEFT%
		CALL SHOW_CHILDDATA(list_chara_has_child:printing_chara, "ICON")
	NEXT
	DRAWLINE
	PRINTFORML [{PREV_PAGE}] 向前翻页
	PRINTFORML [{NEXT_PAGE}] 向后翻页
	PRINTL [  0] 返回

	WHILE 1
		INPUT
		;ページが1枚しかない场合はページ移动を要求されてもなにも处理しない
		;儿童が居ないとこの函数は呼ばれないのでchara_has_child_numは常に正であることに注意すると
		;(chara_has_child_num - 1) / MAX_LINE + 1 は最后のページを表す
		IF (chara_has_child_num - 1) / MAX_LINE + 1 == 1 && (RESULT == NEXT_PAGE || RESULT == PREV_PAGE)
			CLEARLINE 1
			CONTINUE
		ENDIF
		BREAK
	WEND

	SELECTCASE RESULT
		CASE 0
			;クリアするべき？
			;CLEARLINE LINECOUNT - leave_line_count_last
			RETURN 0
		;ページ移动は循环するようにする 例えば1ページ目で前のページを要求されたら最后のページを表示する
		CASE PREV_PAGE
			SELECTCASE printing_page
				CASE 1
					printing_page = (chara_has_child_num - 1) / MAX_LINE + 1
				CASEELSE
					printing_page --
			ENDSELECT
		CASE NEXT_PAGE
			SELECTCASE printing_page
				CASE (chara_has_child_num - 1) / MAX_LINE + 1
					printing_page = 1
				CASEELSE
					printing_page ++
			ENDSELECT
		CASEELSE
			SIF INRANGE(RESULT, 1, CHARANUM) && CFLAG:RESULT:儿童人数
				CALL SHOW_ALLCHILDLIST_CHARA(RESULT)
	ENDSELECT
WEND

@SHOW_ALLCHILDLIST_CHARA(chara_id)
#DIM chara_id
#DIM leave_line_count_last
leave_line_count_last = LINECOUNT

PRINTFORML %"━" * 50%
PRINTFORML 　%NAME_OUTPUT_TRANSLATION("CALLNAME", chara_id)%的儿童一览
PRINTFORML %"━" * 50%
CALL SHOW_CHILDDATA(chara_id)
PRINTW
CLEARLINE LINECOUNT - leave_line_count_last
RETURN 0

;虫列表を参考に
@SHOW_SP_DISH_LIST
#DIM C_ID
#DIM START,10
#DIM 页
#DIM 最察看后一页
#DIM P调整
#DIM L调整
#DIM LASTNUM
#DIM CONST MAXLINE = 20
;各页の头番を设定する
IF !START
	P调整 = 0
	L调整 = 0
	LASTNUM = 0
	FOR LOCAL, 1, CHARANUM
		RESULT = 0
		;addition custom code, for custom charadata special menu
		CALL KOJO_CHECK(LOCAL, "CHARADATA" , "SPECIAL_MENU")
		IF RESULT == -1 ;等于-1时,表明口上侧需要是结果为0
			RESULT = 0
			TRYCALLFORM SPECIAL_MENU_{NO:LOCAL}
		ELSEIF RESULT ;等于其他正值时，表明口上侧有自己的值
		ELSE 
		;addition custom code, duplicated characters
		TRYCALLFORM SPECIAL_MENU_{NO:LOCAL}
		ENDIF
		SIF !RESULT
			CONTINUE
		L调整 ++
		IF L调整 > MAXLINE
			P调整 ++
			START:P调整 = LOCAL
			L调整 = 1
		ENDIF
		LASTNUM = LOCAL
	NEXT
	START = 1
	页 = 1
	;最察看后一页と最察看后一页の头番を设定
	最察看后一页 = P调整 + 1
	START:最察看后一页 = LASTNUM + 1
ENDIF

VARSET LOCAL
PRINTFORML ◆固有料理列表 PAGE.{页}
DRAWLINE
FOR LOCAL, START, START:页
	RESULT = 0
	;addition custom code, for custom charadata special menu
	CALL KOJO_CHECK(LOCAL, "CHARADATA" , "SPECIAL_MENU")
	IF RESULT == -1 ;等于-1时,表明口上侧需要是结果为0
		RESULT = 0
		TRYCALLFORM SPECIAL_MENU_{NO:LOCAL}
	ELSEIF RESULT ;等于其他正值时，表明口上侧有自己的值
	ELSE 
	;addition custom code, duplicated characters
	TRYCALLFORM SPECIAL_MENU_{NO:LOCAL}
	ENDIF
	SIF !RESULT
		CONTINUE
	SIF LEARNED_MENU:LOCAL > 100
		SETCOLOR C_YELLOW
	PRINTFORM  [{LOCAL, 3}] %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL), 16, LEFT%
	PRINTFORML %DISH_NAME% （%RESULTS%）
	LOCAL:1 ++
	RESETCOLOR
NEXT
WHILE LOCAL:1 < MAXLINE
	PRINTL 
	LOCAL:1 ++
WEND
DRAWLINE
PRINTL [999] 察看下一页
PRINTL [998] 察看前一页
PRINTL [  0] 返回
INPUT
;返回
IF !RESULT
	VARSET START
	RETURN
;察看下一页
ELSEIF RESULT == 999
	页 ++
	IF 页 > 最察看后一页
		页 = 1
		START = 1
	ELSE
		START = START:(页 - 1)
	ENDIF
;察看前一页
ELSEIF RESULT == 998
	页 --
	IF 页 < 1
		页 = 最察看后一页
		START = START:(最察看后一页 - 1)
	ELSEIF 页 == 1
		START = 1
	ELSE
		START = START:(页 - 1)
	ENDIF
ELSEIF INRANGE(RESULT, 1, CHARANUM)
	C_ID = RESULT
	RESULT = 0
	;addition custom code, for custom charadata special menu
	CALL KOJO_CHECK(C_ID, "CHARADATA" , "SPECIAL_MENU")
	IF RESULT == -1 ;等于-1时,表明口上侧需要是结果为0
		RESULT = 0
		TRYCALLFORM SPECIAL_MENU_{NO:C_ID}
	ELSEIF RESULT ;等于其他正值时，表明口上侧有自己的值
	ELSE 
	;addition custom code, duplicated characters
	TRYCALLFORM SPECIAL_MENU_{NO:C_ID}
	ENDIF
	IF RESULT
		PRINTFORML 【%DISH_NAME%】%DISH_COMMENT%
		CALL SHOW_TASTE(DISH_TASTE, "DISH_DATA")
		PRINTFORMW 
		CLEARLINE 3
	ENDIF
ENDIF
CALL RESET_DISH
CLEARLINE (MAXLINE + 7)
RESTART



;Moe：没搓完，太麻烦了，等后人来搓
;目前只支持单参数函数
@DEBUG_CALL_FUNCTION(ARGS) 
#LOCALSSIZE 3
#DIM NUM_OF_BRACKETS , 2 
#DIM ARG_TYPE,  20
#DIM FUNCTION_ARG, 20 
#DIMS FUNCTION_ARGS, 20 
#DIM DYNAMIC _LINECOUNT
;LOCALS:0是原结果
;LOCALS:1是切出的函数名
;LOCALS:2是切出的全部函数参数
;LOCALS:3之后是函数的各个字符串参数
;ARG_TYPE是每个参数的变量类型，1为数字，2为字符串
;预期是想允许多参数，但是era的语法限制了发挥……
_LINECOUNT = LINECOUNT
WHILE 1
	PRINTDL 这里可以主动调用游戏内存在的函数
	PRINTDL 仅适用于功能开发和口上制作等用途
	PRINTFORMDL 请在下面输入你想要调用的函数
	PRINTL 
	CALL COLORMESSAGE(@"请使用全角括号代替原来的半角括号，无需加@",0xFFFCAA,1)
	CALL COLORMESSAGE(@"由于era语言限制，仅支持单参数函数调用",0xFFFCAA,1)
	PRINTL 
	INPUTS 
	VARSET LOCAL
	VARSET LOCALS 
	VARSET ARG_TYPE
	VARSET NUM_OF_BRACKETS
	; 行头行尾の空白文字削除
	REPLACE RESULTS, "\(\^\\s\+\|\\s\+\$)", ""
	;暂存结果
	LOCALS:0 = %RESULTS%
	SIF LOCALS:0 == "_BACK"
		BREAK 
	;查找调用函数有无括号
	STRFINDU RESULTS , "（"
	IF RESULT != -1 
		;记录第一个括号的位置
		NUM_OF_BRACKETS:0 = RESULT:0 
		;记录第二个括号的位置
		STRFINDU RESULTS , "）"
		NUM_OF_BRACKETS:1 = RESULT:0 
		;切出函数定义名
		SUBSTRING LOCALS:0 , 0 , NUM_OF_BRACKETS:0
		LOCALS:1 = %RESULTS%
		;去除括号
		;NUM_OF_BRACKETS:0 ++ 
		;NUM_OF_BRACKETS:1 -- 
		;切除函数传入参数
		SUBSTRING LOCALS:0 , NUM_OF_BRACKETS:0 + 1 , NUM_OF_BRACKETS:1 - NUM_OF_BRACKETS:0 - 1
		;去除残留括号
		;REPLACE RESULTS , "）" , ""
		LOCALS:2 = %RESULTS%
		;检测多参数
		VARSET RESULT
		VARSET RESULTS
		STRFINDU LOCALS:2 , ","
		IF RESULT == -1 
			;此时为单参数函数
			;检测是否含有非数字字符
			IF ISNUMERIC(LOCALS:2)
				;不含非数字字符，直接转为数值变量
				;计入数字参数组
				FUNCTION_ARG:0 = TOINT(LOCALS:2) 
				ARG_TYPE:0 = 1 
			ELSE 
				;含有非数字字符
				;计入字符参数组
				FUNCTION_ARGS:0 = LOCALS:2
				ARG_TYPE:0 = 2  
			ENDIF 
		ELSEIF RESULT >= 0
			SETCOLOR C_RED
			PRINTFORMW 多参数函数无法调用
			RESETCOLOR
			CLEARLINE LINECOUNT - _LINECOUNT
			RESTART
		ENDIF
		IF ARG_TYPE:0 == 1
			TRYCCALLFORM %LOCALS:1%(FUNCTION_ARG:0)
				SETCOLOR 0xFFFCAA
				PRINTL 
				PRINTW （函数已经被调用）
				RESULT = 0
			CATCH 
				PRINTL 
				PRINTFORMDW 您所输入的函数并不存在，或者输入格式有误
				RESULT = 1
				PRINTL 
			ENDCATCH
		ELSEIF ARG_TYPE:0 == 2
			TRYCCALLFORM %LOCALS:1%(FUNCTION_ARGS:0)
				SETCOLOR 0xFFFCAA
				PRINTL 
				PRINTW （函数已经被调用）
				RESULT = 0
			CATCH 
				PRINTL 
				PRINTFORMDW 您所输入的函数并不存在，或者输入格式有误
				RESULT = 1
				PRINTL 
			ENDCATCH
		ENDIF 
	ELSE 
		;无括号的情况
		TRYCCALLFORM %LOCALS%
			SETCOLOR 0xFFFCAA
			PRINTL 
			PRINTW （函数已经被调用）
			RESULT = 0
		CATCH 
			PRINTL 
			PRINTFORMDW 您所输入的函数并不存在
			RESULT = 1
			PRINTL 
		ENDCATCH
	ENDIF
;复原字体与颜色
RESETCOLOR
SETFONT
CLEARLINE LINECOUNT - _LINECOUNT
WEND
