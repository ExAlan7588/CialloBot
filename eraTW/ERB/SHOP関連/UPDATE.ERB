@UPDATE
#DIM C_ID
#DIM 更新能力番号
#DIM 最低能力值
#DIM 最低经验数
#DIM 育儿中の儿童
#DIM 儿童设定初回
#DIM 口上实装チェック, 人物数量上限
#DIM リセット画面
#DIM 技能素質更新用
#DIMS rname

PRINTL 旧版本不存在的人物・初期素質・FLAG等可能会产生混乱。

VARSET 口上实装チェック
リセット画面 = 0

;まずは路人を削除
DELCHARA CHARANUM - 1

FOR C_ID,1,CHARANUM ;CSVに名前を参照してNOを书き直す
	VARSET LOCAL
	FOR LOCAL,1,人物数量上限
		SIF !EXISTCSV(LOCAL)
			CONTINUE
		IF NAME:C_ID == CSVNAME(LOCAL)
			NO:C_ID = LOCAL
			BREAK
		ENDIF
	NEXT
NEXT

FOR C_ID,1,人物数量上限 ;终わったら新角色追加
	IF GETCHARA(C_ID) == -1 && EXISTCSV(C_ID)
		ADDCHARA C_ID
		TARGET = C_ID
		RESULT = 0
	ENDIF
	IF TALENT:C_ID:路人
		RANDOM_CHARANUM = C_ID
		BREAK
	ENDIF
NEXT

FOR C_ID, 1, CHARANUM
	IF NO:C_ID != C_ID && NO:C_ID != 0 ;もしずれたら并べなおす
		IF NO:C_ID > C_ID ;CSVNOが今のNOより后ろなら、SWAPして续く
			SWAPCHARA C_ID,NO:C_ID
			FOR LOCAL,0,CHARANUM
				SWAP RELATION:LOCAL:C_ID,RELATION:LOCAL:(NO:C_ID)
			NEXT
		ELSE
			SWAPCHARA C_ID,NO:C_ID ;そうじゃなかったらSWAPして一から并べなおす
			FOR LOCAL,0,CHARANUM
				SWAP RELATION:LOCAL:C_ID,RELATION:LOCAL:(NO:C_ID)
			NEXT
			C_ID = 1
		ENDIF
	ENDIF
	IF TALENT:C_ID:路人
		RANDOM_CHARANUM = C_ID
		BREAK
	ENDIF
NEXT
SIF !CM_LIMIT
	CM_LIMIT = 100
YOGORE:348 = 0
YOGORE:949 = 0
FOR C_ID, 1, CHARANUM
	SIF TALENT:C_ID:路人
		CONTINUE
	;CSTR:工作情报をCSTR:3に移行する手续き
	IF CSTR:C_ID:3 == "" && CSVCSTR(C_ID, 3) != ""
		CSTR:C_ID:3 = %CSVCSTR(C_ID, 3)%
		CSTR:C_ID:1 = 
	ENDIF
	TALENT:C_ID:186 = 0
	IF TALENT:C_ID:恋慕
		TALENT:C_ID:思慕 = 0
		TALENT:C_ID:炮友 = 0
		TALENT:C_ID:爱欲 = 0
	ENDIF
	CALL 履历文字列变换(C_ID)
	IF TALENT:C_ID:育儿中
		FOR LOCAL:1, 1, 11
			SIF (CHILD_BIRTHDAY:C_ID:(LOCAL:1) > CHILD_BIRTHDAY:C_ID:(LOCAL:1 - 1)) || (LOCAL:1 - 1) == 0
				育儿中の儿童 = LOCAL:1
		NEXT
		TALENT:C_ID:育儿中 = 育儿中の儿童
	ENDIF
	SIF !TALENT:C_ID:妊娠
		CFLAG:C_ID:妊娠自觉状态 = 0
	SIF !CFLAG:C_ID:依赖状况
		CFLAG:C_ID:依赖内容 = 0
;	;固有料理フラグ
;	SIF !LEARNED_MENU:C_ID
;		LEARNED_MENU:C_ID = CFLAG:C_ID:固有料理
	
	FOR LOCAL:1, 0, 3
		SIF !EFFECTIVE_PATTERN_V:C_ID:(LOCAL:1)
			EFFECTIVE_PATTERN_V:C_ID:(LOCAL:1) = 1 + RAND:3
		SIF !EFFECTIVE_PATTERN_A:C_ID:(LOCAL:1)
			EFFECTIVE_PATTERN_A:C_ID:(LOCAL:1) = 1 + RAND:3
	NEXT

	;种族系素質を更新
	FOR LOCAL:1, 190, 198
		TALENT:C_ID:(LOCAL:1) = CSVTALENT(C_ID, LOCAL:1)
	NEXT
	;水栖、冰精、种族(未使用？)、半白泽、动物耳も变动しないと思われるので更新
	FOR LOCAL:1, 163, 167
		TALENT:C_ID:(LOCAL:1) = CSVTALENT(C_ID, LOCAL:1)
	NEXT
	
	;技能系素質,移行完了
;	技能素質更新用 = MAX(ABL:C_ID:钓鱼技能, CSVTALENT(C_ID, 47))
	TALENT:C_ID:钓鱼Lv = MAX(TALENT:C_ID:钓鱼Lv, CSVTALENT(C_ID, 47))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "钓鱼经验"), TALENT:C_ID:钓鱼Lv)
	
;	技能素質更新用 = MAX(ABL:C_ID:采集技能, CSVTALENT(C_ID, 48))
	TALENT:C_ID:采集Lv = MAX(TALENT:C_ID:采集Lv, CSVTALENT(C_ID, 48))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "采集经验"), TALENT:C_ID:采集Lv)
	
;	技能素質更新用 = MAX(ABL:C_ID:调合技能, CSVTALENT(C_ID, 49))
	TALENT:C_ID:调合Lv = MAX(TALENT:C_ID:调合Lv, CSVTALENT(C_ID, 49))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "调合经验"), TALENT:C_ID:调合Lv)

;	技能素質更新用 = CSVTALENT(C_ID, 46)
	TALENT:C_ID:采伐Lv = MAX(TALENT:C_ID:采伐Lv, CSVTALENT(C_ID, 46))
	CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "采伐经验"), TALENT:C_ID:采伐Lv)

	TALENT:C_ID:痛觉 = CSVTALENT(C_ID, GETNUM(TALENT, "痛觉"))
	TALENT:C_ID:动物耳 = CSVTALENT(C_ID, GETNUM(TALENT, "动物耳"))
	SIF TALENT:C_ID:恢复速度 < CSVTALENT(C_ID, GETNUM(TALENT, "恢复速度")) && !TALENT:C_ID:妊娠 && CFLAG:C_ID:分娩经过日 > 20
		TALENT:C_ID:恢复速度 = CSVTALENT(C_ID, GETNUM(TALENT, "恢复速度"))
	;多分大丈夫だと思うが、后天的な取得处理を见込んで判定
	SIF TALENT:C_ID:扫除系 == 0
		TALENT:C_ID:扫除系 = CSVTALENT(C_ID, GETNUM(TALENT, "扫除系"))
	SIF TALENT:C_ID:音感 == 0
		TALENT:C_ID:音感 = CSVTALENT(C_ID, GETNUM(TALENT, "音感"))
	SIF TALENT:C_ID:音乐知识 == 0
		TALENT:C_ID:音乐知识 = CSVTALENT(C_ID, GETNUM(TALENT, "音乐知识"))
	SIF GETBIT(TALENT:C_ID:性别, 1) && !TALENT:C_ID:形状
		CALL TINKO_DECIDE(C_ID)

	;地位の读み込み
	SIF CFLAG:C_ID:310 != CSVCFLAG(C_ID, 310)
		CFLAG:C_ID:310 = CSVCFLAG(C_ID, 310)
	;活动时间を读み込み
	FOR LOCAL:1, 352, 356
		SIF CFLAG:C_ID:(LOCAL:1) != CSVCFLAG(C_ID, LOCAL:1)
			CFLAG:C_ID:(LOCAL:1) = CSVCFLAG(C_ID, LOCAL:1)
		CONTINUE
	NEXT

	;来访概率补正を读み込み
	FOR LOCAL:1, 420, 432
		CFLAG:C_ID:(LOCAL:1) = CSVCFLAG(C_ID, LOCAL:1)
	NEXT
	;来访位置を读み込み
	FOR LOCAL:1, 450, 462
		CFLAG:C_ID:(LOCAL:1) = CSVCFLAG(C_ID, LOCAL:1)
	NEXT
	FOR LOCAL:1, 1, CHARANUM
		RELATION:C_ID:(LOCAL:1) = CSVRELATION(C_ID, LOCAL:1)
	NEXT
	;替换用画像の有无を确认
	;CALL Update_ImageSets
	IF CFLAG:C_ID:别颜有 < CSVCFLAG(C_ID, 562) || CFLAG:C_ID:别立ち有 < CSVCFLAG(C_ID, 563)
		PRINTFORML 确认到%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的新替换画像。
		IF CFLAG:C_ID:别颜有 < CSVCFLAG(C_ID, 562)
			CALL 画像セット(@"颜绘_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "默认颜绘")
			FOR LOCAL:1, 1, CSVCFLAG(C_ID, 562)+1
				CALL 画像セット(@"\@LOCAL:1 >= 2?别颜{LOCAL:1}#别颜\@_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "差し替え颜绘")
			NEXT
			CALL 画像一斉表示("左", 0, 1)
		ENDIF
		IF CFLAG:C_ID:别立ち有 < CSVCFLAG(C_ID, 563)
			CALL 画像セット(@"立绘_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "默认立绘")
			FOR LOCAL:1, 1, CSVCFLAG(C_ID, 563)+1
				VARSET RESULT
				CALL 画像セット(@"\@LOCAL:1 >= 2?别立ち{LOCAL:1}#别立ち\@_服_通常_{C_ID}", 0, 0, 100, 0, 0, "", "差し替え立绘")
					SIF RESULT == 0
						CALL 画像セット("im_av_chara", 0, 0, 100, 0, 0, "", "差し替え立绘")
			NEXT
			CALL 画像一斉表示("左", 0, 1)
		ENDIF
		PRINT [0] 不使用替换画像
		FOR LOCAL:1, 1, MAX(CSVCFLAG(C_ID, 562), CSVCFLAG(C_ID, 563)) + 1
			PRINTFORM [{LOCAL:1}] 使用{LOCAL:1}号替换画像
		NEXT
		INPUT
		CFLAG:C_ID:应用差分替换 = RESULT
	ENDIF
	CFLAG:C_ID:别颜有 = CSVCFLAG(C_ID, 562)
	CFLAG:C_ID:别立ち有 = CSVCFLAG(C_ID, 563)
	;家主flag
	CFLAG:C_ID:房东候补 = CSVCFLAG (C_ID, GETNUM(CFLAG, "房东候补"))
	CFLAG:C_ID:居住必要信赖度 = CSVCFLAG (C_ID, GETNUM(CFLAG, "居住必要信赖度"))
	CFLAG:C_ID:房租 = CSVCFLAG (C_ID, GETNUM(CFLAG, "房租"))
	CFLAG:C_ID:素材贩卖 = CSVCFLAG (C_ID, GETNUM(CFLAG, "素材贩卖"))
	CFLAG:C_ID:固有指令 = CSVCFLAG (C_ID, GETNUM(CFLAG, "固有指令"))
	CFLAG:C_ID:地位 = CSVCFLAG (C_ID, GETNUM(CFLAG, "地位"))
	CFLAG:C_ID:初始位置 = CSVCFLAG (C_ID, GETNUM(CFLAG, "初始位置"))
	SIF FLAG:一时滞在 == C_ID
		CFLAG:C_ID:初始位置 = CFLAG:C_ID:神社在住
	CFLAG:C_ID:来访时间 = CSVCFLAG (C_ID, GETNUM(CFLAG, "来访时间"))
	CFLAG:C_ID:回家时间 = CSVCFLAG (C_ID, GETNUM(CFLAG, "回家时间"))
	CFLAG:C_ID:就寝时间 = CSVCFLAG (C_ID, GETNUM(CFLAG, "就寝时间"))
	CFLAG:C_ID:起床时间 = CSVCFLAG (C_ID, GETNUM(CFLAG, "起床时间"))
	CFLAG:C_ID:经常去的地方 = CSVCFLAG (C_ID, GETNUM(CFLAG, "经常去的地方"))
	CFLAG:C_ID:自宅位置 = CSVCFLAG (C_ID, GETNUM(CFLAG, "自宅位置"))
	CFLAG:C_ID:移动率补正 = CSVCFLAG (C_ID, GETNUM(CFLAG, "移动率补正"))
	CFLAG:C_ID:移动节制 = CSVCFLAG (C_ID, GETNUM(CFLAG, "移动节制"))
	CFLAG:C_ID:来访抑制值 = CSVCFLAG(C_ID, GETNUM(CFLAG, "来访抑制值"))
	CFLAG:C_ID:弹幕特能 = CSVCFLAG (C_ID, GETNUM(CFLAG, "弹幕特能"))
	SIF !CFLAG:C_ID:弹幕特能
		CALL 弹幕特殊能力设定(C_ID)
	CFLAG:C_ID:口吻 = CSVCFLAG (C_ID, GETNUM(CFLAG, "口吻"))
	;最大气力体力がCSV值より低い场合CSVに合わせる
	IF !CFLAG:C_ID:恢复速度降低
		SIF MAXBASE:C_ID:0 < CSVBASE (C_ID, 0)
			MAXBASE:C_ID:0 = CSVBASE (C_ID, 0)
		SIF MAXBASE:C_ID:1 < CSVBASE (C_ID, 1)
			MAXBASE:C_ID:1 = CSVBASE (C_ID, 1)
	ENDIF
	CFLAG:C_ID:产假TYPE = CSVCFLAG (C_ID, GETNUM(CFLAG, "产假TYPE"))
	;酒耐性
	MAXBASE:C_ID:酒气 = CSVBASE(C_ID, GETNUM(BASE, "酒气"))
	CALL SET_MINIMUM_EXP_ALL(C_ID)
	FOR 更新能力番号, 0, 111
		SIF EXP:C_ID:更新能力番号 < CSVEXP (C_ID,更新能力番号)
			EXP:C_ID:更新能力番号 = (EXP:C_ID:更新能力番号 + CSVEXP (C_ID,更新能力番号))
	NEXT
	;胖次种类非存在时消去
	FOR LOCAL:1, 2, MAXPANTS
		SIF CFLAG:C_ID:(LOCAL:1 + 胖次管理) && GET_STR(C_ID, "下半身内衣_ずらし可能", LOCAL:1, "名前") == ""
			CFLAG:C_ID:(LOCAL:1 + 胖次管理) = 0
	NEXT
	;儿童の性别と生年月日
	IF EXP:C_ID:分娩经验
		FOR LOCAL:1, 1, MAXCHILD + 1
			;生まれている儿童の设定
			IF CHILD_BIRTHDAY:C_ID:(LOCAL:1)
				IF !CHILD_BIRTHDATE:C_ID:(LOCAL:1)
					;生年月日设定
					CHILD_BIRTHDATE:C_ID:(LOCAL:1) = DAY_TO_DATE(CHILD_BIRTHDAY:C_ID:(LOCAL:1))
					;天气は遡れないので晴天ってことで
					CHILD_WEATHER:C_ID:(LOCAL:1) = たしか晴天
					;艾伦の儿童は例外
					IF C_ID == [[艾伦]]
						CHILD_SEX:C_ID:(LOCAL:1) = 0
					ELSE
						;初回
						IF !儿童设定初回 && !FLAG:儿童の性别
							儿童设定初回 = 1
							PRINTL 请设定小孩的性别
							PRINTL 请进行选择
							CALL ASK_M("随机", 1, "女の子", 1, "男の子", 1)
							SIF RESULT == 1 || RESULT == 2
								FLAG:儿童の性别 = RESULT
						ENDIF
						IF !FLAG:儿童の性别
							CHILD_SEX:C_ID:(LOCAL:1) = RAND:2 + 1
						ELSE
							CHILD_SEX:C_ID:(LOCAL:1) = FLAG:儿童の性别
						ENDIF
						PRINTFORM %CHILDNAME:C_ID:(LOCAL:1), 15, LEFT% 是 \@ CHILD_SEX:C_ID:(LOCAL:1) == 1 ? 女 # 男 \@孩子 出生日期是 
						CALL PRINT_DATE(CHILD_BIRTHDAY:C_ID:(LOCAL:1))
						PRINTFORML
					ENDIF
				ENDIF
				;初期爱情度の设定
				IF !CHILD_LOVE:C_ID:(LOCAL:1)
					;无自觉妊娠情报は失われているので陷落无しと判定
					IF TALENT:C_ID:恋慕
						CHILD_LOVE:C_ID:(LOCAL:1) = 300
					ELSEIF TALENT:C_ID:思慕
						CHILD_LOVE:C_ID:(LOCAL:1) = 200
					ELSE
						CHILD_LOVE:C_ID:(LOCAL:1) = 100
					ENDIF
				ENDIF
				;自立先の设定
				SIF !CHILD_AREA:C_ID:(LOCAL:1)
					CALL 自立先选定(C_ID, LOCAL:1)
			ENDIF
		NEXT
	ENDIF
	;赠り物情报の修正
	IF !GIFT_GOT:C_ID:1
		CFLAG:C_ID:礼物保管库 = 0
		IF CFLAG:C_ID:获得的礼物
			CFLAG:C_ID:获得的礼物 -= (GET_GIFTDATA(CFLAG:C_ID:获得的礼物, "回数") * 10000000000000000)
			CFLAG:C_ID:获得的礼物 += 1 * 10000000000000000
			GIFT_GOT:C_ID:1 = CFLAG:C_ID:获得的礼物
		ENDIF
	ENDIF
	;キャラデータのエラーチェック
	LOCAL:4 = 0
	FOR LOCAL:1, 0, MAXHOME
		FOR LOCAL:2, 1, 10
			LOCAL:3 = LOCAL:1 * 100 + LOCAL:2 * 10
			;addition custom code, for custom charadata movement
			CALL KOJO_CHECK(C_ID, "CHARADATA", "MOVE", 7, LOCAL:3)
			IF RESULT == -1 ;等于-1时,表明口上侧需要是结果为0
				RESULT = 0
				CALLFORM CHARAMOVE_DATA_{NO:C_ID}(7, LOCAL:3)
			ELSEIF RESULT ;等于其他正值时，表明口上侧有自己的值
			ELSE 
				CALLFORM CHARAMOVE_DATA_{NO:C_ID}(7, LOCAL:3)
			ENDIF 
			IF RESULT
				LOCAL:4 = 1
				BREAK
			ENDIF
		NEXT
		SIF LOCAL:4
			BREAK
	NEXT
	IF !LOCAL:4
		PRINTFORML ※%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%({C_ID})のCHARAMOVE_DATAに问题があります
		THROW ※%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%({C_ID})のCHARAMOVE_DATAに问题があります
	ENDIF

NEXT
CFLAG:MASTER:神社在住 = CFLAG:MASTER:初始位置
IF FLAG:住宿人物 && SUMIKOMI_ROOM
	CFLAG:(FLAG:住宿人物):神社在住 = SUMIKOMI_ROOM
	CFLAG:(FLAG:住宿人物):初始位置 = SUMIKOMI_ROOM
	CFLAG:(FLAG:住宿人物):自宅位置 = SUMIKOMI_ROOM
ENDIF
;SIF !FLAG:酒虫 
;	FLAG:酒虫 = 1
SIF VAR_FERMENT == "" || VAR_FERMENT == "0"
	VAR_FERMENT = 普通的水
IF GETBIT(FLAG_DAILY_EV23, 0) && !FLAG_DAILY_EV23_贩卖员
	PRINTFORML 访问销售员的模样是…
	CALL ASK_YN("头发后面扎成了两束的","像男孩子一样的短发")
	FLAG_DAILY_EV23_贩卖员 = RESULT + 1
ENDIF

;灵梦
;NAME:[[灵梦]] = 博丽 灵梦

;留琴

;卡娜

;魅魔

;桑尼

;露娜

;斯塔

;千百合

;梦美

;萃香

;魔理沙

;露米亚

;大ちゃん

;⑨

;女仆长

;おぜう

;爱丽丝

;白百合

;黑百合

;莉莉卡

;梅露兰

;露娜萨

;妖梦

;橙

;蓝

;紫

;莉格露

;ミスチー
;文

;映姫样

;早苗

;神奈子样

;诹访子样

;てんこ

;衣玖さん

;おりんりん
;阿空

;恋

;ナズ

;小伞

;鵺

;果

;华扇

;艾伦

;理香子

;明罗

;里香

;露易兹

;觉ん

;芙兰

;荷取

;铃仙
;NAME:[[铃仙]] = 铃仙・优昙华院・因幡
;帝
;帕秋莉

;白莲
;NAME:[[白莲]] = 圣 白莲

;神子样

;心
;CFLAG:57:神社在住 = CFLAG:57:初始位置

;美铃

;こぁ

;帕露西

;妹红

;姫样

;影狼
;勇仪


;椛
;ゆゆさま

;慧音
;NAME:[[慧音]] = 上白泽 慧音

;幽香
;TALENT:68:105 = 1

;猯藏

;小铃

;针妙丸
;NAME:[[针妙丸]] = 少名 针妙丸
;CFLAG:71:神社在住 = CFLAG:71:初始位置
;TALENT:71:100 = -5

;永琳
;NAME:[[永琳]] = 八意 永琳

;蛮奇

;蕾蒂

;梅蒂欣

;小町

;静叶

;穣子

;雏

;阿求

;莲子

;梅莉

;琪斯美

;山女

;一轮

;水蜜

;星


;响子
;芳香


;青娥

;屠自古


;布都


;若鹭姫

;弁弁

;八桥

;雷鼓

;正邪

;依姫
;NAME:[[依姫]] = 绵月 依姫

;丰姫
;NAME:[[丰姫]] = 绵月 丰姫

;铃仙二号

;朱鹭子

;神绮

;梦子

;雪

;​舞​

;菫子


;清兰

;铃瑚

;哆来咪

;探女

;克劳恩皮丝
;CALLNAME:[[克劳恩皮丝]] = 克劳恩皮丝

;纯狐

;赫卡提亚
;CALLNAME:[[赫卡提亚]] = 赫卡提亚

;隐岐奈

;千亦
;CALLNAME:[[千亦]] = 千亦

;SIF !EXP:[[白莲]]:Ｖ经验
;	TALENT:[[白莲]]:0 = 1
;初对面管理をFLAG:13からCFLAG:18に移行
IF FLAG:13 != 0
	FOR C_ID, 1, 63
		SIF GETBIT(FLAG:13, C_ID)
			CFLAG:C_ID:认识 = 1
	NEXT
	FLAG:13 = 0
ENDIF

;衣裳フラグの修正
CFLAG:[[灵梦]]:基本服装SET = 101
CFLAG:[[阿空]]:基本服装SET = 137
CFLAG:[[荷取]]:基本服装SET = 151
CFLAG:[[铃仙]]:基本服装SET = 152
CFLAG:[[帝]]:基本服装SET = 153
CFLAG:[[帕秋莉]]:基本服装SET = 154
CFLAG:[[白莲]]:基本服装SET = 155
CFLAG:[[神子]]:基本服装SET = 156
CFLAG:[[心]]:基本服装SET = 157
CFLAG:[[针妙丸]]:基本服装SET = 171
CFLAG:[[小兔姫]]:基本服装SET = 245
CFLAG:[[伊丽丝]]:基本服装SET = 246
CFLAG:[[萨里爱尔]]:基本服装SET = 247
CFLAG:[[萨拉]]:基本服装SET = 248
CFLAG:[[奥莲姬]]:基本服装SET = 249
CFLAG:[[矜羯罗]]:基本服装SET = 250
CFLAG:[[瑞灵]]:基本服装SET = 258
CFLAG:[[先代]]:基本服装SET = 259
CFLAG:[[麟]]:基本服装SET = 260


;你关联处理

;本来つかない筈の心情不快がMASTERについてる场合の削除处理
SIF CFLAG:MASTER:心情不快 == 1
	CFLAG:MASTER:心情不快 = 0
SIF CFLAG:MASTER:勃然大怒 == 1
	CFLAG:MASTER:勃然大怒 = 0
SIF TALENT:MASTER:心情 == -1
	TALENT:MASTER:心情 = 0


MAXBASE:MASTER:酒气 = 1500
;MASTERが女、扶她时
IF GETBIT(TALENT:MASTER:性别, 0)
	IF FLAG:周回数 && TALENT:MASTER:处女 == 1 && CSTR:MASTER:处女丧失履历 != ""
		CSTR:MASTER:处女丧失履历 = 
		CFLAG:MASTER:处女丧失纪念日 = 0
		CFLAG:MASTER:处女丧失目标对象 = 0
	ENDIF
;MASTERが男
ELSE
	TALENT:MASTER:胸围 = -2
ENDIF
SIF GETBIT(TALENT:MASTER:性别, 1) && !TALENT:MASTER:形状
	CALL TINKO_DECIDE(MASTER)
;小槌の代偿が结束せずにずっと粗陋の物になっている场合の修正
;要注意大きくした后に二本にした场合、及び二本にした后に大きくした场合、粗陋の物が默认になる可能性があります
;その场合、自己元の形状を再设定しなければなりません
SIF TALENT:MASTER:形状 != CFLAG:MASTER:382 && CFLAG:MASTER:魔力回收 < 0
	TALENT:MASTER:形状 = CFLAG:MASTER:382
SIF CFLAG:MASTER:魔力回收 > 0 && CFLAG:MASTER:小槌 == 0
	CFLAG:MASTER:魔力回收 = 0
IF TALENT:MASTER:体型 == -3
	TALENT:MASTER:体型 = 0
	PRINTFORML ---重新设定%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的体型---
	PRINTL 
ENDIF

;折叠伞
SIF ITEM:36 == 0 && ITEMSTOCK(36) == 2
	ITEMSALES:36 ++
;跳蛋
SIF ITEM:0 < 0
	ITEM:0 = 0
SIF ITEM:0 == 0 && ITEMSTOCK(0) == 2
	ITEM:0 = 1

FLAG:出禁人数 = 0
FOR C_ID, 1, CHARANUM
	SIF TALENT:C_ID:路人
		CONTINUE
	SIF CFLAG:C_ID:出禁
		FLAG:出禁人数 ++
NEXT

SIF FLAG:3 == 0
	PRINTL 宴会的有无设定改为了OFF

LOCAL = TARGET
FOR C_ID, 1, CHARANUM
	RESULT = 0
	TARGET = C_ID
	CALL KOJO_MULTIPLE(C_ID)
	IF RESULT
		;新规口上追加时のリセット处理のスイッチ
		IF RESULT == 2
			口上实装チェック:C_ID = 1
			リセット画面 = 1
		ENDIF
		; RESULTS =
		; ;口上の存在判定 and RESULTSに文字列代入
		; IF CFLAG:C_ID:口上セレクタ
		; 	TRYCALLFORM M_KOJO_K{C_ID}_{CFLAG:C_ID:口上セレクタ}
		; ELSE
		; 	TRYCALLFORM M_KOJO_K{C_ID}
		; ENDIF
		; PRINTFORML 正在确认%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%%RESULTS:1%的口上是否存在UPDATE
		; TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{C_ID}
		; PRINTL 
	ENDIF
NEXT
CALL UPDATE_AND_SELECTION_OF_KOJO_MODDING
TARGET = LOCAL

SIF FLAG:开始日 < DAY
	FLAG:开始日 = DAY + 1
VARSET FLAG, 0, 300, 500
VARSET LOCAL
;胖次をCFLAGに移行
;SAVESTR:10に2文字以上那个ば实行
IF STRLENS(SAVESTR:10) > 1
	FLAG:700 = 0
	FOR LOCAL,1,CHARANUM
		FOR LOCAL:1,0,MAXPANTS
			LOCALS = /%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%{LOCAL:1}/
			CFLAG:LOCAL:(LOCAL:1 + 胖次管理) = STRCOUNT(SAVESTR:10,LOCALS)
			SIF CFLAG:LOCAL:(LOCAL:1 + 胖次管理)
				FLAG:700 ++
		NEXT
	NEXT
	SAVESTR:10 =
	PRINTL 你重要的内裤藏品已移交完毕
ENDIF

;小作人あぷで
IF FLAG:地主 && !FLAG:农民难易度
	CALL MODE_NOUMIN
	CALL LOAN_NOUMIN
ENDIF

;红魔赌场用
SIF !FLAG:筹码变动状况
	CALL SET_CHARISMA("NEW")

PRINTFORML 如果使用了旧版存档、不重置委托的话、有非常高可能出现BUG。
PRINTFORML 要重置委托么？
CALL ASK_YN
SIF !RESULT
	CALL IRAI_RESET

;昆虫诱捕装置破弃
IF MUSHI_TRAP
	PRINTFORML 要破弃所有捕虫器吗？
	CALL ASK_YN
	SIF !RESULT
		MUSHI_TRAP = 0
ENDIF

;钱汤にゅーぷらんパッチ
IF !旧地狱温泉_ニュープラン_完成日 && FLAG:周回数
	VARSET SENTO_CONSUMPTION, 0
	VARSET SENTO_FLAG, 0
	旧地狱温泉_ニュープラン_完成日 = 1
	PRINTL 旧地狱温泉的『New-Plan』已重新开放
ENDIF
;道具类の在库管理
FOR LOCAL, 0, 1000
	CALL ITEMDATA(LOCAL, "SHOP:食料品店")
	RESULT:1 = RESULT
	CALL ITEMDATA(LOCAL, "SHOP:酒屋")
	RESULT:2 = RESULT
	CALL ITEMDATA(LOCAL, "SHOP:花屋")
	RESULT:3 = RESULT
	CALL ITEMDATA(LOCAL, "SHOP:蔬菜店")
	RESULT:4 = RESULT
	IF RESULT:1 && !INRANGE(ITEMSALES:LOCAL, 1, 30)
		ITEMSALES:LOCAL = LIMIT(ITEMSALES:LOCAL, 10, 30)
	ELSEIF RESULT:2 && !INRANGE(ITEMSALES:LOCAL, 1, 30)
		ITEMSALES:LOCAL = LIMIT(ITEMSALES:LOCAL, 5, 30)
	ELSEIF RESULT:3 && !INRANGE(ITEMSALES:LOCAL, 0, 3)
		ITEMSALES:LOCAL = 3
	ELSEIF RESULT:4 && !INRANGE(ITEMSALES:LOCAL, 0, 5)
		ITEMSALES:LOCAL = RAND:5
	ENDIF
	;ついでに所持数制限,换金はしません
	IF ITEM:LOCAL > 所持数制限 && !GROUPMATCH(ITEMNAME:LOCAL, "喜欢的道具")
		ITEM:LOCAL = 所持数制限
		PRINTFORMW 因为%ITEMNAME:LOCAL%超过了持有数量上限所以减少到了最大值
	ENDIF
	;ついでのついでに存在外道具の削除
	IF ITEMNAME:LOCAL == ""
		ITEM:LOCAL = 0
		ITEMSALES:LOCAL = 0
	ENDIF
NEXT

IF TALENT:MASTER:体型 == -3
	TALENT:MASTER:体型 = 0
	PRINTFORML ---重新设定%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的体型---
	PRINTL 
ENDIF
IF !FLAG:宴会举办FLAG
ELSEIF !FLAG:32
	;必要なのかわからんが念のため、です
	;暂定处理でとりあえず博丽神社境内を指定、主催者がズレたりしても气にするな
	FLAG:32 = 2
	PRINTFORML 修改宴会的举办位置
ELSEIF FLAG:32 == 230
	;バグ报告でのFLAG:32の内容はおそらくこっち
	FLAG:32 = 218
	PRINTFORML 修改宴会的举办位置
ENDIF

CALL SET_ENKAI_LOCATIONDISPLAY
SAVESTR:本体バージョン = %eraTW_Version%

IF リセット画面
	PRINTFORML 以下的角色的口上是新实装的。
	PRINTFORML 请选择想要重置好感度、经验、能力等的角色。
	$PRINT_RESETCHARA
	FOR LOCAL,1,CHARANUM
		SIF !口上实装チェック:LOCAL
			CONTINUE
		PRINTFORMLC [{LOCAL}] %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%
	NEXT
	PRINTFORML 
	DRAWLINE
	PRINTFORML [999] 结束
	INPUT
	SIF 人物数量上限 < RESULT
		GOTO UPDATE_END
	SIF !口上实装チェック:RESULT
		GOTO UPDATE_END
	IF 口上实装チェック:RESULT
		口上实装チェック:RESULT = 0
		VARSET LOCAL, RESULT
		WAIT
		CALL RESET_KOJO_CHARA(RESULT)
		PRINTFORMW 是否要进行%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%目前口上的update？
		CALL ASK_YN
		IF !RESULT
			VARSET RESULTS
			;口上の存在判定 and RESULTSに文字列代入
			IF CFLAG:LOCAL:口上セレクタ
				TRYCALLFORM M_KOJO_K{LOCAL}_{CFLAG:LOCAL:口上セレクタ}
			ELSE
				TRYCALLFORM M_KOJO_K{LOCAL}
			ENDIF
			PRINTFORML 正在确认%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%%RESULTS:1%的口上是否存在UPDATE
			TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{LOCAL}
			TWAIT 40, 0
			PRINTL 
		ENDIF
		GOTO PRINT_RESETCHARA
	ENDIF
ENDIF

$UPDATE_END
VARSET LOCAL


PRINTFORML 重新计算儿童人数和育儿人数
CFLAG:MASTER:儿童人数 = 0
CFLAG:MASTER:育儿人数 = 0
FOR C_ID, 1, CHARANUM
	SIF TALENT:C_ID:路人
		CONTINUE
	CFLAG:MASTER:儿童人数 += CFLAG:C_ID:儿童人数
	CFLAG:MASTER:育儿人数 += CFLAG:C_ID:育儿人数
NEXT
PRINTFORML 儿童人数:{CFLAG:MASTER:儿童人数}/育儿人数:{CFLAG:MASTER:育儿人数}

PRINTL 
;据点Mapの再设定
IF CFLAG:MASTER:当前位置 != OMANEKIBEYA() && !FLAG:气绝中断
	CALL MAP_DEFAULTRESIDENT(MAIN_MAP, 1)
	PRINTFORML 据点地图的设定被重设了
ELSE
	CALL COLORMESSAGE(@"※无法重设据点地图", C_YELLOW, 1)
	IF CFLAG:MASTER:当前位置 == OMANEKIBEYA()
		PRINTL 理由：被招待中
	ELSEIF FLAG:气绝中断
		PRINTL 理由：气绝中
	ENDIF
	FORCEWAIT
ENDIF

PRINTW 更新完毕

;アプデでCSV读み込みによる更新されない素質
@TALENT_NOT_UPDATED(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 15,28,29,35 TO 39, 42 TO 49, 58, 59, 63 TO 69, 72 TO 79, 87 TO 91, 95 TO 99, 107 TO 120, 122 TO 129
		RETURNF 1
	CASEELSE
		RETURNF 0
ENDSELECT

@履历文字列变换(ARG)
#DIM 日期
VARSET LOCALS
IF CFLAG:ARG:初吻丧失纪念日 && CSTR:ARG:初吻丧失履历 == ""
	日期 = CFLAG:ARG:初吻丧失纪念日
	LOCALS = %PRINT_DATE_F(日期)%、%NAME_OUTPUT_TRANSLATION("CALLNAME", (CFLAG:ARG:初吻丧失互动对象))%
	SIF CSTR:ARG:初吻位置 != ""
		LOCALS += @"在%CSTR:ARG:初吻位置%"
	IF CFLAG:ARG:初吻丧失目标对象 & (丧失_自己 | 丧失_恋慕 | 丧失_思慕)
		LOCALS += "献上了初吻"
	ELSE
		SIF CFLAG:ARG:初吻丧失目标对象 & 丧失_无崩し
			LOCALS += "就那样"
		LOCALS += "被夺走初吻了"
	ENDIF
	CSTR:ARG:初吻丧失履历 = %LOCALS%
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的初吻成为了历史
ENDIF

IF CFLAG:ARG:肛门处女丧失纪念日 && CSTR:ARG:肛门处女丧失履历 == ""
	日期 = CFLAG:ARG:肛门处女丧失纪念日
	LOCALS:1 = %PRINT_DATE_F(日期)%、\@ CFLAG:ARG:肛门处女丧失目标对象 & 丧失_时间停止? 时间停止中# \@
	SIF CSTR:ARG:Ａ处女丧失位置 != ""
		LOCALS:1 += @"在%CSTR:ARG:Ａ处女丧失位置%"
	IF GETBIT (CFLAG:ARG:肛门处女丧失目标对象,3)
		LOCALS:1 += @"被%NAME_OUTPUT_TRANSLATION("CALLNAME", (CFLAG:ARG:肛门处女丧失互动对象))%用穿戴式假阴茎"
	ELSEIF GETBIT (CFLAG:ARG:肛门处女丧失目标对象,2)
		LOCALS:1 += @"被%NAME_OUTPUT_TRANSLATION("CALLNAME", (CFLAG:ARG:肛门处女丧失互动对象))%用肛用振动棒"
	ELSEIF GETBIT (CFLAG:ARG:肛门处女丧失目标对象,1)
		LOCALS:1 += @"被%NAME_OUTPUT_TRANSLATION("CALLNAME", (CFLAG:ARG:肛门处女丧失互动对象))%的阴茎"
	ENDIF
	IF CFLAG:ARG:肛门处女丧失目标对象 & 丧失_时间停止
		LOCALS:1 += "夺走了肛门处女"
	ELSE
		LOCALS:1 += "奉献了肛门处女"
	ENDIF
	CSTR:ARG:肛门处女丧失履历 = %LOCALS:1%
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的肛门处女成为了历史
ENDIF

IF CFLAG:ARG:处女丧失纪念日 && CSTR:ARG:处女丧失履历 == ""
	日期 = CFLAG:ARG:处女丧失纪念日
	LOCAL = CFLAG:ARG:处女丧失目标对象
	LOCALS:2 = %PRINT_DATE_F(日期)%、
	SIF LOCAL & 丧失_时间停止
		LOCALS:2 += "时间停止中"
	SIF GETBIT (LOCAL,9)
		LOCALS:2 += "喝得酩酊大醉的时候烂醉如泥的时候"
	SIF GETBIT (LOCAL,16)
		LOCALS:2 += "在睡觉的时候"
	SIF CSTR:ARG:处女丧失位置 != ""
		LOCALS:2 += @"在%CSTR:ARG:处女丧失位置%\@ LOCAL & 丧失_恋慕 ? 心爱的# \@%NAME_OUTPUT_TRANSLATION("CALLNAME", (CFLAG:ARG:处女丧失互动对象))%"
	IF GETBIT (LOCAL,3)
		LOCALS:2 += "用穿戴式假阴茎"
	ELSEIF GETBIT (LOCAL,2)
		LOCALS:2 += "用振动棒"
	ELSEIF GETBIT (LOCAL,1)
		LOCALS:2 += "的阴茎"
	ENDIF
	SIF LOCAL & 丧失_无崩し
		LOCALS:2 += "破坏性地"
	IF LOCAL & ( 丧失_恋慕 | 丧失_自己 ) || ( LOCAL & 丧失_思慕 && ! ( LOCAL & ( 丧失_烂醉 | 丧失_强行 | 丧失_睡眠 ) ) )
		LOCALS:2 += "献上处女"
	ELSE
		SIF LOCAL & 丧失_强行
			LOCALS:2 += "用力地"
		LOCALS:2 += @"就那样\@ARG == MASTER ? 献上了处女# 被夺走了处女\@"
	ENDIF
	CSTR:ARG:处女丧失履历 = %LOCALS:2%
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的处女成为了历史
ENDIF

@RESET_KOJO_CHARA(ARG)
CFLAG:ARG:好感度 = 0
CFLAG:ARG:信赖度 = 0
CFLAG:ARG:掌握把柄 = 0
CFLAG:ARG:被掌握把柄 = 0
CFLAG:ARG:既成事实 = 0
CFLAG:ARG:允许无套 = 0
CFLAG:ARG:认识 = 0
CFLAG:ARG:指出缺点 = 0
CFLAG:ARG:自慰暴露 = 0
CFLAG:ARG:眠奸发觉 = 0
CFLAG:ARG:睡眠奸 = 0
CFLAG:ARG:烂醉奸 = 0
CFLAG:ARG:情人节 = 0
CFLAG:ARG:最后见面的日子 = 0
CFLAG:ARG:无自觉妊娠 = 0
CFLAG:ARG:由于妊娠而追加尺寸 = 0
CFLAG:ARG:纯洁的交往 = 0
CFLAG:ARG:分娩经过日 = 0
CFLAG:ARG:育儿人数 = 0
CFLAG:ARG:身份认同_母 = 0
CFLAG:ARG:身份认同_父 = 0
CFLAG:ARG:妊娠自觉状态 = 0
CFLAG:ARG:不会喝酒 = 0
CFLAG:ARG:强行内射 = 0
CFLAG:ARG:妊娠 = 0
CFLAG:ARG:妊娠经过日数 = 0
CFLAG:ARG:儿童人数 = 0
CFLAG:ARG:生子补正 = 0
CFLAG:ARG:产假中 = 0
CFLAG:ARG:累计膣内精液 = 0
CFLAG:ARG:累计肛门精液 = 0
CFLAG:ARG:累计精饮 = 0
CFLAG:ARG:累计精浴 = 0
CFLAG:ARG:ヤラせちゃった = 0
CFLAG:ARG:喜欢的体位 = 0
MARK:ARG:时奸刻印 = 0
MARK:ARG:反抗刻印 = 0
MARK:ARG:反抗取得履历 = 0
MARK:ARG:苦痛刻印 = 0
MARK:ARG:快乐刻印 = 0
MARK:ARG:屈服刻印 = 0

;CSTR:ARG:偷情败露 =
;CSTR:ARG:浮气公认 =
CSTR:ARG:处女丧失履历 =
CSTR:ARG:肛门处女丧失履历 =
CSTR:ARG:初吻丧失履历 =

;経験
EX:ARG:喷乳 = 0
EX:ARG:射精 = 0
EX:ARG:放尿 = 0
EX:ARG:射精量 = 0
EX:ARG:膣内精液 = 0
EX:ARG:肛门内精液 = 0
EX:ARG:子宫内精液 = 0

UWAKI_KNOWN:ARG:0 =
UWAKI_ACCEPTED:ARG:0 =

;陷落
TALENT:ARG:把柄 = 0
TALENT:ARG:恋慕 = 0
TALENT:ARG:思慕 = 0
TALENT:ARG:淫乱 = 0
TALENT:ARG:爱欲 = 0
TALENT:ARG:炮友 = 0
TALENT:ARG:妊娠 = 0
TALENT:ARG:育儿中 = 0
TALENT:ARG:母性 = 0
TALENT:ARG:恋人 = 0
TALENT:ARG:初潮 = 0
TALENT:ARG:母乳体质 = 0
SIF TALENT:MASTER:恋人 == ARG
	TALENT:MASTER:恋人 = 0
;淫乱系
TALENT:ARG:受虐狂 = 0
TALENT:ARG:自慰狂 = 0
TALENT:ARG:淫壶 = 0
TALENT:ARG:尻穴狂 = 0
TALENT:ARG:淫乳 = 0
TALENT:ARG:接吻魔 = 0
TALENT:ARG:子宫口性感 = 0

IF CFLAG:ARG:恢复速度降低
	MAXBASE:ARG:体力 += 500
	MAXBASE:ARG:气力 += 500
	TALENT:ARG:恢复速度 = CFLAG:ARG:恢复速度降低 - 2
	CFLAG:ARG:恢复速度降低 = 0
ENDIF
IF CFLAG:ARG:乳品质提升 == 1 && TALENT:ARG:胸围 > -2
	TALENT:ARG:胸围 -= 1
	CFLAG:ARG:乳品质提升 = 0
ENDIF
TALENT:ARG:痛觉 = CSVTALENT(ARG,40)
CFLAG:ARG:肛门处女丧失纪念日 = 0
CFLAG:ARG:处女丧失纪念日 = 0
CSTR:ARG:肛门处女丧失履历 =
CSTR:ARG:处女丧失履历 =
CSTR:ARG:初吻丧失履历 =

FOR LOCAL,0,100
	ABL:ARG:LOCAL = CSVABL(ARG,LOCAL)
	EXP:ARG:LOCAL = CSVEXP(ARG,LOCAL)
NEXT
VARSET JUEL:ARG:0, 0
VARSET CHILD_BIRTHDAY:ARG:0, 0
VARSET CHILD_WEATHER:ARG:0, ""
VARSET CHILD_SEX:ARG:0, 0
VARSET CHILDNAME:ARG:0, ""
SIF !EXP:ARG:接吻经验
	TALENT:ARG:无接吻经验 = 1
TALENT:ARG:0 = CSVTALENT(ARG,0)
;カスタム素質
VARSET CUSTOM_TALENT:ARG:0, 0
VARSET CUSTOM_TALENT_NAME:ARG:0, ""
VARSET CUSTOM_TALENT_COLOR:0, 0
VARSET CUSTOM_TALENT_TYPE:0, 0
PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%重置了。



;CSV更新用
@SET_MINIMUM_EXP_ALL(C_ID)
#DIM C_ID
#DIM 更新能力番号
#DIM 最低能力值

FOR 更新能力番号,0,100
	最低能力值 = CSVABL(C_ID, 更新能力番号)
	SIF 最低能力值 == 0
		CONTINUE
	;战斗能力
	IF ABLNAME:更新能力番号 == "战斗能力"
		;战斗能力修正
		SIF ABL:C_ID:更新能力番号 < 最低能力值 && EXP:C_ID:战斗经验 == CSVEXP(C_ID, GETNUM(EXP, "战斗经验"))
			ABL:C_ID:更新能力番号 = 最低能力值
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "战斗经验"), 最低能力值)
		CONTINUE
	ENDIF
	ABL:C_ID:更新能力番号 = MAX(最低能力值, ABL:C_ID:更新能力番号)
	SELECTCASE ABLNAME:更新能力番号
	CASE "清扫技能"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "清扫经验"), 最低能力值)
	CASE "话术技能"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "会话经验"), 最低能力值)
	CASE "教养"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "学习经验"), 最低能力值)
	CASE "料理技能"
		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "料理经验"), 最低能力值)
	CASE "音乐技能"
		SELECTCASE TALENT:C_ID:音乐知识
		CASE 1
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "演奏经验"), 最低能力值)
		CASE 2
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "歌唱经验"), 最低能力值)
		CASE 3
			CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "舞蹈经验"), 最低能力值)
		ENDSELECT
	;移行完了
;	CASE "钓鱼技能"
;		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 47))
;		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "钓鱼经验"), 最低能力值)
;		TALENT:C_ID:钓鱼Lv = MAX(TALENT:C_ID:钓鱼Lv, ABL:C_ID:钓鱼技能)
;	CASE "采集技能"
;		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 48))
;		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "采集经验"), 最低能力值)
;		TALENT:C_ID:采集Lv = MAX(TALENT:C_ID:采集Lv, ABL:C_ID:采集技能)
;	CASE "调合技能"
;		ABL:C_ID:更新能力番号 = MAX(ABL:C_ID:更新能力番号, CSVTALENT(C_ID, 49))
;		CALL SET_MINIMUM_EXP(C_ID, GETNUM(EXP, "调合经验"), 最低能力值)
;		TALENT:C_ID:调合Lv = MAX(TALENT:C_ID:调合Lv, ABL:C_ID:调合技能)
	ENDSELECT
NEXT


;非エロ系ABL、40～49において
;CSV准拠で能力が上升した场合に最低限のEXPを取得し、表示する
@SET_MINIMUM_EXP(C_ID, VAR_ID, MINIMUM_ABL)
#DIM C_ID
#DIM VAR_ID
#DIM MINIMUM_ABL
#DIM MINIMUM_EXP
SIF MINIMUM_ABL == 0
	RETURN
MINIMUM_EXP = EXPLV:(MINIMUM_ABL + 2)
SIF EXP:C_ID:VAR_ID >= MINIMUM_EXP
	RETURN
EXP:C_ID:VAR_ID = MINIMUM_EXP
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的%EXPNAME:VAR_ID%上升了{EXP:C_ID:VAR_ID}

;-------------------------------------------------
;CSVを使わず弹幕特殊能力を个别に设定する函数
;新しく角色を追加する际にCSVだと手间なので增设したものです
;▼memo
;0	【言笑自若】: 相手の骰子数Down
;	何があっても决して慌てず、落ち着いていることのたとえ。
;1	【温柔敦厚】: 相手の骰子面Down
;	温柔的穏やかで、思いやりがあること。
;2	【勇气凛々】: %NAME_OUTPUT_TRANSLATION("CALLNAME", OPPONENT)%の骰子数Up
;	失败や危险をかえりみず、勇敢に物事に立ち向かっていこうとするさま。
;3	【豪放磊落】: %NAME_OUTPUT_TRANSLATION("CALLNAME", OPPONENT)%の骰子面Up
;	心が广く大らかで、些细なことは气にしないこと。または、そのような样子。
;4	【怪力乱神】: %NAME_OUTPUT_TRANSLATION("CALLNAME", OPPONENT)%の骰子值が相手の骰子值より小さくても、その差が一定数以下なら、残机が减らない
;	人の知识では理解することが出来ない、怪しく奇怪な现象や物事のこと。
;5	【洽览深识】: %NAME_OUTPUT_TRANSLATION("CALLNAME", OPPONENT)%の初期残机が4になる
;	体验して得た経験や知识が多くあり、样々なことを知っていること。
;6	【残忍酷薄】: 相手の被弹からの复归时のＢ数が-1される
;	人のことを气遣うこともなく、むごいこと。
;7	【乾坤一掷】: %NAME_OUTPUT_TRANSLATION("CALLNAME", OPPONENT)%が一定概率でＢを使用ようになる
;	自己的运命を赌けて、运まかせの大博打をすること。
;8	【狷介孤高】: %NAME_OUTPUT_TRANSLATION("CALLNAME", OPPONENT)%の骰子修正值Up
;	意志を曲げずに、他人と协力しないこと。
;9	【幽愁暗恨】: 相手の骰子修正值Down
;	谁にも知られることのない深い恨みや忧いのこと。
;10  【百载无穷】: 全ての特殊能力をあわせ持つ。最强
;	いつまでも无くなることがないこと。
;-------------------------------------------------
@弹幕特殊能力设定(ARG)
#DIM CONST SKL言笑自若 = 0
#DIM CONST SKL温柔敦厚 = 1
#DIM CONST SKL勇气凛々 = 2
#DIM CONST SKL豪放磊落 = 3
#DIM CONST SKL怪力乱神 = 4
#DIM CONST SKL洽览深识 = 5
#DIM CONST SKL残忍酷薄 = 6
#DIM CONST SKL乾坤一掷 = 7
#DIM CONST SKL狷介孤高 = 8
#DIM CONST SKL幽愁暗恨 = 9
#DIM CONST SKL百载无穷 = 10

CFLAG:ARG:弹幕特能 = 0

SELECTCASE ARG
CASE [[璎花]]
	SETBIT CFLAG:ARG:弹幕特能, SKL温柔敦厚
CASE [[润美]]
	SETBIT CFLAG:ARG:弹幕特能, SKL豪放磊落
CASE [[久侘歌]]
	SETBIT CFLAG:ARG:弹幕特能, SKL言笑自若
	SETBIT CFLAG:ARG:弹幕特能, SKL勇气凛々
CASE [[八千慧]]
	SETBIT CFLAG:ARG:弹幕特能, SKL言笑自若
	SETBIT CFLAG:ARG:弹幕特能, SKL洽览深识
	SETBIT CFLAG:ARG:弹幕特能, SKL残忍酷薄
CASE [[磨弓]]
	SETBIT CFLAG:ARG:弹幕特能, SKL勇气凛々
	SETBIT CFLAG:ARG:弹幕特能, SKL怪力乱神
CASE [[袿姫]]
	SETBIT CFLAG:ARG:弹幕特能, SKL言笑自若
	SETBIT CFLAG:ARG:弹幕特能, SKL温柔敦厚
	SETBIT CFLAG:ARG:弹幕特能, SKL洽览深识
CASE [[早鬼]]
	SETBIT CFLAG:ARG:弹幕特能, SKL怪力乱神
	SETBIT CFLAG:ARG:弹幕特能, SKL豪放磊落
	SETBIT CFLAG:ARG:弹幕特能, SKL乾坤一掷
CASE [[美宵]]
	SETBIT CFLAG:ARG:弹幕特能, SKL豪放磊落
CASE [[三花]]
	SETBIT CFLAG:ARG:弹幕特能, SKL狷介孤高
CASE [[高岭]]
	SETBIT CFLAG:ARG:弹幕特能, SKL洽览深识
CASE [[驹草]]
	SETBIT CFLAG:ARG:弹幕特能, SKL乾坤一掷
	SETBIT CFLAG:ARG:弹幕特能, SKL勇气凛々
CASE [[魅须丸]]
	SETBIT CFLAG:ARG:弹幕特能, SKL言笑自若
	SETBIT CFLAG:ARG:弹幕特能, SKL温柔敦厚
CASE [[典]]
	SETBIT CFLAG:ARG:弹幕特能, SKL残忍酷薄
CASE [[龙]]
	SETBIT CFLAG:ARG:弹幕特能, SKL言笑自若
	SETBIT CFLAG:ARG:弹幕特能, SKL勇气凛々
	SETBIT CFLAG:ARG:弹幕特能, SKL洽览深识
CASE [[千亦]]
	SETBIT CFLAG:ARG:弹幕特能, SKL言笑自若
	SETBIT CFLAG:ARG:弹幕特能, SKL勇气凛々
	SETBIT CFLAG:ARG:弹幕特能, SKL洽览深识
CASE [[百百世]]
	SETBIT CFLAG:ARG:弹幕特能, SKL怪力乱神
	SETBIT CFLAG:ARG:弹幕特能, SKL残忍酷薄
	SETBIT CFLAG:ARG:弹幕特能, SKL幽愁暗恨
CASE [[尤魔]]
	SETBIT CFLAG:ARG:弹幕特能, SKL言笑自若
	SETBIT CFLAG:ARG:弹幕特能, SKL残忍酷薄
	SETBIT CFLAG:ARG:弹幕特能, SKL狷介孤高
CASE [[美天]]
	SETBIT CFLAG:ARG:弹幕特能, SKL温柔敦厚
	SETBIT CFLAG:ARG:弹幕特能, SKL怪力乱神
CASE [[慧之子]]
	SETBIT CFLAG:ARG:弹幕特能, SKL勇气凛々
	SETBIT CFLAG:ARG:弹幕特能, SKL洽览深识
CASE [[血枪]]
	SETBIT CFLAG:ARG:弹幕特能, SKL残忍酷薄
	SETBIT CFLAG:ARG:弹幕特能, SKL幽愁暗恨
CASE [[日狭美]]
	SETBIT CFLAG:ARG:弹幕特能, SKL怪力乱神
	SETBIT CFLAG:ARG:弹幕特能, SKL豪放磊落
	SETBIT CFLAG:ARG:弹幕特能, SKL乾坤一掷
CASE [[残无]]
	SETBIT CFLAG:ARG:弹幕特能, SKL洽览深识
	SETBIT CFLAG:ARG:弹幕特能, SKL残忍酷薄
	SETBIT CFLAG:ARG:弹幕特能, SKL狷介孤高
ENDSELECT

