@SHOW_COLLECTION
#DIM 总数
#DIM 着用可能数
DRAWLINE
IF FLAG:胖次收集状况TYPEB
	PRINT [0] - 默认排序
	PRINT [1] - 未所持数升序排序
	INPUT
ENDIF
IF FLAG:胖次收集状况TYPEB && RESULT
	CALL DISPLAY_SORT_ALL_COLLECTION
ELSE
	CALL DISPLAY_ALL_COLLECTION
ENDIF
总数 = RESULT:0
FLAG:获得胖次种数 = RESULT:1
着用可能数 = RESULT:2
PRINTL 　 
IF FLAG:胖次收集状况TYPEB == 0
	PRINTFORML 总数：{总数}枚　　种别：{FLAG:获得胖次种数}种　　重复：{总数 - FLAG:获得胖次种数}枚
ELSE
	PRINTFORML 总数：{总数}枚　　种别：{FLAG:获得胖次种数}/{着用可能数}种　　重复：{总数 - FLAG:获得胖次种数}枚
ENDIF
PRINT [0] - 返回 
SIF 总数 - FLAG:获得胖次种数
	PRINT [1] - 每种留下1条剩下的全部卖出
INPUT
IF RESULT == 1 && (总数 - FLAG:获得胖次种数) != 0
	CALL SPEND_COLLECTION
	PRINTFORMW 全部{RESULT:0}条 %金额表示(100 * RESULT:0)%卖出了
	MONEY += 100 * RESULT:0
ELSE
	RETURN 0
ENDIF
RESTART

;全收藏表示
;参数	ARG:0	表示对象	0=收藏	,1=手持
;		ARG:1	表示种类	0=通常表示		,1=选择
;返回值	RESULT:0	总数
;		RESULT:1	所有种类数
@DISPLAY_ALL_COLLECTION(ARG:0=0,ARG:1=0)
#DIM 对象
#DIM 总数
#DIM 所有种类数
#DIM 着用可能数

VARSET RESULT
总数 = 0
所有种类数 = 0
着用可能数 = 0

FOR 对象,1,CHARANUM
	CALL CHECK_COLLECTION(对象,ARG:0)
	;一枚も胖次持ってない角色でも着用可能数をカウントするため
	IF FLAG:胖次收集状况TYPEB && ARG:0 == 0
		PRINTL
		PRINTFORML 【%NAME_OUTPUT_TRANSLATION("CALLNAME", 对象)%】
		CALL DISPLAY_COLLECTION_B(对象, ARG:0, ARG:1)
		总数 += RESULT:0
		所有种类数 += RESULT:1
		着用可能数 += RESULT:2
	ELSEIF RESULT:0 > 0
		PRINTL
		PRINTFORML 【%NAME_OUTPUT_TRANSLATION("CALLNAME", 对象)%】
		CALL DISPLAY_COLLECTION(对象, ARG:0, ARG:1)
		总数 += RESULT:0
		所有种类数 += RESULT:1
	ENDIF
NEXT
RETURN 总数, 所有种类数, 着用可能数

;全收藏升顺表示
@DISPLAY_SORT_ALL_COLLECTION
#DIM 对象
#DIM 总数
#DIM 所有种类数
#DIM 着用可能数
#DIM 未所持数
;本当は999ではなくCHARANUM使いたい
#DIM 未所持数配列,999
#DIM 角色配列,999
未所持数配列:0 = -1
VARSET RESULT
总数 = 0
所有种类数 = 0
着用可能数 = 0

SIF !FLAG:胖次收集状况TYPEB
	RETURN

FOR 对象,1,CHARANUM
	CALL CHECK_COLLECTION(对象)
	未所持数 = RESULT:2 - RESULT:1
	;0入ってるとARRAYMSORTが机能しないので-1を代わりに入れる
	未所持数配列:对象 = 未所持数 ? 未所持数 # -1
	角色配列:对象 = 对象
NEXT
ARRAYMSORT 未所持数配列,角色配列
FOR 对象,1,CHARANUM
	PRINTL
	PRINTFORML 【%NAME_OUTPUT_TRANSLATION("CALLNAME", (角色配列:对象))%】
	CALL DISPLAY_COLLECTION_B(角色配列:对象)
	总数 += RESULT:0
	所有种类数 += RESULT:1
	着用可能数 += RESULT:2
NEXT
RETURN 总数, 所有种类数, 着用可能数

;收藏表示
;参数	对象	表示对象ID
;		ARG:0	表示对象	0=收藏	,1=手持
;		ARG:1	表示种类	0=通常表示		,1=选择
;返回值	RESULT:0	总数
;		RESULT:1	所有种类数
@DISPLAY_COLLECTION(对象,ARG:0=0,ARG:1=0)
#DIM 对象
#DIM 种类
#DIM 所有数
#DIM 改行用
#DIM 总数
#DIM 所有种类数
#DIM 下半身内衣确认保存
#DIM 睡衣下半身内衣确认保存

VARSET RESULT
总数 = 0
所有种类数 = 0
改行用 = 0
下半身内衣确认保存 = CFLAG:对象:下半身内衣确认
睡衣下半身内衣确认保存 = CFLAG:对象:睡衣下半身内衣确认

FOR 种类,0,MAXPANTS
	IF ARG:0 == 0
		所有数 = CFLAG:对象:(种类 + 胖次管理)
	ELSE
		;所有数 = STRCOUNT(CSTR:MASTER:12,CALLNAME:对象 + TOSTR(种类) + "/")
		所有数 = PANTS_TEMP:对象:种类
		IF CFLAG:对象:(种类 + 胖次管理) == 0
			SETCOLOR COLOR("PINK")
		ELSE
			RESETCOLOR
		ENDIF
	ENDIF
	IF 所有数
		总数 += 所有数
		所有种类数 ++
		改行用 ++
		SIF LINEISEMPTY()
			PRINTFORM 　
		IF ARG:1 == 0
			PRINTFORM %PANTSNAME(种类,对象),22,LEFT%{所有数,3}枚　
		ELSE
			LOCALS = [%PANTSNAME(种类,对象)%]
			LOCALS = %LOCALS,24,LEFT%{所有数,3}枚
			PRINTBUTTON LOCALS,(对象 * 100 + 种类)
			PRINTS "  "
		ENDIF
		IF 改行用 % 4 == 0 && 改行用
			PRINTL
			改行用 = 0
		ENDIF
	ENDIF
NEXT
SIF 改行用
	PRINTL

CFLAG:对象:下半身内衣确认 = 下半身内衣确认保存
CFLAG:对象:睡衣下半身内衣确认 = 睡衣下半身内衣确认保存

RESETCOLOR
RETURN 总数, 所有种类数

;收藏消费
;参数	ARG:0	消费对象种类	-1=1枚残して消费
;返回值	RESULT:0	消费数
@SPEND_COLLECTION(ARG=-1)
#DIM 对象
#DIM 种类
#DIM 所有数

VARSET RESULT

IF ARG == -1
	FOR 对象,1,CHARANUM
		FOR 种类,0,MAXPANTS
			所有数 = CFLAG:对象:(种类 + 胖次管理)
			IF 所有数 > 1
				RESULT:0 += (所有数 - 1)
				CFLAG:对象:(种类 + 胖次管理) = 1
			ENDIF
		NEXT
	NEXT
ELSE
	对象 = ARG / 100
	种类 = ARG % 100
	IF CFLAG:对象:(种类 + 胖次管理) >= 1
		RESULT:0 = 1
		CFLAG:对象:(种类 + 胖次管理) -= 1
	ENDIF
ENDIF

RETURN (RESULT:0)

;收藏枚数确认
;参数	ARG:0		对象
;参数	ARG:1		表示对象	0=收藏	,1=手持
;返回值	RESULT:0	总数
;		RESULT:1	种类数
;		RESULT:2	着用可能数
@CHECK_COLLECTION(对象, ARG:1=0)
#DIM 对象
#DIM 种类
#DIM 所持数

VARSET RESULT

FOR 种类,0,MAXPANTS
	IF ARG:1 == 0
		所持数 = CFLAG:对象:(种类 + 胖次管理)
	ELSE
		所持数 = PANTS_TEMP:对象:种类
	ENDIF
	IF 所持数
		RESULT:0 += 所持数
		RESULT:1 ++
		RESULT:2 ++
	ELSEIF GET_INT(对象, "下半身内衣_ずらし可能", 种类, "收藏表示可能")
		RESULT:2 ++
	ENDIF
NEXT

RETURN RESULT:0,RESULT:1,RESULT:2

;手持の收藏1枚消费(DISPLAY_COLLECTION(1,1)と并用)
;参数	ARG:0	消费对象种类
;返回值	RESULT:0	消费判定
@SPEND_HAVE_COLLECTION(ARG)
#DIM 对象
#DIM 种类
#DIM 所有数
#DIM 消费フラグ

VARSET LOCALS
VARSET RESULT
所有数 = 0
FOR 对象,1,CHARANUM
	FOR 种类,0,MAXPANTS
		;LOCALS:0 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 对象)%{种类}/
		;所有数 = STRCOUNT(CSTR:MASTER:12, LOCALS:0)
		;IF 所有数
		IF PANTS_TEMP:对象:种类
			消费フラグ = 0
			IF ARG != 0 && ARG == (对象 * 100 + 种类)
				消费フラグ = 1
				RESULT:0 = 1
				;胖次フラグ
				CFLAG:对象:293 = 0
				PANTS_TEMP:对象:种类 -= 1
			ENDIF
			;LOCALS:1 = %LOCALS:1 + LOCALS:0 * (所有数 - 消费フラグ)%
		ENDIF
		SIF PANTS_TEMP:对象:种类
			所有数 += PANTS_TEMP:对象:种类
	NEXT
NEXT
SIF !所有数
	FLAG:胖次输送 = 0
;CSTR:MASTER:12 = %LOCALS:1%

RETURN (RESULT:0)

;收藏表示 TypeB
;取得可能な胖次を全部显示
;参数	对象	表示对象ID
;返回值	RESULT:0	总数
;	RESULT:1	所有种类数
;	RESULT:2	着用可能数
@DISPLAY_COLLECTION_B(对象,ARG:0=0,ARG:1=0)
#DIM 对象
#DIM 种类
#DIM 所有数
#DIM 改行用
#DIM 总数
#DIM 下半身内衣确认保存
#DIM 睡衣下半身内衣确认保存
#DIM 所有种类数
#DIM 着用可能数
#DIM 条件あり
#DIMS 条件

VARSET RESULT
总数 = 0
下半身内衣确认保存 = CFLAG:对象:下半身内衣确认
睡衣下半身内衣确认保存 = CFLAG:对象:睡衣下半身内衣确认
所有种类数 = 0
改行用 = 0
着用可能数 = 0

;男性用例外处理
;一応入れたけどこれ实际に动かすことあるのか？
IF TALENT:对象:性别  == 2
	FOR 种类,0,MAXPANTS
		SIF !GET_INT(对象, "下半身内衣_ずらし可能", 种类, "男性内衣")
			CONTINUE
		IF 所有数 > 0
			总数 += 所有数
			所有种类数 ++
		ELSE
			SETCOLOR C_GRAY
		ENDIF
		着用可能数 ++
		PRINTL
		SIF LINEISEMPTY()
			PRINTFORM 　
		PRINTFORM %COLLECTION_B_PANTS_STR(对象, 种类, 所有数)%
		RESETCOLOR
		IF 改行用 % 4 == 0 && 改行用
			PRINTL
			改行用 = 0
		ENDIF
	NEXT
	SIF 改行用
		PRINTL
	RETURN 总数, 所有种类数,着用可能数
ENDIF

;以后、女性とふた
FOR 种类,0,MAXPANTS
	所有数 = CFLAG:对象:(种类 + 胖次管理)
	IF 所有数
		总数 += 所有数
		所有种类数 ++
		着用可能数 ++
		改行用 ++
		SIF 种类 == 18
			SETCOLOR C_H_PINK
	ELSE
		SIF !GET_INT(对象, "下半身内衣_ずらし可能", 种类, "收藏表示可能")
			CONTINUE
		着用可能数 ++
		改行用 ++
		SETCOLOR C_GRAY
	ENDIF
	SIF LINEISEMPTY()
		PRINTFORM 　
	PRINTFORM %COLLECTION_B_PANTS_STR(对象, 种类, 所有数)%
	RESETCOLOR
	IF 改行用 % 4 == 0 && 改行用
		PRINTL
		改行用 = 0
	ENDIF
NEXT
SIF 改行用
	PRINTL

CFLAG:对象:下半身内衣确认 = 下半身内衣确认保存
CFLAG:对象:睡衣下半身内衣确认 = 睡衣下半身内衣确认保存

RETURN 总数, 所有种类数,着用可能数



;收藏表示文字列
@COLLECTION_B_PANTS_STR(对象, 种类, 所有数)
#FUNCTIONS
#DIM  对象
#DIM  种类
#DIM  所有数
#DIMS 条件
#DIMS 胖次文字列
IF 所有数
	胖次文字列 '= @"%PANTSNAME(种类,对象),22,LEFT%{LIMIT(所有数, 0, 999),3}枚" + "　"
ELSE
	条件 '= GET_STR(对象, "下半身内衣_ずらし可能", 种类, "收藏解禁条件")
	IF 种类 == 18
		条件 '= "？？？？？"
		;付与文字列は「XXX枚　」で全角3+半角1
		胖次文字列 '= @"%条件,22,LEFT%" + " " + "　" * 3
	ELSEIF STRLENS(条件)
		胖次文字列 '= @"%条件,22,LEFT%" + " " + "　" * 3
	ELSE
		胖次文字列 '= @"%PANTSNAME(种类,对象),22,LEFT%" + " " + "　" * 3
	ENDIF
ENDIF
RETURNF 胖次文字列
