;-------------------------------------------------
;Eratoho The World 4.920用 露米娅Moe版口上  
;-------------------------------------------------
;※※※※※※※※※※※※※※※※※※※※※※※※※
;自定义函数系Customuze
;※※※※※※※※※※※※※※※※※※※※※※※※※
;-------------------------------------------------
;-------------------------------------------------
;口上作者及两位代码协力者制作的一些小函数
;已经分不清哪些是哪些人写的了……
;函数的编辑者是Moe，比利和DOKU
;将原先自制的函数都整合过来了
;嗯，大概是都看得懂的写法
;==================================================

;!FIRSTTIME("ARGS")似乎可以判断是否触发过某个FIRSTTIME

;
;这些函数可以按口上设置，但是custom_button_name、custom_talent等变量是角色变量。
;有多个口供的场合，如果盖上ID的话，会覆盖其他人的口上，所以要注意鉴别CFLAG:X:998是否是自己的口上编号。
;
;该部分是4.920之后更新的，更多的能给予口上自定义的接口
;暂未翻译
;==================================================
;多周目继承用函数
;CALL的位置在FLAGSETTING
@K12_1_DUOZHOUMUJICHENG
IF ONCE("RUMIA_1_LOST_TIME_AND_MEMORY")
	SIF (TALENT:12:7 && (CFLAG:12:4 >= 1000)) || CFLAG:12:K12C_1_朋友_2
		MOE_RUMIA_RESOUND:1 = 1
		;PRINTFORML 【人见人爱的好孩子！】: 不再袭击人类，是人见人爱的好孩子！
	SIF (CFLAG:12:K12C_1_已上学) || CFLAG:12:K12C_1_已上学_2
		MOE_RUMIA_RESOUND:2 =1
		;PRINTFORML 【寺子屋的妖怪学生！】：会去寺子屋上学，而且学习成绩还不错！
	SIF (CFLAG:12:K12C_1_已买FUMO) || CFLAG:12:K12C_1_已买FUMO_2
		MOE_RUMIA_RESOUND:3 = 1
		;PRINTFORML 【会打工的妖怪女孩！】：会自己赚点小钱，打猎采集卖钱，帮⑨卖冰淇淋，帮米斯琪抓八目鳗之类的，都可以！
	SIF (CFLAG:12:K12C_1_笨蛋组 ) || CFLAG:12:K12C_1_笨蛋组_2
		MOE_RUMIA_RESOUND:4 =1 
		;PRINTFORML 【笨蛋组合的小太阳！】：跟笨蛋组交际更加密切，是永远的好朋友！
	SIF (CFLAG:12:K12C_1_EX开启 ) || CFLAG:12:K12C_1_EX开启_2
		MOE_RUMIA_RESOUND:5 = 1
ENDIF 
;==================================================
;多周目转换用函数
@K12_1_DUOZHOUMUZHUANHUAN
;应该没人多周目还改名字吧
;懒得整名字继承了
;素质变更
#DIM WILL_RETURN_WHAT = 0
CALL MOE_RUMIA_TALENT_CHANGE 
IF MOE_RUMIA_RESOUND:1
	;二周目辨别函数
	CFLAG:12:K12C_1_二周目 = 1
	CFLAG:12:K12C_1_朋友_2 = 1
	;这两个似乎用处不是很大……故更换为了390生活时间补正
	CFLAG:12:390 = -240
	;CFLAG:12:355 = 420
	;CFLAG:12:354 = 1320
	;话术等级C，经验
	ABL:12:41 = 2
	EXP:12:71 = 77
	;清扫等级C，经验
	ABL:12:40 = 2
	EXP:12:70 = 77
	;教养等级C，经验
	ABL:12:43 = 2
	EXP:12:75 = 77
	;料理等级C，经验
	ABL:12:44 = 2 
	EXP:12:76 = 77
	;天生亲密1
	ABL:12:9 += 1
	SETBIT WILL_RETURN_WHAT,1
ENDIF
IF MOE_RUMIA_RESOUND:2
	CFLAG:12:K12C_1_已上学_2 = 1
	;教养等级B，经验
	ABL:12:43 = 3
	EXP:12:75 = 200
	SETBIT WILL_RETURN_WHAT,2
ENDIF
IF MOE_RUMIA_RESOUND:3
	CFLAG:12:K12C_1_已买FUMO_2 = 1
	;钓鱼Lv2
	TALENT:12:47 = 2
	EXP:12:82 = 77
	;采集Lv3
	TALENT:12:48 = 3
	EXP:12:84 = 200
	;调和Lv1
	TALENT:12:49 = 1
	EXP:12:85 = 20
	SETBIT WILL_RETURN_WHAT,3
ENDIF
IF MOE_RUMIA_RESOUND:4
	CFLAG:12:K12C_1_笨蛋组_2 = 1
	;摧毁自尊心低
	TALENT:12:13 = 0
	;相性
	RELATION:12:28 = 120
	RELATION:12:27 = 120
	RELATION:12:13 = 120
	RELATION:12:14 = 120
	SETBIT WILL_RETURN_WHAT,4
ENDIF
IF MOE_RUMIA_RESOUND:5
	CFLAG:12:K12C_1_EX开启_2 = 1
	;战斗能力
	ABL:12:42 = 6 
	EXP:12:74 = 21 
	SETBIT WILL_RETURN_WHAT,5
ENDIF
RETURN WILL_RETURN_WHAT
;==========================================================
;
;施工中……
;
;==========================================================
;怕太阳函数，FLAGSETTING里面CALL的部分
;==========================================================
@MOE_RUMIA_SUN_BURN_IN_FLAGSETTING

IF  ( (TIME > 360) && (TIME < 1080) ) && (CFLAG:12:K12C_1_晒伤设定 != 1) && (TIME - TCVAR:12:K12T_1_上次晒伤时间 > 5) 
	CALL MOE_RUMIA_SUN_BURN() 
	SIF ( CFLAG:12:K12C_1_朋友 || CFLAG:12:K12C_1_朋友_2 )	&& OUTROOF(CFLAG:12:300);解锁了雨伞用法
	TEQUIP:12:201 = 1
ENDIF 

;==========================================================
;宵暗妖怪怕太阳函数
;还原原设的，露米娅不适应光线的设定……
;CFLAG:12:K12C_1_晒伤设定 记录是否使用晒伤设定，0为削弱强度，2为原设强度
;TCVAR:12:K12T_1_当日总晒伤值	当日已经被扣除的总体力
;TCVAR:12:K12T_1_上次晒伤时间	本函数内记录的上次时间变化
;==========================================================
@MOE_RUMIA_SUN_BURN()
#DIM SUN_STRENGTH	;单次被太阳晒伤的程度
#DIM SUN_STRENGTH_REALITY	;实际上需要转换成这个值（削弱的情况
#DIM TOTAL_SUN_BRUN;被太阳晒得太多次的程度	

SUN_STRENGTH = 0 
SUN_STRENGTH_REALITY = 0 

SIF ONCE("MOE_RUMIA_SUN_BURN_FIRST")
TCVAR:12:K12T_1_上次晒伤时间 = TIME 
;-------------------------------------------------

;太阳最盛的时候……
IF ( (TIME > 720) && (TIME < 960) ) 
	SUN_STRENGTH += 100
ELSEIF  ( (TIME > 600) && (TIME < 720) ) ||  ( (TIME > 960) && (TIME < 1020) ) 
	SUN_STRENGTH += 75
ELSEIF ( (TIME > 480) && (TIME < 600) ) ||  ( (TIME > 1020) && (TIME < 1050) ) 
	SUN_STRENGTH += 50
ELSE 
	SUN_STRENGTH += 30
ENDIF 
;-------------------------------------------------

;季节不一样的时候
IF DAY:2 == 2
	SUN_STRENGTH += 20
ELSEIF DAY:2 == 4
	SUN_STRENGTH -= 10
ENDIF

;-------------------------------------------------

;天气不一样的时候
IF GROUPMATCH(DAY:5 , 8 , 9 , 10 , 11 , 12 )	;下雪天
	SUN_STRENGTH -= 15
ELSEIF GROUPMATCH(DAY:5 , 3 , 4 , 13 )	;下雨或冰雹
	SUN_STRENGTH -= 10
ELSEIF DAY:5 == 3 	;多云
	SUN_STRENGTH += 5
ELSE 		;晴天或晴朗
	SUN_STRENGTH += 20
ENDIF 
;-------------------------------------------------

;其他的情况
IF SUN_STRENGTH > 0

	;异常气象：烈日的时候
	SIF FLAG:89 == 13
	SUN_STRENGTH *= 3

	;非朋友约会的时候
	SIF !(CFLAG:12:K12C_1_朋友 || CFLAG:12:K12C_1_朋友_2) && FLAG:73 != 12
	SUN_STRENGTH = 0

	;自由行动黑球的时候
	SIF TCVAR:12:K12T_1_带进黑球状态
	SUN_STRENGTH = 0

	;一周目没有见面的时候
	SIF (CFLAG:12:300 != CFLAG:0:300) && !CFLAG:12:K12C_1_二周目
	SUN_STRENGTH /= 4	

	;二周目的时候
	SIF CFLAG:12:K12C_1_二周目
	SUN_STRENGTH /= 5

	;在室内的时候
	SIF !OUTROOF(CFLAG:12:300)
	SUN_STRENGTH /= 5

	;在地底的时候
	SIF GET_MAPID(CFLAG:12:300) == 9
	SUN_STRENGTH /= 2

	;在冥界的时候
	SIF GET_MAPID(CFLAG:12:300) == 6
	SUN_STRENGTH /= 5

	;有共用雨伞的时候
	SIF TEQUIP:12:201
	SUN_STRENGTH /= 2

	;在家的时候，与室内可以叠加，极大地削弱光线
	IF CFLAG:12:300 == CFLAG:12:311 
		SUN_STRENGTH /= 3
		SIF SUN_STRENGTH  > 29 
			SUN_STRENGTH = 20 
	ENDIF  

	;非约会且不在主地图的时候
	SIF (CFLAG:12:300 / 100) != MAIN_MAP && FLAG:73 != 12
	SUN_STRENGTH /= 2

ELSE 
	SUN_STRENGTH = 0
ENDIF 
;-------------------------------------------------

IF SUN_STRENGTH
	;单日内越晒越受不了
	SELECTCASE TCVAR:12:K12T_1_当日总晒伤值 	;当日被晒掉的总体力
		CASE IS > 3000
			TIMES SUN_STRENGTH , 3.21
		CASE IS > 2000
			TIMES SUN_STRENGTH , 2.43
		CASE IS > 1500
			TIMES SUN_STRENGTH , 2.01
		CASE IS > 1000
			TIMES SUN_STRENGTH , 1.72
		CASE IS > 750
			TIMES SUN_STRENGTH , 1.45
		CASE IS > 500
			TIMES SUN_STRENGTH , 1.27
		CASE IS > 250
			TIMES SUN_STRENGTH , 1.10
		CASEELSE
	ENDSELECT
	;数值随机化
	IF SUN_STRENGTH > 100 
		SUN_STRENGTH *= (90 + RAND:20)
		SUN_STRENGTH /= 100 
	ELSEIF SUN_STRENGTH > 50 
		SUN_STRENGTH *= (45 + RAND:10)
		SUN_STRENGTH /= 50
	ELSE 
		SUN_STRENGTH *= (10 + RAND:3)
		SUN_STRENGTH /= 10
	ENDIF 
	;太低的时候就不扣了
	SIF SUN_STRENGTH < 7
		SUN_STRENGTH = 0
ENDIF 

;次数足够多的话会产生耐受
SELECTCASE CFLAG:12:K12C_1_晒伤总次数 
	CASE IS > 10000 
		TIMES SUN_STRENGTH,0.10
	CASE IS > 7500
		TIMES SUN_STRENGTH,0.21
	CASE IS > 4500
		TIMES SUN_STRENGTH,0.35
	CASE IS > 2500
		TIMES SUN_STRENGTH,0.50
	CASE IS > 1500
		TIMES SUN_STRENGTH,0.65
	CASE IS > 1000
		TIMES SUN_STRENGTH,0.72
	CASE IS > 750
		TIMES SUN_STRENGTH,0.78
	CASE IS > 300
		TIMES SUN_STRENGTH,0.85
	CASE IS > 100
		TIMES SUN_STRENGTH,0.90
ENDSELECT
;-------------------------------------------------

;削弱状态下，将大大减少……
IF !CFLAG:12:K12C_1_晒伤设定
	IF SUN_STRENGTH > 300 
		SUN_STRENGTH = 100 
	ELSE 
		SUN_STRENGTH /= 3
	ENDIF 
ENDIF 
;-------------------------------------------------

SUN_STRENGTH_REALITY = SUN_STRENGTH

;SUN_STRENGTH此时还是单位值，
;-------------------------------------------------

;检测时间是否有发生变化
IF SUN_STRENGTH_REALITY && TIME > TCVAR:12:K12T_1_上次晒伤时间

	IF TIME - TCVAR:12:K12T_1_上次晒伤时间 > 5	

		;记录当前时间段被晒掉的单位量
		SUN_STRENGTH_REALITY *= ( TIME - TCVAR:12:K12T_1_上次晒伤时间 )
		SUN_STRENGTH_REALITY /= 5

		;记录总次数
		LOCAL = ((TCVAR:12:K12T_1_上次晒伤时间 - TIME) / 5 )
		CFLAG:12:K12C_1_晒伤总次数 += LOCAL

		;记录时间变化
		TCVAR:12:K12T_1_上次晒伤时间 = TIME 

		;记录当日被晒掉的总体力
		TCVAR:12:K12T_1_当日总晒伤值 += SUN_STRENGTH_REALITY

		;只有目标为露米娅时才会触发的口上
		IF  (CFLAG:12:300 == CFLAG:0:300)  && TFLAG:104 == 12
			IF FIRSTTIME("MOE_RUMIA_FIRST_GET_SUN_BURN_MESSAGE")
				CALL MOE_RUMIA_SUN_BURN_MESSAGE(1,SUN_STRENGTH)
			ELSEIF BASE:12:1	;有气力就扣除气力
				CALL MOE_RUMIA_SUN_BURN_MESSAGE(2,SUN_STRENGTH)
			ELSE 	;没有气力就扣除体力
				CALL MOE_RUMIA_SUN_BURN_MESSAGE(3,SUN_STRENGTH)
			ENDIF 
		ENDIF 

		;扣除对应值，分开使用两种变量就可以保持旁白的严重性，与实际上扣除的不会冲突……
		IF (CFLAG:12:300 == CFLAG:0:300) && TFLAG:104 == 12
			IF (BASE:12:1 + 20)  > SUN_STRENGTH_REALITY ;有气力就扣除气力
				IF SUN_STRENGTH_REALITY > 30
					;修正为单位值
					IF SUN_STRENGTH > 200
						CALL RECOVER(12 , -SUN_STRENGTH_REALITY, "气力", "被阳光灼伤了")
					ELSEIF SUN_STRENGTH > 100
						CALL RECOVER(12 , -SUN_STRENGTH_REALITY, "气力", "被阳光晒到了")
					ELSEIF SUN_STRENGTH > 30
						CALL RECOVER(12 , -SUN_STRENGTH_REALITY, "气力", "不适应阳光")
					ENDIF 
					WAIT 
				ELSE 
					BASE:12:1 -= SUN_STRENGTH_REALITY
				ENDIF 
			;ELSEIF BASE:12:1 < SUN_STRENGTH_REALITY
			;	CALL RECOVER(12 , -BASE:12:1, "气力", "被阳光灼伤了")
			ELSEIF BASE:12:0 > SUN_STRENGTH_REALITY ;没气力就扣除体力
				IF SUN_STRENGTH_REALITY > 50
					;修正为单位值
					IF SUN_STRENGTH > 200
						CALL RECOVER(12 , -SUN_STRENGTH_REALITY, "体力", "被阳光灼伤了")
					ELSEIF SUN_STRENGTH > 100
						CALL RECOVER(12 , -SUN_STRENGTH_REALITY, "体力", "被阳光晒到了")
					ELSEIF SUN_STRENGTH > 50
						CALL RECOVER(12 , -SUN_STRENGTH_REALITY, "体力", "不适应阳光")
					ENDIF 
					WAIT
				ELSE 
					BASE:12:0 -= SUN_STRENGTH_REALITY
				ENDIF 
			ELSEIF BASE:12:0 < SUN_STRENGTH_REALITY
				CALL RECOVER(12 , -BASE:12:0, "气力", "被阳光灼伤了")
			ENDIF 	
		ELSE 	;非面对面的情况，就偷偷地扣好了
			IF (BASE:12:1 + 20 ) > SUN_STRENGTH_REALITY ;有气力就扣除气力
				BASE:12:1 -= SUN_STRENGTH_REALITY
			ELSE ;没气力就扣除体力
				BASE:12:0 -= SUN_STRENGTH_REALITY
			ENDIF 
		ENDIF 
	ENDIF 
ENDIF 
;-------------------------------------------------
;口上的部分
;-------------------------------------------------
@MOE_RUMIA_SUN_BURN_MESSAGE(ARG,ARG:1=0)
CALL M_KOJO_Moe_COLOR_K12_SET(MOE_RUMIA)	
;ARG的值是差分识别
;ARG:1 的值是扣除值
IF ARG == 1 
	;初次触发该事件
ELSEIF ARG == 2 
	;开始烧气力
	IF ARG > 100 
		PRINTDATAL 
			DATAFORM 「晕乎乎的哇……」
			DATAFORM 「诶？脑袋……有点晕……」
			DATAFORM 「好晕哇……」
			DATAFORM 「不太舒服呢……」
			DATAFORM 「呜……」
		ENDDATA
		IF !CFLAG:12:317 
			PRINTDATADL 
				DATAFORM 露米娅摇摇晃晃地走着……
				DATAFORM 露米娅努力地避让着阳光……
				DATAFORM 露米娅有些昏昏沉沉的样子……
			ENDDATA
		ENDIF 
	ELSE 
		PRINTDATAL 
			DATAFORM 「不太舒服呢……」
			DATAFORM 「呜……」
			DATAFORM 「好热……」
			DATAFORM 「好亮啊……」
			DATAFORM 「有太阳呢……」
		ENDDATA
	ENDIF 
ELSEIF ARG == 3
	;开始烧体力
	IF ARG > 100 
		PRINTDATAL 
			DATAFORM 「晕乎乎的哇……」
			DATAFORM 「诶？脑袋……有点晕……」
			DATAFORM 「好晕哇……」
			DATAFORM 「不太舒服呢……」
			DATAFORM 「呜……」
		ENDDATA
		IF !CFLAG:12:317 
			PRINTDATADL 
				DATAFORM 露米娅摇摇晃晃地走着……
				DATAFORM 露米娅努力地避让着阳光……
				DATAFORM 露米娅有些昏昏沉沉的样子……
			ENDDATA
		ENDIF 
	ELSE 
		PRINTDATAL 
			DATAFORM 「不太舒服呢……」
			DATAFORM 「呜……」
			DATAFORM 「好热……」
			DATAFORM 「好亮啊……」
			DATAFORM 「有太阳呢……」
		ENDDATA
	ENDIF 
ENDIF 
RESETCOLOR 
;==================================================
;护主之黑暗 函数
;未启用惩罚机制时，失败两次后第三次不再取消
;我不希望看到有人拿这个东西说事……
;看到就原地反抗三
@MOE_RUMIA_DARKNESS_PROTECT
;烂醉状态允许的
IF ONCE("RUMIA_DARKNESS_NORMAL_PROTECT_3") && ( FLAG:70 || ( CFLAG:12:313 || CFLAG:12:316 ) || CFLAG:12:313) && !( CFLAG:12:334 && TALENT:12:7 ) && !CFLAG:12:K12C_1_惩罚内容 && !( FLAG:70 && TALENT:12:7 )
	CALL DOKU_COMF_FAIL_FOR_Moe_K12
	CALL M_KOJO_Moe_MESSAGE_COM_K12_350_END_3
ELSEIF  ( FLAG:70 || ( CFLAG:12:313 || CFLAG:12:316 ) || CFLAG:12:313) && !( CFLAG:12:334 && TALENT:12:7 ) && !CFLAG:12:K12C_1_惩罚内容 && !( FLAG:70 && TALENT:12:7 ) && !( TALENT:12:3 || TALENT:12:8 )
	CALL DOKU_COMF_FAIL_FOR_Moe_K12
	CALL M_KOJO_Moe_MESSAGE_COM_K12_350_END_3
ELSE 
	IF ONCE("RUMIA_DARKNESS_NORMAL_PROTECT_2") && ( FLAG:70 || ( CFLAG:12:313 || CFLAG:12:316 ) || CFLAG:12:313) && !( CFLAG:12:334 && TALENT:12:7 ) && !CFLAG:12:K12C_1_惩罚内容 && !( FLAG:70 && TALENT:12:7 )
		CALL DOKU_COMF_FAIL_FOR_Moe_K12
		CALL M_KOJO_Moe_MESSAGE_COM_K12_350_END_2
	ELSE 
		IF ONCE("RUMIA_DARKNESS_NORMAL_PROTECT_1") && ( FLAG:70 || ( CFLAG:12:313 || CFLAG:12:316 ) || CFLAG:12:313) && !( CFLAG:12:334 && TALENT:12:7 ) && !CFLAG:12:K12C_1_惩罚内容 && !( FLAG:70 && TALENT:12:7 )
			CALL M_KOJO_Moe_MESSAGE_COM_K12_350_END_1
		ELSE 
			TFLAG:192 = 0
		ENDIF 
	ENDIF 
ENDIF 
@MOE_RUMIA_DARKNESS_COMPLETE_PROTECT
;绝对不允许的
IF ONCE("MOE_RUMIA_DARKNESS_COMPLETE_PROTECT_3") && ( FLAG:70 || TCVAR:12:145 || ( CFLAG:12:313 || CFLAG:12:316 ) || CFLAG:12:313) && !( CFLAG:12:334 && TALENT:12:7 ) && !CFLAG:12:K12C_1_惩罚内容 
	CALL DOKU_COMF_FAIL_FOR_Moe_K12
	CALL M_KOJO_Moe_MESSAGE_COM_K12_350_END_3
ELSEIF ( FLAG:70 || TCVAR:12:145 || ( CFLAG:12:313 || CFLAG:12:316 ) || CFLAG:12:313) && !( CFLAG:12:334 && TALENT:12:7 ) && !CFLAG:12:K12C_1_惩罚内容 && !( TALENT:12:3 || TALENT:12:8 )
	CALL DOKU_COMF_FAIL_FOR_Moe_K12
	CALL M_KOJO_Moe_MESSAGE_COM_K12_350_END_3
ELSE 
	IF ONCE("MOE_RUMIA_DARKNESS_COMPLETE_PROTECT_2") && ( FLAG:70 || TCVAR:12:145 || ( CFLAG:12:313 || CFLAG:12:316 ) || CFLAG:12:313) && !( CFLAG:12:334 && TALENT:12:7 ) && !CFLAG:12:K12C_1_惩罚内容 
		CALL DOKU_COMF_FAIL_FOR_Moe_K12
		CALL M_KOJO_Moe_MESSAGE_COM_K12_350_END_2
	ELSE 
		IF ONCE("MOE_RUMIA_DARKNESS_COMPLETE_PROTECT_1") && ( FLAG:70 || TCVAR:12:145 || ( CFLAG:12:313 || CFLAG:12:316 ) || CFLAG:12:313) && !( CFLAG:12:334 && TALENT:12:7 ) && !CFLAG:12:K12C_1_惩罚内容 
			CALL M_KOJO_Moe_MESSAGE_COM_K12_350_END_1
			TFLAG:192 = 0
		ELSEIF  ( FLAG:70 || TCVAR:12:145 || ( CFLAG:12:313 || CFLAG:12:316 ) || CFLAG:12:313) && !( CFLAG:12:334 && TALENT:12:7 ) && !CFLAG:12:K12C_1_惩罚内容 
			TFLAG:192 = 0
		ELSE 
			TFLAG:192 = 0
		ENDIF 
	ENDIF 
ENDIF 
@M_KOJO_Moe_MESSAGE_COM_K12_350_END_3
;护主之黑暗
;我在写什么啊，明明直接禁掉就好了嘛，为什么要加这一堆东西啊
;明明要做坏事的人就一定会做……写这一堆东西，未必还真的能帮人改邪归正不是？
PRINTFORML 
PRINTFORMW 露米娅的身体周围，萦绕着丝丝黑暗
AWAIT 777
IF FLAG:70
	PRINTFORM 在停滞的时间中，
ELSEIF TCVAR:12:145
	PRINTFORM 在露米娅不省人事的时候，
ELSEIF CFLAG:12:313
	PRINTFORM 在露米娅睡着的时候，
ENDIF 
PRINTFORMW 这黑暗仿佛有了实质一样
AWAIT 777
PRINTFORMW 想要伸手，却难以靠近分毫……
AWAIT 777
PRINTFORMW 
AWAIT 777
IF FLAG:70 
	BASE:0:8 -= 100 
ELSE 
ENDIF 
@M_KOJO_Moe_MESSAGE_COM_K12_350_END_2 
CALL M_KOJO_Moe_MESSAGE_COM_K12_350_END_3
PRINTFORMW 只能暂且放弃，带着缠绕在手掌上的丝丝黑暗收回了手
AWAIT 777
IF TALENT:12:7 
	PRINTFORMW 环着手心的黑暗很快就消散了，透过指缝看见了
ELSE 
PRINTFORMW 正想从手上拨开黑暗，却透过指缝看见了
ENDIF 
AWAIT 777
IF FLAG:70
	IF TCVAR:12:145
		PRINTFORMW 在时间停止中，露米娅满是酒气的红润小脸
	ELSEIF CFLAG:12:313
		PRINTFORMW 露米娅微微张着嘴，流着口水的睡颜定格在了这一刻
	ELSEIF TALENT:12:7 || TALENT:12:8
		PRINTFORMW 露米娅悄悄显现的幸福的微笑，正好因时停定格在这一刻
	ELSEIF TALENT:12:3 
		PRINTFORMW 露米娅悄悄瞄着你，忽而脸颊微红起来的模样
	ELSEIF CFLAG:12:K12C_1_朋友 || CFLAG:12:K12C_1_朋友_2 
		PRINTFORMW 露米娅发自内心的微笑
	ELSE 
		PRINTFORMW 黑球中伸直双臂的女孩
	ENDIF 
ELSEIF TCVAR:12:145
	PRINTFORM 露米娅满是酒气的红润小脸
ELSEIF CFLAG:12:313
	PRINTFORM 露米娅微微张着嘴，流着口水的睡颜
ENDIF 
AWAIT 777
PRINTFORML 
IF FLAG:70 
	BASE:0:8 -= 150 
ELSE 
ENDIF 
@M_KOJO_Moe_MESSAGE_COM_K12_350_END_1 
PRINTFORMW 
PRINTFORMW 终于，密布在露米娅身旁的黑暗被你撕扯殆尽
AWAIT 777
PRINTFORMW 
PRINTFORMW 不再有任何东西阻碍着你
AWAIT 777
SETCOLOR 0x323232 ;不可触及的灰
PRINTFORMW 
PRINTFORMW 但是这样有什么意思呢……
AWAIT 777
PRINTFORMW 
AWAIT 777
IF MARK:12:3  
	PRINTFORMW 明明好好道歉的话，小妖怪就不会这样了……
ELSEIF TALENT:12:7 || TALENT:12:8 
	PRINTFORMW 明明想要的话，小妖怪一定会同意的……
ELSEIF TALENT:12:3
	PRINTFORMW 明明小妖怪暗地里是喜欢你的……
ELSE
	PRINTFORMW 明明努努力就好了啊……
ENDIF 
AWAIT 777
RESETCOLOR
CFLAG:12:1108 += 1
IF FLAG:70 
	BASE:0:8 -= 200
ELSE 
ENDIF 
;==================================================
;过夜 触发函数集
;-------------------------------------------------
;从最开始就想好的过夜用设定
;一开始还需要临时增加恋慕来避免被赶出去
;现在在Moe的优化下，变成了为了换衣服赶出去
;不过，这部分代码可以变得重复利用了
;-------------------------------------------------
;相关CFLAG
;CFLAG:12:K12C_1_思慕过夜确认 强制过夜用，等于1时触发，触发期间也可更改数值，用于思慕确认
;CFLAG:12:K12C_1_强制过夜差分确认 强制过夜的差分判定值，等于3时是思慕，等于5时是夜袭
;CFLAG:12:1300 过夜时记录被更改的原住址
;CFLAG:12:K12C_1_强制过夜发生地址 思慕时记录事件发生的地址
;把露米娅送回自己家的情况，我还没设计好……
;我知道可能会出现很大的bug……但是心力有限……
;24.04.23 精简掉了原来更换初始位置的用法，现在将直接在EVENTTRAIN中强行变位置，实测可行
;24.06.06 发现非主地图会把人送进隙间，故制定非主地图的时候传送到古木的大树
;-------------------------------------------------
;过夜 前置函数
;在需要触发当日过夜的情况CALL 它就可以了
;目前的话。用在了思慕取得里面
;未来还打算用在晚上的夜袭中
;CFLAG:12:K12C_1_思慕过夜确认 ： 确认是否当日过夜,确认则值 = 1
;-------------------------------------------------
@RUMIA_OVER_NIGHT_WITH_YOU_START(ARG)
	;确定同居，则事件发生在共同的家
	IF CFLAG:12:311 == CFLAG:0:311 
		CFLAG:12:K12C_1_强制过夜发生地址 = CFLAG:MASTER:311
	;确认有无其他的人同居，如果有的话需要把露米娅抱回家，没有的话需要发生在自己家里
	ELSE 
		RESULT = 0
		FOR LOCAL,1,CHARANUM
			SIF LOCAL == 12
				CONTINUE 
			IF CFLAG:MASTER:311 == CFLAG:LOCAL:311
				RESULT = 1 
				BREAK
			ENDIF 
			;TARGET决定
		NEXT
		IF RESULT 
			IF MAIN_MAP == 5
				CFLAG:12:K12C_1_强制过夜发生地址 = CFLAG:12:311	;事件发生在露米娅家
			ELSE
				CFLAG:12:K12C_1_强制过夜发生地址 = 530	;事件发生在露米娅家，完成后会赶到古木的大树
			ENDIF 
		ELSE
			CFLAG:12:K12C_1_强制过夜发生地址 = CFLAG:MASTER:311	;事件发生在自己家
		ENDIF 
	ENDIF 
	CFLAG:12:K12C_1_思慕过夜确认 = 1	;确认事件发生
;-------------------------------------------------
;过夜 EVENTTRAIN函数
;需要在K12_BEFORETRAIN_Moe里面CALL
;目前的话。用在了思慕取得与夜袭里面
;把触发的位置调整为eventtrain，大概就不会有bug了吧？
;更换了位置，以尝试看是否能避免，露米娅会锁在隙间的bug
;CFLAG:12:K12C_1_思慕过夜确认 ： 确认当日过夜的位置，值 = 12时，在露米娅家过夜，值 = 2时，在自机家过夜
;-------------------------------------------------
@RUMIA_OVER_NIGHT_WITH_YOU_IN_EVENTTRAIN
	IF (CFLAG:12:K12C_1_强制过夜发生地址 == CFLAG:12:311)	;事件发生在露米娅家
		CFLAG:0:300 = CFLAG:12:K12C_1_强制过夜发生地址	;传送至露米娅家
	ELSE ;剧情上是送回露米娅家了
		CFLAG:12:300 = CFLAG:12:K12C_1_强制过夜发生地址	;传送至露米娅处
	ENDIF 
	;思慕早上特殊对话
    SIF FIRSTTIME("RUMIA_LIKE_YOU_GOOD_MORNING") && (CFLAG:12:K12C_1_强制过夜差分确认 == 3) && TALENT:12:8	
		CALL RUMIA_LIKE_GOODMORNING
	;夜袭后早上特殊对话？
    ;SIF FIRSTTIME("RUMIA_LIKE_YOU_GOOD_MORNING") && (CFLAG:12:K12C_1_强制过夜差分确认 == 3) && TALENT:12:8	
	SIF MAIN_MAP != CFLAG:12:311 / 100	;如果不在本地居住则复原非神社居住
		CFLAG:12:30 = 0 
	TFLAG:104 = 12	;当前目标设定为露米娅
	TCVAR:12:149 = 0 ;大早上的吼一声精神多了（确信）
    CFLAG:12:K12C_1_强制过夜差分确认 = 0 ;下次触发用
	CFLAG:12:K12C_1_强制过夜发生地址 = 0 ;下次触发用
[SKIPSTART]
;==================================================
身体检查……大函数集
;-------------------------------------------------
[SKIPEND]
;### 身体检查用函数 ###################################
;此段使用的CFLAG如下
;CFLAG:12:K12C_1_身体测定冷却 身体检查
;==================================================
;身体检查确认用函数，
;满足条件后才会CALL对应的主要函数
;身体检查触发要求：恋人，概率1/7，当前位置在露米娅家，每日一次，每七天没检查过，则有概率检查
;通过对话和身体接触可触发
;RETURN 1:满足            0:不满足 
;-------------------------------------------------
@M_KOJO_Moe_K12_BODY_MEASURE_COMFIRM
IF TALENT:12:7 && !RAND:3 && ( CFLAG:12:300 == CFLAG:12:311 ) && ONCE("RUMIA_BODY_MEASURE") && !CFLAG:12:K12C_1_身体测定冷却
	CALL M_KOJO_Moe_K12_BODY_MEASURE_MESSAGE("START")
	SIF RESULT != 1 
	CALL M_KOJO_Moe_K12_SOMATOME_TRY
	RETURN 1
ELSE 
	RETURN 0 
ENDIF 
;==================================================
;这个部分，是从原版中截出来的，用于测定露米娅身体尺寸的函数
;-------------------------------------------------
@M_KOJO_Moe_K12_SOMATOME_TRY
$RUMIA_BODY_MEASURE_AGAIN 
PRINTFORML 
PRINTFORMDL 进行%CALLNAME:12%的身体测定
PRINTL 
;CALL两遍的目的是避免原有的测定结果干扰
CALL M_KOJO_Moe_K12_CUSTOM_MAKE_SIZE(12)
CFLAG:12:351 = 1
	IF (( DAY:0 - CFLAG:12:K12C_1_恋人纪念日 ) >= 180 ) && FIRSTTIME("RUMIA_BODY_GROW_UP_TO_LITTLE_SIZE")
		;成为恋人180天后，触发长高文本
		CALL M_KOJO_Moe_K12_BODY_GROWED_TIP_MESSAGE(1,1)
	ELSEIF (( DAY:0 - CFLAG:12:K12C_1_恋人纪念日 ) >= 480 ) && FIRSTTIME("RUMIA_BODY_GROW_UP_TO_SHORT_SIZE")
		;成为恋人480天后，触发长高文本
		CALL M_KOJO_Moe_K12_BODY_GROWED_TIP_MESSAGE(2,1)
	ENDIF 
CALL M_KOJO_Moe_K12_CUSTOM_SHOW_SIZE(12)
CALL M_Moe_K12_ASK_M("再测定一次",1,"这样就行",1)
SELECTCASE RESULT
	CASE 0
		CFLAG:12:351 = 2
		CALL M_KOJO_Moe_K12_CUSTOM_MAKE_SIZE(12)
		CALL M_KOJO_Moe_K12_BODY_MEASURE_MESSAGE("TWICE")
		GOTO RUMIA_BODY_MEASURE_AGAIN 
	CASE 1
		CALL M_KOJO_Moe_K12_BODY_MEASURE_MESSAGE("END")
ENDSELECT
;==================================================
;身体尺寸生成用函数
;==================================================
[SKIPSTART]
这个部分，是从原版中截出来的，
成为恋人180天后，露米娅将成长至128cm
成为恋人480天后，露米娅将成长至480cm
在此期间，身高体重以及三围都会慢慢慢慢地上升
（参考了正常人类幼女的生长规律）
笔记
幼儿体型 默认身高1200 默认体重 230
矮小体型 默认身高1350 默认体重 328
普通体型 默认身高1500 默认体重 450
[SKIPEND]
;-------------------------------------------------
;身高・体重・3尺寸の表示处理
;-------------------------------------------------
;BASE:20 … 身高
;BASE:21 … 体重
;BASE:22 … 胸围
;BASE:23 … 腰围
;BASE:24 … 臀围
;MAXBASE:20 … 身高系数
;MAXBASE:21 … 体重系数
;MAXBASE:22 … 胸围系数
;MAXBASE:23 … 腰围系数
;MAXBASE:24 … 臀围系数
;351,身体测定
;100,体型,;-5=小人 -2=幼儿体型 -1=矮小体型 1=长身 2=巨躯 3=巨人
;身体测定フラグが立っている场合は系数を生成
;身体测定済みかつ未设定の场合も生成
;CSVに记入されていない场合の标准值は1000
;-------------------------------------------------
@M_KOJO_Moe_K12_CUSTOM_MAKE_SIZE(ARG)
#DIM 数值影响值,5
VARSET LOCAL
IF CFLAG:ARG:351 == 2 || (CFLAG:ARG:351 == 1 && (MAXBASE:ARG:20 == 0 || MAXBASE:ARG:21 == 0 || MAXBASE:ARG:22 == 0 || MAXBASE:ARG:23 == 0 || MAXBASE:ARG:24 == 0 || MAXBASE:ARG:25 == 0 ))
	FOR LOCAL, 20, 25
		MAXBASE:ARG:LOCAL = CSVBASE(ARG,LOCAL)
		SIF MAXBASE:ARG:LOCAL == 0
			MAXBASE:ARG:LOCAL = 980
		MAXBASE:ARG:LOCAL = MAXBASE:ARG:LOCAL * RAND(980, 1020) / 1000
		;这里稍微提高了一点测量器具的精度
	NEXT
ENDIF
;===身高の算出===
;基本值
IF (TALENT:ARG:2 == 2)
	BASE:ARG:20 = 1700
ELSE 
	BASE:ARG:20 = 1500
ENDIF 
BASE:ARG:20 *= MAXBASE:ARG:20 
BASE:ARG:20 /= 1000
;体型补正
IF TALENT:ARG:100 == -2
	TIMES BASE:ARG:20, 0.80
ELSEIF TALENT:ARG:100 == -1
	TIMES BASE:ARG:20, 0.90
ELSEIF TALENT:ARG:100 == 1
	TIMES BASE:ARG:20, 1.10
ELSEIF TALENT:ARG:100 == 2
	TIMES BASE:ARG:20, 1.20
ELSEIF TALENT:ARG:100 == 3
	TIMES BASE:ARG:20, 1.50
ENDIF
;-------------------------------------------------
;Moe的自定义部分 身高
;-------------------------------------------------
IF ( DAY:0 - CFLAG:12:K12C_1_恋人纪念日 ) < 180
	数值影响值:1 = 960 + (((DAY:0 - CFLAG:12:K12C_1_恋人纪念日) * 120) / 180 )
	BASE:ARG:20 *= 数值影响值:1 
	BASE:ARG:20 /= 1000
ELSEIF ( DAY:0 - CFLAG:12:K12C_1_恋人纪念日 ) < 480 
	数值影响值:1 = 1080 + (((DAY:0 - CFLAG:12:K12C_1_恋人纪念日 - 180) * 70) / 300)
	BASE:ARG:20 *= 数值影响值:1 
	BASE:ARG:20 /= 1000
ELSE 
	数值影响值:1 = 1150
	BASE:ARG:20 *= 数值影响值:1 
	BASE:ARG:20 /= 1000
ENDIF 
;-------------------------------------------------
;===腰围の算出===
;基本值
BASE:ARG:23 = BASE:ARG:20 * 40 / 100
BASE:ARG:23 = BASE:ARG:23 * MAXBASE:ARG:23 / 1000
;体型补正
IF TALENT:ARG:100 == -2
	TIMES BASE:ARG:23, 1.16 
ELSEIF TALENT:ARG:100 == -1
	TIMES BASE:ARG:23, 1.08 
ENDIF
;-------------------------------------------------
;Moe的自定义部分 腰围
;-------------------------------------------------
IF TALENT:ARG:100 == -2
	数值影响值:2 = 1120 - (((DAY:0 - CFLAG:12:K12C_1_恋人纪念日) * 40) / 180 )
	BASE:ARG:23 *= 数值影响值:2 
	BASE:ARG:23 /=  1000
ELSEIF TALENT:ARG:100 == -1
	数值影响值:2 = 1080 - (((DAY:0 - CFLAG:12:K12C_1_恋人纪念日) * 80) / 480 )
	BASE:ARG:23 *= 数值影响值:2
	BASE:ARG:23 /=  1000
ENDIF
;-------------------------------------------------
;其他补正
SIF TALENT:ARG:魅力
	TIMES BASE:ARG:23, 0.95
;===胸围の算出===
;基本值
BASE:ARG:22 = BASE:ARG:20 * 51 / 100
BASE:ARG:22 = BASE:ARG:22 * MAXBASE:ARG:22 / 1000
;胸围补正
IF TALENT:ARG:22 == -2
	TIMES BASE:ARG:22, 0.85
ELSEIF TALENT:ARG:22 == -1
	TIMES BASE:ARG:22, 0.92 
ELSEIF TALENT:ARG:22 == 1
	TIMES BASE:ARG:22, 1.08
ELSEIF TALENT:ARG:22 == 2
	TIMES BASE:ARG:22, 1.16
ENDIF
;-------------------------------------------------
;Moe的自定义部分 胸围
;-------------------------------------------------
IF TALENT:ARG:22 == -2
	数值影响值:3 = 840 + (((DAY:0 - CFLAG:12:K12C_1_恋人纪念日) * 80) / 180 )
	BASE:ARG:22 *= 数值影响值:3 
	BASE:ARG:22 /= 1000
ELSEIF TALENT:ARG:22 == -1
	数值影响值:3 = 920 + (((DAY:0 - CFLAG:12:K12C_1_恋人纪念日) * 80) / 480 )
	BASE:ARG:22 *= 数值影响值:3 
	BASE:ARG:22 /= 1000
ENDIF 
;-------------------------------------------------
;Ｂ感觉による补正
BASE:ARG:22 = BASE:ARG:22 * (1000 + MIN(ABL:ARG:3, 100)) / 1000
;其他补正
SIF TALENT:ARG:魅力
	TIMES BASE:ARG:22, 1.05
;アンダー胸围以下なら补正
BASE:ARG:22 = MAX(BASE:ARG:20 * 43 / 100, BASE:ARG:22)
;===臀围の算出===
;基本值
BASE:ARG:24 = BASE:ARG:20 * 53 / 100
BASE:ARG:24 = BASE:ARG:24 * MAXBASE:ARG:24 / 1000
;其他补正
SIF TALENT:ARG:魅力
	TIMES BASE:ARG:24, 1.05
;===体重の算出===
;基本值
BASE:ARG:21 = BASE:ARG:20 * BASE:ARG:20 / 5000
BASE:ARG:21 = BASE:ARG:21 * MAXBASE:ARG:21 / 1000
;体型补正
IF TALENT:ARG:100 == -2
	TIMES BASE:ARG:21, 0.80
ELSEIF TALENT:ARG:100 == -1
	TIMES BASE:ARG:21, 0.90
ELSEIF TALENT:ARG:100 == 1
	TIMES BASE:ARG:21, 1.10
ELSEIF TALENT:ARG:100 == 2
	TIMES BASE:ARG:21, 1.20
ELSEIF TALENT:ARG:100 == 3
	TIMES BASE:ARG:21, 1.50
ELSEIF TALENT:ARG:100 == 5
	TIMES BASE:ARG:21, 10.00
ENDIF
;-------------------------------------------------
;Moe的自定义部分 体重
;-------------------------------------------------
IF TALENT:ARG:100 == -2
	IF ( DAY:0 - CFLAG:12:K12C_1_恋人纪念日 ) < 180
		数值影响值:4 =  960 + (((DAY:0 - CFLAG:12:K12C_1_恋人纪念日) * 120) / 180 )
		BASE:ARG:21 *= 数值影响值:4 
		BASE:ARG:21 /=  1000
	ELSEIF ( DAY:0 - CFLAG:12:K12C_1_恋人纪念日 ) < 480 
		数值影响值:4 = 1080 + (((DAY:0 - CFLAG:12:K12C_1_恋人纪念日 - 180) * 70) / 300)
		BASE:ARG:21 *= 数值影响值:4
		BASE:ARG:21 /=  1000
	ELSE 
		数值影响值:4 = 1150
		BASE:ARG:21 *= 数值影响值:4
		BASE:ARG:21 /= 1000
	ENDIF 
ENDIF 
SIF ARG == 12 
	TIMES BASE:ARG:21 , 0.77 

;-------------------------------------------------
;其他补正
SIF TALENT:ARG:魅力
	TIMES BASE:ARG:21, 0.95

@M_KOJO_Moe_K12_CUSTOM_SHOW_SIZE(ARG)
#DIM L_PENIS,20 ; P尺寸计算用
;LOCAL:21 … 妊娠时の体重の计算值
;LOCAL:23 … 妊娠时の腰围の计算值
VARSET LOCAL
IF CFLAG:ARG:身体测定 == 0
	PRINTL
	PRINTFORMDL □ 尺　寸 □-------------------------------------------------------------------------
	PRINTL  未测定
ELSE
	PRINTL
	PRINT □ 
	CALL PRINTBUTTON_EX("尺寸",160,TFLAG:160)
	PRINTL  □------------------------------------------------------------------------
	IF !TFLAG:尺寸显示
		;プレイヤーの身高・体重は不明
		IF ARG == NO:0 && TALENT:ARG:性别 == 2
			PRINTFORM  身高:???cm  体重:??kg
		ELSE
			;身高
			CALL ShowScaleInCM(ARG, BASE:ARG:20, " 身高:")
			;体重
			IF TALENT:ARG:妊娠
				;身高150cmで10kg程度增加
				LOCAL:21 = BASE:ARG:体重 + (BASE:ARG:20 * BASE:ARG:20 * CFLAG:ARG:妊娠经过日数 / 2250000)
				PRINTFORM    体重:{LOCAL:21 / 10}.{LOCAL:21 % 10}\@ TALENT:ARG:体型 == -5? g# kg\@
				PRINTFORML  妊娠前({BASE:ARG:体重 / 10}.{BASE:ARG:体重 % 10}\@ TALENT:ARG:体型 == -5? g# kg\@)
			ELSE
				;小人の体重は1/10^3
				PRINTFORML    体重:{BASE:ARG:体重 / 10}.{BASE:ARG:体重 % 10}\@ TALENT:ARG:体型 == -5? g# kg\@
			ENDIF
		ENDIF
		;[男人]持ちは3尺寸は表示しない
		IF TALENT:ARG:性别 != 2
			;胸围
			CALL ShowScaleInCM(ARG, BASE:ARG:胸围, "   Ｂ:")
			PRINTFORM %LOCALS:0%
			CALL PRINT_CUPSIZE, ARG
			;腰围
			IF TALENT:ARG:妊娠
				;身高150cmで30cm程度增加
				LOCAL:23 = BASE:ARG:腰围 + (BASE:ARG:20 * CFLAG:ARG:妊娠经过日数 / 500)
				CALL ShowScaleInCM(ARG, LOCAL:23, "/ Ｗ:")
				CALL ShowScaleInCM(ARG, BASE:ARG:腰围, " 妊娠前(")
				PRINTPLAIN )/
			ELSE
				CALL ShowScaleInCM(ARG, BASE:ARG:腰围, "/ Ｗ:")
				PRINTFORM /
			ENDIF
			;臀围
			CALL ShowScaleInCM(ARG, BASE:ARG:臀围, " Ｈ:")
			PRINTFORML 
		ENDIF
		IF HAS_PENIS(ARG)
			;Ｐ
			CALL GET_PENIS_SIZE, L_PENIS, ARG
			CALL ShowScaleInCM(ARG, L_PENIS:1, " Ｐ:")
			CALL ShowScaleInCM(ARG, L_PENIS:5, " 直径：")
			CALL ShowScaleInCM(ARG, L_PENIS:8, "  （平常时:")
			CALL ShowScaleInCM(ARG, L_PENIS:7, " 直径：")
			PRINTPLAIN )
			CALL ShowScaleInCM(ARG, L_PENIS:11, " 片玉直径：")
			PRINTFORML 
		ENDIF
	ENDIF
ENDIF
PRINTL


;==================================================
;半夜夜袭 触发函数集
;-------------------------------------------------
;搬了原版游戏中的DAIRY_EV13中的DAILY_EV13函数
;AFTERTRA.ERB中的;@ONANISM函数
;CALL的位置在日记系
;因为原版那个不太好，所以打算弄简单一点
;结果越写越多，越搓越大，越做越离谱
;-------------------------------------------------
@RUMIA_DAILY_EV13_COMFIRM 
#DIM 判定无效
VARSET 判定无效
	;避免思慕和夜袭撞在一起产生奇妙的化学反应
	SIF TALENT:12:8 && FIRSTTIME("RUMIA_NIGHT_ATTACK_WHEN_LIKE_FIRST") 
		判定无效 = 1 
	;最低要求		
	SIF ( CFLAG:12:K12C_1_自记录积攒度 < 199 ) && TALENT:12:154
			判定无效 = 1 
	SIF ( CFLAG:12:K12C_1_自记录积攒度 < 599 ) && !TALENT:12:154
			判定无效 = 1 
	;检测自机有无与别人同居
	IF CFLAG:12:311 == CFLAG:0:311
	ELSE 
		FOR LOCAL,1,CHARANUM
			SIF LOCAL == 12
				CONTINUE
			IF CFLAG:LOCAL:311 == CFLAG:MASTER:311
				判定无效 = 1 
				BREAK
			ENDIF 
		NEXT
	ENDIF 
	;检测MAIN_MAP，这里面是不在主地图的情况，故追加较大的概率
	IF !CFLAG:12:30 && !TALENT:12:154
		;当日做过就不去
		IF (充填率_OF_RUMIA(TARGET,1) > 33) && (TALENT:12:8 || TALENT:12:3) 
				判定无效 = 1 
		;已经会面的情况
		ELSEIF (CFLAG:12:K12C_1_当日见面次数 > 0 ) && !( RAND:( (1201 - (CFLAG:12:K12C_1_自记录积攒度) )/ (ABL:12:9 + 1) * 2 ) + 1  > 0 ) && (TALENT:12:8 || TALENT:12:3) 
		;没会面
		ELSEIF !( RAND:( (1201 - (CFLAG:12:K12C_1_自记录积攒度) )/ (ABL:12:9 + 5) ) + 1 > 0 ) && TALENT:12:7 
		ELSEIF !( RAND:( (1201 - (CFLAG:12:K12C_1_自记录积攒度) )/ (ABL:12:9 + 1) ) + 1 > 0 ) && (TALENT:12:8 || TALENT:12:3) 
		ELSE 
				判定无效 = 1 
		ENDIF 
	ENDIF  
	;吃保底（确信
	;CFLAG:12:1725 是没有触发夜袭的天数
	SIF TALENT:12:7 && TALENT:12:3 && (CFLAG:12:1725 < 10) && !判定无效
			判定无效 = 1 
	SIF TALENT:12:7 || TALENT:12:3 && (CFLAG:12:1725 < 21) && !判定无效
			判定无效 = 1 
	SIF ( CFLAG:12:1725 < 42 ) && !判定无效
			判定无效 = 1 
	;陷落后重置保底,，其实应该优先思慕后再这样，索性多加好了
	SIF FIRSTTIME("RUMIA_NIGHT_ATTACK_WHEN_LIKE") && (TALENT:12:8 || TALENT:12:185)
		CFLAG:12:1725 = 0
	SIF FIRSTTIME("RUMIA_NIGHT_ATTACK_WHEN_ANOTHER_LIKE") && (TALENT:12:185 == 2)
		CFLAG:12:1725 = 0
	;带保底的情况下连抽
	IF  !判定无效
		FOR LOCAL,1,(CFLAG:12:1725 + 1)
			RESULT = 0
			;PRINTFORML 现在，露米娅连抽第{LOCAL}发中  ;测试用
			CALL RUMIA_DAILY_EV13_CALCULATE
			IF RESULT == 1 
				CALL RUMIA_DAILY_EV13
				BREAK
			ENDIF 
		NEXT
	ENDIF 
;==================================================
;夜袭事件选择支 概率计算函数
;-------------------------------------------------
;自己搓的概率计算
;大概有用，结合上面的保底机制大概能做到不会一直不出
;嗯，认真玩的话，后期大概是十几二十多天一次吧
;-------------------------------------------------
@RUMIA_DAILY_EV13_CALCULATE
;TALENT:12:154	育儿中
	RESULT  = 0
		IF TALENT:12:154
				IF (ABL:12:11 > 20 ) && !RAND:MAX( (1000 / ABL:12:9 + 20) , 10 ) && TALENT:12:7  ;MIN 1/20 MAX 1/12 
					RESULT = 1
				ELSEIF  (ABL:12:11 < 20) && !RAND:MAX(( 1000 / (ABL:12:9 + 15 ) ) ,15 ) && (TALENT:12:8 || TALENT:12:3)  ;MIN 1/33 MAX 1/25
					RESULT = 1
				ELSEIF  (ABL:12:11 < 10) && RAND:MAX(( 1600 / (ABL:12:9 + 10) ) , 20) && (TALENT:12:8 || TALENT:12:3) ;MIN 1/64 MAX 1/53
					RESULT = 1
				ELSEIF (ABL:12:11 < 5 ) && RAND:MAX(( 2000 / (ABL:12:9 + 5) ) , 25) && (TALENT:12:8 || TALENT:12:3)  ;MIN 1/100 MAX 1/80 
					RESULT = 1
				ELSE 
					RESULT = 0
				ENDIF 
		ELSEIF CFLAG:0:311 == CFLAG:12:311
				IF (ABL:12:11 > 20) && !RAND:MAX(((1301 - (CFLAG:12:K12C_1_自记录积攒度))/( ABL:12:9 + 1 ) ) , 10 ) && TALENT:12:7  ;MIN 1/15 MAX  1/10 
					RESULT = 1
				ELSEIF  (ABL:12:11 < 20) && !RAND:MAX(((1301 - (CFLAG:12:K12C_1_自记录积攒度))/ ( ABL:12:9 + 1) ) , 15 )  ;MIN 1/30 MAX 1/15
					RESULT = 1
				ELSEIF  (ABL:12:11 < 10) && !RAND:MAX(((1301 - (CFLAG:12:K12C_1_自记录积攒度))/ ABL:12:9 + 1 ) , 20 )  ;MIN 1/30 MAX 1/15
					RESULT = 1
				ELSEIF  (ABL:12:11 < 5 ) && !RAND:MAX(((K12C_1_朋友_2 - (CFLAG:12:K12C_1_自记录积攒度)) / ABL:12:9 + 2 ) , 25)  ;MIN 1/ 100  MAX 1/14  
					RESULT = 1
				ELSE 
					RESULT = 0
				ENDIF 
			;非同居，变为原来的差不多60%
		ELSE 
				IF (ABL:12:11 > 20) && !RAND:MAX(((1501 - (CFLAG:12:K12C_1_自记录积攒度))/( ABL:12:9 + 1 ) ) , 10) && TALENT:12:7 
					RESULT = 1
				ELSEIF  (ABL:12:11 < 20) && !RAND:MAX(((1501 - (CFLAG:12:K12C_1_自记录积攒度))/ ( ABL:12:9 + 1) ) , 15 )  
					RESULT = 1
				ELSEIF  (ABL:12:11 < 10) && !RAND:MAX(((1501 - (CFLAG:12:K12C_1_自记录积攒度))/ ABL:12:9 + 1 ) , 20 ) 
					RESULT = 1
				ELSEIF  (ABL:12:11 < 5 ) && !RAND:MAX((1351 - (CFLAG:12:K12C_1_自记录积攒度)) / (ABL:12:9 + 2) , 25) 
					RESULT = 1
				ELSE 
					RESULT = 0
				ENDIF 
		ENDIF 
	RETURN RESULT
;==================================================
;夜袭事件选择支 触发函数
;-------------------------------------------------
;这段代码是从DAIRY_EV13.ERB里面掏出来的
;用于选择并触发夜袭事件……诶嘿嘿
;虽然嘴上说是自定义化了……但是我还是没想到怎么自定义比较好
;而且完全没有以前那个看着自慰的桥段……不知道怎么加比较好
;-------------------------------------------------
@RUMIA_DAILY_EV13
	CFLAG:12:1725 = 0 
	RESETCOLOR
	PRINTFORMW 在半夜醒了过来
	PRINTFORMW 想继续睡觉但是怎么也睡不着了……
	CALL ASK_YN("散步","待在房间")
	IF RESULT
		PRINTFORMD %CALLNAME:MASTER%直到重新发困为止
		PRINTDATAD
			DATAFORM 整理・鉴赏着自己的收藏
			DATAFORM 看着美丽的胖次陷入了沉思
			DATAFORM 看着墙上的斑点发愣
		ENDDATA
		$RUMIA_APPEARED 
		PRINTFORMDW ……
		PRINTFORMDL 在寂静的夜中思索着，忽然察觉到些许风声
		PRINTFORMDW %CALLNAME:MASTER%慌忙收拾好自己的收藏，四处看了看，却没发现任何异样
		PRINTFORML 
		PRINTFORMDW ……
		PRINTFORML 
		PRINTFORMDL 只是风声吗？
		PRINTFORMDW 即使吹灭了灯……窗外依旧是死一般的漆黑……
		CALL M_Moe_K12_ASK_M("不去在意，回床上睡觉",1,"走近窗户看看",1)
		SELECTCASE RESULT 
			CASE 0 
				PRINTFORML 
				PRINTFORMDW 你躺回了床上，放松下身心，很快便睡着了
				PRINTFORML 
				IF RAND:(CFLAG:12:K12C_1_自记录积攒度) > RAND:1000 
					PRINTFORML 
					PRINTFORMDW ……
					PRINTFORMDW ……
					PRINTFORMDW ……
					PRINTFORML
					CALL M_KOJO_Moe_K12_NIGHT_ATTACK
				ELSE 
					PRINTFORMDL 似乎……有一种……无比温暖，又夹杂着醇香的感觉……
					PRINTFORMDW 不知是因为什么原因，今晚睡得很好，体力，气力和tsp都因此增加了
					CALL BUFF_BASE(MASTER,BASE_体力,200)
					CALL BUFF_BASE(MASTER,BASE_气力,200)
					CALL BUFF_BASE(MASTER,BASE_TSP,200)
				ENDIF 
			CASE 1
				PRINTFORML 
				PRINTFORMDW 你走近窗户，依旧是一团漆黑，看来眼睛还没有适应黑暗……
				PRINTFORMDW ……
				PRINTFORMDL 奇怪……似乎……是被黑暗笼罩住了？
				PRINTFORMDW 向外推开窗户，向外面张望，却还是没法看见任何东西……
				PRINTFORML 
				CALL M_Moe_K12_ASK_M("露米娅！！",1,"关窗睡觉",1)
					SELECTCASE RESULT
						CASE 0 
						PRINTFORMDW ……
						PRINTFORMDW ……
						PRINTFORMDW 没有任何反应
						PRINTFORMDW ……
						PRINTFORMDW 还是老老实实的回去睡觉吧……
						PRINTFORMDW ……
						PRINTFORML 
						PRINTFORML 
						AWAIT 777 
						PRINTFORML 
						AWAIT 777 
						PRINTFORML 
						AWAIT 777 
						PRINTFORML 
						PRINTFORML 
						CALL M_KOJO_Moe_K12_NIGHT_ATTACK
						CASE 1
							PRINTFORML 
							PRINTFORMDW 你躺回了床上，放松下身心，很快便睡着了
							PRINTFORML 
							IF RAND:(CFLAG:12:K12C_1_自记录积攒度) > RAND:1000 
								PRINTFORML 
								PRINTFORMDW ……
								AWAIT 777 
								PRINTFORMDW ……
								AWAIT 777 
								PRINTFORMDW ……
								AWAIT 777 
								PRINTFORML
								CALL M_KOJO_Moe_K12_NIGHT_ATTACK
							ELSE 
								PRINTFORMDL 似乎……有一种……无比温暖，又夹杂着醇香的感觉……
								PRINTFORMDW 不知是因为什么原因，今晚睡得很好，体力，气力和tsp都因此增加了
								CALL BUFF_BASE(MASTER,BASE_体力,200)
								CALL BUFF_BASE(MASTER,BASE_气力,200)
								CALL BUFF_BASE(MASTER,BASE_TSP,200)
							ENDIF 
					ENDSELECT
		ENDSELECT
	ELSEIF ITEM:130
			PRINTDL 咦、开运大纹的样子…？
			CALL ASK_YN("取出","置之不理")
			IF !RESULT
				ITEM:130 --
				SETCOLOR COLOR ("YELLOW")
				PRINTFORMDW 兔符「开运大纹」发动！
				RESETCOLOR
				PRINTFORMDL ……
				PRINTFORMDW 随意散步了一会，%CALLNAME:MASTER%发现好像没有什么有趣的事、于是决定老老实实的回去睡觉了
				GOTO RUMIA_APPEARED 
			ELSEIF !RAND:2
				GOTO OYATU
			ELSEIF !RAND:2
				GOTO NANIMONAI
			ELSE 
				PRINTFORMDW 随意散步了一会，%CALLNAME:MASTER%发现好像没有什么有趣的事、于是决定老老实实的回去睡觉了
				GOTO RUMIA_APPEARED 
			ENDIF
	ELSEIF !RAND:2 && (CFLAG:12:311 == CFLAG:0:311)
		$OYATU
		PRINTFORMDL 肚子饿了。而且如果吃了什么可能会犯困。
		PRINTFORMDW %CALLNAME:MASTER%到厨房的柜子、找到一个好吃的东西。
		PRINTFORMDW 不是%CALLNAME:MASTER%买的东西、但是%CALLNAME:MASTER%自己买了忘了也不是不可能
		PRINTFORMDL 
		CALL COLORMESSAGE("怎么办，马上就要过赏味期限了哇！有没有人快点来吃掉我啊!",C_YELLOW,2,2)
		PRINTFORMDL 
		PRINTFORMDW 虽然只是小小的点心、还是请你收下把。
		CALL BUFF_BASE(MASTER,BASE_气力,300)
		CALL COLORMESSAGE("吃了好吃的点心、一段时间内气力增加",C_YELLOW,2,1)
		PRINTFORMDW %CALLNAME:MASTER%重新刷牙、高兴地睡着了。
		PRINTFORMDW ……
		PRINTFORMDW ……
		PRINTFORMDW ……
		SETCOLOR MOE_RUMIA
		PRINTFORMW 「……居然没发现我吗……」
		PRINTFORMW 「呜啊啊！……趁着露米娅不在把点心吃掉了……呜呜呜呜呜呜……」
		PRINTFORMW 「不行！！……那就……呜%UNICODE(0x2764)%！……那就必须要给点惩罚才行%UNICODE(0x2764)%！」
		CALL M_KOJO_Moe_K12_NIGHT_ATTACK
	ELSE
		$NANIMONAI
		PRINTFORMDW 出去闲逛后发现好像没有什么有趣的事、于是决定老老实实的回去睡觉了
		CFLAG:MASTER:354 += 60
		CALL COLORMESSAGE("睡眠时间减少了1小时",C_YELLOW,2,1)
	ENDIF
;==================================================
;自慰 触发函数
;-------------------------------------------------
;这段代码是从AFTERTRAIN.ERB里面掏出来的
;用于补正CFLAG:12:K12C_1_自记录积攒度的数值变化，同时更加使得露米娅的自慰自定义化
;虽然嘴上说是自定义化了……但是我还是没想到怎么自定义比较好
;不过目前是把无知消除的那个给去掉了
;-------------------------------------------------
@RUMIA_COMFORT_HERSELF_DURING_NIGHT
#DIM 欲求不满度
#DIM 满足度
#DIM 嫉妒度
#DIM 珠入手倍率
#DIM 部位指定
#DIM 追加Ｖ自慰
#DIM 追加Ａ自慰
#DIM 追加Ｂ自慰
#DIM 自慰タイプ
#DIM 自慰发生阈值
#DIM CONST 文字尺寸 = 15
#DIMS 自慰画像
VARSET LOCAL
LOCAL = 12 
	SIF CFLAG:12:出禁	
		RETURN 0 
	部位指定 = 0
	自慰タイプ = 0
	追加Ｖ自慰 = 0
	追加Ａ自慰 = 0
	追加Ｂ自慰 = 0
	自慰发生阈值 = LIMIT(650 - 200 * TALENT:12:60 - 100 * TALENT:12:快感应答 - 100 * TALENT:12:自己爱 + 200 * TALENT:12:自制心,298,999)
	自慰发生阈值 -= 200 * TALENT:12:73
	IF CFLAG:12:K12C_1_夜袭正戏与否 && CFLAG:12:K12C_1_夜袭正戏总次数 
		PRINTFORML 
		PRINTFORMDW %CALLNAME:12%把脸埋在了枕头里面…
		PRINTFORML 
		SETCOLOR MOE_FADED_RED 
		PRINTFORMW 「……大坏蛋%UNICODE(0x2764)%……弄得人家%UNICODE(0x2764)%……说话都变奇怪了%UNICODE(0x2764)%……」
		RESETCOLOR
		CFLAG:12:K12C_1_夜袭正戏与否 = 0 		
	ELSEIF CFLAG:12:K12C_1_自记录积攒度 > 自慰发生阈值
		CALL PrintOnanismImage(12)
		LOCAL:1 = 0
		IF TALENT:12:184
			LOCAL:1 = 3
		ELSEIF TALENT:12:3
			LOCAL:1 = 3
		ELSEIF TALENT:12:185
			;爱人もここ
			LOCAL:1 = 2
		ELSEIF TALENT:12:8
			LOCAL:1 = 1
		ENDIF
		IF TALENT:12:自制心 && LOCAL:1 < 5
			LOCAL:1 += 1
		ENDIF
		IF CFLAG:12:1 & 1 && !RAND:(7 - LOCAL:1)		;既成事实达到合意
			PRINTFORML %CALLNAME:12%忍耐着身体的蠢动、想到明天就能和%CALLNAME:MASTER%做了、强忍着睡着了……
			自慰タイプ = 7
			CFLAG:12:K12C_1_自记录积攒度 = LIMIT(CFLAG:12:K12C_1_自记录积攒度 + 100, 0 ,1000)
			SIF !CFLAG:12:30 
				CFLAG:12:362 = 1
			PRINTL
		ELSEIF CFLAG:MASTER:300 == CFLAG:12:311
			;PRINTFORML 夜里、%CALLNAME:MASTER%听到了一些动静而睁开了眼睛
			;PRINTFORML ……%CALLNAME:12%一边抓着%CALLNAME:MASTER%的手一边压抑着呻吟声沉溺于自慰之中……
			自慰タイプ = 2
		ELSEIF EX:12:14 
			PRINTFORML 无法忍耐的%CALLNAME:12%、伴着膣内残留的%CALLNAME:MASTER%的精液开始了自慰……
			自慰タイプ = 3
		ELSEIF TALENT:LOCAL:无知
			;使用了部位指定传递了ARG
			LOCAL:1 = 0
			LOCAL:1 = CFLAG:12:K12C_1_自记录自慰经验 - EXP:LOCAL:调教自慰经验	;使用正常经验代替了这种东西
			IF LOCAL:1 >= 120 - (MIN(ABL:LOCAL:9,6) * 10)  && (FIRSTTIME("MOE_RUMIA_FIRST_COMFORT_HERSELF_DURING_NIGHT_BREAK_IGNORANT?") || !RAND:6)
				PRINTFORML 露米娅懵懂无知地接受着性的快感，进行着越来越激烈的自慰行为……
				PRINTFORML 可是，羞于坦白此事的小妖怪，没有让任何人知道，也从来没理解过这种行为的含义……
				部位指定 = 5
			ELSEIF (LOCAL:1 >= 80 - (MIN(ABL:LOCAL:9,4) * 10)) && !RAND:5
				;PRINTFORML %CALLNAME:LOCAL%在桌子角扭动腰肢、一边用手揉搓着小豆豆%UNICODE(0x2764)%……
				PRINTFORML %CALLNAME:LOCAL%躺在被窝里激烈地揉搓着蜜缝%UNICODE(0x2764)%……
				部位指定 = 4
			ELSEIF (LOCAL:1 >= 50 - (MIN(ABL:LOCAL:9,3) * 10)) && !RAND:4
				PRINTFORML 尚不明白自慰是什么的%CALLNAME:LOCAL%，用双腿夹紧了枕头%UNICODE(0x2764)%……
				部位指定 = 3
			ELSEIF (LOCAL:1 >= 30 - (MIN(ABL:LOCAL:9,2) * 10)) && !RAND:3
				PRINTFORML %CALLNAME:LOCAL%把书夹在胯间刺激着幼小的秘裂%UNICODE(0x2764)%……
				部位指定 = 2
			;ELSEIF (LOCAL:1 >= 15 - (MIN(ABL:LOCAL:9,1) * 10)) && !RAND:2
			;	PRINTFORML %CALLNAME:LOCAL%在学习的时候把笔贴在了胯下、就这样摩擦起秘缝%UNICODE(0x2764)%……
			;	部位指定 = 1
			ELSE
				PRINTFORML 难以忍受陌生情欲的%CALLNAME:LOCAL%，凑近了家里的桌子%UNICODE(0x2764)%……
			ENDIF
			自慰タイプ = 4
		ELSEIF ABL:12:自慰中毒 >= 3 && (TALENT:12:8 || IS_LOVER(12) || TALENT:12:7 || TALENT:12:185) && !RAND:3
			PRINTFORML 怎么也忍耐不住了的%CALLNAME:LOCAL%、一边嗅着沾在衣服上的%CALLNAME:MASTER%的气味一边激烈的自慰着……
			LOCAL:2 = RAND:5
			IF (TALENT:12:Ｃ感度 == 1 || ABL:12:0 >= 10) && LOCAL:2 == 4
				PRINTFORM 揉搓着阴核、即使潮吹了还一味地持续着动作
				部位指定 = 1
			ELSEIF (TALENT:12:Ｖ感度 == 1 || ABL:12:1 >= 10) && LOCAL:2 == 3
				PRINTFORM 把手指深深地插入到蜜壷之中持续扣弄着爱液
				部位指定 = 2
				追加Ｖ自慰 = 1
			ELSEIF (TALENT:12:Ａ感度 == 1 || ABL:12:2 >= 10) && LOCAL:2 == 2
				PRINTFORM 持续玩弄着蜜穴和菊穴
				部位指定 = 3
				追加Ａ自慰 = 1
			ELSEIF (TALENT:12:Ｂ感度 == 1 || ABL:12:3 >= 10) && LOCAL:2 == 1
				IF TALENT:12:22 >= 1
					PRINTFORM 吸吮着自己那丰满的乳房、
				ELSEIF TALENT:12:22 <= -1
					PRINTFORM 抚摸着贫瘠的胸部来回揉搓着乳头、
				ELSE
					PRINTFORM 用下流的手法揉弄着柔软的乳房、
				ENDIF
				PRINTFORM 高潮了好几次
				部位指定 = 4
				追加Ｂ自慰 = 1
			ELSE
				;PRINTFORML しかし、どんなに快感を得ようとも%CALLNAME:MASTER%への想いは治まることはなく、%CALLNAME:LOCAL%は朝まで闷々とした时间を过ごした……
				PRINTFORM 因体内的快感余韵而颤抖着身体
				部位指定 = 5
			ENDIF
			IF TALENT:12:8 || TALENT:12:3 || TALENT:12:7
				PRINTFORML 、对%CALLNAME:MASTER%的思念平息了……
			ELSE
				PRINTFORML 、燃烧了身体的热量还没有平息……
			ENDIF
			追加Ｖ自慰 = 1
			自慰タイプ = 5
		ELSEIF ABL:12:自慰中毒 >= 3 && !RAND:2
			PRINTFORM 已经无法用手指满足的%CALLNAME:12%、用从河童那买来的玩具
			IF ABL:12:2 >= 5
				IF TALENT:12:贞操 == -1 && TALENT:12:0 == 0
					PRINTFORM 尽可能深的插入阴道和肛门的深处
					追加Ｖ自慰 = 1
				ELSEIF TALENT:12:贞操 == 1
					PRINTFORM 抵住私处和肛门
				ELSE
					PRINTFORM 玩弄着私处和尻穴
				ENDIF
				部位指定 = 3
				追加Ａ自慰 = 1
			ELSE
				IF TALENT:12:贞操 == -1 && TALENT:12:0 == 0
					PRINTFORM 插入阴道的深处
					追加Ｖ自慰 = 1
				ELSEIF TALENT:12:贞操 == 1
					PRINTFORM 抵住私处
				ELSE
					PRINTFORM 玩弄着私处
				ENDIF
				部位指定 = 1
			ENDIF
			PRINTFORML 沉溺于自慰之中…
			自慰タイプ = 6
		ELSE
			PRINTFORML 身体变的躁动难安的%CALLNAME:12%、自己用手指玩弄起了私处…
			自慰タイプ = 1
		ENDIF
		CALL KOJO_MESSAGE_SEND("EVENT",22,12,自慰タイプ,部位指定)
		IF RESULT == 1 
			SIF 自慰タイプ == 7
				RETURN 0  
			EXP:12:22 += ABL:12:自慰中毒 * 2 + 1
			CALL COLORMESSAGE(@"自慰经验＋{ABL:12:自慰中毒 * 2 + 1}",C_PINK,1)
			SELECTCASE ABL:12:自慰中毒
				CASE 0 TO 2
					珠入手倍率 = 5
				CASE 3 TO 5
					珠入手倍率 = 3
				CASEELSE
					珠入手倍率 = 1
			ENDSELECT
			;快C
			CALL JUEL_UP_SPECIAL_FOR_RUMIA(12,0,0,珠入手倍率,1)
			SIF 追加Ｖ自慰
				CALL JUEL_UP_SPECIAL_FOR_RUMIA(12,1,1,珠入手倍率,1)
			SIF 追加Ａ自慰
				CALL JUEL_UP_SPECIAL_FOR_RUMIA(12,2,2,珠入手倍率,1)
			SIF 追加Ｂ自慰
				CALL JUEL_UP_SPECIAL_FOR_RUMIA(12,3,3,珠入手倍率,1)
			;羞耻
			CALL JUEL_UP_SPECIAL_FOR_RUMIA(12,14,30,珠入手倍率 * 2,1)
			;情欲
			CALL JUEL_UP_SPECIAL_FOR_RUMIA(12,11,11,珠入手倍率,1)
			CFLAG:12:K12C_1_自记录积攒度 /= 4
			PRINTW 
			;まず欲望で基礎值を决める
			SELECTCASE ABL:12:11
				CASE 0 TO 2
					欲求不满度 = 10
				CASE 3 TO 5
					欲求不满度 = 20
				CASE 6 TO 10
					欲求不满度 = 30
				CASE 10 TO 15
					欲求不满度 = 50
				CASEELSE
					欲求不满度 = 80
			ENDSELECT
			欲求不满度 += ABL:12:自慰中毒 * 7 + (ABL:12:膣射中毒 + ABL:12:肛射中毒) * 3
			欲求不满度 = 欲求不满素質补正(12,欲求不满度)
			SELECTCASE 欲求不满度
				CASE IS > 400
					欲求不满度 = (欲求不满度 - 400) / 10 + 400
				CASE IS > 200
					欲求不满度 = (欲求不满度 - 200) / 4 + 200
				CASE IS > 100
					欲求不满度 = (欲求不满度 - 100) / 2 + 100
			ENDSELECT
			SIF 自慰タイプ
				欲求不满度 = 0
			SIF TCVAR:12:182
				欲求不满度 /= 2
			SIF TALENT:12:153
				欲求不满度 /= 5
			SIF TALENT:12:154
				欲求不满度 /= 2
			;积攒度は0~1000の间に
			CFLAG:12:K12C_1_自记录积攒度 = LIMIT(CFLAG:12:K12C_1_自记录积攒度 + 欲求不满度, 0, 1000)
			CFLAG:12:340 = CFLAG:12:K12C_1_自记录积攒度 
			DEBUGPRINTFORML %CALLNAME:12%的满足度：{满足度}　欲求不满上升值：{欲求不满度}　积攒度：{CFLAG:12:K12C_1_自记录积攒度}
		ENDIF 
	ENDIF 
;==================================================
;生育许可的好感判断用 函数
;-------------------------------------------------
;以下代码照搬ERB/イベント关联/妊娠关联/OGAMITAOSHI.ERB
;经过一点点屏蔽后制成
;-------------------------------------------------
@OGAMITAOSHI_MOE_RUMIA(ARG,ARG:1)
#DIM 理性判定值
#DIM 好意判定值
#DIM 好意表示
VARSET LOCAL
PRINTFORMW %CALLNAME:ARG%的心中
好意判定值 = CHK_HININ_LOVE(ARG) 
CALL SHOW_LOVEPOWER(好意判定值)
WAIT
理性判定值 = CHK_HININ_REASON(ARG)
;考虑到小孩子对生孩子这样的事情，肯定会有极大的恐惧
;而且事件发生的时候无任何PALAM……反倒能固定好期望值
;因此，加上这个补正之后，恋慕恋人默认的理性之壁将成为20星，即为2000 
;测试的时候，好感度34200+16亲密+17顺从+10侍奉精神，爱情经验371，且恋人加300，刚好21心
理性判定值 += 1200 
;不为什么
;SIF TEQUIP:12:绳 || TEQUIP:12:拘束
SIF TEQUIP:12:19 || TEQUIP:12:25
理性判定值 += 1000 
CALL SHOW_REASONWALL(理性判定值)
;蜜汁魅力
IF TALENT:MASTER:94
	CALL COLORMESSAGE(@"%CALLNAME:ARG%、因%CALLNAME:MASTER%的谜之魅力感觉理性有些蒸发……",C_PINK,1,1)
	理性判定值 = 理性判定值 * 9 / 10
	CALL SHOW_REASONWALL(理性判定值)
ENDIF
;自尊心
IF TALENT:ARG:13 == -1
	理性判定值  = 理性判定值 * 9 / 10
ELSEIF TALENT:ARG:13 == 1
	理性判定值 = 理性判定值 * 6 / 5
	CALL COLORMESSAGE(@"架子很大的%CALLNAME:ARG%、不想让自己表现的太过廉价",C_PINK,1,1)
	CALL SHOW_REASONWALL(理性判定值)
ENDIF
;贞操重视
IF TALENT:ARG:30 == 1
	理性判定值 = 理性判定值 * 6 / 5
	CALL COLORMESSAGE(@"非常重视贞操的%CALLNAME:ARG%、在改变观念之前没那么容易就接受的……",C_PINK,1,1)
	CALL SHOW_REASONWALL(理性判定值)
ENDIF
;性别嗜好
IF OTOKOGIRAI(ARG)
	理性判定值 = 理性判定值 * 3 / 2
	IF TALENT:ARG:81 == 3
		CALL COLORMESSAGE(@"%CALLNAME:ARG%对他人抱有同样的忌避感情……",C_PINK,1,1)
	ELSEIF TALENT:ARG:81 == 1
		CALL COLORMESSAGE(@"%CALLNAME:ARG%对男性抱有同样的忌避感情……",C_PINK,1,1)
	ENDIF
	CALL SHOW_REASONWALL(理性判定值)
ENDIF
;倒错的
IF TALENT:ARG:80
	CALL COLORMESSAGE(@"%CALLNAME:ARG%对，糜烂的关系感到有颠倒性的魅力……",C_PINK,1,1)
	理性判定值 = 理性判定值 * 8 / 10
	CALL SHOW_REASONWALL(理性判定值)
ENDIF
;难以逾越
IF TALENT:ARG:25
	SELECTCASE EXP:ARG:41
		CASE IS >= 500
			CALL COLORMESSAGE(@"%CALLNAME:ARG%、已经就快要完全接受%CALLNAME:MASTER%了……！",C_PINK,1,1)
			理性判定值 = 理性判定值 / 2
		CASE IS >= 300
			CALL COLORMESSAGE(@"%CALLNAME:ARG%、虽然是面对%CALLNAME:MASTER%但还是有些抵抗……",C_PINK,1,1)
			理性判定值 = 理性判定值 * 6 / 5
		CASE IS >= 100
			CALL COLORMESSAGE(@"%CALLNAME:ARG%、即使是和亲密的对象也保持着一定距离……",C_PINK,1,1)
			理性判定值 = 理性判定值 * 3 / 2
		CASEELSE
			CALL COLORMESSAGE(@"%CALLNAME:ARG%、不愿意接受其他人……",C_PINK,1,1)
			理性判定值 *= 2
	ENDSELECT
	CALL SHOW_REASONWALL(理性判定值)
ENDIF
;傲娇
IF TALENT:ARG:14
	IF TALENT:ARG:3
		理性判定值 = 理性判定值 * 2 / 3
		CALL COLORMESSAGE(@"%CALLNAME:ARG%撒娇求欢！",C_PINK,1,1)
	ELSE
		理性判定值 = 理性判定值 * 3 / 2
		CALL COLORMESSAGE(@"%CALLNAME:ARG%抱有了反感……",C_PINK,1,1)
	ENDIF
	CALL SHOW_REASONWALL(理性判定值)
ENDIF
WAIT
IF CHK_HININ_LOVE(ARG) > 理性判定值
	CFLAG:ARG:14 = 2
ELSE
ENDIF
;
;==================================================
;好感度补正函数
;这部分是在PALAMCNG里面被CALL的，如果在EVENTCOMEND被call会清空
;-------------------------------------------------
@MOE_FAVOR_FIX_FOR_RUMIA_IN_PALAMCNG(ARGS)
#DIM ADJUST
VARSET ADJUST
SIF CFLAG:12:K12C_1_好感度补正 == -1 
	RETURN 0 
;位于指令结束时还原
SIF ARGS == "EVENTCOMEND" 
	TFLAG:95 = LOCAL 
IF (TARGET == 12) 
	;
	;好意
	SELECTCASE ABL:12:9 
		CASE IS < 3 
			ADJUST -= 45 + RAND:(25 - ABL:12:9 * 2 )
		CASE IS < 5 
			ADJUST -= 40 + RAND:(20 - ABL:12:9)
		CASE IS < 10 
			ADJUST -= 35 + RAND:(25 - ABL:12:9)
		CASE IS < 20 
			ADJUST -= 30 - RAND:(ABL:12:9 - 5)
		CASE IS < 30 
			ADJUST -= 30 - RAND:(MIN(ABL:12:9 , 31)) + RAND:15
		CASEELSE 
			ADJUST -= 25 - RAND:(MIN(ABL:12:9 , 45)) + RAND:10
	ENDSELECT 
	;
	;恋人
	IF TALENT:12:7 
		;爱情经验
		SELECTCASE EXP:12:41
			CASE IS < 100 
				ADJUST += 10 + RAND:(EXP:12:41 / 10 + 1)
			CASE IS < 500 
				ADJUST += 15 + RAND:(EXP:12:41 / 20 + 10)
			CASEELSE 
				ADJUST += 20 + RAND:(MIN(EXP:12:41 / 50 , 75))
		ENDSELECT
		;在作艾时
		IF CFLAG:12:317 
			;危险日插入了
			IF (CFLAG:12:14 == 2) && (CFLAG:12:900 == 7) && (TEQUIP:50 == MASTER) 
				ADJUST += 15 + MIN(EXP:12:41 / 30 , 20) - RAND:10
				;子宫口玩弄
				IF SELECTCOM == 73 
					ADJUST += 25 + MIN(EXP:12:41 / 10 , 50) - RAND:10
				;插入系
				ELSEIF GROUPMATCH(SELECTCOM,60,61,65,67,68,72)
					ADJUST += 15 + MIN(EXP:12:41 / 15 , 30) - RAND:10
				ENDIF 
			ENDIF 
			;接吻追加
			SIF TEQUIP:50 == MASTER && GROUPMATCH(SELECTCOM , 312 , 19 , 20)
				ADJUST += 10 + MIN(EXP:12:41 / 30 , 20) + RAND:10
		ENDIF 
	ENDIF 
	;
	;爱欲
	IF TALENT:12:184 || TALENT:12:185 
		IF CFLAG:12:317 
			ADJUST += MIN(ABL:12:11,20) + RAND:10 + 10
		ELSE 
			ADJUST -= 30 + MIN(ABL:12:9 , 20) - RAND:10
		ENDIF 
	ENDIF 
	;
	;刻印补正
	SIF MARK:12:3 
		ADJUST -= MARK:12:3 * (40 - TALENT:12:7 * 15 - TALENT:12:3 * 5)
	SIF MARK:12:4
		ADJUST -= MARK:12:4 * (15 - TALENT:12:7 * 5 - TALENT:12:3 * 3)
	SIF MARK:12:5 && !CFLAG:12:345 
		ADJUST -= MARK:12:5 * (10 + MIN(ABL:12:11 + MIN(EXP:12:65 / 50 , 25))) - RAND:20
	;
	;开局约会补正，无亲热约会
	IF FLAG:73 == 12 && !TCVAR:0:130 && !CFLAG:12:K12C_1_二周目  
		IF CFLAG:12:K12C_1_朋友 || CFLAG:12:K12C_1_朋友_2 
			ADJUST -= ADJUST / 4
		ELSE 
			ADJUST -= ADJUST / 3
		ENDIF 
	ENDIF 
	;
	;自定义补正
	IF CFLAG:12:K12C_1_好感度补正 != 0 && CFLAG:12:K12C_1_好感度补正 > -1 
		IF INRANGE(CFLAG:12:K12C_1_好感度补正 , 0 , 100 )
			ADJUST *= CFLAG:12:K12C_1_好感度补正
			ADJUST /= 100 
		;负数值了
		ELSEIF (CFLAG:12:K12C_1_好感度补正 >= 100) && (TFLAG:95 + ADJUST * CFLAG:12:K12C_1_好感度补正 / 100 < -100)
			LOCAL = TFLAG:95
			TFLAG:95 = -100 
			RETURN 0 
		ENDIF 
	ENDIF 
	;
	;上限确定
	IF CUSTOM_TALENT:12:2 == 1 
		ADJUST = MIN(ADJUST,500)
	ELSEIF CUSTOM_TALENT:12:2 == 1 
		ADJUST = MIN(ADJUST,400)
	ELSEIF TALENT:12:7 
		ADJUST = MIN(ADJUST,300)
	ELSEIF TALENT:12:184 || TALENT:12:185 || TALENT:12:3 || TALENT:12:8 
		ADJUST = MIN(ADJUST,150)
	ELSE 
		ADJUST = MIN(ADJUST,50)
	ENDIF 
	;
	;下界限，比原版要高
	SIF TFLAG:95 + ADJUST < -100 
		ADJUST = -100 - TFLAG:95  
	;
	;位于绝顶函数时应用       
	IF ARGS == "PALAMCNG"
		LOCAL = TFLAG:95
		TFLAG:95 += ADJUST 
	ENDIF 
ENDIF 

;==================================================
;插入是否满足检测函数
;检测是否允许进入，以最难的体位为准
;删除了显示文本的部分以保证悄悄检测
;返回1时允许，返回0时拒绝
;用于LOST_VIRGIN_STOP
;==================================================
@MOE_RUMIA_CHECK_ENABLE_TO_INSERT(ARG)
#FUNCTION
#DIM 允许值
#DIM 污垢值
SIF !(TALENT:12:3 || TALENT:12:184 || TALENT:12:185 || (TALENT:12:8 && TALENT:12:7) )
	RETURNF 0 
SIF PALAM:12:9 < 3000 
	RETURNF 0 
SIF OUTROOF(CFLAG:12:300)
	RETURNF 0 
允许值 = 0
;全部の命令に共通の要素を考虑
;(顺从が高いと命令に从いやすいなど)
;常识改变
;避免无此补丁的版本报错
;IF FLAG:催眠强度
;	允许值 += 30 * FLAG:催眠强度 / 100
;ENDIF
;ABL:9
IF ABL:9
	允许值 += ABL:9
ENDIF
;ABL:顺从
IF ABL:10
	允许值 += ABL:10 * 4
ENDIF
;ABL:15
IF ABL:15
	允许值 += ABL:15 * 2
ENDIF
;レズ调教
IF HETEROSEX(TARGET,PLAYER) == 1
	;ABL:17
	IF ABL:17
		允许值 += ABL:17 * 3
		ENDIF
	;ABL:32
	IF ABL:32
		允许值 += ABL:32 * 3
		ENDIF
	;TALENT:两面通吃
	IF TALENT:性别嗜好 == -1
		允许值 += 10
		ENDIF
	;好奇心
	IF TALENT:23 > 0
		允许值 += 7
		ENDIF
	;保守的
	IF TALENT:23 < 0
		允许值 -= 13
		ENDIF
;ゲイ调教
ELSEIF HETEROSEX(TARGET,PLAYER) == 2
	;ABL:18
	IF ABL:18
		允许值 += ABL:18 * 3
		ENDIF
	;ABL:33
	IF ABL:33
		允许值 += ABL:33 * 3
		ENDIF
	;TALENT:两面通吃
	IF TALENT:性别嗜好 == -1
		允许值 += 10
		ENDIF
	;好奇心
	IF TALENT:23 > 0
		允许值 += 7
		ENDIF
	;保守的
	IF TALENT:23 < 0
		允许值 -= 13
		ENDIF
ELSE
	;好奇心
	IF TALENT:23 > 0
		允许值 += 5
		ENDIF
	;保守的
	IF TALENT:23 < 0
		允许值 -= 10
		ENDIF
ENDIF

;刻印チェック
IF MARK:苦痛刻印
	允许值 += MARK:苦痛刻印 * 5
ENDIF
IF TALENT:13 > 0
	T = 4
ELSEIF TALENT:13 < 0
	T = 1
ELSE
	T = 2
ENDIF

;屈服刻印および反抗刻印
IF MARK:屈服刻印
	允许值 += MARK:屈服刻印 * 3 * T
ENDIF
IF MARK:反抗刻印
	允许值 -= MARK:反抗刻印 * 2 * T
ENDIF


;PALAM:恭顺
IF GETPALAMLV(PALAM:恭顺,5)
	允许值 += GETPALAMLV(PALAM:恭顺,5) * 3
ENDIF
;PALAM:恐怖
IF GETPALAMLV(PALAM:恐怖,5)
	允许值 += GETPALAMLV(PALAM:恐怖,5) * 3
ENDIF

;服从
IF TALENT:服从
	允许值 += 10
ENDIF
;隷属
IF TALENT:服从 > 1
	允许值 += 20
ENDIF
;叛逆（服从、隷属で无效）
IF TALENT:态度 > 0 && !TALENT:服从
	允许值 -= 5
ENDIF
;坚强
IF TALENT:10 > 0
	允许值 -= 5
ENDIF
;坦率
IF TALENT:态度 < 0
	允许值 += 5
ENDIF
;自尊心高
IF TALENT:13 > 0
	允许值 -= 15
ENDIF
;自尊心低
IF TALENT:13 < 0
	允许值 += 5
ENDIF
;喜欢引人注目
IF TALENT:26
	允许值 += 2
ENDIF
;圧抑
IF TALENT:自己爱 < 0
	允许值 -= 10
ENDIF
;抵抗
IF TALENT:抵抗
	允许值 -= 10
ENDIF
;把柄
IF TALENT:34
	允许值 += 12
ENDIF
;容易高潮
IF TALENT:72
	允许值 += 10
ENDIF
;迷信
IF TALENT:85
	允许值 += 8
ENDIF


;调教者の能力、素質
;ABL:16
IF ABL:PLAYER:16
	允许值 += ABL:PLAYER:16 * 2
ENDIF
;魅惑
IF TALENT:PLAYER:93
	允许值 += 6
ENDIF
;谜之魅力
IF TALENT:PLAYER:94
	允许值 += 8
ENDIF
;施虐狂
IF TALENT:PLAYER:82
	允许值 += 3
ENDIF

;助手の素質
;鼓舞
IF ASSIPLAY && TALENT:PLAYER:鼓舞
	允许值 += 1
ENDIF

IF TCVAR:145 == 1
	允许值 += 20
ENDIF

;相性チェック
R = NO:PLAYER
IF RELATION:R > 0 && RELATION:R < 30
	允许值 -= 10
ELSEIF RELATION:R > 0 && RELATION:R < 70
	允许值 -= 6
ELSEIF RELATION:R > 0 && RELATION:R < 100
	允许值 -= 3
ELSEIF RELATION:R >= 100 && RELATION:R < 130
	允许值 += 3
ELSEIF RELATION:R >= 100 && RELATION:R < 170
	允许值 += 6
ELSEIF RELATION:R >= 100 && RELATION:R >= 170
	允许值 += 10
ENDIF
;ABL:欲望
IF ABL:欲望
	允许值 += ABL:欲望 * 3
ENDIF
;ABL:Ｖ感觉
IF ABL:Ｖ感觉
	允许值 += ABL:Ｖ感觉 * 2
ENDIF
;快乐刻印
IF MARK:快乐刻印
	允许值 += MARK:快乐刻印 * 3
ENDIF
;PALAM:润滑（の不足）
IF PALAM:9 < PALAMLV:3
	允许值 -= 5
ENDIF
;PALAM:情欲
IF GETPALAMLV(PALAM:11,5)
	允许值 += GETPALAMLV(PALAM:11,5) * 3
ENDIF
;害羞
IF TALENT:羞耻心 > 0
	允许值 -= 2
ENDIF
;否定快感
IF TALENT:快感应答 < 0 && !TALENT:3
	允许值 -= 5
ENDIF
;讨厌男人（恋慕、亲爱で无效）
IF OTOKOGIRAI(TARGET)
	允许值 -= 12
ENDIF
;恋慕
IF TALENT:3 && ASSIPLAY == 0
	允许值 += 15
ENDIF
;亲爱
IF TALENT:3 > 1 && ASSIPLAY == 0
	允许值 += 5
ENDIF
;处女
LOCAL = MAX(10,40 - EXP:Ｖ经验)
IF TALENT:0
	允许值 -= LOCAL
;V経験
ELSEIF EXP:Ｖ经验 < EXPLV:2
	允许值 -= 15
ENDIF

;调教者が扶她
IF (TALENT:PLAYER:2 & 3) == 3
	允许值 += 4
ENDIF

;媚药
IF TCVAR:媚药
	允许值 += 6
ENDIF

污垢值 = 0
;爱液的污垢
SIF STAIN:PLAYER:2 & 1
	污垢值 += 1
;精液的污垢
SIF STAIN:PLAYER:2 & 4
	污垢值 += 3
;肛门的污垢
SIF STAIN:PLAYER:2 & 8
	污垢值 += 7
SIF STAIN:PLAYER:2 & 16
	污垢值 += 15
污垢值 = 污垢值 * (2 - TALENT:污臭耐性) / 2

;有污垢
SIF 污垢值
	允许值 -= 污垢值

;合计を表示(20以上で实行)
;以最高的背面座位为准，避免允许了但是CAN失败
IF 允许值 < 42
	RETURNF 0
ELSE 
	RETURNF 1 
ENDIF 

