;帝口上を参考に作った口上のない角色でウフフ中呼び出す自动喘息函数
;口上持ちでも任意で台词に挟めるようにARGS次第でカッコ省略できるようにしてみた
;を、丸々流用して稍微お淑やか方面で辉夜用に调整。

@K10_AUTO_AEGI(ARGS,ARG)
#DIM 喘ぎ强度
#DIM 喘ぎ回数

; 第二参数(ARG)未指定で呼び出した场合、萃香として扱う
SIF !ARG
	ARG = [[萃香]]

;シラフじゃないと駄目
SIF !SHIRAHU(ARG)
	RETURN 0
SIF !CFLAG:ARG:诶嘿嘿
	RETURN 0
SIF GROUPMATCH(SELECTCOM, 19, 20, 81, 88, 89)
	RETURN 0

喘ぎ强度 = K10_AEGI_LEVEL(ARG)

SIF !喘ぎ强度
	RETURN 0

SIF ARGS != "前カッコ省略" && ARGS != "カッコ省略"
	PRINT 「

SELECTCASE 喘ぎ强度
	CASE 1
		喘ぎ回数 = 2 + RAND:2
	CASE 2
		喘ぎ回数 = 2 + RAND:2 + RAND:3
	CASE 3
		喘ぎ回数 = 3 + RAND:2 + RAND:3 + RAND:3
	CASEELSE
		喘ぎ回数 = 3 + RAND:喘ぎ强度 + RAND:喘ぎ强度 + RAND:喘ぎ强度
ENDSELECT

FOR LOCAL,0,喘ぎ回数
	LOCALS:LOCAL = %K10_AEGI_PATTERN(喘ぎ强度, ARG)%
	;前のと一绪だったら一度だけ再抽选
	SIF LOCAL > 0 && LOCALS:LOCAL == LOCALS:(LOCAL - 1)
		LOCALS:LOCAL = %K10_AEGI_PATTERN(喘ぎ强度, ARG)%
	PRINTFORM %LOCALS:LOCAL%
	SIF !RAND:2 && 喘ぎ强度 <= 3
		PRINT ……
	SIF 喘ぎ强度 >= 4 || (喘ぎ强度 == 3 && !RAND:4)
		PRINT ♥
	SIF LOCAL != 喘ぎ回数 - 1
		PRINTPLAIN  
NEXT

SIF ARGS != "后カッコ省略" && ARGS != "カッコ省略"
	PRINTL 」


@K10_AEGI_LEVEL(ARG)
#FUNCTION
LOCAL = CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ + CUP:ARG:快Ｂ + CUP:ARG:快Ｍ

SELECTCASE LOCAL
	CASE 0
		RETURNF 0
	CASE IS < 2500
		RETURNF 1
	CASE IS < 5000
		RETURNF 2
	CASE IS < 10000
		RETURNF 3
	CASE IS < 30000
		RETURNF 4
	CASE IS < 50000
		RETURNF 5
	CASEELSE
		RETURNF 6
ENDSELECT


;バリエーションおよび喘ぎ强度と台词の对応状况は改善の余地があるだろう
;
;>まず是やらしくいじられてる场所をつぶやくようにしよう
;>对象のCUPを见るために参数を扩张
@K10_AEGI_PATTERN(喘ぎ强度, ARG)
#FUNCTIONS
#DIM 喘ぎ强度

;部位つぶやき、とりあえず20%で（部位そのものをつぶやくとは言っていない）
;部位ごとのCUPを合计して乱数をとり、Ｃから顺に乱数が合计值以内かで部位を判定、ヒットすればその部位をつぶやく（喘ぎ强度と陷落系素質と部位感度を合わせて计算していい嗯だけど、それは先の话）
;部位陷落は感度5が条件なのを考えるともう少し分けつつ「感觉*30(最大180)+1d100」の结果で分岐させることでバリエーションが出るといいなぁ
IF TCVAR:ARG:强行 && 喘ぎ强度 < 3

ELSEIF RAND:100 < 20 && (CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ + CUP:ARG:快Ｂ + CUP:ARG:快Ｍ) 
	SELECTCASE RAND:(CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ + CUP:ARG:快Ｂ + CUP:ARG:快Ｍ)
	;CUPを合计して乱数を取る。これでCUPの值に応じて拾う概率に差が生まれる
	CASE IS < CUP:ARG:快Ｃ
		;クリ、肉棒どうしよう…（このあとに性别判定入れれば分岐は可能ではある）
		LOCAL = MIN(ABL:ARG:Ｃ感觉 * 20, 180) + RAND:100
		SIF CFLAG:ARG:自动喘息 != 2
			LOCAL = MIN(LOCAL, 150)
		SELECTCASE LOCAL
		;感觉＋乱数による分岐、下に行くほどアレです
		CASE 0 TO 80
			LOCALS = %TEXTR("啊/咿呀/咿呀呜/那里是/")%
		CASE 81 TO 150
			LOCALS = %TEXTR("不要/呼啊/呼啊/呀/那里是/")%
		CASE 151 TO 200
			LOCALS = %TEXTR("咿呜呜！？/呀/真的不行/痉挛着/不要/真的不行/")%
		CASEELSE
			LOCALS = %TEXTR("咿、啊/咿/等下/不行/咕行/真的咕行/")%
		ENDSELECT

	CASE IS < CUP:ARG:快Ｃ + CUP:ARG:快Ｖ
		LOCAL = MIN(ABL:ARG:Ｖ感觉 * 20, 180) + RAND:100
		SIF CFLAG:ARG:自动喘息 != 2
			LOCAL = MIN(LOCAL, 150)
		;膣、肉オナホまで言わせるのは稍微违うと思ったので控えました
		SELECTCASE LOCAL
		;感觉＋乱数による分岐、下に行くほどアレです
		CASE 0 TO 80
			LOCALS = %TEXTR("咕/嗯……/咿呀/呀……/那里/")%
		CASE 81 TO 150
			LOCALS = %TEXTR("深处/咕叽咕叽的/捣来捣去的/好热/那里/里面/肚子里面/")%
		CASE 151 TO 200
				LOCALS = %TEXTR("要坏了/咿呜呜/子宫/里面/深处/深过头了")%
		CASEELSE
				LOCALS = %TEXTR("要被弄坏了/咿、――呜/撞来撞去的/撞来撞去的咕行/子宫、在/腻面/要变得奇怪/啊啊/")%
		ENDSELECT

	CASE IS < CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ
		;あなぅ、多分一番はっちゃけられる场所だと思う（のであとは任せた）
		LOCAL = MIN(ABL:ARG:Ａ感觉 * 20, 180) + RAND:100
		SIF CFLAG:ARG:自动喘息 != 2
			LOCAL = MIN(LOCAL, 150)
		SELECTCASE LOCAL
		;感觉＋乱数による分岐、下に行くほどアレです
		CASE 0 TO 80
			LOCALS = %TEXTR("唔咕……/啊咕……/咿呀/咿呀呜/好奇怪/那里/")%
		CASE 81 TO 150
			LOCALS = %TEXTR("屁股/屁ー/那里/不要/")%
		CASE 151 TO 200
			LOCALS = %TEXTR("要坏了/屁股/被顶着/好硬/唔咕/呀、啊――唔/一顶一顶的/")%
		CASEELSE
			LOCALS = %TEXTR("臀咕/刮来刮去的/被刮蹭着/咕行/一顶一顶的/要坏了/腻面/")%
		ENDSELECT

	CASE IS < CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ + CUP:ARG:快Ｂ
		;胸、いろいろかけると思ったらない人のことを忘れてた(ﾋﾟﾁｭｰﾝ)
		LOCAL = MIN(ABL:ARG:Ｂ感觉 * 20, 180) + RAND:100
		SIF CFLAG:ARG:自动喘息 != 2
			LOCAL = MIN(LOCAL, 150)
		SELECTCASE LOCAL
		;感觉＋乱数による分岐、下に行くほどアレです
		CASE 0 TO 80
			LOCALS = %TEXTR("哈啊…/咿…/不要/咿呀唔/咕呜/")%
		CASE 81 TO 150
			LOCALS = %TEXTR("胸部/乳头/被拧着/")%
		CASE 151 TO 200
			LOCALS = %TEXTR("乳房/乳房/乳房/奶头/被揉个不停/")%
		CASEELSE
			LOCALS = %TEXTR("胸部/胸部/胸咕呜/奶头/捏来捏呜/尖尖/拧来拧去的/")%
		ENDSELECT

	CASEELSE
		LOCAL = MIN(ABL:ARG:Ｍ感觉 * 20, 180) + RAND:100
		SIF CFLAG:ARG:自动喘息 != 2
			LOCAL = MIN(LOCAL, 150)
		;おくち、实は死に分岐的可能性が高いという。指令にゆびちゅぱください
		SELECTCASE LOCAL
		;感觉＋乱数による分岐、下に行くほどアレです
		CASE 0 TO 80
			LOCALS = %TEXTR("嗯/唔呣…/唔咕/咿呀/唔嗯……/")%
		CASE 81 TO 150
			LOCALS = %TEXTR("嘴巴/嘴/舌头/啾卟/唔噗/啾噗/")%
		CASE 151 TO 200
			LOCALS = %TEXTR("唔呣/舌头/还想要/里面/咕啾/唔啾/")%
		CASEELSE
			LOCALS = %TEXTR("锥巴/还想奥/我舔/锥巴/啾噜/啾卟/")%
		ENDSELECT

	ENDSELECT
	RETURNF LOCALS
ENDIF
IF TCVAR:ARG:强行 && (喘ぎ强度 < 3 || (喘ぎ强度 == 3 && RAND:4))
	IF TALENT:ARG:胆量 > 0 || TALENT:ARG:回应 > 0 || TALENT:ARG:自尊心 > 0
		LOCALS = %TEXTR("请住手/拙劣/放开/别碰我/别碰/去死/你个/咕……")%
		SIF !RAND:10 && !TALENT:ARG:人类
			LOCALS = 区区人类、呜
	ELSE
		LOCALS = %TEXTR("呜呜…/人渣…/不要…/快停下…/别这样…/放过我…/很讨厌啊/咿咕")%
	ENDIF
ELSE
;通常喘ぎ
SELECTCASE 喘ぎ强度
	CASE 1
		LOCALS = %TEXTR("啊/嗯/嗯……/呼、呜/哈啊……/")%
	CASE 2
		LOCALS = %TEXTR("呀啊/哈啊/呼啊啊/呼啊/呼呜/咕呼/呼ー/啊/啊ー/啊啊/啊嗯/哈啊/哈ー/咿啊/咿唔/咿呀/嗯嗯/哈呜/嗯哈啊/唔咕")%
	CASEELSE
		IF RAND:喘ぎ强度 >= 3
			LOCALS = %TEXTR("呜啊/啊、唔/咿咕/咿/咿/好腻哈/好厉/――唔/咕行/～～唔/咿啊啊啊嗯/嗯嗯唔/啊、――唔/")%
		ELSEIF !RAND:3
			LOCALS = %TEXTR("哈呜/哈呼/咕嗯/啊啊/呀嗯/哈啊/啊/")%
		ELSEIF !RAND:2
			LOCALS = %TEXTR("咿咿/还想要/咿咿唔/咿ー/唔哈啊/啊啊嗯/好激烈/好舒　呜")%
		ELSE
			LOCALS = %TEXTR("呀啊/呼啊啊/呼啊/呼ー/啊/啊ー/啊啊/啊嗯/哈啊/哈ー/唏嗯/咿呀/嗯嗯/哈呜/唔咕")%
		ENDIF
ENDSELECT
ENDIF
;随机で「っ」をカタ卡娜にすることでバリエーションが增えたように错觉させる姑息な处理
IF !RAND:4 && 喘ぎ强度 >= 3
	LOCALS = %REPLACE(@"%LOCALS%","ぁ","ァ")%
	LOCALS = %REPLACE(@"%LOCALS%","っ","ッ")%
ENDIF
RETURNF LOCALS