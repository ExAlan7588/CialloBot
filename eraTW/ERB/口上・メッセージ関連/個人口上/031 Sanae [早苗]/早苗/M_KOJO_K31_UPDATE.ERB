;--------------------------------------------------------------------------------------------------
;eraTW东风谷早苗口上UPDATE
;
;※注意　本体バージョン更新の际には
;　　　　『@M_KOJO_UPDATE_K31』の「#DIM CONST 早苗口上ver」の更新を忘れないこと！！
;--------------------------------------------------------------------------------------------------

;------M_KOJO_K31_UPDATE.ERB-----------------------------------------------------------------------
;2017/4/26　Ver 0.03.00　「M_KOJO_K31_イベント.ERB」より分离
;--------------------------------------------------------------------------------------------------
;※※※※※※※※※※※※※※※※※※※※※※※※※※
;"UPDATE判定"
;※※※※※※※※※※※※※※※※※※※※※※※※※※
@M_KOJO_UPDATE_K31
;口上独自のUPDATE用
;ENCOUNTER専用に独自分岐フラグを仕込んだ场合等にどうぞ
;UPDATE判定でCFLAGを使用场合、角色番号の指定が必要になります

;现在的verはver0.03です
#DIM CONST 早苗口上ver = 3


#DIM 现在的ver整数
#DIM 现在的ver小数
#DIM 早苗口上ver整数
#DIM 早苗口上ver小数


;ver0.02以前にはこのUPDATE CHECKはなかったので
SIF !CFLAG:31:K31_CFLAG_SYSTEM_VERSION
	CFLAG:31:K31_CFLAG_SYSTEM_VERSION = 2

;PRINTFORM中に%を登场させて变な感じにさせたくないのでここで变换
现在的ver整数 = CFLAG:31:K31_CFLAG_SYSTEM_VERSION / 100
现在的ver小数 = CFLAG:31:K31_CFLAG_SYSTEM_VERSION % 100
早苗口上ver整数 = 早苗口上ver / 100
早苗口上ver小数 = 早苗口上ver % 100

CALL M_KOJO_COLOR_K31
;UP约会时の头の文章
PRINTL 
PRINTL --早苗口上UPDATE CHECK--
RESETCOLOR

;『素質の变更』を行っている场合早苗の清扫技能は0になるが
;本体のUPDATEによってCSVの值が读み込まれてしまうため再修正を行う
;ほんとはUPDATE书き换えて、早苗だけ无效にしたいけどバグの温床になりそうなんで放弃
IF CFLAG:31:3121 == 1
	;---清扫技能　( D 1 → E 0)---
	;---清扫经验　( 20 → 4)---
	;流石に、料理までつけると素質盛りすぎ状态なので（技能にEがひとつもないとかいう状态）清扫技能に下方修正
	;既に技能,経験が育っている场合は变更しない
	IF ABL:31:40 == 1 && EXP:31:70 < 21
		PRINTL 
		PRINTL 根据本体的UPDATE、重新替换修正早苗的技能
		PRINTL 
		PRINT 	清扫技能(
		CALL PRINT_RANK(ABL:31:40,"MAX_6")
		PRINTFORM  {ABL:31:40} →
		CALL PRINT_RANK(0,"MAX_6")
		RESETCOLOR
		PRINTL  0)
		
		PRINTFORML 	清扫经验( {EXP:31:70} → 4)
		ABL:31:40 = 0
		EXP:31:70 = 4
	ENDIF
ENDIF
;--------------------------------------------------------------------------------------------
;早苗口上のバージョンチェック
;--------------------------------------------------------------------------------------------
;同じバージョンの场合
IF 早苗口上ver == CFLAG:31:K31_CFLAG_SYSTEM_VERSION
	PRINTL 
	PRINTFORML %"现在：Ver " + TOSTR(现在的ver整数) + "." + TOSTR(现在的ver小数,"D2")%
	PRINTFORML %"适用：Ver " + TOSTR(早苗口上ver整数) + "." + TOSTR(早苗口上ver小数,"D2")%
	PRINTL 
	PRINTFORML 因为适用的是最新的版本 所以不需要update
	RETURN 1
ENDIF

;ダウングレードしようとしてる～
IF 早苗口上ver < CFLAG:31:K31_CFLAG_SYSTEM_VERSION
	PRINTL 
	SETCOLOR C_RED
	PRINTFORML 【注意！】适用的是现在的版本，不是以前的版本
	RESETCOLOR
	PRINTL 
	PRINTFORML %"现在：Ver " + TOSTR(现在的ver整数) + "." + TOSTR(现在的ver小数,"D2")%
	SETCOLOR C_RED
	PRINTFORML %"适用：Ver " + TOSTR(早苗口上ver整数) + "." + TOSTR(早苗口上ver小数,"D2")%
	RESETCOLOR
	PRINTL 
	PRINTL 要继续update吗？
	PRINTL -如果你清楚自己为什么会进入到这个分歧的话再选择YES-
	PRINTL *就算发生BUG我也不管呦*
	PRINTFORML [0]-YES(强制进行适用版本的变更)
	PRINTFORML [1]-NO
	INPUT
	IF !RESULT
		PRINTL 继续update　请注意一下、说不定会出现BUG
	ELSE
		PRINTL update中断　请下载最新的版本
		RETURN 1
	ENDIF
ENDIF

IF 早苗口上ver > CFLAG:31:K31_CFLAG_SYSTEM_VERSION
	PRINTL 
	SETCOLOR C_YELLOW
	PRINTFORML 开始进行版本的update
	RESETCOLOR
	PRINTL 
	PRINTFORML %"现在：Ver " + TOSTR(现在的ver整数) + "." + TOSTR(现在的ver小数,"D2")%
	CALL M_KOJO_COLOR_K31
	PRINTFORML %"适用：Ver " + TOSTR(早苗口上ver整数) + "." + TOSTR(早苗口上ver小数,"D2")%
	RESETCOLOR
ENDIF

;####################################################
;ここからバージョンのUP约会が始まる
;####################################################
;----------------------------------------------------
;素質の变更(Ver 0.03より)
;----------------------------------------------------

IF CFLAG:31:3121 == 0 && (ABL:31:44 < 2 || EXP:31:79 < 200 || EXP:31:76 < 50 || EXP:31:70 < 50)
	PRINTL 
	PRINTL -----素質技能の变更-----
	;要するに、今のままだと口上の内容と技能・素質が噛み合わない场合があるのでそれが气になる人はどうぞ
	PRINTL 为了口上内容的整合性、开始变更早苗的素質技能
	PRINTL 就算不变更、也不会有系统上的影响
	;PRINTL 『この子メシマズのくせになんか语ってる…』が气になる方は变更をどうぞ
	PRINTL 
	PRINTL ※变更的素質・能力
	
	;---[音乐知识]付与(正确には、乐器知识)---
	;音乐技能がBであることにともなって追加
	;流石にみすちーとか九十九姊妹と同じレベルで何もないというのも
	;なんか[演奏]の地の文が变化するらしいのでコメントアウト
	;自力で取れるらしいので顽张ろう(演奏经验1000+技巧4以上)
	;IF !TALENT:31:音乐知识
	;	PRINTL [音乐知识]追加
	;	PRINTL 
	;ENDIF

	;---演奏经验　( 0 → 200)---
	;音乐技能がBであることにともなって追加
	SIF EXP:31:79 < 200
		PRINTFORML 	演奏经验( {EXP:31:79} → 200)

	;---料理技能　( E 0 → C 2)---
	;音乐技能と间违えられたクッソ哀れな子
	;流石に可哀想なので追加
	IF ABL:31:44 < 2
		PRINT 	料理技能(
		CALL PRINT_RANK(ABL:31:44,"MAX_6")
		PRINTFORM  {ABL:31:44} →
		CALL PRINT_RANK(2,"MAX_6")
		RESETCOLOR
		PRINTL  2)
	ENDIF

	;---料理经验　( 0 → 50)---
	;料理技能がCであることにともなって追加
	;料理技能をつけようとして音乐技能がついてるっぽい早苗さんに料理技能及び料理经验をつける
	;3人揃って技能0のメシマズ神社救済法案
	SIF EXP:31:76 < 50
		PRINTFORML 	料理经验( {EXP:31:76} → 50)
	
	;---清扫技能　( D 1 → E 0)---
	;---清扫经验　( 20 → 4)---
	;流石に、料理までつけると素質盛りすぎ状态なので（技能にEがひとつもないとかいう状态）清扫技能に下方修正
	;既に技能,経験が育っている场合は变更しない
	IF ABL:31:40 == 1 && EXP:31:70 < 21
		PRINT 	清扫技能(
		CALL PRINT_RANK(ABL:31:40,"MAX_6")
		PRINTFORM  {ABL:31:40} →
		CALL PRINT_RANK(0,"MAX_6")
		RESETCOLOR
		PRINTL  0)
		
		PRINTFORML 	清扫经验( {EXP:31:70} → 4)
	ENDIF
	
	
	PRINTL 
	PRINTFORML [0]-保留		(现在不变更、这个选择肢会在版本变更的时候再登场)
	PRINTFORML [1]-变更		(这个选择肢不会再登场了)
	PRINTFORML [2]-不变更	(这个选择肢不会再登场了)
	INPUT
	SELECTCASE RESULT
	;保留しました
	CASE 0
		CFLAG:31:3121 = 0
		PRINTL 保留了
		PRINTW 
	;变更しました
	CASE 1
		CFLAG:31:3121 = 1
		PRINTL 变更了
		
		;变更内容はここに记述
		;SIF !TALENT:31:音乐知识
		;	TALENT:31:音乐知识 = 1
		SIF EXP:31:79 < 200
			EXP:31:79 = 200
		SIF ABL:31:44 < 2
			ABL:31:44 = 2
		SIF EXP:31:76 < 50
			EXP:31:76 = 50
		IF ABL:31:40 == 1 && EXP:31:70 < 21
			ABL:31:40 = 0
			EXP:31:70 = 4
		ENDIF
		PRINTW 
	;变更させません
	CASEELSE
		CFLAG:31:3121 = -1
		PRINTL 不会让你变更的
		PRINTW 
	ENDSELECT
	;============================================================
	;[1]、[2]を选んだけれどもどうしてももう一度选择肢を选びたい时
	;1.CFLAG:31:3121 = 0 にする
	;============================================================
ENDIF
;----------------------------------------------------
;认识の剥夺(Ver 0.01より)
;----------------------------------------------------
;口上的には「最初から」みたいなもの　バージョンUPで仕样が变わった场合や、他のルートに行きたい时とかにどうぞ
;本当はフラグ等のリセットは初对面时に行っている
IF CFLAG:31:18 != 0
	PRINTL 
	PRINTL -----关于认识-----
	PRINTL 你现在已经、认识了早苗了
	PRINTL 在「update口上」时、比较推荐使用「初次导入」来重置认识的情况
	PRINTL 以及、口上里的「从最初开始」、会把口上内的进行状况・flag全都初期化
	PRINTL 如果版本更新时的「readme.txt」里出现了强烈推荐使用重置认识的情况的话
	PRINTL 根据最新版本的情况、也有改变进行情况・flag的可能性
	PRINTL (因此、在上记条件下没有重置的话出现BUG也是没有办法的事)
	PRINTL 
	PRINTFORML [0]-重置喽
	PRINTFORML [1]-才不要
	INPUT
	IF !RESULT
		CFLAG:31:18 = 0
		PRINTL -认识重置了-　可以重新认识早苗了
	ELSE
		PRINTL -没有重置认识-
	ENDIF
ENDIF

CFLAG:31:3120 = 早苗口上ver

;PRINTFORM中に%を登场させて变な感じにさせたくないのでここで变换
现在的ver整数 = CFLAG:31:K31_CFLAG_SYSTEM_VERSION / 100
现在的ver小数 = CFLAG:31:K31_CFLAG_SYSTEM_VERSION % 100

PRINTL 
PRINTL update完成了

CALL M_KOJO_COLOR_K31
PRINTFORML 适用版本　%"Ver " + TOSTR(现在的ver整数) + "." + TOSTR(现在的ver小数,"D2")%
RESETCOLOR
PRINTL 

RETURN 1
