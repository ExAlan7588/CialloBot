
;### 函数 ###################################
;==================================================
;人目があるか判定函数＠掀裙子判定のをまとめただけ
;人目がある =1 无い =0
;-------------------------------------------------
@K30_BE_SEEN()
#FUNCTION
SIF GET_TARGETNUM() > 1 || DATE_HITOGOMI(CFLAG:PLAYER:当前位置) || WITH_MOB()
	RETURNF 1
RETURNF 0 
;==================================================
;挨拶を返す式中函数
;4时～10时前・10时～18时前・それ以外
;-------------------------------------------------
@K30_GREETING_EIKI()
#FUNCTIONS
SELECTCASE TIME
	CASE 240 TO 599
		SIF TALENT:恋人
			RETURNF "早上好"
		RETURNF "早安"
	CASE 600 TO 1079
		RETURNF "你好"
	CASEELSE
		RETURNF "晚上好"
ENDSELECT
;==================================================
;MASTERの恋人がその场に居るかどうか返す式中函数
;=-1 映姫大人が恋人 =0 元から恋人无 =1 その场に居ない =2 恋人と同席
;非恋人时の映姫大人を面倒くさい女にさせる
;-------------------------------------------------
@K30_FIND_LOVER()
#FUNCTION
SIF TALENT:MASTER:恋人 == 0
	RETURNF 0
SIF TALENT:30:恋人
	RETURNF -1
SIF GET_TARGETNUM() < 2
	RETURNF 1
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF TALENT:(TARGET:LOCAL):恋人 && CFLAG:MASTER:当前位置 == CFLAG:(TARGET:LOCAL):当前位置
		RETURNF 2
NEXT
RETURNF 1
;==================================================
;变Ｔで映姫大人の目が死ぬか判别する式中函数
;=0 平气 =1 目が死ぬ 
;换装指令の存在忘れてた
;-------------------------------------------------
@K30_CHECK_HEN_T()
#FUNCTION
SIF DAY % 7 == 5
	RETURNF 1
SIF FLAG:ファッション == 62
	RETURNF 1
SIF CFLAG:30:衣物暂时更换 == 62
	RETURNF 1
RETURNF 0
;==================================================
;醉っぱらい度を数字で返す式中函数
;=0 清醒 =1 微醺 =2 醉酒 3=不省人事
;-------------------------------------------------
@K30_DRUNK()
#FUNCTION
SIF BASE:30:酒气 <= 2
	RETURNF 0
SELECTCASE BASE:30:酒气
	;不省人事
	CASE IS > (MAXBASE:30:酒气 / 10) * 8
		RETURNF 3
	;醉酒
	CASE IS > (MAXBASE:30:酒气 / 10) * 5
		RETURNF 2
	;微醺
	CASE IS <= (MAXBASE:30:酒气 / 10) * 5
		RETURNF 1
ENDSELECT
;==================================================
;接吻其他を视线加味して通すか判断する式中函数 
;フレーバーのだから辛めでもいいや
;=0 失败 =1 成功
;-------------------------------------------------
@K30_TRY_HARASS
#FUNCTION
;状态异常・诶嘿嘿时・招待时は通す
SIF SHIRAHU(30) != 1 || CFLAG:30:诶嘿嘿 || CFLAG:30:招待
	RETURNF 1
;接吻魔がついたら接吻は全通し
SIF TALENT:接吻魔 && SELECTCOM == 312
	RETURNF 1
LOCAL = RAND:110
LOCAL:1 = 25 + GET_SUCCESS_RATE() / 8
SIF LOCAL:1 > 109
	LOCAL:1 = 109
;キャップをかけた后に视线が通ってたら-33%、违ったら+20%
IF K30_BE_SEEN() == 0
	LOCAL:1 += 20
ELSE
	LOCAL:1 -= 33
ENDIF
;反抗刻印1につき-20%
LOCAL:1 -= MARK:30:反抗刻印 * 20
SIF LOCAL >= LOCAL:1
	RETURNF 0
RETURNF 1
;==================================================
;同房间の角色を拾う式中函数
;爱丽丝口上の函数お借りして平文化しました。ERHだと动作が速かったり安定するのかな
;丸コピは那个なので知り合い以外は随机で选んでIDを返すように。谁もいなければ0を返す
;参数が真-TARGET内随机、伪-优先顺序付き（[恋人]持ち→知り合い→TARGET内随机）
;映姫大人の知り合いは8人,小町>久侘歌>觉>赫卡提亚>阿求>幽幽子>紫>璎花の顺に(胜手に)设定
;-------------------------------------------------
@K30_FIND_AROUND(ARG = 0)
#FUNCTION
#DIM 知り合い = 76, 129, 49, 113, 80, 66, 26, 127
#DIM 总人数
#DIM 抽选
#LOCALSIZE 200
VARSET LOCAL
抽选 = 0
总人数 = GET_TARGETNUM() 
;房间に映姫大人一人なら当然0
SIF 总人数 == 1
	RETURNF 0
IF !ARG
	;[恋人]持ちがいたら优先してIDを返す
	SIF K30_FIND_LOVER() == 2
		RETURNF TALENT:MASTER:恋人
	;知り合い优先度顺に当前位置を调べMASTERと同じならIDを返す
	FOR COUNT, 0, 8
		SIF CFLAG:(知り合い:COUNT):当前位置 == CFLAG:MASTER:当前位置
			RETURNF 知り合い:COUNT
	NEXT
ENDIF
;知り合いがいなければ同室のTARGET_ID全部を拾う
FOR COUNT,1, 总人数 + 1
	;地狱の断头台withジェロニモ状态を防ぐため口上主はスキップ
	SIF TARGET:COUNT == TARGET:0
		CONTINUE
	;MASTERと映姫大人の当前位置にいなければスキップ
	SIF (CFLAG:(TARGET:COUNT):当前位置 != CFLAG:MASTER:当前位置) || (CFLAG:(TARGET:COUNT):当前位置 != CFLAG:30:当前位置)
		CONTINUE
	;配列のCOUNTは别で取りながら代入
	LOCAL:抽选 = TARGET:COUNT
	抽选 ++
NEXT
;この条件でここまで来たのはなんでだろう
;ともかくエラー出さないためにヨシ！
SIF 抽选 < 1
	RETURNF 0
;抽选してIDを决定
RETURNF LOCAL:(RAND(抽选))
;==================================================
;映姫大人が他のキャラを呼ぶ式中函数
;呼び舍て =小町 久侘歌：敬称 =ヘカーティア「大人」
;阿求 =「御阿礼の子」：リリー姊妹 =略さない：他は皆「桑」づけ
;ARG=0でMASTER 条件付けで呼び名を变える。需要なさそうだけどおにい桑にも对応
;参数 =1or2をとると行きずりキャラ/MASTER/对象キャラ无し(ARG=0)が指定されたら「だれか」「他の人」と呼ぶ＠性骚扰が捗る
;参数 =3で种族・属性呼び。CSV读みでTALENT分けより雰囲气出る气がするけど、个别で设定しないからやっぱり变なのはある
;-------------------------------------------------
@K30_C_NAME(ARG, TYPE = 0)
#FUNCTIONS
#DIM TYPE
#DIM VARIANT
#DIMS STR_LIST, 10
VARSET LOCALS
VARSET STR_LIST
VARIANT = 0
SIF TYPE == 3
	GOTO TRIBE
IF TYPE && (ARG == RANDOM_CHARANUM || ARG == 0)
	SIF TYPE == 1
		RETURNF "谁"
	RETURNF "其他人"
ENDIF
SELECTCASE ARG
	;MASTERの呼び名
	CASE 0
		;反抗持ち、勃然大怒时はよそよそしい感じに
		SIF CFLAG:30:勃然大怒 || MARK:30:反抗刻印 > 0
			RETURNF CALLNAME:MASTER + "桑"
		;恋慕无ければ通常形
		SIF !TALENT:30:恋慕
			RETURNF MASTERNAME:30 + "桑"
		;名有り角色周りにいない+恋慕时に设定で固有の呼び方
		SIF GET_TARGETNUM() <= 1 && CSTR:30:80 != ""
			RETURNF CSTR:30:80
		;恋人は呼び舍て
		SIF TALENT:30:恋人
			RETURNF MASTERNAME:30
		;诶嘿嘿中で人目が无い时も呼び舍て
		SIF CFLAG:30:诶嘿嘿 && K30_BE_SEEN() == 0
			RETURNF MASTERNAME:30
		;约会中は固有の呼び名
		SIF CHK_DATENOW(CFLAG:约会中) && CSTR:30:80 != ""
			RETURNF CSTR:30:80
		;それ以外は通常形
		RETURNF MASTERNAME:30 + "桑"
	;リリー姊妹
	CASE 18, 19
		RETURNF @"%\@ ARG == 18 ? 莉莉霍瓦特 # 黑莉莉\@%"
	;小町・久侘歌
	CASE 76, 129
		RETURNF CALLNAME:ARG
	;阿求
	CASE 80
		SIF CFLAG:30:当前位置 == CFLAG:80:当前位置
			RETURNF CALLNAME:ARG + "桑"
		RETURNF "御阿礼之子"
	;ヘカーティア
	CASE 113
		RETURNF CALLNAME:ARG + "大人"
	;其他
	CASEELSE
		RETURNF CALLNAME:ARG + "桑"
ENDSELECT
$TRIBE
;琪露诺だけ强烈に违和感あったので直接指定
SIF ARG == 14
	RETURNF "冰精"
;CSTR:10からの抽出はPRINT_STATEの流用。介绍文が无い、'●'で区切られてないなら飞ばす
LOCALS =  %CSVCSTR(ARG, 10)%
IF STRFIND(LOCALS, "●") > -1
	SPLIT LOCALS, "●", STR_LIST
	FOR LOCAL, 0, RESULT
		IF STRFIND(STR_LIST:LOCAL, "种族：") > -1
			LOCALS:1 = %REPLACE(STR_LIST:LOCAL, "种族：", "")%
			LOCALS:1 = %REPLACE(LOCALS:1, "　", "")%
			BREAK
		ENDIF
	NEXT
ENDIF
;'（）'で种族が括られた角色は个别对応
IF STRFIND(LOCALS:1, "（") > -1
	SPLIT LOCALS:1, "（", LOCALS
	LOCALS:1 = %REPLACE(LOCALS:1, "）", "")%
	LOCALS:1 = %\@ GROUPMATCH(ARG,35,40,104,105) ? %LOCALS:1% # %LOCALS:0% \@%
;'人类''妖怪'はTALENTの方で更に詳しく见る
ELSEIF  LOCALS:1 == "人类"
	VARIANT = 1
	LOCALS:1 = 
ELSEIF LOCALS:1 == "妖怪"
	VARIANT = 2
	LOCALS:1 = 
ENDIF
SIF LOCALS:1 != ""
	RETURNF LOCALS:1
;'人类''妖怪'は追加种族→TALENTで判断
IF VARIANT
	IF TALENT:ARG:GETNUM(TALENT, "追加种族")
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "追加种族"))%
	ELSEIF VARIANT == 1
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "人类"))%
	ELSE
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "妖怪"))%
	ENDIF
;其他はしらみつぶしで拾いに行く
ELSE
	;条件できちんと分けるべきだが手抜き。仕大人变わったらちゃんと合わせる
	FOR LOCAL, 0, 7
		IF TALENT:ARG:(190 + (LOCAL))
			LOCALS:1 = %GET_TRIBENAME(ARG, (190 + LOCAL))%
			BREAK
		ENDIF
	NEXT
	;'人类''妖怪'は追加种族を优先させる
	SIF TALENT:ARG:GETNUM(TALENT, "追加种族") && (TALENT:ARG:GETNUM(TALENT, "妖怪") == 1 || TALENT:ARG:GETNUM(TALENT, "人类") == 1)
		LOCALS:1 = %GET_TRIBENAME(ARG, GETNUM(TALENT, "追加种族"))%
ENDIF
RETURNF LOCALS:1
;==================================================
;中出しOKか判别する式中函数
;性交关联口上の准备（书けるかは不明）
;--------------------------------------------------
;=0 中出しOK =1 妊妇桑だからダメ =2 育儿中だからダメ =3 合意が无い
;--------------------------------------------------
@K30_CREAMPIE()
#FUNCTION
;口服避孕药さえ饮めば出すのは安心
SIF TCVAR:30:口服避孕药
	GOTO PREG
;合意状况を见る
SIF (CFLAG:30:允许无套 == 0 && ESTRUS_CYCLE(30) != 15) || (CFLAG:30:允许无套 == 1 && ESTRUS_CYCLE(30) == 150)
	RETURNF 3
;育儿状况を见る
SIF TALENT:30:育儿中 && CFLAG:30:分娩经过日 < CHILD_就学
	RETURNF 2
$PREG
;母胎の成育を见る
SIF TALENT:30:妊娠 && !INRANGE(CFLAG:妊娠经过日数, 31, 70)
	RETURNF 1
;ここまで抜けたらOK
RETURNF 0
;==================================================
;接吻ハメ实行可能か见る式中函数
;性交关联口上をシチュ固定して埋めやすくする
;--------------------------------------------------
;=0 OK =-1 猫舌だからダメ =-2 ばっちいからダメ
;--------------------------------------------------
@K30_KISS_CANCEL()
#FUNCTION
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:舌 < 2
	RETURNF -1
SIF TALENT:猫舌 && ABL:舌 < 2
	RETURNF -1
SIF TALENT:PLAYER:污臭耐性 > 1
	RETURNF 0
SIF STAIN:0 & 污渍_精液 && ABL:PLAYER:精液中毒 < 3
	RETURNF -2
SIF STAIN:0 & 污渍_肛门
	RETURNF -2
SIF STAIN:0 & 污渍_阴茎 && TALENT:PLAYER:污臭耐性 < 0
	RETURNF -2
SIF TALENT:PLAYER:污臭耐性 == -2 && (STAIN:0 & 污渍_破瓜血 || STAIN:0 & 污渍_爱液)
	RETURNF -2
RETURNF 0
;==================================================
;REDRAW制御とCLEARLINEで直前の文章を消す函数
;拟似的にイベントをスキップする目的で使用
;--------------------------------------------------
@K30_CLEAR_TEXTLINE(ARG = 1)
LOCAL = CURRENTREDRAW()
REDRAW 0
CLEARLINE ARG
REDRAW LOCAL
RETURN
;==================================================
;儿童の口上色を随意决める函数
;名前の文字コードから起こして固定化する
;--------------------------------------------------
@K30_CHILDWORD()
#FUNCTION
SIF !FLAG:口上色
	RETURNF GETDEFCOLOR()
RETURNF GET_CN_WORD_COLOR(STR:(2050 + (ENCODETOUNI(SHOW_CHILDNAME(30)) % 13)))
;==================================================
;映姫大人がどれだけヤル气か见る式中函数
;@ENDUREを仕事や身体接触中に使用为に少し削って持ってきただけ
;本体ではこの值が RAND:300 + 300 より大きいと被逆推に移行
;-------------------------------------------------
@K30_PUSH_DOWN()
#FUNCTION
;素質により判定变化
LOCAL = CFLAG:30:合意判定
LOCAL += (EX:30:Ｃ寸止 + EX:30:Ｖ寸止 + EX:30:Ａ寸止 + EX:30:Ｂ寸止 + EX:30:Ｍ寸止) * 20
;抱き着くと被逆推率上升
SIF SELECTCOM == 311
	LOCAL += 30
LOCAL += TALENT:30:性的兴趣 * 20
SIF TALENT:30:处女
	LOCAL -= 50
SIF TALENT:30:恋慕
	LOCAL = LOCAL + 50
SIF TALENT:30:自尊心 > 0
	LOCAL -= 20
SIF CFLAG:30:允许无套 == 2
	LOCAL = LOCAL + 50
SIF CFLAG:30:强行内射
	LOCAL -= 100
SIF TALENT:30:胆量 < 0 || TALENT:30:回应 < 0 || TALENT:30:冷漠
	LOCAL = LOCAL - 30
SIF TALENT:30:羞耻心 > 0
	LOCAL -= 30
SIF TALENT:30:羞耻心 < 0
	LOCAL = LOCAL + 20
SIF TALENT:30:自制心 
	LOCAL = LOCAL - 20
SIF TALENT:30:贞操 < 0 || TALENT:30:小恶魔
	LOCAL = LOCAL + 20
SIF TALENT:30:贞操 > 0 || TALENT:30:难以越过的底线
	LOCAL -= 20
IF !GETBIT(CFLAG:30:既成事实, 1) && TALENT:30:贞操 > 0
	LOCAL -= 30
	SIF !TALENT:30:恋慕 && !TALENT:30:炮友
		LOCAL -= 50
ENDIF
SIF OTOKOGIRAI(30) && !(IsHeat(30) || IsInsenceValid(30))
	LOCAL = LOCAL / 2
SIF TALENT:30:难以越过的底线 && TALENT:30:恋慕
	LOCAL = LOCAL * 12 / 5
SIF MARK:30:反抗刻印 == 3
	LOCAL = 0
SELECTCASE CFLAG:30:积攒度
	CASE IS >= 1000
		LOCAL *= 3
	CASE IS >= 800
		LOCAL = LOCAL * 3 / 2
	CASE IS >= 600
		LOCAL = LOCAL * 5 / 4
	CASE IS >= 500
	CASE IS >= 300
		LOCAL /= 2
	CASE IS >= 200
		LOCAL /= 4
	CASE IS >= 100
		LOCAL /= 10
	CASEELSE
		LOCAL = 0
ENDSELECT
;体力に依存して推倒率减少
SELECTCASE BASE:30:体力
	CASE IS < 750
		LOCAL = 0
	CASE IS < 1000
		LOCAL /= 2
	CASE IS < 1500
		LOCAL = LOCAL * 3 / 4
	CASEELSE
ENDSELECT
SELECTCASE BASE:30:气力
	CASE 0
		LOCAL = 0
	CASE IS < 500
		LOCAL /= 2
	CASEELSE
ENDSELECT
;理性に依存して推倒率减少
IF BASE:30:理性 > 800
	LOCAL = LOCAL / 5
ELSEIF BASE:30:理性 > 600
	LOCAL = LOCAL / 2
ENDIF
SELECTCASE BASE:30:情绪
	CASE IS < 200
		LOCAL = 0
	CASE 201 TO 400
		LOCAL /= 4
	CASE 401 TO 600
		LOCAL /= 2
	CASE 601 TO 800
	CASEELSE
		LOCAL = LOCAL * 6 / 5
ENDSELECT
SIF FLAG:甲斐性无
	LOCAL = LOCAL * 2
SIF CFLAG:30:勃然大怒
	LOCAL = 0
RETURNF LOCAL
;==================================================
;日记用に场所を文字列で返す式中函数＠月Map对応
;数值で残すと据点引っ越しで变わる场合があるので
;-------------------------------------------------
@K30_DIARY_PLACE(ARG)
#FUNCTIONS
#DIM 目标对象Map
#DIMS TEMP_NAME
SIF CFLAG:30:初始位置 == ARG
	RETURNF "自分的房间"
目标对象Map =  GET_MAPID(ARG)
SELECTCASE 目标对象Map
	CASE 0
		SELECTCASE MAP_SEARCH_REPLACEMENT_0(ARG)
			CASE 妖精的大树
				TEMP_NAME = 三妖精之家
			CASE 梦幻遗迹
				TEMP_NAME = 梦幻遗迹
			CASEELSE
				TEMP_NAME = 博丽神社
		ENDSELECT
	CASE 1
		SELECTCASE MAP_SEARCH_REPLACEMENT_1(ARG)
			CASE 神灵庙广场, 神灵庙道场
				TEMP_NAME = 神灵庙
			CASEELSE
				TEMP_NAME = 命莲寺
		ENDSELECT
	CASE 2
		TEMP_NAME = 人里
	CASE 3
		SELECTCASE MAP_SEARCH_REPLACEMENT_3(ARG)
			CASE 雾之湖, 冰造的小雪屋
				TEMP_NAME = 雾之湖
			CASE 废洋馆
				TEMP_NAME = 骚灵の废洋馆
			CASEELSE
				TEMP_NAME = 红魔馆
		ENDSELECT
	CASE 4
		SELECTCASE MAP_SEARCH_REPLACEMENT_4(ARG)
			CASE 永远亭, 永远亭的内院
				TEMP_NAME = 永远亭
			CASE 无名之丘
				TEMP_NAME = 无名之丘
			CASE 太阳花田
				TEMP_NAME = 太阳花田
			CASE 梦幻馆
				TEMP_NAME = 梦幻馆
			CASEELSE
				TEMP_NAME = 迷途竹林
		ENDSELECT
	CASE 5
		SELECTCASE MAP_SEARCH_REPLACEMENT_5(ARG)
			CASE 香霖堂
				TEMP_NAME = 香霖堂
			CASE 雾雨魔法店
				TEMP_NAME = 雾雨魔法店
			CASE 人偶的洋馆
				TEMP_NAME = 人偶的洋馆
			CASE 再思之道
				TEMP_NAME = 再思之道
			CASE 无缘冢
				TEMP_NAME = 无缘冢
			CASEELSE
				TEMP_NAME = 魔法之森
		ENDSELECT
;		SIF TEMP_NAME == "魔理沙の店"
;			TEMP_NAME = 雾雨魔法店
;		SIF TEMP_NAME == "艾伦の店"
;			TEMP_NAME = 魔女の店
	CASE 6
		SELECTCASE MAP_SEARCH_REPLACEMENT_6(ARG)
			CASE 迷途之家
				TEMP_NAME = 八云家
			CASE 彼岸
				TEMP_NAME = 是非曲直厅
			CASE 白玉楼庭院, 白玉楼
				TEMP_NAME = 白玉楼
			CASE 地狱
				TEMP_NAME = 地狱
			CASE 畜生界
				TEMP_NAME = 畜生界
			CASE 灵长园
				TEMP_NAME = 灵长园
			CASEELSE
				TEMP_NAME = 三途之川
		ENDSELECT
	CASE 7
		SELECTCASE MAP_SEARCH_REPLACEMENT_7(ARG)
			CASE 正邪的藏身处前
				TEMP_NAME = 天邪鬼的住所
			CASE 仙人的宅邸
				TEMP_NAME = 仙人的宅邸
			CASEELSE
				TEMP_NAME = 妖怪之山の山麓
		ENDSELECT
;		SIF TEMP_NAME == "秋姊妹的家"
;			TEMP_NAME = 秋神大人の住まい
;		SIF TEMP_NAME == "正邪的藏身处前"
;			TEMP_NAME = 天邪鬼的住所
	CASE 8
		SELECTCASE MAP_SEARCH_REPLACEMENT_8(ARG)
			CASE 山之湖, 守矢神社境内, 守矢神社本殿
				TEMP_NAME = 守矢神社
			CASE 虹龙洞
				TEMP_NAME = 虹龙洞
			CASE 天界
				TEMP_NAME = 天界
			CASEELSE
				TEMP_NAME = 妖怪之山の山顶
		ENDSELECT
	CASE 9
		SELECTCASE MAP_SEARCH_REPLACEMENT_9(ARG)
			CASE 旧地狱街道, 旧地狱温泉, 情人旅馆
				TEMP_NAME = 旧都
			CASE 地灵殿
				TEMP_NAME = 地灵殿
			CASE 灼热地狱遗迹
				TEMP_NAME = 旧地狱
			CASEELSE
				TEMP_NAME = 地底
		ENDSELECT
	CASE 10
		SELECTCASE MAP_SEARCH_REPLACEMENT_10(ARG)
			CASE 槐安通路
				TEMP_NAME = 槐安通路
			CASE 月之都, 绵月亭, 玉兔之街
				TEMP_NAME = 月之都
			CASE 桃林～沙滩, 宁静海
				TEMP_NAME = 月都の郊外
			CASEELSE
				TEMP_NAME = 月の世界
		ENDSELECT
	CASEELSE
		TEMP_NAME = 见知らぬ场所
ENDSELECT
RETURNF TEMP_NAME
;==================================================
;日记用データを呼び出す式中函数
;--------------------------------------------------
;ARG: =0 日期  =1 天气 =2 特记事项
;	  =3 名前  =4 场所 =5 别记事项
;--------------------------------------------------
@K30_DIARY_DATA(キー, 种别)
#FUNCTIONS
#DIM 种别
#DIMS キー
#DIMS 返回值
#DIMS 取得文字列, 4
#DIMS 仮天气, 5 = "晴天", "晴朗", "多云", "阴天", "雨"
VARSET 取得文字列
LOCAL = 0
;读み込むCSTRの选别
IF 种别 > 2
	LOCAL ++
	种别 -= 3
ENDIF
;日记IDをキーとしてCSTRを検索
返回值 = %DIC_GET(CSTR:30:(81 + LOCAL), キー)%
IF 返回值 != ""
	;情报种别ごとに分割
	SPLIT 返回值, "/", 取得文字列
	;取得值なければフォローへ移动
	SIF 取得文字列:种别 == ""
		GOTO FOLLOW
	;参数に応じて文字列を返す
	RETURNF 取得文字列:种别
;VerUP,不具合等で返回值が空だった场合のフォロー
ELSE
	$FOLLOW
	SIF LOCAL
		种别 += 3
	SELECTCASE 种别
		CASE 0
			RETURNF "1"
		CASE 1
			LOCAL = TOINT(キー) % 5
			RETURNF 仮天气:LOCAL
		CASE 2, 5
			RETURNF ""
		CASE 3
			RETURNF MASTERNAME:30
		CASE 4
			RETURNF "あの场所"
	ENDSELECT
ENDIF
;==================================================
;日记に书き込む函数。第2参数を取ると未公开日记(DIARY:30:XX = 3)
;同时に日期等をCSTR:81,CSTR:82に记录。日记IDをキーとして使える@DIC_SETを拝借
;连想配列とかちゃんと意味わかってないけど、なんか使えて动いてるからこれでよし
;-------------------------------------------------
@K30_WRITE_DIARY(日记ID, ARG = 0)
#DIM 日记ID
#DIMS 日时
#DIMS 场所
#DIMS 天气
#DIMS 名前
#DIMS 特记
#DIMS 别记
#DIMS キー
#DIMS 书き込み值, 2
#DIMS 仮天气, 5 = "晴天", "晴朗", "多云", "阴天", "雨"
VARSET LOCALS
特记 =
别记 =
;残す记录を变换
キー = %TOSTR(日记ID)%
日时 = %TOSTR(DAY:0)%
场所 = %K30_DIARY_PLACE(CFLAG:30:当前位置)%
;异常气象は一部言い换え
SELECTCASE FLAG:异常气象
	CASE 1 TO 6
		天气 = %仮天气:(RAND:4)%　%\@ DAY:2 == 2 ? じっとりと暑い # 妙に暖かい \@%
	CASE 7, 8
		天气 = %仮天气:(RAND:4)%　%\@ FLAG:异常气象 == 7 ? 内衣が飞んでいる？ # 妙に风が强い \@%
	CASEELSE
		天气 = %GET_WEATHER()%
ENDSELECT
;UPDATEからなら天气は随机
SIF TFLAG:100 == 0
	天气 = %仮天气:(RAND:5)%
;名前はMASTERNAMEor恋慕后の爱称。陷落无しなら「桑」付け。变遷があると日记感がでる…かもしれない
名前 = %\@ CSTR:30:80 != "" ? %CSTR:30:80% # %MASTERNAME:30% \@%%\@ TALENT:30:思慕 || TALENT:30:恋慕 ? # 桑 \@%
;反抗３で名前を呼んで是けないあの人扱い
名前 = %\@ MARK:反抗刻印 == 3 ? あの人 # %名前% \@%
;页ごとの特记事项
SELECTCASE 日记ID
	;接吻：色々选别
	CASE 1
		;「と思っているが、实は」の场合、前の段落だけ拾って无自觉を弹く
		IF STRFIND(CSTR:30:初吻丧失履历, "￥ＢＲ") > -1
			SPLIT CSTR:30:初吻丧失履历, "￥ＢＲ", LOCALS
		ELSE
			LOCALS:0 = %CSTR:30:初吻丧失履历%
		ENDIF
		;强行だと日记书かない
		IF STRCOUNT(LOCALS:0, "强行")
			SETBIT CFLAG:30:1910, 2
			RETURN
		ELSE
			;UPDATEからなら场所をぼかす
			SIF TFLAG:100 == 0
				场所 = 大切な场所
			SIF TALENT:30:恋慕
				特记 = 恋
			SIF STRCOUNT(LOCALS:0, "献给了") || GETBIT(CFLAG:30:1910, 6)
				别记 = 捧
		ENDIF
	;处女丧失：色々选别
	CASE 2
		;无意识と你以外で破瓜は以后スルー
		IF STRCOUNT(CSTR:30:处女丧失履历, "时间停止中|醉|沉睡|不满")
			SETBIT CFLAG:30:1910, 3
			RETURN
		ENDIF
		;破瓜口上用意してないせいでかなりガバガバだけど3段阶で选别
		日时 = %TOSTR(CFLAG:30:处女丧失纪念日)%
		IF TALENT:30:恋慕 && !STRCOUNT(CSTR:30:处女丧失履历, "霸王硬上弓|振动棒")
			特记 = 恋
		ELSEIF TALENT:30:思慕 || TALENT:30:恋慕
			特记 = 思
		ELSE
			特记 = 无
		ENDIF
	;お小言もらった：理由と说教の长さ
	CASE 5
		特记 = %TOSTR(TFLAG:193)%
	;お说教たいむ：小町のサボり现场
	CASE 8
		场所 = %K30_DIARY_PLACE(CFLAG:MASTER:当前位置)%
		SIF 场所 == "三途之川"
			场所 = 稍微别の场所
	;ぶん殴られた：耐えたor倒れた
	CASE 10
		SIF BASE:MASTER:体力 <= 500
			特记 = 倒
	;赠り物(GOOD)：赠り物フルネーム、恋慕の有无
	CASE 12
		CALL GIFTNAME(TCVAR:30:今天的礼物)
		特记 = %RESULTS%
		别记 = %\@ TALENT:恋慕 ? 恋 # \@%
	;赠り物(NORMAL&BAD)：赠り物本体名
	CASE 13, 14
		CALL GIFTDATA_MAIN(GET_GIFTDATA(TCVAR:30:今天的礼物, "本体"))
		特记 = %RESULTS%
	;掀裙子：是てるぱんつと反应
	CASE 15
		特记 = %PANTSNAME(EQUIP:下半身内衣２, 30)%
		SIF 特记 == "没穿内裤"
			特记 = 没穿の
		SELECTCASE TFLAG:193
			CASE IS < 0
				别记 = 怒
			CASE IS > 10
				别记 = 喜
			CASEELSE
				别记 = 照
		ENDSELECT
	;说法10回结束：MASTER现住所
	CASE 16
		场所 = %K30_DIARY_PLACE(CFLAG:MASTER:初始位置)%
	;ヘカ大人からお手纸：手纸の中身
	CASE 18
		SIF STRCOUNT(CSTR:30:0, "变Ｔ指令")
			特记 = 变Ｔ
	;何か钓れた：钓ったもの
	CASE 22
		SELECTCASE TFLAG:193
			CASE 1 TO 20, 22 TO 31
				特记 = 鱼
				SIF GROUPMATCH(TFLAG:193, 9, 23, 24, 28, 30, 31)
					特记 = 大きな鱼
			CASE 21
				特记 = あんな变なもの
			CASE 606, 609, 643
				特记 = 素材
			CASE 131, 610
				特记 = 谁かが舍てたゴミ
			CASE 706
				特记 = ただの青蛙
		ENDSELECT
	;宴会のお知らせ：举办要项
	CASE 24
		SELECTCASE FLAG:宴会举办FLAG
			CASE 2
				场所 = 博丽神社
				特记 = 忘年会
			CASE 3
				场所 = 永远亭
				特记 = お赏月会
			CASE 4
				场所 = 人里
				特记 = 收获祭
		ENDSELECT
	;单薄的本见ちゃった：本の中身、兴味の有无
	CASE 25
		特记 = %\@ TFLAG:193 ? 惨 # \@%
		SIF ABL:欲望 > 6 || DIARY:30:31 != 0
			别记 = 有
	;恋慕：呼び名变えたか
	CASE 30
		IF !STRCOUNT(CSTR:30:0, "称呼")
			特记 = 1
		ELSEIF CSTR:30:80 == "" || CSTR:30:80 == MASTERNAME:30
			特记 = 2
		ELSE
			别记 = %MASTERNAME:30%
		ENDIF
	;分娩：产まれた子どもの名前
	CASE 34
		特记 = %CHILDNAME:30:(TALENT:30:育儿中)%
	;小町と同时：映姫大人と小町の陷落状态
	CASE 36
		SELECTCASE K30_FIND_LOVER()
			CASE -1
				特记 = 映姫
			CASE 0
			CASE 1, 2
				SIF TALENT:76:恋人
					特记 = 小町
		ENDSELECT
	;尻穴狂：振动棒约会经验、恋慕の有无
	CASE 37
		特记 = %\@ CFLAG:1608 ? 有 # \@%
		别记 = %\@ TALENT:恋慕 ? 恋 # \@%
	;炮友：取得手段、契约时の分岐
	CASE 39
		IF COM_STR == "K30_契约" && TFLAG:193
			特记 = 契约
			SELECTCASE TFLAG:193
				CASE 3
					别记 = 前世
				CASE 2
					别记 = Ａ调教
				CASE 1
					别记 = 无意识
			ENDSELECT
		ELSE
			特记 = 素質
		ENDIF
ENDSELECT
IF ARG == 0
	DIARY:30:日记ID = 2
ELSE
	DIARY:30:日记ID = 3
	CALL CHARA_DIARY_PAGESETTING(30, 日记ID)
ENDIF
书き込み值:0 = %日时%/%天气%/%特记%
书き込み值:1 = %名前%/%场所%/%别记%
CSTR:30:81 = %DIC_SET(CSTR:30:81, キー, 书き込み值:0)%
CSTR:30:82 = %DIC_SET(CSTR:30:82, キー, 书き込み值:1)%
RETURN
;==================================================
;地の文改变时につじつま合わせる函数
;-------------------------------------------------
@K30_COMPENSATE_MESSAGE
SIF CFLAG:MASTER:诶嘿嘿 != 2 && SELECTCOM != 461
	CALL Counter呼び出し处理
CALL M_KOJO_COLOR_K30
RETURN 1
;==================================================
;白い线を引く。ただそれだけの函数
;-------------------------------------------------
@K30_D_LINE
RESETCOLOR
DRAWLINE
CALL M_KOJO_COLOR_K30
RETURN RESULT
;==================================================
;诶嘿嘿入る时の准备函数
;绝顶回数等リセット,掌握把柄保存
;さらに意识がある场合、前日行动反映をリセット・诶嘿嘿回数/时间记录・映姫大人自室なら描写用フラグ立て
;-------------------------------------------------
@K30_PRESET_UFUFU
VARSET TCVAR:30:0, 0, 389, 399
TCVAR:30:381 = CFLAG:掌握把柄
IF SHIRAHU(30)
	CFLAG:1603 = 0
	TCVAR:30:380 ++
	TCVAR:30:383 = TIME:1
	SIF GROUPMATCH(CFLAG:MASTER:当前位置, CFLAG:30:初始位置, OMANEKIBEYA())
		TCVAR:30:378 = 1
ENDIF
RETURN RESULT
;==================================================
;诶嘿嘿结束の准备函数
;各种フラグ扫除
;意识あり合意ありなら结束时刻を记录,
;你が指令から结束させた场合、媚药模式待机フラグ残す
;-------------------------------------------------
@K30_UFUFU_RECORD(ARG = 0)
SIF TCVAR:30:383 && !MAXARRAY(TCVAR:30:0, 385, 388) 
	CFLAG:30:1504 = TIME:1
CFLAG:30:澡堂 = MIN(CFLAG:30:澡堂 + (SUMARRAY(TCVAR:30:0, 390, 395) * 150), 2500)
CFLAG:30:延迟 += 15
VARSET TCVAR:30:0, 0, 383, 388
TCVAR:30:376 = 0
SIF !ARG
	CLEARBIT TCVAR:30:367, 2
RETURN RESULT
;==================================================
;TARGET外のまま复数人诶嘿嘿に入った场合のフォロー函数
;ひと指令おいて突然口吻が变わるかもだが、そのままよりはマシなはず
;-------------------------------------------------
@K30_FOLLOW_UP
CALL K30_PRESET_UFUFU
SIF CFLAG:1801 == 0
	RETURN
IF !TALENT:恋慕
	TCVAR:30:385 = 1
ELSEIF K30_FIND_LOVER() == 2 && !GROUPMATCH(K30_FIND_AROUND(), 76, 129)
	TCVAR:30:385 = 1
	TCVAR:30:387 = 1
ENDIF
;==================================================
;画面を揺らす函数。再配布ＯＫなので流用
;ここから引用
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;「ぱにめーしょん」0527版　By ぱ。
;Emuera専用泛用、アニメーション・ビジュアル扩张函数ファイル。ほぼバリアント间互换あり
;无断再配布ＯＫ、その际改变・追记は元の机能を损なわない范围でお愿いします
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;-------------------------------------------------
;振动EFFECT函数@QUAKE
;	参数0：振动数。省略すると2
;
;画面を上下に揺らします。揺れ幅はある程度随机です
;-------------------------------------------------
@K30_QUAKE(ARG = 2)
#LOCALSIZE 3
;表示オフだと返回
;SIF !ANIME_CONFIG("QUAKE")
;	RETURN RESULT
REDRAW 0
FOR LOCAL, 0, ARG
	LOCAL:1 = RAND:4+1
	FOR LOCAL:2, 0, LOCAL:1
		PRINTL 
	NEXT
	TWAIT 100, 0
	CLEARLINE LOCAL:1
	TWAIT 100, 0
NEXT
REDRAW 3
RETURN RESULT
;引用ここまで
;==================================================
;主人公の呼び名を变更する函数
;スレで话题にあがってたから、なんとなく入れてみる
;构文は命OD势の口上からほぼそのまま持ってきてます
@K30_SET_C_NAME(ARG)
#DIMS C_NAM
;--------------------------------------------------
;ARG =0:通常の初回入力 =1:恋慕呼び名入力 =2:祈愿からの通常呼び名变更 =3:祈愿からの恋慕呼び名变更
;--------------------------------------------------
RESETCOLOR
PRINTFORMDL 呼び名を入力してください(直接入力するか、ボタンで选择してください)
IF ARG == 1 || ARG == 3
	C_NAM = %MASTERNAME:30%
	SIF ARG == 3 && CSTR:30:80 != ""
		C_NAM = %CSTR:30:80%
	PRINTBUTTON "[主人就好]", "主人"
	PRINTL 
	PRINTBUTTON "[老公就好]", "老公"
	PRINTL
	PRINTBUTTON @"[果然还是%C_NAM%就好]", @"%C_NAM%"
	PRINTL
ELSE
	C_NAM = %CALLNAME:MASTER%
	PRINTBUTTON @"[%C_NAM%\@ TALENT:30:恋人 ? # 桑 \@就这么叫了]", @"%C_NAM%"
	PRINTL
ENDIF
$INPUT_LOOP
RESETCOLOR
INPUTS
CALL M_KOJO_COLOR_K30
;行头行尾の空白文字削除
C_NAM = %RESULTS%
REPLACE C_NAM, "\(\^\\s\+\|\\s\+\$)", ""
;2文字未满
IF STRLENS(C_NAM) < 2
	SIF STRLENS(RESULTS)
		CLEARLINE 1
	REUSELASTLINE 「…请更加珍惜从双亲那里得到的姓名」
	GOTO INPUT_LOOP
;12文字(全角6文字)超えた
ELSEIF STRLENS(C_NAM) > 12
	CLEARLINE 1
	REUSELASTLINE 「对不起。平时称呼的话稍微有点太长了…」
	GOTO INPUT_LOOP
;以下その角色の扮演でない时
;同姓同名
ELSEIF FLAG:扮演 != 30 && STRCOUNT(C_NAM, "\(四季\)\*\\s\*映姫")
	CLEARLINE 1
	REUSELASTLINE 「…请不要取笑我」
	GOTO INPUT_LOOP
;小町or久侘歌
ELSEIF (FLAG:扮演 != 129 && STRCOUNT(C_NAM, "\(庭渡\)\*\\s\*久侘歌")) || (FLAG:扮演 != 76 && STRCOUNT(C_NAM, "\(小野冢\)\*\\s\*小町"))
	CLEARLINE 1
	REUSELASTLINE 「…这种叫法容易混淆，请用其他的称呼」
	GOTO INPUT_LOOP
;阿求
ELSEIF (FLAG:扮演 != 80 && STRCOUNT(C_NAM, "\(稗田\)\*\\s\*阿求"))
	CLEARLINE 1
	REUSELASTLINE 「…新的御阿礼之子？ 请不要开奇怪的玩笑」
	GOTO INPUT_LOOP
;赫卡提亚
ELSEIF (FLAG:扮演 != 113 && STRCOUNT(C_NAM, "赫卡提亚"))
	CLEARLINE 1
	REUSELASTLINE 「…玩笑就到此为止吧。也太失礼了」
	GOTO INPUT_LOOP
ENDIF
;变えるって言ったのに呼び名变えてなかったら返回
SIF (C_NAM == CSTR:30:80 && ARG == 3) || (C_NAM == MASTERNAME:30 && ARG == 2)
	RETURN -1
;直接入力对応＠响子口上からそのまま拝借。改造したいけど正规表现わかんね
IF MASTERNAME:30 == "" || ARG == 2
	IF STRCOUNT(C_NAM, "\(お\(\(兄\|にい\)\|\(姊\|ねえ\)\)\(桑\|ちゃん\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「恕我无法听这种过分的玩笑」
		GOTO INPUT_LOOP
	ELSEIF STRCOUNT(C_NAM, "\(お\(\(兄\|にい\)\|\(姊\|ねえ\)\)\(大人\|さま\)\)\$")
		CLEARLINE 1
		REUSELASTLINE 「恕我无法听这种过分的玩笑」
		GOTO INPUT_LOOP
	ELSEIF STRCOUNT(C_NAM, "\(\(御\|ご\)\(主人\|しゅじん\)\(大人\|さま\)\)$")
		CLEARLINE 1
		IF TALENT:恋慕
			REUSELASTLINE 「嗯，在人前那样称呼……会让人害羞的…」
		ELSE
			REUSELASTLINE 「恕我无法听这种过分的玩笑」
		ENDIF
		GOTO INPUT_LOOP
	ELSEIF STRCOUNT(C_NAM, "\(\(旦那\|だんな\)\(大人\|さま\)\)\$")
		CLEARLINE 1
		IF TALENT:恋慕
			REUSELASTLINE 「嗯，在人前那样称呼……会让人害羞的…」
		ELSE
			REUSELASTLINE 「恕我无法听这种过分的玩笑」
		ENDIF
		GOTO INPUT_LOOP
	ENDIF
ENDIF
IF ARG == 1
	PRINTFORML 「%C_NAM%… 可以这样称呼你吗？」
ELSEIF ARG == 3
	PRINTFORML 「%C_NAM%… 这样叫你就可以了吧？」
ELSE
	PRINTFORML 「%C_NAM%\@ TALENT:30:恋人 ? # 桑 \@、是吗」
ENDIF
RESETCOLOR
CALL ASK_YN
IF !RESULT
	IF MASTERNAME:30 == "" || ARG == 2
		MASTERNAME:30 = %C_NAM%
	ELSE
		CSTR:30:80 = %C_NAM%
	ENDIF
ELSE
	PRINTFORMDL 请输入称呼
	GOTO INPUT_LOOP
ENDIF
CALL M_KOJO_COLOR_K30
RETURN 1
;==================================================
;映姫大人からぱんつを受け取る函数
;@PANTS_GETは隙间使用强制だったので别函数化
;-------------------------------------------------
@K30_PANTS_GETTING
TCVAR:30:377 = EQUIP:30:下半身内衣２
CFLAG:30:没穿内裤 = 2
EQUIP:30:下半身内衣２ = 0
CALL CLOTHES_SETTING_TRAIN(30)
SIF CFLAG:30:睡衣 == 1
	CFLAG:30:睡衣胖次 = 0
CALLF ONCE("ぱんつ无")
CALLF ONCE("ぱんつ预かり")
RETURN 1
;==================================================
;颜绘付きメッセージをEFFECT込みで表示する函数
;パッチと競合したのでひとまず分けてみる
;-------------------------------------------------
@K30_SPTALK, 表情="", MESSAGE
#DIM EFFECT数
#DIM 行数
#DIMS 色
#DIMS 表情
#DIMS MESSAGE
#DIMS 服装
#DIMS MESSAGE_PRINT,50
服装 =
VARSET MESSAGE_PRINT
IF 颜绘付きメッセージ == 1
	色 = 0%TOSTR(CFLAG:1501, "X")% 
	CALL GET_BASE_RESOURCE(30, "颜绘", 服装, 表情)
	SIF FLAG:多尺寸
		默认角色画像横幅 = SPRITEHEIGHT(RESULTS)
	CALL 画像セット(RESULTS, 0, 0, 角色画像表示尺寸比, 0, 1, "100", CALLNAME:30)
	CALL GET_EFFECT_RESOURCE(30)
	EFFECT数 = RESULT
	FOR LOCAL, 0, EFFECT数
		CALL 画像セット(RESULTS:LOCAL, 0, 0, 角色画像表示尺寸比, LOCAL + 1)
	NEXT
	MESSAGE = %SPTALK整形(MESSAGE)%
	SPLIT MESSAGE, "/", MESSAGE_PRINT
	行数 = RESULT
	FOR LOCAL, 0, 行数
		SIF EFFECT数 - LOCAL < 0
			CALL ダミーセット(0, 0, 角色画像表示尺寸比, LOCAL)
		TEMP_HTML:LOCAL += @"<font color='#%色%'>%MESSAGE_PRINT:LOCAL%</font>"
	NEXT
	CALL 画像一斉表示(0, 0, 1, -1)
	PRINTL
ELSE
	;颜绘メッセージOFF时は通常街道に表示させる
	SPLIT MESSAGE, "/", MESSAGE_PRINT
	FOR LOCAL, 0, RESULT
		SIF MESSAGE_PRINT:LOCAL != ""
			PRINTFORML %MESSAGE_PRINT:LOCAL%
	NEXT
ENDIF
;==================================================
;映姫大人の口交をアニメーションさせる函数
;发型とかめちゃくちゃなんであんまり映姫大人に见えない
;--------------------------------------------------
;ARG	0=通常 1=エンドレス 2=オート:函数内で判定, 3= 颜射:指定时のみ 4=射精:函数内で描写判定
;		5~8 真空口交个别
;--------------------------------------------------
;PAT		强化ごっくんアニメパターン
;G_TIME		强化ごっくんのウエイトタイム
;S_TIME		颜射のアニメウエイトタイム
;SUCCESS	ごっくん成功判定
;CLEAN		清洁口交判定
;BLOCK		演出弱化フラグ、非陷落时や强行时
;FAVOR		演出强化フラグ、恋慕+高情欲+高精液中毒
;--------------------------------------------------
@K30_BLOW_JOB_ANIME(ARG)
#DIM PAT = 1, 2, 3, 4, 3, 2, 3, 4, 3, 2, 3, 4, 3, 2, 3, 4, 5 
#DIM G_TIME = 1400, 750, 450, 350, 350, 250, 250, 250, 250, 250, 250, 250, 250, 450, 550, 750, 1200
#DIM S_TIME = 300, 900, 1000, 1200, 1000
#DIM WAIT_T
#DIM LOOP_C
#DIM SUCCESS
#DIM SIZE
#DIM BLOCK
#DIM FAVOR
#DIM CLEAN
#DIM TEMP
#DIMS HTML_FORM
SIF !FLAG:画像表示 || !FLAG:插绘表示 || !GETBIT(CFLAG:1500, 0)
	RETURN

VARSET LOCAL
;アニメ画像尺寸は颜绘表示に准拠
SIZE = K30_SET_IMAGESIZE()
BLOCK = TCVAR:30:385 || TCVAR:30:强行 || !(TALENT:恋慕 || TALENT:炮友 || TALENT:爱欲)
TEMP = CURRENTREDRAW()
REDRAW 0
IF !FLAG:アニメーション
	SELECTCASE ARG
		CASE 0 TO 2
		CALL PRINT_IMAGE("30_blow5", SIZE, "left")
		CASE 3, 4
			GOTO SKIP2
		CASE 5 TO 8
			LOCALS '= "success1", "success5", "dara5", "success3"
			CALL PRINT_IMAGE(@"30_%LOCALS:(ARG -5)%", SIZE, "left")
	ENDSELECT
		REDRAW TEMP
		RETURN
ENDIF
SELECTCASE ARG
	;通常
	CASE 0, 1, 2
		CLEAN = GROUPMATCH(TCVAR:MASTER:112, 1, 2) && !TCVAR:21 && TALENT:恋慕
		LOOP_C = 3 + (NOWEX:MASTER:射精 > 0) - BLOCK + ((TCVAR:30:357 !=0) * 5) + CLEAN
		;前ターン咥えて无ければひと舐め
		IF MASTER_POSE(SET_M, SET_P, 1) != 30 && TALENT:恋慕 && TCVAR:30:357 < 2
			CALL PRINT_IMAGE("30_start", SIZE, "left")
			TWAIT 800, 0
			CLEARLINE SIZE / 100
		ENDIF
		;通常パターン
		IF ARG == 0 || (ARG == 2 && NOWEX:MASTER:射精) || CLEAN
			WAIT_T = 100 - (NOWEX:MASTER:射精 > 0) * 25
			FOR COUNT:1 ,0, LOOP_C
				FOR COUNT, 1, 7
					CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
					TWAIT WAIT_T, 0
					CLEARLINE SIZE / 100
					SIF GETKEY(1) && COUNT:1
						GOTO SKIP
				NEXT
			NEXT
			IF !CLEAN
				FOR COUNT, 1, 4
					CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
					TWAIT WAIT_T, 0
					CLEARLINE SIZE / 100
					SIF GETKEY(1)
						BREAK
				NEXT
			ENDIF
			$SKIP
			IF CLEAN
				;清洁口交で口を离す描写ある(=Counter发生しない)パターン
				CALL PRINT_IMAGE(@"30_shower1", SIZE, "left")
			ELSE
				;射精直前パターン
				CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
			ENDIF
		;エンドレスパターン
		;Emuera本体にあるアニメスプライト机能使用、ログ流しても咥えっぱ无にするテスト
		ELSE
			HTML_FORM = <img src='30_おしゃぶり映姫大人' height='{SIZE}' width='{SIZE}'>
			HTML_PRINT HTML_FORM
			FOR LOOP_C, 0, (SIZE / 100)
				PRINTFORML
			NEXT
			;描画间隔を制御する命令
			SETANIMETIMER 150
		ENDIF
	;射精
	CASE 3, 4
		SUCCESS = (EXP_UP(15, 30) == NOWEX:MASTER:射精 * 3) || TFLAG:193 == -999 || TCVAR:30:357
		FAVOR = (TALENT:恋慕 && ABL:精液中毒 > 4 && PALAM:情欲 > PALAMLV:7 && RAND:3) || TCVAR:30:357
		;スパート
		FOR LOCAL, 0, 4 + (ARG == 4)
			FOR COUNT, 1, 7
				CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
				TWAIT 55, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1) && LOCAL > 0
					GOTO SKIP2
			NEXT
		NEXT
		;ぶっかけ
		IF ARG == 3
			FOR COUNT, 1, 6
				CALL PRINT_IMAGE(@"30_shower{COUNT}", SIZE, "left")
				TWAIT (S_TIME:(COUNT -1)), 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1) && COUNT > 1
					GOTO SKIP2
			NEXT
			GOTO SKIP2
		ENDIF
		;奥まで饮み込む动作
		FOR COUNT, 1, 5
			CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
			TWAIT 55 + (COUNT == 4) * 100, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
		NEXT
		WAIT_T = 1200 - (!SUCCESS * 300)
		FOR COUNT, 7, 8 + !BLOCK
			CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
			TWAIT WAIT_T, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
		NEXT
		;陷落ない场合は演出少なめ
		SIF BLOCK
			GOTO SKIP2
		;だらだらこぼす
		IF !SUCCESS && NOWEX:MASTER:射精 > 1 
			FOR COUNT, 1, 2 + (TALENT:恋慕 && ABL:精液中毒 > 4)
				CALL PRINT_IMAGE(@"30_failed{COUNT}", SIZE, "left")
				TWAIT 1200, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
		;手に吐き出し
		ELSEIF !SUCCESS
			FOR COUNT, 2, 5
				CALL PRINT_IMAGE(@"30_success{COUNT}", SIZE, "left")
				TWAIT 550 , 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
			FOR COUNT, 1, 5 + FAVOR
				CALL PRINT_IMAGE(@"30_dara{COUNT}", SIZE, "left")
				TWAIT 550 + COUNT * 75 + (COUNT == 2) * 700, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
		;ごっくんパターン1、陷落高くてMASTER大量射精
		ELSEIF FAVOR && SUCCESS
			FOR COUNT, 0, 17
				CALL PRINT_IMAGE(@"30_success{PAT:COUNT}", SIZE, "left")
				TWAIT (G_TIME:COUNT), 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
			NEXT
		;ごっくんパターン2
		ELSE
			LOCAL = 500
			FOR COUNT, 2, 5
				CALL PRINT_IMAGE(@"30_success{COUNT}", SIZE, "left")
				TWAIT LOCAL, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1)
					GOTO SKIP2
				LOCAL += 250
			NEXT
		ENDIF
		$SKIP2
		;颜射
		IF ARG == 3
			CALL PRINT_IMAGE("30_shower6", SIZE, "left")
		;未陷落
		ELSEIF BLOCK
			LOCALS = 30_%\@ SUCCESS ? blow8 # failed1 \@%
			CALL PRINT_IMAGE(LOCALS, SIZE, "left")
		;ごっくん
		ELSEIF SUCCESS
			CALL PRINT_IMAGE(@"30_success{FAVOR + 5}", SIZE, "left")
		;见せつけ
		ELSEIF NOWEX:MASTER:射精 == 1
			CALL PRINT_IMAGE(@"30_dara{FAVOR + 5}", SIZE, "left")
		;だらだら
		ELSE
			LOCAL = TALENT:恋慕 && ABL:精液中毒 > 4 ? 3 # 2
			CALL PRINT_IMAGE(@"30_failed{LOCAL}", SIZE, "left")
		ENDIF
	CASE 5
		LOCAL = 300
		FOR COUNT, 2, 5
			CALL PRINT_IMAGE(@"30_success{COUNT}", SIZE, "left")
			TWAIT LOCAL, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
			LOCAL += 150
		NEXT
		CALL PRINT_IMAGE("30_success5", SIZE, "left")
	CASE 6
		FOR COUNT, 1, 3
			CALL PRINT_IMAGE(@"30_failed{COUNT}", SIZE, "left")
			TWAIT 500, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
		NEXT
		CALL PRINT_IMAGE("30_success5", SIZE, "left")
	CASE 7
		FOR COUNT, 1, 5
			CALL PRINT_IMAGE(@"30_dara{COUNT}", SIZE, "left")
			TWAIT 600, 0
			CLEARLINE SIZE / 100
			SIF GETKEY(1)
				GOTO SKIP2
		NEXT
		CALL PRINT_IMAGE("30_dara5", SIZE, "left")
	CASE 8
		FOR COUNT:1 ,0, 2
			FOR COUNT, 1, 7
				CALL PRINT_IMAGE(@"30_blow{COUNT}", SIZE, "left")
				TWAIT 75, 0
				CLEARLINE SIZE / 100
				SIF GETKEY(1) && COUNT:1
					GOTO SKIP
			NEXT
		NEXT
		CALL PRINT_IMAGE(@"30_blow7", SIZE, "left")
ENDSELECT
REDRAW TEMP
RETURN
;==================================================
;PRINT_IMAGEの大きさを颜绘设定に合わせる式中函数
;自作绘は扩大に耐えられるクオリティないので最大值に制限かける
;--------------------------------------------------
;默认角色画像横幅	颜绘を表示するベース尺寸(单位:ピクセル
;角色画像表示尺寸比		ベース画像を表示する倍率(50～150の范围
;--------------------------------------------------
@K30_SET_IMAGESIZE()
#FUNCTION
LOCAL = 默认角色画像横幅 > 180 ? 1000 # 700
RETURNF MIN(LOCAL * 角色画像表示尺寸比 / 10000 * 100 , 1800)
;--------------------------------------------------
;映姫大人のおしりをUPで表示する函数
;IMAGE.ERBとやってることはだいたい同じ、角色データ使用と既存颜绘や口上削除プレイに支障でそうなので口上内に处理ベタ置き
;もっと洗练されたやり方ありそうだけどよくわかんなくなってきた、函数をライ罩罩リ化出来る人って本当にスゴい
;--------------------------------------------------
;COM		SELECTCOM,负の场合はイベント文から直接呼び出し: -1=服 -2=裸 -3=变Ｔ
;LAYER:0	差分指定: 1=纵割れ肛门 2=ぽっかり肛门 3=中出し肛门
;LAYER:1	EFFECT指定:BIT0=尻叩き-着衣 BIT1=尻叩き-裸 BIT2=濡れ BIT3=精液 BIT4=振动棒 
;LAYER:2	ぱんつ指定: -1=没穿内裤 1=通常 2=ずらし 3=振动棒ぱんつ
;LAYER:3	腕指定: 1=手で押さえ 2=振动棒掴み 3=开帐
;LAYER:4	谜の影指定: 1=指插入 2=振动棒插入 3=舐め 4=掀裙子 5=TINKO 6=尻叩き
;L_NUM		レイヤ阶层数
;--------------------------------------------------
@K30_PRINT_BUTT_IMAGE(COM, LAYER:0 = 0, LAYER:1 = 0, LAYER:2 =0, LAYER:3 = 0, LAYER:4 = 0)
#DIM L_NUM
#DIM S_NUM
#DIM COM
#DIM TEMP_SIZE
#DIM TEMP_WIDTH
#DIM TEMP_SCALE
#DIM 纵割
#DIM たっぷり
#DIM 尻叩き
#DIM 肿れ
#DIM 濡れ
#DIM 白浊
#DIM 装着
#DIM 表示扩张
#DIM セットEFFECT
#DIM LAYER, 5
#DIM レイヤ配列位置 = 0, 3, 3, 3, 7
#DIMS SET_RESOURCE, 10
#DIMS SECTIONAL
#DIMS 袖
#DIMS ぱんつ
{
#DIMS レイヤ素材 = "", "30_BASE_VERTICAL", "30_BASE_HOLE", "30_BASE_CREAMPIE",
					 "_HOLD", "_GRAB", "_OPEN", "KETSU-TOUCH",
					 "30_SHADOW_HAND", "30_SHADOW_INST", "30_SHADOW_LICK", "30_SHADOW_LIFT", "30_SHADOW_PENIS", "30_SHADOW_SPANK", "30_SHADOW_DEEPER"
}

SIF !FLAG:画像表示 || !GETBIT(CFLAG:1500, 1)
	RETURN

VARSET SET_RESOURCE
VARSET SECTIONAL
L_NUM = 1
S_NUM = 1
表示扩张 = 0
;レイヤ设定、上手く构文缠められずIF文まみれ
纵割 = GETBIT(CFLAG:30:1910, 7)
たっぷり = 充填率(30, 2) > 30
尻叩き = TIME_PROGRESS(TCVAR:30:355) < 20
;BIT0=着衣肿れ,BIT1=裸肿れ、足し算すると0～2になるから排他表示にできる
肿れ = 尻叩き + (尻叩き * K30_NUDITY())
濡れ = (PALAM:润滑 > PALAMLV:3) * 4
白浊 = (充填率(30, 1) > 20) * 8
装着 = (TEQUIP:肛用振动棒) * 16
セットEFFECT = 肿れ + 濡れ + 白浊 + 装着
CALL K30_SELECT_PANTS_TYPE(LAYER:2)
ぱんつ = %RESULTS%
袖 = _%\@ NUDITYCHECK(30) > 3 && !K30_CHECK_HEN_T() ? WEAR # NUDE \@%

;イベント构文から直接指定时
IF COM < 0
	SELECTCASE COM
		CASE -1
			SET_RESOURCE:0 = 30_BODY_WEAR
		CASE -2
			SET_RESOURCE:0 = 30_BODY_NUDE
		CASE -3
			SET_RESOURCE:0 = 30_BODY_HEN-T
	ENDSELECT
	袖 = _%\@ COM == -1 ? WEAR # NUDE \@%
	SIF !LAYER:1
		LAYER:1 = セットEFFECT
	IF LAYER:0
		SET_RESOURCE:L_NUM = %レイヤ素材:((レイヤ配列位置:0) + LAYER:0)%
		L_NUM ++
	ENDIF
	IF LAYER:1
		CALL K30_SEARCH_EFFECT(SET_RESOURCE, LAYER:1, L_NUM)
		L_NUM += RESULT
	ENDIF
	IF LAYER:2
		SET_RESOURCE:L_NUM = %ぱんつ%
		SIF ぱんつ != ""
			L_NUM ++
	ENDIF
	IF LAYER:3
		SET_RESOURCE:L_NUM = 30_HAND%袖%%レイヤ素材:((レイヤ配列位置:3) + LAYER:3)%
		L_NUM ++
	ENDIF
	IF LAYER:4
		SET_RESOURCE:L_NUM = %レイヤ素材:((レイヤ配列位置:4) + LAYER:4)%
		L_NUM ++
	ENDIF
	GOTO PRINT_IMAGE
ENDIF

;素体を设定
SET_RESOURCE:0 = 30_BODY%\@ K30_NUDITY() ? _NUDE # _WEAR \@%
SIF !K30_NUDITY() && K30_CHECK_HEN_T()
	SET_RESOURCE:0 = 30_BODY_HEN-T

;素体差分レイヤの设定
IF 纵割
	SET_RESOURCE:1 = %\@ CFLAG:诶嘿嘿 ? 30_BASE_VERTICAL # ダミー \@%
	SIF TCVAR:30:392 > 4
		SET_RESOURCE:1 = 30_BASE_HOLE
	SIF たっぷり
		SET_RESOURCE:1 = 30_BASE_CREAMPIE
	L_NUM ++
ENDIF

;EFFECTレイヤの设定
CALL K30_SEARCH_EFFECT(SET_RESOURCE, セットEFFECT, L_NUM)
L_NUM += RESULT

;ぱんつレイヤの设定
IF ぱんつ != ""
	SET_RESOURCE:L_NUM = %ぱんつ%
	L_NUM ++
ENDIF

;指令每に其他レイヤを决定
SELECTCASE COM
	;摸屁股
	CASE 310
		SIF CFLAG:诶嘿嘿
			RETURN
		;素体を入れ替え
		SET_RESOURCE:0 = %REPLACE(SET_RESOURCE:0, "BODY_", "BODY_KETSU-TOUCH_")%
		SET_RESOURCE:1 = 30_SHADOW_KETSU-TOUCH
		L_NUM =2
		IF TFLAG:193 < 4 && SHIRAHU(30)
			SET_RESOURCE:2 = 30_HAND%袖%_GUARD
			L_NUM ++
		ENDIF
	;肛门爱抚（日常系）
	CASE 314
		;谜の影
		SET_RESOURCE:L_NUM = 30_SHADOW_%\@ TCVAR:30:364 && TEQUIP:肛用振动棒 ? INST # HAND \@%
		L_NUM ++
	;掀裙子,走光
	CASE 330
		;素体差分个别设定
		IF TCVAR:30:364
			SET_RESOURCE:1 = 30_BASE%\@ たっぷり ? _CREAMPIE # _HOLE \@%
		ELSEIF たっぷり && 纵割
			SET_RESOURCE:1 = 30_BASE_CREAMPIE
		ELSEIF TCVAR:30:392 && 纵割
			SET_RESOURCE:1 = 30_BASE_VERTICAL
		ENDIF
		;谜の影
		IF SELECTCOM == 330
			SET_RESOURCE:L_NUM = 30_SHADOW_LIFT
			L_NUM ++
		ENDIF
	;舐肛
	CASE 4
		;映姫大人の腕
		IF SHIRAHU(30) && RAND:2 
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		;谜の影
		SET_RESOURCE:L_NUM = 30_SHADOW_LICK
		L_NUM ++
	;肛门爱抚
	CASE 5
		;映姫大人の腕
		IF SHIRAHU(30) && RAND:2 
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		;谜の影
		SET_RESOURCE:L_NUM = 30_SHADOW_%\@ TEQUIP:肛用振动棒 ? INST # HAND \@%
		L_NUM ++
	;自慰
	CASE 9
		SIF !TEQUIP:肛用振动棒
			RETURN
		;映姫大人の腕
		SET_RESOURCE:L_NUM = 30_HAND%袖%_GRAB
		L_NUM ++
	;张开肛门
	CASE 13
		;素体差分个别设定
		SIF SET_RESOURCE:1 == "30_BASE_VERTICAL"
			SET_RESOURCE:1 = 30_BASE_HOLE
		;映姫大人の腕
		SET_RESOURCE:L_NUM = 30_HAND%袖%%\@ 纵割 && RAND:3 ? _OPEN # _HOLD \@%
		L_NUM ++
	;肛用振动棒
	CASE 45
		;素体差分个别设定
		SIF !TEQUIP:肛用振动棒 && 纵割
			SET_RESOURCE:1 = 30_BASE%\@ たっぷり ? _CREAMPIE # _HOLE \@%
		;映姫大人の腕
		IF SHIRAHU(30)
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		IF TEQUIP:肛用振动棒
			;谜の影
			SET_RESOURCE:L_NUM = 30_SHADOW_INST
			L_NUM ++
			;肛用振动棒断面图、本体侧のフラグに从う
			表示扩张 = FLAG:射精画像种类 == 2 ? 0 # 2
		ENDIF
	;Ａ后背位/Ａ背面座位/打屁股
	CASE 63, 70, 100
		;映姫大人の腕
		IF SHIRAHU(30) && COM == 63 && RAND:2 
			SET_RESOURCE:L_NUM = 30_HAND%袖%_HOLD
			L_NUM ++
		ENDIF
		;谜の影,打屁股
		IF COM == 100
			SET_RESOURCE:L_NUM = 30_SHADOW_SPANK
			SIF TEQUIP:51 == MASTER && GROUPMATCH(TEQUIP:体位, 2, 5)
				SET_RESOURCE:L_NUM = 30_SHADOW_SPANK-INST
		;谜の影,TINKO
		ELSE
			SET_RESOURCE:L_NUM = 30_SHADOW_PENIS
		ENDIF
		L_NUM ++
	;结肠责め/隔着刺激阴道
	CASE 74, 75
		SIF !纵割 || GROUPMATCH(TEQUIP:体位, 1, 3, 4)
			RETURN
		;断面图表示设定、本体侧のフラグに从う
		表示扩张 = FLAG:射精画像种类 == 2 ? 0 # 1
		;素体差分个别设定
		SET_RESOURCE:1 = 30_BASE%\@ たっぷり ? _CREAMPIE # _HOLE \@%
		;谜の影
		SET_RESOURCE:L_NUM = 30_SHADOW_DEEPER
		L_NUM ++
	CASEELSE
		RETURN
ENDSELECT

$PRINT_IMAGE
TEMP_SIZE = FLAG:多尺寸
TEMP_WIDTH = 默认角色画像横幅
TEMP_SCALE = 角色画像表示尺寸比
FLAG:多尺寸 = 1
IF TEMP_WIDTH * TEMP_SCALE > 27000
默认角色画像横幅 = 270
角色画像表示尺寸比 = 100
ENDIF
;レイヤのセットと表示
DEBUGPRINTFORML LINECOUNT:{LINECOUNT}
FOR COUNT, 0, L_NUM
	CALL 画像セット(SET_RESOURCE:COUNT, 0, 0, 角色画像表示尺寸比, COUNT)
	DEBUGPRINTFORML レイヤ:{COUNT} %SET_RESOURCE:COUNT%
NEXT
IF FLAG:时间停止
	CALL 画像セット("时间停止", 0, 0, 角色画像表示尺寸比, L_NUM)
	L_NUM ++
ENDIF
;断面图、290スレ>>545桑のアイデア使ってます
IF 表示扩张
	CALL 画像セット(@"30_SECTIONAL_RECTUM{表示扩张}", 0, 0, 角色画像表示尺寸比, 0)
	IF たっぷり
		SECTIONAL = 30_SECTIONAL%\@ 充填率(30, 2) > 60 ? _POKKORI # _TAPPURI \@%
		CALL 画像セット(SECTIONAL, 0, 0, 角色画像表示尺寸比, 1)
		S_NUM ++
	ENDIF
	IF FLAG:时间停止
		CALL 画像セット("时间停止", 0, 0, 角色画像表示尺寸比, S_NUM)
		S_NUM ++
	ENDIF
	
	FOR COUNT, S_NUM, L_NUM 
		CALL ダミーセット(0, 0, 角色画像表示尺寸比, COUNT)
	NEXT
ENDIF
CALL 画像一斉表示("LEFT", 0, 1, -1)
FLAG:多尺寸 = TEMP_SIZE
默认角色画像横幅 = TEMP_WIDTH
角色画像表示尺寸比 = TEMP_SCALE
RETURN 1
;==================================================
;おしり画像のEFFECTを决定する函数
;REF函数の部分は路人子画像生成から拝借しました
;--------------------------------------------------
@K30_SEARCH_EFFECT(リソース, セットEFFECT, レイヤ数)
#DIM セットEFFECT
#DIM レイヤ数
#DIM L_NUM
#DIMS REF リソース, 0
#DIMS レイヤ素材 = "30_EFFECT_SPANK-W", "30_EFFECT_SPANK-N", "30_EFFECT_WET", "30_EFFECT_SPERM", "30_EFFECT_VIBE"

L_NUM = 0
FOR COUNT, 0, 5
	SIF !GETBIT(セットEFFECT, COUNT)
		CONTINUE
	リソース:(レイヤ数 + L_NUM) = %レイヤ素材:COUNT%
	L_NUM ++
NEXT
RETURN L_NUM
;==================================================
;おしり画像基准で着衣状况を判别する式中函数
;@CHECK_NUDEも@NUDITYCHECKも下半身基准だとなんか合わない
;--------------------------------------------------
;0=着衣 1=おしり丸出し
;--------------------------------------------------
@K30_NUDITY()
#FUNCTION
;上半身着衣状况(TEQUIP:1)を见る
SIF GROUPMATCH(TEQUIP:1, 1, 3)
	RETURNF 0
;下半身着衣状况(TEQUIP:0)を见る
SIF GETBIT(TEQUIP:0, 0) || GETBIT(TEQUIP:0, 3)
	RETURNF 0
;ここまで抜けたら丸出し
RETURNF 1
;==================================================
;ぱんつ画像を决定する函数
;全部描ける画力も无いので似た系统のを表示、气が向いたので追加
;--------------------------------------------------
@K30_SELECT_PANTS_TYPE(指定)
#DIM 指定
#DIM G_ID
#DIM ASIDE
#DIMS 色
#DIMS 装着
#DIMS ずらし
#DIMS ヌード
#DIMS ベースぱんつ
#DIMS TEMP_PANTS
#DIMS COLOR = "", "白", "绿", "黑"
LOCAL = 0
RESULTS =
色 = %COLOR:(DAY % 4)%
ASIDE = (CFLAG:诶嘿嘿 == 1 || TEQUIP:50 >= 0 || TEQUIP:51 >= 0 || TEQUIP:振动棒 || TEQUIP:肛用振动棒 || 指定 == 2)
ずらし = _%\@ ASIDE ? SHIFT # NORMAL \@% 
ヌード = %\@ K30_NUDITY() ? _NUDE # _WEAR \@%
装着 = %\@ TEQUIP:肛用振动棒 ? _VIBE # \@%
;指定那个ば画像をそちらに合わせる
SELECTCASE 指定
	CASE -1
		RETURN
	CASE 1, 2
		ヌード = %\@ 指定 == 1 ? _NUDE # _WEAR \@%
	CASE  3
		RESULTS = 30_PANTS_HARNESS
		RETURN
ENDSELECT
SELECTCASE EQUIP:30:下半身内衣２
	CASE 0
		;没穿内裤orガーター吊带or振动棒吊带
		IF TCVAR:30:364 && !CFLAG:诶嘿嘿 && TEQUIP:肛用振动棒 && !CHECK_NUDE(30)
			RESULTS = 30_PANTS_HARNESS
			RETURN
		ENDIF
		SIF TCVAR:30:354 > 0
			RESULTS = 30_PANTS%ヌード%_TYPE10_NO-PANTS
		
	CASE 1, 2, 26
		;1/男式四角裤,2/构造宽送的灯笼裤,26/在胯股间打了开大口的排便用拉链（屁股处可开）
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE1_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE1_NORMAL
		ENDIF
		
	CASE 5
		;5/凸显出臀部曲线的条纹胖次＠もちろん白黑
		RESULTS = 30_PANTS%ヌード%_TYPE4%ずらし%
		
	CASE 7, 10
		;7/毫不做作的纯色胖次,10/设计质朴的低腰胖次
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE8_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE8_NORMAL
		ENDIF
		LOCAL = 1
	
	CASE 11, 20
		;11/仿佛能看到股沟的低腰紧身胖次,20/极为短小的超短胖次
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE11_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE11_NORMAL
		ENDIF
		LOCAL = 1
		
	CASE 12
		;12/格外修长的高开叉胖次
		RESULTS = 30_PANTS%ヌード%_TYPE3%ずらし%
		
	CASE 13, 23
		;13/整洁并有些透明的蕾丝胖次（白）,23/重要的部位能直接看见的透明胖次
		RESULTS = 30_PANTS%ヌード%_TYPE6%ずらし%
		
	CASE 14, 16
		;14/充满成熟女人韵味的蕾丝胖次（黑）,16/用细绳系在腰上的绳系胖次
		RESULTS = 30_PANTS%ヌード%_TYPE5%ずらし%
		
	CASE 15
		;15/裸露大片臀部的大胆的Ｔ字裤
		RESULTS = 30_PANTS%ヌード%_TYPE12%ずらし%
		
	CASE 17
		;缝着竖向拉链的皮革胖次
		RESULTS = 30_PANTS%ヌード%%装着%_TYPE7%ずらし%
		
	CASE 18
		;穿旧了的木绵のぱんつ(固有内衣)
		RESULTS = 30_PANTS%ヌード%_TYPE0%ずらし%
		
	CASE 19
		;19/充满少女味的可爱的三角胖次
		IF ASIDE
			RESULTS = 30_PANTS_COMMON_TYPE2_SHIFT
		ELSE
			RESULTS = 30_PANTS%ヌード%_TYPE2_NORMAL
		ENDIF
		
	CASE 21
		;紧紧抱住屁股的兜裆布
		RESULTS = 30_PANTS%ヌード%_TYPE13%ずらし%
	CASE 22
		;面积极小简直跟绳一样的丁字裤＠なんとなくガーター吊带仕大人
		RESULTS = 30_PANTS%ヌード%_TYPE10%ずらし%
		
	CASE 24
		;清楚的暴露出耻丘的开放式胖次
		RESULTS = 30_PANTS%ヌード%%装着%_TYPE9
	
	CASEELSE
		;初期or获得可能素質では穿かない/换装のみのぱんつはスルー
		;3/小熊胖次,4/波点胖次,6/褶边胖次,25/创口贴 = 无知ロリ用、27以降 = 更衣可能判定无
		RESULTS = 30_PANTS%ヌード%_TYPE0%ずらし%
ENDSELECT

SIF !LOCAL
	RETURN

;色违いぱんつの生成
ベースぱんつ = %RESULTS%
RESULTS = %RESULTS%%色%
SIF SPRITECREATED(RESULTS)
	RETURN
G_ID = 0
CALL 画像合成(G_ID, ベースぱんつ, 0, 0, 0, 0, 色)
SPRITECREATE RESULTS, G_ID
RETURN
;==================================================
;映姫大人のアヘ颜を表示する函数
;行送り中に目だけ表示されると可怕的のでスプライト合成を使用
;白莲别立绘に使った构文をぶっこ抜き
;--------------------------------------------------
@K30_PRINT_AHEGAO
#DIM G_ID
#DIM NUM
#DIM ORG
#DIMS SET_RESOURCE,7
#DIMS RESOURC_NAME
#DIMS 差分
;睡眠中は表示しない
SIF !FLAG:画像表示 || CFLAG:睡眠
	RETURN
ORG = (SUMARRAY(NOWEX:30:0, 8, 10) > 0)
;ベースと素体を设定
VARSET SET_RESOURCE
SET_RESOURCE:0= 30_AHE_BASE
差分 = %\@ CHECK_NUDE(30) ? _NUDE # _WEAR \@%
SIF !CHECK_NUDE(30) && CFLAG:衣物暂时更换 == 62
	差分 = _HEN-T
SET_RESOURCE:1 = 30_AHE_BODY%差分%
SET_RESOURCE:2 = 30_AHE_ACME
RESOURC_NAME = 30_あへ颜%差分%

;口を设定-随机２种、恋慕かつ四重绝顶以上で３种类
NUM = RAND:(2 + (ORG && TALENT:恋慕))
SET_RESOURCE:3 = 30_AHE_MOUTH{NUM}
RESOURC_NAME += @"口{NUM}"

;目を设定-恋慕で目にハート付き、恋慕かつ四重/五重绝顶で差分增
NUM = TALENT:恋慕 ? 1 + RAND(ORG + (NOWEX:9 > 0) + 1) # 0
SET_RESOURCE:4 = 30_AHE_EYE{NUM}
RESOURC_NAME += @"目{NUM}"

;眉を设定-随机２择
NUM = RAND:2
SET_RESOURCE:5 = 30_AHE_BROW{NUM}
RESOURC_NAME += @"眉{NUM}"

;EFFECT-恋慕で随机４种
IF TALENT:恋慕
	NUM = RAND:4
	SET_RESOURCE:6 = 30_AHE_EFFECT{NUM}
	RESOURC_NAME += @"效果{NUM}"
ENDIF
;合成したスプライトをリソース登录して表示
IF !SPRITECREATED(RESOURC_NAME)
	G_ID = 0
	FOR COUNT, 0, 6 + (TALENT:恋慕 > 0)
		CALL 画像合成(G_ID, SET_RESOURCE:COUNT)
	NEXT
	SPRITECREATE RESOURC_NAME, G_ID
ENDIF
CALL PRINT_IMAGE(RESOURC_NAME, 1000, "left")
PRINTFORML 
RETURN
;==================================================
;映姫样の振动棒约会中颜绘を表示する函数
;--------------------------------------------------
@K30_PRINT_VIBE_DATE
#DIM G_ID
#DIM LAYER
#DIM NUM
#DIMS TEX
#DIMS TEX2
#DIM L_NUM
#DIM TEMP_SIZE
#DIM TEMP_WIDTH
#DIM TEMP_SCALE
#DIMS SET_RESOURCE,7
#DIMS RESOURC_NAME
SIF !FLAG:画像表示 || !GETBIT(CFLAG:1500, 1)
	RETURN
VARSET SET_RESOURCE
LAYER = 0
L_NUM = 2
TEX '= \@ TCVAR:30:365 == 2 || NOWEX:Ａ绝顶 ? AHE_ # \@
SET_RESOURCE:0 = 30_%TEX%BASE
SET_RESOURCE:1 = 30_AHE_BODY_WEAR
RESOURC_NAME = 30_振动棒约会%TEX%
IF NOWEX:Ａ绝顶
	SET_RESOURCE:2 = 30_AHE_ACME
	RESOURC_NAME += "イき"
	L_NUM ++
ENDIF
NUM = RAND:2
SET_RESOURCE:L_NUM = 30_AHE_BROW{NUM}
RESOURC_NAME += @"眉{NUM}"
L_NUM ++
;弱
IF TCVAR:30:365 == 1
	TEX '= \@ NOWEX:Ａ绝顶 ? 4 # %TOSTR(RAND(5, 7))% \@
	TEX2 '= \@ NOWEX:Ａ绝顶 ? %TOSTR(RAND:2)% # %TOSTR(RAND(3, 6))% \@
;强
ELSE
	TEX '= \@ NOWEX:Ａ绝顶 ? %TOSTR(RAND(2, 4))% # 1 \@
	TEX2 '= TOSTR(RAND(3, 6))
ENDIF
SET_RESOURCE:L_NUM = 30_AHE_EYE%TEX%
RESOURC_NAME += @"目%TEX%"
L_NUM ++
SET_RESOURCE:L_NUM = 30_AHE_MOUTH%TEX2%
RESOURC_NAME += @"口%TEX2%"
L_NUM ++
IF NOWEX:Ａ绝顶
	TEX '= \@ TCVAR:30:365 == 1 ? 0 # %TOSTR(RAND(2, 4))% \@
	SET_RESOURCE:L_NUM = 30_AHE_EFFECT%TEX%
	RESOURC_NAME += @"效果%TEX%"
ELSEIF TCVAR:30:365 == 2
	SET_RESOURCE:L_NUM = 30_AHE_EFFECT1
	RESOURC_NAME += "效果1"
ELSEIF K30_BE_SEEN() && !TFLAG:约会道中
	SET_RESOURCE:L_NUM = 别颜_吐息_30
	RESOURC_NAME += "效果4"
	LAYER = 1
ELSE
	SET_RESOURCE:L_NUM = 泛用EFFECT_spoken squiggle
	RESOURC_NAME += "效果5"
	LAYER = 2
ENDIF
IF !SPRITECREATED(RESOURC_NAME)
	G_ID = 0
	FOR COUNT, 0, L_NUM
		CALL 画像合成(G_ID, SET_RESOURCE:COUNT)
	NEXT
	SELECTCASE LAYER
		CASE 0
			CALL 画像合成(G_ID, SET_RESOURCE:L_NUM)
		CASE 1
			CALL 画像合成(G_ID, SET_RESOURCE:L_NUM, -15, 0, 320, 320)
		CASE 2
			CALL 画像合成(G_ID, SET_RESOURCE:L_NUM, 0, -15, 270, 270)
	ENDSELECT
	SPRITECREATE RESOURC_NAME, G_ID
ENDIF
TEMP_SIZE = FLAG:多尺寸
TEMP_WIDTH = 默认角色画像横幅
TEMP_SCALE = 角色画像表示尺寸比
FLAG:多尺寸 = 1
IF TEMP_WIDTH * TEMP_SCALE > 27000
	默认角色画像横幅 = 270
	角色画像表示尺寸比 = 100
ENDIF
CALL 画像セット(RESOURC_NAME, 0, 0, 角色画像表示尺寸比, 0)
SIF 充填率(30, 2) > 30
	CALL ダミーセット(0, 0, 角色画像表示尺寸比, 1)
CALL 画像セット(@"30_SECTIONAL_DATE{TCVAR:30:365}", 0, 0, 角色画像表示尺寸比, 0)
SIF 充填率(30, 2) > 30
	CALL 画像セット(@"30_SECTIONAL_%\@ 充填率(30, 2) > 60 ? POKKORI # TAPPURI \@%", 0, 0, 角色画像表示尺寸比, 1)
CALL 画像一斉表示("LEFT", 0, 1, -1)
FLAG:多尺寸 = TEMP_SIZE
默认角色画像横幅 = TEMP_WIDTH
角色画像表示尺寸比 = TEMP_SCALE
RETURN 1
;==================================================
;映姫样のメシ颜を表示する函数
;--------------------------------------------------
@K30_PRINT_MESHIGAO(ARGS)
SIF !FLAG:画像表示 || !颜绘付きメッセージ
	RETURN
SETANIMETIMER 150
CALL 画像セット("30_BG1", 0, 0, 100, 0)
CALL GET_BASE_RESOURCE(30, "颜绘", "", ARGS)
CALL 画像セット(RESULTS:0, 0, 0, 100, 1)
CALL 画像一斉表示("LEFT", 0, 1, -1)
RETURN

;==================================================
;泛用说教判定＆实行
@K30_LECTURE_CHECK(说教长, 理由)
;--------------------------------------------------
;说教长= 说教の行数＝愤怒具合
;理由= 说教理由（=0 胖次 =1 性骚扰指令失败时 =2ＶＡ强制わいせつ）
;--------------------------------------------------
#DIM 说教长
#DIM 理由
#DIM ぱんつ１
#DIM ぱんつ２
#DIM 性骚扰１
#DIM 性骚扰２
#DIM 强制わいせつ
#DIM TEMP
;ガード低下时・其他状态异常・诶嘿嘿时は判定无无罪
SIF (CFLAG:1801 == 0 || SHIRAHU(30) != 1 || CFLAG:30:诶嘿嘿) && 理由 != 0
	RETURN 1
;变Ｔで目が死んでいるときは说教无し
SIF K30_CHECK_HEN_T() && TALENT:心情 > -1
	RETURN 1
;情人旅馆と待客室にいても说教しない
SIF (CFLAG:30:当前位置 == 情侣酒店 || CFLAG:30:当前位置 == 情人旅馆 || CFLAG:30:当前位置 == OMANEKIBEYA()) && 理由 != 0
	RETURN 1
SELECTCASE 理由
	;胖次密输摘发
	CASE 0
		;说教orぶん殴りで把柄はチャラ
		CFLAG:掌握把柄 -= 5
		CFLAG:1700 -= 5
		CLEARBIT CFLAG:1699, 1
		;２回まではお说教
		IF ぱんつ１ != DAY
			ぱんつ１ = DAY
			PRINTFORML 
			PRINTFORMW 「……那么,%K30_C_NAME(MASTER)%」
			GOTO LECTURE
		ELSEIF ぱんつ２ != DAY
			ぱんつ２ = DAY
			PRINTFORML 
			PRINTFORMW 「%K30_C_NAME(MASTER)%也真是屡教不改呢…」
			GOTO LECTURE
		;３回目でぶん殴られる
		ELSE
			PRINTFORML 
			PRINTFORMW 「………%CALLNAME:MASTER%桑」
			PRINTFORMW 「都已经说了这么多,还是不明白么？」
			TCVAR:30:371 = 1
			CALL K30_DISCIPLINE_HEAD
		ENDIF
	;性骚扰失败时
	CASE 1
		;愤怒具合30以下ノーカン
		IF 说教长 > 30
			;１日２回お说教
			IF 性骚扰１ != DAY
				性骚扰１ = DAY
				PRINTFORML 
				PRINTFORMW 「…有点看不下去了」
				GOTO LECTURE
			ELSEIF 性骚扰２ != DAY
				性骚扰２ = DAY
				PRINTFORML 
				PRINTFORMW 「…真是完全没有吸取教训呢」
				GOTO LECTURE
			;３回目でぶん殴られる
			ELSE
				PRINTFORML 
				PRINTFORMW 「…请适可而止」
				PRINTFORMW 「如果光说没用的话」
				TCVAR:30:371 = 1
				CALL K30_DISCIPLINE_HEAD
			ENDIF
		ENDIF
	;通常时ＶＡ指令概率で
	CASE 2
		;最初は说教
		IF 强制わいせつ != DAY
			强制わいせつ = DAY
			PRINTFORML 
			PRINTFORMW 「…你,有些太过分了」
			GOTO LECTURE
		;我慢は一回だけ
		ELSE
			PRINTFORML 
			PRINTFORMW 「…我已经忍无可忍了」
			PRINTFORMW 「如果光说没用的话」
			TCVAR:30:371 = 2
			CALL K30_DISCIPLINE_HEAD
		ENDIF
ENDSELECT
RETURN 1
;说教
$LECTURE
PRINTFORML 「先在那里坐下吧」
FORCEWAIT
PRINTFORMDW 映姬指了指地面。%CALLNAME:MASTER%老实正坐下来…
PRINTFORMW 
SELECTCASE 理由
	CASE 0
		PRINTFORMW 「你到底偷了多少内裤？ 嗯，捡到的这种借口我完全不相信」
		PRINTFORMW 「没错，你就是有点手脚不干净！」
	CASE 1
		PRINTFORMW 「话说回来，你动不动就碰女性，就没有一点自制力吗？」
		PRINTFORMW 「没错，你就是有点太忠于欲望了！」
	CASE 2
		PRINTFORMW 「\@ TALENT:30:恋人 ? 即使是恋人 # 就算关系再好 \@，能做的事情也是有限度的！ 你到底要羞辱别人到什么地步！」
		PRINTFORMW 「没错，你就是有点太不体贴了！」
	CASEELSE
		PRINTFORMDW 出现了意料之外的情况。 麻烦您连同上次使用的命令一起报告bug
		PRINTFORMW 「K30_LECTURE_CHECK，你的参数太奇怪了！ …咦？」
ENDSELECT
PRINTFORML 
TEMP = 说教长
WHILE TEMP
	;行头の修饰。2行に1回概率でつける
	IF  (((说教长 + TEMP) % 2 == 0) && RAND:5 == 0) || (TEMP == 说教长)
		PRINTFORM 「%TEXTR("原本说起来/请好好想一想/坦白的说/给我好好听着/具体地说/听好了么/")%………………
	ELSE
		PRINT 「……
	ENDIF
	;1行の长さは随意
	REPEAT 6 + RAND:4
		;杂に百分率
		SELECTCASE RAND:100
			CASE 0 TO 9
				SELECTCASE 理由
					CASE 0
						PRINTFORM …%TEXTR("胖次/稍微/你的/善行/胖次/从哪里/胖次/用来做什么/在听吗…？/为了什么")%…
					CASE 1
						PRINTFORM …%TEXTR("死后/总是毫不顾忌地/纠缠不清的/明明不愿意的/你的/善行是/反省/别人的感受…/不害羞吗…/他人的目光…/毫不客气地…")%…
					CASE 2
						PRINTFORM …%TEXTR("死后/意志的/说了不行了/明明不愿意的/你的/善行是/自我反省/别人的感受…/不害臊吗…/他人的目光…/毫不客气地…")%…
					CASEELSE
						PRINTFORM …%TEXTR("反省/稍微/罪孽/善行/地狱/你这家伙/过错/在听吗…？/死后的")%…
				ENDSELECT
			CASE 10 TO 13
				PRINTFORM ………!
			CASEELSE
				PRINTFORM ………
		ENDSELECT
	REND
	PRINTW 」
	TEMP --
WEND
说教长 *=2
TIME += 说教长
TEMP = MAXBASE:MASTER:气力 / 3
SIF TEMP > BASE:MASTER:气力
	TEMP = MAX(BASE:MASTER:气力, 1)
CALL K30_D_LINE
PRINTFORML 「………………………………」
PRINTFORMW 「…就是这样。没错，你现在应该做的是」
SELECTCASE 理由
	CASE 0
		PRINTFORMW 「好好处理掉你囤积的内裤。这就是你现在所能积累的善行」
	CASE 1
		PRINTFORMW 「作为你满足了欲望的代价,好好的去面壁自我思考反省。这就是你现在所能积累的善行」
	CASE 2
		PRINTFORMW 「好好的去学习如何贴照顾他人的感受。这就是你现在所能积累的善行」
	CASEELSE
ENDSELECT
FORCEWAIT
PRINTFORML
PRINTFORMDW 终于结束了……
PRINTFORMDL 被映姬{说教长}分钟的说教，非常疲惫……
PRINTFORMDW 正坐的脚都麻了…
CALL RECOVER(MASTER, -50, "体力", "脚麻了")
CALL RECOVER(MASTER, -(TEMP), "气力", "长到令人厌烦的说教")
;映姫大人が趣味を满喫
SOURCE:郁闷 -= 1000
SIF SOURCE:郁闷 < 0
	SOURCE:郁闷 = 0
SOURCE:欢乐 += ABL:30:亲密 * 70
SOURCE:情爱 += 300
TEMP = RAND:5 - 理由
SIF TEMP <= 0
	TEMP = 1
;日记フラグ
IF DIARY:30:5 == 0
	;かなり行仪の恶い受け渡し方だけど、ターン结束直前の茶番だからこれでいいや
	TFLAG:193 = 理由 * 1000 + 说教长
	CALL K30_WRITE_DIARY(5)
ENDIF
PRINTFORML 
PRINTFORMDL 因为乖乖听了说教，所以映姬的信赖度稍微上升了一点
SIF 理由 == 0
	PRINTFORMDL 胖次的事情好像也因此原谅我了
CALL CHANGE_CFLAG(4, 30, TEMP)
RETURN 1
;==================================================
;映姫大人の折檻＠远虑のない一击
@K30_DISCIPLINE_HEAD
#DIM DAMAGE
;--------------------------------------------------
LOCAL = MIN(1 + 2 * TCVAR:30:371, 7)
DAMAGE = 400 + 400 * TCVAR:30:371
SIF TCVAR:30:371 > 4
	DAMAGE += BASE:MASTER:体力 + 1000 * TCVAR:30:371
IF TCVAR:30:371 < 3
	PRINTFORMW 「你要稍微吃点苦头」
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「好好反省一下自己的生活吧！」
ELSEIF TCVAR:30:371 < 5
	PRINTFORML 「是的，我不会心软的」
	PRINTFORMW 「好好地」
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「忏悔你所犯下的罪行吧！！」
ELSEIF TCVAR:30:371 == 5
	PRINTFORML 「……是的，我决定了」
	PRINTFORMW 「你今天剩下的时间」
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「在自己的房间里好好反省！！」
ELSE
	PRINTFORMDW 在挥下的瞬间清楚地告知了
	SETCOLOR 206, 8, 21
	PRINTFORML 
	PRINTFORML 「下地狱吧！！」
ENDIF
CALL K30_QUAKE(LOCAL)
PRINTFORMW 
PRINTFORMDL 被悔悟棒狠狠地抽了一下…
CALL M_KOJO_COLOR_K30
BASE:MASTER:体力 -= DAMAGE
PRINTFORML 
SIF DIARY:30:10 == 0
	CALL K30_WRITE_DIARY(10)
IF BASE:MASTER:体力 <= 500
	IF !TALENT:恋慕
		IF TCVAR:30:371 == 1
			PRINTFORML 「啊，哎呀？ 是不是稍微做得过火了？」
			PRINTFORMDL 映姬困惑的声音…在我的脑海里，回响…
		ELSE
			PRINTFORML 「好好反省吧」
			IF SELECTCOM == 356
				PRINTFORMDL 映姬已经不再看这边，回到了%GET_JOBNAME(30)%的座位上…
			ELSE
				PRINTFORMDL 映姬无情地离开了现场…
			ENDIF
		ENDIF
	ENDIF
ELSE
	IF TCVAR:30:371 < 3
		PRINTFORML 「怎么样，有所体会了吗」
		PRINTFORMW 「如果因此得到了教训，就请改正你的行为」
	ELSE
		PRINTFORML 「虽然稍微有些手下留情，但这次就到此为止了」
		PRINTFORML 「好好反省，现在立刻改正你的行为」
		PRINTFORMDW ……似乎低估了映姬的责任感
		PRINTFORMDW 总之还是别妨碍她工作了
	ENDIF
ENDIF
RETURN 1
;==================================================
;映姫大人の说法と折檻
;茶を饮みながらお话をのんびりと闻くイメージ
;--------------------------------------------------
;CFLAG:1701 良いお话の累计
;--------------------------------------------------
@K30_LECTURE_BENEFIT
#DIM SIN_VAL
#DIM EXC
SIN_VAL = 0
EXC = 0
CFLAG:1701 ++
PRINTFORMW 
;お话し6种类しかないので累计して6で割った余りで振り分け
LOCAL = CFLAG:1701 % 6
SELECTCASE LOCAL
	CASE 0
		PRINTFORMW 「说起来…你对『欲望』是怎么想的？」
		PRINTFORML 
		PRINTFORML 「很早以前有这样的故事」
		PRINTFORML 「很久很久以前,在某个地方,有一个禅僧在师父的身边持续修行着」
		PRINTFORML 「禅僧在自己的房间里坐禅时,一位陌生的女性突然靠在了禅僧的身边」
		PRINTFORML 「『被这样做之后是什么样的感觉呢』被她这么询问着」
		PRINTFORML 「这位女性也是师父为他准备的一种试炼,所以才来诱惑他」
		PRINTFORML 「禅僧丝毫不为所动,说了『枯木倚寒岩,三冬无暖气』」
		PRINTFORMW 「『三冬,指的是孟冬、仲冬、季冬。也就是冬天的三个月份。在寒冬的季节里,依附在断崖绝壁上的枯木,更加没有温暖的气息』」
		PRINTFORML 
		PRINTFORMW 「你觉得这位僧侣怎么样？ 你觉得他很了不起吗？」
		PRINTFORML 
		PRINTFORML 「不不不,师父对这个回答非常生气,说着『想不到原来只是个庸俗的和尚,真是看走眼了』」
		PRINTFORMW 「说着就把那个和尚赶出去了。你知道他哪里做错了么？」
		PRINTFORML 
		PRINTFORML 「实际上,作为一个受女性喜爱的男性,应该采取怎样的态度才是解决这个问题的研究」
		PRINTFORML 「对方是陌生的女性。只有很了解对方才会有想谈恋爱的感觉吧」
		PRINTFORML 「但这是第一次见面的女性。这种情况下,作为一个男性,一般会暂且拒绝或者永久拒绝吧」
		PRINTFORML 「应该不要让对方的女性下不来台,适当的用有人情味的语言应对,这样的话自己也不会有失风度」
		PRINTFORMW 「说着枯木倚寒岩的高僧的态度,就好比将女性看做了无生命的物件,或者是土或者是石」
		PRINTFORML 
		PRINTFORML 「即使是同样的拒绝方法,也有能照顾到女性的心情一边拒绝的方法」
		PRINTFORMW 「师父已经看穿了这点,知道了这位禅僧已经毫无人情可言」
		PRINTFORML 
		PRINTFORML 「断定只是逃避诱惑并不是完美的人生」
		PRINTFORML 「倒不如像泥中的荷花那样,在充斥着杂沓的野心、诱惑和爱欲的人生中」
		PRINTFORML 「而不染上那种污浊,那些欲望,以及那些诱惑」
		PRINTFORML 「应该好好消化善用让自己更加优秀,才能开出无上的幸福之花」
		PRINTFORML 「刚才的僧人也是,如果体会不到这点的话,那还不如俗人」
		PRINTFORMW 「隐遁于世俗,因为害怕被诱惑而拼命逃避毫无疑问是错误的」
		PRINTFORML 
		PRINTFORMW 「有欲望不是罪过。沉溺于欲望,虚张声势自己毫无欲望才是罪过」
	CASE 1
		PRINTFORMW 「说起来,你对「眼泪的价值」是怎么看的？」
		PRINTFORML 
		PRINTFORML 「草草了事,无忧忧虑,不会流泪。因为不会感觉痛苦」
		PRINTFORML 「认真考虑事情,认真面对事情却会有眼泪。因为很痛苦」
		PRINTFORMW 「为什么,敷衍了事却不会痛苦,而认真起来就很痛苦呢？」
		PRINTFORML 
		PRINTFORML 「因为马马虎虎的话,就算有什么矛盾也看不出矛盾,也就看不到更好更纯正的东西」
		PRINTFORML 「这种状态,就像去让你泡一杯浑浊的茶水一般,因此不会有任何烦恼和痛苦」
		PRINTFORML 「相反,认真一点,认真一些的话,就会看到矛盾之物,也能得到看到更好更纯正的东西。」
		PRINTFORMW 「因为对现状感到强烈不满。因此而痛苦」
		PRINTFORML 
		PRINTFORML 「渴望得到最好的东西的心,这就叫做做『菩提心』吧」
		PRINTFORMW 「所谓『菩提心』,是指对自己好,对别人也好的真诚愿望心」
		PRINTFORML 
		PRINTFORML 「例如,良心会随着时代而变化,也会因周围的形势而变化」
		PRINTFORML 「即使出卖了自己肉体的贞操,但只要内心的贞操还是向着丈夫,那就感觉没什么关系」
		PRINTFORMW 「封建时代女性的良心已经不是现在女性的良心了」
		PRINTFORML 
		PRINTFORML 「但是,『菩提心』并不会随着时代的变化而改变」
		PRINTFORML 「只要有人在,在其之中就会有朝着这个方向发展的存在,并」
		PRINTFORML 「细菌潜入，内心的药也就是『菩提心』在不知不觉中被净化」
		PRINTFORML 「当我们不知道还有『菩提心』,保持着纯洁的心灵时」
		PRINTFORMW 「这时候恶菌就会与『菩提心』相反,进行诱惑,变成恶心」
		PRINTFORML 
		PRINTFORML 「但是在这个世界上没有万能药。药见效的时候也会产生不如期望的东西」
		PRINTFORML 「恶心被『菩提心』时留下的残渣,残骸是最初留下的眼泪和痛苦」
		PRINTFORML 「当人类受到痛苦和眼泪的引诱时，不要徒然，要深入探究其原因的时候」
		PRINTFORMW 「一定是『菩提心』的显露。然后根据你的心意指引你的人生新的方向」
		PRINTFORML 
		PRINTFORML 「『生的痛苦』有这样的事。从旧的生中生出新的生的时候，一定会有苦恼。有眼泪」
		PRINTFORML 「树发芽的时候，出现在树皮上的东西首先是瑕疵。这是一种痛苦…接下来是树脂——」
		PRINTFORMW 「也就是说眼泪。从那里终于孕育出了新生的五月新绿」
	CASE 2
		PRINTFORMW 「说起来…你对『喜恶』是怎么看的呢？」
		PRINTFORML 
		PRINTFORML 「既然人有心,心有感情,对谁都有好恶的心情也是理所当然的」
		PRINTFORML 「我也不会对此加以评判。樱花、梅花、竹子、百木千草的变化才让自然风光变得更加有趣」
		PRINTFORML 「因为人有好恶心情的一面,不如说正因为人们有了变化和韵律才更加有趣。
		PRINTFORMW 「世界也不会单调地流动,所以有着喜恶的差别才更好」
		PRINTFORML 
		PRINTFORML 「但是,虽说这样,也有那种特殊的好恶心理」
		PRINTFORML 「抓住一点,一意孤行,固执的认为自己所做的一切都是对的时候,那个人就会变成任性的人」
		PRINTFORML 「就像『我讨厌那个人,所以不去交朋友』」
		PRINTFORML 「『所以其他人也不能和那个人交朋友』像这样厌恶的心理就绝对太任性了」
		PRINTFORML 「『我讨厌松树。所以让世界上一棵松树都没有吧』」
		PRINTFORML 「『我喜欢竹子,所以让世界上只剩下竹子吧』这也是一样的」
		PRINTFORMW 「这样的话幻想乡也只剩下竹林,变成了没有什么意思的风景」
		PRINTFORML 
		PRINTFORML 「所以人类也是一样的」
		PRINTFORML 「『虽然我讨厌那个人,但或许有谁能喜欢他,了解他能和他成为朋友』这样就可以了」
		PRINTFORML 「像这样冷静下来,自己讨厌的东西自己讨厌就好了,不能只因为自己的感觉将其彻底排除」
		PRINTFORML 「去那个人喜欢的地方去捣乱,这是任性的行为。」
		PRINTFORML 「这是擅自扰乱社会和谐不可原谅的行为。」
		PRINTFORMW 「必须让陷入这种弊端的人,体会一次同样的窘境」
		PRINTFORML 
		PRINTFORML 「在我看来,天地间所有的生物草木,都是值得尊敬的生命。」
		PRINTFORML 「因此,根据使用方法的不同,没有什么是需要舍弃的,需要废弃的」
		PRINTFORML 「所以,无论对什么东西,绝对不能忽视其本身的价值。那是对自然的亵渎。」
		PRINTFORMW 「不然的话,这是对天地佛神的不敬,必然最终不得不由我来进行判罚」
		PRINTFORMW 
		PRINTFORMW 「你知道如果我来惩罚会变成什么样吧？ 你也请小心点」
	CASE 3
		PRINTFORMW 「说起来…你对『宝塔』是怎么看的呢？」
		PRINTFORML 
		PRINTFORML 「…不是经常丢失或丢失的东西。关于那个请暂时忘记」
		PRINTFORMW 「她也作为毘沙门天的代理人，如果能再自重一点就好了」
		PRINTFORML 
		PRINTFORML 「…话扯远了，继续吧」
		PRINTFORML 「这里我说的『宝塔』是非常有价值的器具、道具す」
		PRINTFORML 「而且『宝塔』是你我都有的东西，不能让它掉下来」
		PRINTFORMW 「是我们唯一的财产，也是最后的财产……也就是『身体』」
		PRINTFORML 
		PRINTFORML 「我说的是不是稍微夸张了点？ 不不，绝对没有那种事」
		PRINTFORMW 「是啊，我来介绍一下『身体』的出色工作吧」
		PRINTFORML 
		PRINTFORML 「在头脑中思考并进行分辨，在内心中使感情涌出」
		PRINTFORML 「把它收进肚子里储存起来，实际运用起来」
		PRINTFORMW 「然后整体协同工作，做各种各样的事情」
		PRINTFORML 
		PRINTFORML 「更厉害的是，即使各自不能协调，『身体』也能起作用」
		PRINTFORML 「不能用头的话用胸，不能用胸的话用腹，不能用腹的话用手足」
		PRINTFORMW 「即使思考和感情和行动不一致，『身体』也会动起来。你有没有想到过呢？」
		PRINTFORML 
		PRINTFORML 「世上还有其他具备这种功能的工具吗?」
		PRINTFORMW 「如果好好使用的话，几乎没有做不到的事情。」
		PRINTFORML 
		PRINTFORML 「在某篇经文中，记载了在宝塔中，多宝如来和释迦牟尼并肩而坐的场面。」
		PRINTFORML 「在这个场景中，多宝如来显现真理，释迦牟尼显现智慧」
		PRINTFORML 「我认为『宝塔』象征着我们的『身体』。」
		PRINTFORML 「我们这个『身体』，只要正确使用，就能获取一切的真理，一切的智慧……」
		PRINTFORMW 「如此珍贵的东西，从出生的那一刻起，我们就被赐予了」
		PRINTFORML 
		PRINTFORML 「所以你也一点一点地把塔中的宝物、『身体』的价值取出来吧。」
		PRINTFORML 「特别是，你拥有我也无法估量的力量，就是说你拥有这么多的宝藏」
		PRINTFORMW 「然而你却……只在女性的身体上寻找宝物吗？ 真是太可惜了」
		PRINTFORML 
		PRINTFORMW 「只有正确使用自己的『身体』，才能取出真正有价值的宝物」
	CASE 4
		PRINTFORMW 「说起来…你对『辛苦』是怎么看的呢？」
		PRINTFORML 
		PRINTFORML 「常有人说『年轻时的辛苦是买来的』，或者『不知道辛苦的少爷是没用的』」
		PRINTFORMW 「『实际上，比起不曾品尝辛劳的人，吃过苦的人确实更有人情味』」
		PRINTFORML 
		PRINTFORML 「但这也不是绝对的。也有人在历尽艰辛的过程中，完全的败给了辛苦」
		PRINTFORML 「变得狡猾，变得自卑，看到觉得不辛苦的人就会变得有攻击性……」
		PRINTFORMW 「你也对这样的人有印象吧。为什么会变成这样呢？」
		PRINTFORML 
		PRINTFORML 「从阎王的角度来看，比起有苦难，没有苦难当然更好」
		PRINTFORML 「但是，只要今世有苦难，想要逃脱，除了死亡别无他法」
		PRINTFORML 「所以、人必须培养面对困难，克服困难的能力」
		PRINTFORML 「如果能培养出凌驾于苦难之上的能力的话，就算辛苦也和没有一样。变得不在意了」
		PRINTFORMW 「也就是说，吃苦并不是目的，而是为了让自己不辛苦也一样的手段。」
		PRINTFORML 
		PRINTFORML 「刚才提到的输给辛苦的人，他们基本上是忘记了辛苦的目的的人」
		PRINTFORML 「遇到痛苦的事情时，要看清其原因和性质，并加以消除。所谓辛苦，就是培养忍耐能力的考验」
		PRINTFORMW 「如果不知道这一点，一味地拖延痛苦的事情，或者只是屏息忍耐的话，总有一天会输给辛苦」
		PRINTFORML 
		PRINTFORML 「在以前接受我审判的灵魂中，有人这样放言」
		PRINTFORML 「人世是真正的苦界，我很早就意识到了这一点。所以在有生之年，我一直埋头修行」
		PRINTFORMW 「故意穿着破烂的衣服，用火烤着身体，贫困地死去。所以说自己有资格转世去天界」
		PRINTFORML 
		PRINTFORML 「在净琉璃镜的映照中，的确，他只靠人们的施舍维持最低限度的温饱，其余的时间都在折磨身心的严格修行中度过」
		PRINTFORML 「乍看之下是很了不起的行为……到哪说得稍微严厉一点的话，不也能这么想吗？」
		PRINTFORML 「这个灵魂在生前，是被周围的善举所支撑着而活的。但是他想的都是自己来世的幸福」
		PRINTFORMW 「因为把修行的辛苦当作目的而不是手段，所以最终也没能满足地积累善行」
		PRINTFORML 
		PRINTFORML 「当然，逃避辛苦逃到轻松的地方也是不行的，那只不过是堕落」
		PRINTFORML 「即便如此，主动寻求并承受忍受苦难，被苦难侵蚀，也不是正确的人生方式」
		PRINTFORML 「不被辛苦侵蚀，把辛苦当作一种研究材料，努力去观察人生的一点一滴」
		PRINTFORMW 「有像这样更多、更广地了解和体验人生的样子，才能称得上是不被苦难所束缚、凌驾苦难的人」
	CASE 5
		PRINTFORMW 「说起来…你对『因果』是怎么看的呢？」
		PRINTFORML 
		PRINTFORML 「这个词的使用，一般都是在人生遭遇不幸或阴暗面的时候」
		PRINTFORML 「虽然说过『父母的因果报应在孩子身上』，但从来没有听说过『因为父母的因果促成孩子的出人头地』」
		PRINTFORML 「还有『因果报应』这个词，也是同样的道理」
		PRINTFORMW 「因为过去或前世的恶行而受到恶报的意思」
		PRINTFORML 
		PRINTFORML 「如此排列就会被认为是不好的词语，但其实它的意思并不是被这样限定的」
		PRINTFORML 「『因果』指的是原因和结果。是自然的道理，对人类既不亲切也不冷酷」
		PRINTFORMW 「任何事情都一定有原因，没有原因而发生的结果是万分之一，亿万分之一」
		PRINTFORML 
		PRINTFORML 「那么，你能想象由什么原因产生什么样的结果吗？」
		PRINTFORML 「不用想得太复杂，原因和结果的关系很简单」
		PRINTFORML 「行善会产生幸福的结果，这就是善因善果」
		PRINTFORMW 「坏事会带来不幸的结果，这就是恶因恶果」
		PRINTFORML 
		PRINTFORML 「这经常被比喻为播种。在地里播种萝卜的种子，就会生出萝卜的芽，播种西瓜的种子，就会生出西瓜的芽」
		PRINTFORML 「与此相同，善的行为必然产生善的结果，恶的行为必然产生恶的结果」
		PRINTFORML 「也是我平时教导人们要积累善行的原因。因为自己的行为会得到回报」
		PRINTFORMW 「这正是自作自受啊，不管是好事还是坏事，一切都是以自己为起点的」
		PRINTFORML 
		PRINTFORML 「将『因果』比喻成播种，顺便再种一个……对了，再种西瓜试试吧」
		PRINTFORMW 「在地里播种西瓜，西瓜就会出来。这是自然的道理，是理所当然的事情」
		PRINTFORML 
		PRINTFORML 「但是你想想看，不是说任谁都只要懂道理，就能做出西瓜来吗?」
		PRINTFORML 「随意地撒下吃完的西瓜籽，然后又长出西瓜来吃…之类的，才没那么顺利呢」
		PRINTFORMW 「要想确实的使它生根发芽，就必须用正确的知识好好地保养它」
		PRINTFORML 
		PRINTFORML 「同样，善因善果、善行和幸福的结果都需要正确的知识和顺序」
		PRINTFORML 「如果没有知识的话，以为好的事情其实是不好的事情、就会发生这种事」
		PRINTFORML 「如果不理解因与果的联系，明明这么努力却没有结果，就会感到不满吧」
		PRINTFORML 「撒了西瓜的种子却长出了萝卜，这是绝对不会有的，但肯定有人坚持这么说」
		PRINTFORMW 「正因为他们没有知识，所以才会犯这样的错误」
		PRINTFORML 
		PRINTFORML 「所以人必须不断学习」
		PRINTFORML 「请从自己的事情、与自己相关的事情、对自己重要的人开始加深理解」
		PRINTFORMW 「这样一来，即使面对未知的『因果』，也能自然而然地采取正确的行动」
ENDSELECT
CALL K30_D_LINE
SIF DIARY:30:16 == 0 && CFLAG:30:1701 > 9
	CALL K30_WRITE_DIARY(16)
RESULT = 0
PRINTFORMW
PRINTFORMW 「怎么样？ 如果这个故事能对你积累善行有所帮助，我也会很高兴的」
IF CFLAG:掌握把柄 && !GETBIT(CFLAG:1803, 1)
	FORCEWAIT
	PRINTFORMW 「……………………」
	PRINTFORMW 「对了，%K30_C_NAME(MASTER)%」
	PRINTFORMW 「你没有什么要向我道歉的吗……？」
	$INPUT_LOOP
	RESETCOLOR
	CALL ASK_M("…对不起，之前的事情",1 , "诶？ 什么事", 1, "…我明白了，我们不谈这个了。好！！不谈了不谈了", 1, "诶？ 什么，这个选项是什么", 1)
	CALL M_KOJO_COLOR_K30
	SELECTCASE RESULT
		CASE 0
			PRINTFORMDW 因为心里有数，所以先道个歉
			PRINTFORML 「…我没看到诚意。你真的明白自己到底在为什么道歉吗？」
			PRINTFORML 「嘛，为了惩罚像你这样的人，我才随身带着这块手板」
			PRINTFORMDW 映姬把手中的悔悟棒指向%CALLNAME:MASTER%…
			PRINTFORML 
			PRINTFORML 「我会根据我的记忆，来审判你那些无法在镜子里看到的罪行…」
			PRINTFORML 「当然，你没有异议吧？」
			RESETCOLOR
			CALL ASK_M("是", 1, "没有", 1, "当然没有", 1)
			CALL M_KOJO_COLOR_K30
			PRINTFORML 「很好。那么…」
			PRINTFORMDW 映姬取出笔，在悔悟棒上写了起来…
			IF GETBIT(CFLAG:1699, 1)
				PRINTFORML 
				PRINTFORML 「偷藏胖次的罪」
				PRINTFORMW 「真不知道你打算用在哪里」
				SIN_VAL ++
			ENDIF
			IF GETBIT(CFLAG:1699, 2)
				PRINTFORML 
				PRINTFORML 「在公共场合沉溺于奸淫的罪」
				PRINTFORMW 「给我记住什么叫做自制」
				SIN_VAL ++
			ENDIF
			;私室ヤリ房间化、睡奸バレは口上で拾えないのでこの边ガバガバ
			;仕事场ヤリ房间化のぶんでさらにガバガバガーバ。仕事案本体取り込まれたら书き直す
			IF (CFLAG:掌握把柄 - CFLAG:1700 - CFLAG:1698) > 15
				PRINTFORML 
				IF CFLAG:MASTER:初始位置 == CFLAG:30:初始位置
					PRINTFORML 「在两人的房间里和其他女性沉溺于奸淫的罪」
					PRINTFORML 「没错，在我们重要的房间里…」
					PRINTFORMDW 握着笔的手微微颤抖…
					SIN_VAL += 100
				ELSE
					PRINTFORML 「在私人房间里沉溺于奸淫的罪」
					SIF TALENT:恋人
						PRINTFORML 「而且，明明有我这个恋人…」
					PRINTFORMW 「真是不可饶恕」
					SIN_VAL ++
					SIF TALENT:恋人
						SIN_VAL += 100
				ENDIF
			ELSEIF (CFLAG:掌握把柄 - CFLAG:1700 - CFLAG:1698)
					PRINTFORML 
					PRINTFORML 「对睡着的女性恶作剧的罪」
					PRINTFORMW 「非常卑鄙可耻的行为」
			ELSEIF GETBIT(CFLAG:1699, 3)
				PRINTFORML 
				SIF TALENT:恋人
					PRINTFORML 「…明明有我这个恋人」
				PRINTFORML 「在私人房间里沉溺于奸淫的罪」
				PRINTFORMW 「真是不可饶恕」
				SIN_VAL ++
				SIF TALENT:恋人
					SIN_VAL += 100
			ENDIF
			PRINTFORML 
			PRINTFORML 「…据此，我判决你有罪」
			PRINTFORML 「这块手板的重量，就是你罪孽的重量」
			PRINTFORMW 「好好品尝这痛苦，充分认识到你自己的罪孽深重吧」
			PRINTFORML 
			PRINTFORMDL 映姬挥舞起写满字迹的悔悟棒…
			IF SIN_VAL < 100
				PRINTFORMDL 光是双手拿着的样子，就感觉相当沉重。
				PRINTFORMDW 或许需要稍微做好觉悟
				LOCAL = 4
			ELSE
				PRINTFORMDL 光是抬起来的样子，就感觉不是寻常的重量
				PRINTFORMDW 这次审判里明显掺杂了私情！
				LOCAL = 6
			ENDIF
			PRINTFORML 
			PRINTFORMW 「用这制裁来…」
			SETCOLOR 206, 8, 21
			PRINTFORML 
			PRINTFORML 「忏悔吧！！！」
			CALL K30_QUAKE(LOCAL)
			PRINTFORMW 
			CALL M_KOJO_COLOR_K30
			PRINTFORMDW 被悔悟棒用尽全力地击倒了…
			IF SIN_VAL > 100
				LOCAL = BASE:MASTER:体力 + 1
				TCVAR:30:371 = 2
			ELSE
				LOCAL = BASE:MASTER:体力 / 4
				TCVAR:30:371 = 1
			ENDIF
			CALL RECOVER(MASTER, -(LOCAL), "体力", "悔悟棒的强烈一击")
			LOCAL = GETBIT(CFLAG:1699, 0) ? 1 # 0
			CFLAG:掌握把柄 = 0
			SETCOLOR C_YELLOW
			PRINTFORML 把柄全部被消除了
			CALL M_KOJO_COLOR_K30
			VARSET CFLAG:30:0, 0, 1698, 1701
			SIF LOCAL
				SETBIT CFLAG:1699, 0
			IF BASE:MASTER:体力 <= 500
				IF !TALENT:恋慕
					PRINTFORMDL 毫不留情的一击让眼前一片漆黑…
					IF SIN_VAL < 100
						PRINTFORML 「啊，哎？ 稍微，是不是做得过火了？」
						PRINTFORMDL 映姬困惑的声音…在脑海里，嗡嗡嗡地…回响…
					ELSE
						PRINTFORML 「你就好好反省吧」
						PRINTFORMDL 映姬无情地离开了现场…
					ENDIF
				ENDIF
			ELSE
				PRINTFORMDL 虽然受到了毫不留情的一击，但还是勉强站住了…
				PRINTFORMW 「把这痛苦当作反省的食粮，今后要努力行善哦」
			ENDIF
			IF DIARY:30:10 == 0
				CALL K30_WRITE_DIARY(10)
				SIF BASE:MASTER:体力 <= 500
					SETBIT CFLAG:1910, 3
			ENDIF
			RETURN 1
		CASE 1
			PRINTFORMDW …悔悟棒微微晃动了一下。映姬似乎有点不高兴
			PRINTFORML 「既然说不记得…那也没办法了」
			PRINTFORML 「就算敲打空空如也的东西，效果也很有限吧」
			PRINTFORMW 「…什么时候想起来了，到时再谈吧」
			GOTO IGNORE
		CASE 2
			PRINTFORML 「……………」
			PRINTFORMDW 拿着悔悟棒的手微微颤抖。也许稍微有点激怒映姬了…
			PRINTFORML 「…既然你这么说，那也没什么」
			PRINTFORML 「只是失去了赎罪的机会而已。迟早会到来的」
			PRINTFORMW 「到那时后悔也没用，好好积累善行吧」
			SETBIT CFLAG:1803, 1
			GOTO IGNORE
		CASE 3
			PRINTFORML 
			PRINTFORMDL 这是选择是否用悔悟棒的折槛一击消除『把柄』的选项
			PRINTFORMDL 从上到下依次是 许可・保留・之后不显示选项
			PRINTFORMDL 伤害最大可到体力上限→当日无法行动
			PRINTFORML 
			PRINTFORMDL 保留・不显示选项会是语气比较消极的对话
			PRINTFORMDL 特别是，没有惩罚
			PRINTFORMW 
			PRINTFORML 「…怎么了？ 有好好想起来了吗？」
	ENDSELECT
	GOTO INPUT_LOOP
ELSE
	$IGNORE
	PRINTFORMDW 听了说教，映姬的好感度和信赖度\@ RESULT ? 稍微 # \@提升了一点
	LOCAL = RESULT
	CALL CHANGE_CFLAG(2, TARGET, RAND:5 + 7 - RESULT)
	CALL CHANGE_CFLAG(4, TARGET, RAND:3 + 3 - RESULT)
	SELECTCASE RAND:3
		CASE 0
			LOCALS = 感谢
		CASE 1
			LOCALS = 有益
		CASE 2
			LOCALS = 意味深长
	ENDSELECT
	CALL RECOVER(MASTER, -100, "气力", @"虽然%LOCALS%但是故事太长了")
	SOURCE:反感 = 0
	SOURCE:郁闷 = 0
	SOURCE:欢乐 += ABL:30:亲密 * 50
	SOURCE:情爱 += 1500
	RETURN LOCAL
ENDIF
RETURN 1

;==================================================
;角色介绍文函数。早苗口上を参考にしてます
;--------------------------------------------------
@CHARA_INFO_KOJO_K30
;介绍文前半は花映冢角色介绍文本とか参考に
PRINTFORML %CSVCSTR(30, 10)%
PRINTFORML 
PRINTFORML 居住在地狱，审判死者的神明大人。每天的大部分时间都花费在死者的审判和地狱的管理工作上。
PRINTFORML 性格非常认真。但是，因为工作的影响，稍微有点爱说教。
PRINTFORML 例如，如果眼前有背负罪孽的人类（或者妖怪，总之是谁都行），阎魔大人绝对不会放过。
CALL COLORMESSAGE("会用宝贵的", 0XD5DEDF, 0, 4)
PRINTFORM 时间
CALL COLORMESSAGE("絮絮叨叨地", 0XD5DEDF, 0, 4)
PRINTFORML 讲述许多有益的故事和教诲。
SIF !CFLAG:30:认识
	RETURN 1
PRINTFORML 
;恋慕取得前
IF !TALENT:30:恋慕
	IF TALENT:30:炮友 || TALENT:30:风骚 || TALENT:30:爱欲
		PRINTFORML 对自己身为阎魔却做出淫荡的行为，抱有深深的罪恶感…
	ELSEIF TALENT:30:思慕
		PRINTFORML 目前的烦恼是，本应不受任何人影响、不会感到迷茫的自己，却被某个人完全迷惑了。
	ELSEIF STRCOUNT(CSTR:30:0, "变Ｔお披露目")
		PRINTFORM 目前的烦恼是，最近引进的阎魔制服太过
		CALL COLORMESSAGE("奇装异服", 0XD5DEDF, 0, 4)
		PRINTFORML 而无法很好地驾驭它…
	ELSE
		PRINTFORML 最近在意着一个无法从净琉璃之镜中看透的人物。
	ENDIF
	PRINTFORML
	;无自觉孕ませ
	IF CFLAG:1805
		CALL K30_UNWANTED_PREGNANCY
	ENDIF
	RETURN 1
ENDIF
;恋慕取得后
PRINTFORML 接受了受他人影响而变化的自己，开始想要珍惜和%\@ TALENT:30:恋人 ? 恋人 # 心上人 \@%共度的时光。

;被逆推实行后、独自イベントっぽいのを匂わせる。文章の加减が难しいねこれ
IF CFLAG:30:1801 && STRCOUNT(CSTR:30:0, "推倒")
	PRINTFORML 同时因为主动跨越了界线，也希望%\@ TALENT:30:恋人 ? 恋人 # 心上人 \@%能够接受自己至今为止一直压抑的欲望…
	IF GETBIT(CFLAG:30:1910, 7)
		PRINTFORML 　・似乎想要更加疼爱为了%CALLNAME:MASTER%专门改造过的屁股…
	ELSE
		PRINTFORML 　・似乎想要%CALLNAME:MASTER%好好疼爱自己的屁股…
	ENDIF
	SIF ABL:30:露出癖 > 4 && CFLAG:30:1607 > 10
		PRINTFORML 　・被%CALLNAME:MASTER%攻击屁股而潮吹，似乎感觉非常舒服，已经变成习惯了…
	SIF !STRCOUNT(CSTR:30:0, "朝から") && ABL:30:精液中毒 > 2
		PRINTFORML 　・\@ CFLAG:MASTER:初始位置 == CFLAG:30:初始位置 ? 情绪高涨的话，似乎想用『特别』的方式叫醒%CALLNAME:MASTER% # 如果能和%CALLNAME:MASTER%同居，早上就用『特别』的方式 \@叫醒他…
	SIF CFLAG:30:信赖度 >= 2000 && !STRCOUNT(CSTR:30:0, "キメセク") && CFLAG:30:允许无套 == 2
		PRINTFORML 　・似乎想要和%CALLNAME:MASTER%水乳交融到身心都融化，就算是被骗喝下可疑的茶也无所谓…
	SIF TALENT:MASTER:恋人 != 76 && TALENT:76:恋慕 && GET_UWAKI_HISTORY(30, 76) == 2 && ABL:30:精液中毒 > 3 && ABL:76:精液中毒 > 3
		PRINTFORML 　・似乎希望%CALLNAME:MASTER%能像自己一样疼爱小町，然后两人一起被爱或者一起侍奉%CALLNAME:MASTER%…
	SIF TALENT:30:母乳体质 && !CFLAG:30:1802 && !STRCOUNT(CSTR:30:0, "授乳PLAY")
		PRINTFORML 　・似乎偷偷地想着，让%CALLNAME:MASTER%含着自己的乳头的同时，也想极尽宠溺地娇惯%CALLNAME:MASTER%…
ENDIF
PRINTFORML 
;无自觉孕ませ
IF CFLAG:1805
	CALL K30_UNWANTED_PREGNANCY
	RETURN 1
ENDIF
;怀孕愿望。恋慕后でも时奸妊娠とか鬼畜你を考虑したら见づらい分岐に
;愿望再取得时
IF GETBIT(CFLAG:30:1502, 4)
	PRINTFORML 希望能再次怀上和%CALLNAME:MASTER%的孩子。
	PRINTFORML 
	RETURN 1
ELSEIF TALENT:30:怀孕愿望
	PRINTFORML 梦想着能怀上和%CALLNAME:MASTER%的孩子。
	PRINTFORML 
	RETURN 1
;怀孕愿望再取得可
ELSEIF GETBIT(CFLAG:30:1502, 3) && !TALENT:30:育儿中 && !TALENT:30:妊娠
	PRINTFORML 期待着如果再次[祈愿]的话，说不定就能怀上孩子。
	PRINTFORML 
	RETURN 1
ENDIF
SIF !INRANGE(CFLAG:30:1502, 1, 7) || TALENT:30:育儿中 || TALENT:30:妊娠 || EXP:30:分娩经验
	RETURN 1
;bit演算を十进数で捉えてCASE分け
SELECTCASE CFLAG:30:1502
	CASE 1
		PRINTFORML 虽然身体已经献给了%CALLNAME:MASTER%，但没有成为母亲的自信。
		PRINTFORML 　・想着如果能更加[信任]%CALLNAME:MASTER%就好了…
		PRINTFORML 　・觉得阎魔想要孩子，即使[祈愿]也很难实现…
	CASE 2
		PRINTFORML 因为%CALLNAME:MASTER%坦白说想要孩子而动摇。
		PRINTFORML 　・觉得明明还没有完全献出身体，是不是太着急了…
		PRINTFORML 　・想着如果能更加[信任]%CALLNAME:MASTER%就好了…
	CASE 3
		PRINTFORML 正在犹豫是否真的想要%CALLNAME:MASTER%的孩子。
		PRINTFORML 　・想着如果能更加[信任]%CALLNAME:MASTER%就好了…
	CASE 4
		PRINTFORML 已经开始模模糊糊地意识到和%CALLNAME:MASTER%的孩子这件事。
		PRINTFORML 　・觉得明明还没有完全献出身体，是不是太着急了…
		PRINTFORML 　・觉得阎魔想要孩子，即使[祈愿]也很难实现…
	CASE 5
		PRINTFORML 已经清楚地意识到了和%CALLNAME:MASTER%的孩子这件事。
		PRINTFORML 　・觉得阎魔想要孩子，即使[祈愿]也很难实现…
	CASE 6
		PRINTFORML 无法下定决心和%CALLNAME:MASTER%造孩子。
		PRINTFORML 　・还在犹豫，甚至无法完全献出身体…
	CASE 7
		PRINTFORML 迷茫一扫而空，终于下定决心和%CALLNAME:MASTER%造孩子。
ENDSELECT
RETURN 1

@K30_UNWANTED_PREGNANCY
IF INRANGE(CFLAG:妊娠经过日数, 1, 19)
ELSEIF INRANGE(CFLAG:妊娠经过日数, 20, 59)
	PRINTFORML 明明身体出现了异样，却故意视而不见
ELSEIF CFLAG:妊娠经过日数
	PRINTFORML 对自己怀上不知是谁的孩子的异常事态感到恐惧和战栗
ELSEIF CFLAG:分娩经过日
	PRINTFORML 被罪恶感折磨，只能靠着好好养育%CSTR:30:83%来支撑自己
ELSE
	CALL COLORMESSAGE, "让自己遭受严厉的惩罚，身体变得再也无法怀孕", C_RED, 1, 1
ENDIF
RETURN

;==================================================
;映姫大人の尻穴と抵抗を缓くするEXTRASOURCE函数群
;丰姫口上参考にしました
;--------------------------------------------------
;COMF4 舐肛
@M_KOJO_EXTRASOURCE_COM_K30_4
CALL K30_LESS_RESISTANCE_ANAL

;COMF5 肛门爱抚
@M_KOJO_EXTRASOURCE_COM_K30_5
CALL K30_LESS_RESISTANCE_ANAL

;COMF45 肛用振动棒
@M_KOJO_EXTRASOURCE_COM_K30_45
CALL K30_LESS_RESISTANCE_ANAL

;COMF62 正常位肛交
@M_KOJO_EXTRASOURCE_COM_K30_62
CALL K30_LESS_RESISTANCE_ANAL

;COMF63 后背位肛交
@M_KOJO_EXTRASOURCE_COM_K30_63
CALL K30_LESS_RESISTANCE_ANAL

;COMF66 骑乘位肛交
@M_KOJO_EXTRASOURCE_COM_K30_66
CALL K30_LESS_RESISTANCE_ANAL

;COM74(SCOMF14) 玩弄Ｓ状结肠
@M_KOJO_EXTRASOURCE_SCOM_K30_14
CALL K30_LESS_RESISTANCE_ANAL

;COM75(SCOMF15) 隔着刺激阴道
@M_KOJO_EXTRASOURCE_SCOM_K30_15
CALL K30_LESS_RESISTANCE_ANAL

;SCOMF16 肛门交互插入
@M_KOJO_EXTRASOURCE_SCOM_K30_16
CALL K30_LESS_RESISTANCE_ANAL

;COMF314 肛门爱抚
@M_KOJO_EXTRASOURCE_COM_K30_314
CALL K30_LESS_RESISTANCE_ANAL

;COMF318 动起腰身（Ａ）
@M_KOJO_EXTRASOURCE_COM_K30_318
CALL K30_LESS_RESISTANCE_ANAL

;COMF323 对方主导后背位Ａ
@M_KOJO_EXTRASOURCE_COM_K30_323
CALL K30_LESS_RESISTANCE_ANAL

;轻减处理函数
@K30_LESS_RESISTANCE_ANAL
#DIM DIVIDED
SIF !(TALENT:思慕 || TALENT:恋慕 || TALENT:炮友 || TALENT:爱欲)
	RETURN
SOURCE:偏离正轨 /= 4
SOURCE:不洁 /= 6
SIF TALENT:心情 == -1 || MARK:反抗刻印
	RETURN
DIVIDED = 3 + (TALENT:恋慕 * 2) - (K30_BE_SEEN() * 2)
SOURCE:郁闷 = SOURCE:郁闷 / DIVIDED
SOURCE:反感 = SOURCE:反感 / DIVIDED
RETURN
;==================================================
;映姫大人が仕事中に口交抜きしてくれるEXTRASOURCE函数群
;本体侧改造しない都合で1ターンで你を发射させる仕大人
;仕事中ならどうしても我慢できないのを手早く済ます…というシチュとしてごまかす
;--------------------------------------------------
;COMF300 会话
@M_KOJO_EXTRASOURCE_COM_K30_300
IF K30_CHECK_BLOW_JOB()
	CALL K30_SET_BLOW_JOB
	TFLAG:193 = -999
;ELSEIF K30_AFTERSEX_FELLA()
;	CALL K30_SET_BLOW_JOB
;	TFLAG:193 = -900
ENDIF
RETURN 1

;COMF304 帮忙干活
@M_KOJO_EXTRASOURCE_COM_K30_304
IF K30_CHECK_BLOW_JOB() && BASE:30:工作量 > 0
	CALL K30_SET_BLOW_JOB
	DOWNBASE:MASTER:体力 -= 50
	DOWNBASE:MASTER:气力 -= 180
	;经过时间の减少と工作量を戻す
	TIME -= 50
	BASE:工作量 = TFLAG:194
	TFLAG:193 = -999
ENDIF

;仕事中口交抜き判别用式中函数
@K30_CHECK_BLOW_JOB()
#FUNCTION
SIF K30_BE_SEEN() || !仕事中チェック(30) || INRANGE(TIME, 540, 719) || CFLAG:职业种类 != 43
	RETURNF 0
SIF !SHIRAHU(30) || !TALENT:恋慕 || MARK:反抗刻印 || TALENT:心情 == -1
	RETURNF 0
SIF ABL:技巧 < 3 || ABL:精液中毒 < 5 || EXP:精饮经验 < 100
	RETURNF 0 
SIF !GETBIT(CFLAG:30:既成事实, 1) || TALENT:怀孕愿望 || GETBIT(CFLAG:1502, 4)
	RETURNF 0
SIF !(TALENT:PLAYER:2 & 2) || 插入不可(30) == 3 || K30_CHECK_HEN_T()
	RETURNF 0
SIF !(TCVAR:发情 || CFLAG:积攒度 > 850 || TCVAR:媚药)
	RETURNF 0
SIF !ONCE("口交抜き")
	RETURNF 0
;ここまで抜けたら确定
RETURNF 1

;口交抜きソース处理函数
@K30_SET_BLOW_JOB
CALL COM81
;映姫大人と你を同时绝顶させる
TCVAR:MASTER:要去了 = 1
BASE:MASTER:射精 = MAXBASE:MASTER:射精
PALAM:30:4 = MAX(PALAMLV:4, PALAM:30:4)
SOURCE:反感 = 0

;==================================================
;映姫大人にオーバードーズする函数群
;媚药成分で诶嘿嘿アスリートになれるよう顽张ってみました
;--------------------------------------------------
;キメセク判定用式中函数
@K30_KIMESEKU()
#FUNCTION
#DIM YANUSHI
IF !CFLAG:诶嘿嘿
	SIF ITEM:媚药 < 2
		RETURNF 0
	;被逆推.ERB から条件コピペ
	FOR COUNT, 1, CHARANUM
		SIF COUNT == 30
			CONTINUE
		SIF CAN_MOVE(CFLAG:MASTER:当前位置, CFLAG:COUNT:当前位置) == 2
			RETURNF 0
	NEXT
ENDIF
SIF TALENT:心情 < 0 || MARK:30:反抗刻印 || !TALENT:恋慕 || K30_CREAMPIE() || FLAG:99
	RETURNF 0
SIF !GETBIT(CFLAG:既成事实, 1) || CFLAG:妊娠自觉状态 || (STRFIND(CSTR:30:0, "推倒") ==-1 && CFLAG:30:1801)
	RETURNF 0
SIF CFLAG:信赖度 < 2000 || !CHARA_HOLIDAY(30) || K30_CHECK_HEN_T()
	RETURNF 0
SIF ROOMDATA:(CFLAG:MASTER:当前位置 % 100) & 场所_公共の场
	RETURNF 0
SIF !GROUPMATCH(PRIVATEROOM:(CFLAG:当前位置 % 100), 0, 30)
	RETURNF 0
RETURNF 1

;キメセク用BUFF函数
;陷落后の茶番なのでBuff盛り盛り・自重しない
@K30_KIMESEKU_BUFF
IF !CFLAG:诶嘿嘿
	CALL BUFF_BASE(TARGET, BASE_气力,666)
	CALL BUFF_BASE(TARGET, BASE_体力,666)
	BASE:MASTER:勃起 = MAXBASE:MASTER:勃起
	BASE:30:理性 /= 10
	CFLAG:30:积攒度 = MIN(CFLAG:30:积攒度 + 666, 1000)
	BASE:30:情绪 = MIN(BASE:30:情绪 + 666, 1200)
	TCVAR:30:媚药 = 630
	CFLAG:30:同行 = 60
	CFLAG:MASTER:同行 = 60
ENDIF
SIF PALAM:润滑 < PALAMLV:6
	PALAM:润滑 = PALAMLV:6
SIF PALAM:情欲 < PALAMLV:9
	PALAM:情欲 = PALAMLV:9
TEQUIP:MASTER:结界发动 = 1
SETBIT TCVAR:30:367, 2
TCVAR:30:发情 = 1
IF !TALENT:淫乱
	TALENT:淫乱 = 1
	SETBIT TCVAR:30:367, 1
ENDIF
TEQUIP:30:子宫 = 1
SIF EXP:子宫口开发经验 > 49 && ABL:膣 > 1
	TEQUIP:30:子宫 = 2
;キメセク用推倒函数
@K30_KIMESEKU_PUSHDOWN
CFLAG:MASTER:诶嘿嘿 = 2
CFLAG:30:诶嘿嘿 = 2
TFLAG:COMABLE管理 = 3
TCVAR:30:随便 = 1
SETBIT TCVAR:30:367, 3
CALL CTRL_CLOTHES_SET(30, "现在衣装の变更_全裸")
CALL CTRL_CLOTHES_SET(MASTER, "现在衣装の变更_全裸")
CALL CLOTHES_SETTING_TRAIN(30)
CALL CLOTHES_SETTING_TRAIN(MASTER)
COM_STR = 
;==================================================
;复数恋人を制御する函数
;TALENT:MASTER:恋人が变动する仕大人变更に对応できないため
;--------------------------------------------------
;ARG	状况分类(0=破局チェック（ターン每） 1=约会后告白イベント制御 2=告白指令制御
;				 3=破局后チェック
;--------------------------------------------------
@K30_DENY_POLYAMORY(ARG)
;↓のコメントアウト外すと制御解除。エラーは出ないはずだけど@K30_C_NAME关联で变なことにはなる
;RETURN 0
SELECTCASE ARG
	CASE 0
		SIF !TALENT:30:恋人
			RETURN
		SIF TALENT:MASTER:恋人 == 30
			RETURN
		CLEARBIT CFLAG:30:既成事实, 2
		TALENT:30:恋人 = 0
		SETBIT CFLAG:30:1505, 1
		PRINTFORML 
		PRINTFORMDW …%CALLNAME:MASTER%获得了新的恋情，另一方面，总觉得有什么东西在心中破碎了
		CALL COLORMESSAGE, "与映姬的恋人关系已经结束", C_YELLOW, 2, 1
	CASE 1
		IF TALENT:MASTER:恋人
			TCVAR:30:中止接吻 = 1
			SETBIT CFLAG:30:约会后EVENTFLAG, 1
		ELSEIF !(TALENT:MASTER:恋人 || TALENT:30:恋人)
			CLEARBIT CFLAG:30:约会后EVENTFLAG, 1
		ENDIF
	CASE 2
		SIF !TALENT:MASTER:恋人
			RETURN 0
		SIF GETBIT(CFLAG:1505, 1)
			RETURN 2
		RETURN 1
	CASE 3
		SIF GETBIT(CFLAG:30:1505, 1)
			RETURN 1
ENDSELECT
