;*********************************************************************************
;
;
;	K17口上内で使用函数群
;
;
;	□目次	^[ \t]*(@)	でアウトライン绞込
;
;----------FUNCTION(S)-------------
;K17_GRT						现在的关系ランクを返す
;K17_RT_FG						关系查定のビットフラグを立てる
;K17_GRT_EX						GRTの值に揺らぎを与える
;K17_CHECK_ANGER_COM			愤怒の上升判定
;K17_MARK_反抗刻印				反抗刻印つける
;K17_HEART						この口上で使用ハートマークを返す
;K17_HEART2						ハートマークを返すサブ　小さい方
;K17_BLUSH						///を返す
;K17_ROUND						roundみたいなやつ
;K17_PERCENT					100%中に对する值を入れて当たるか抽选
;K17_LOWER_LIMIT_RAND			下限值设定随机
;K17_MAX_LIMIT_RAND				上限值设定随机
;K17_RANGE_RAND					值に振り幅を持たせた随机值
;K17_SELECT_RAND				条件付きRAND
;---------お借りしました------------
;K17_RS							RAND_SPLIT 文字列の随机チョイス
;K17_CHANGE_SPLIT				番号值の文字列を操作
;K17_AUTO_SPLIT_NUM				文字列を検索し番号を返す
;K17_CHOICE						文字を[]选择肢に	本体にある函数のカスタ​舞​ズ
;K17_ASK_M						文字自体を选择肢に	同上
;-------------------------------------
;K17_GET_CHARA_POSNUM			その角色のいる位置に何人(MASTER除く)いるか数える
;K17_CHECK_RANDOM_EVENT			エロ系の随机イベント发生を主に积攒度で判定してくれる
;K17_LUCKY_SUKEBE				指定した概率で发生判定をする+二人きりとかが见えれる
;K17_CHECK_输送胖次			指定角色の胖次を何枚输送中か検索する
;K17_GET_CLOTHNAME_TOP			服装の见た目の名前を得る
;K17_GET_CLOTHNAME_TOP_UNDER
;K17_GET_CLOTHNAME_BOTTOM
;K17_GET_CLOTHNAME_BOTTOM_UNDER
;K17_GET_SKIRT					下半身めくりに对する物体の名称 裙子か裾か
;K17_CNAME						角色の呼び方　基本これでMASTERを呼ぶ
;K17_GET_FRIENDS				爱丽丝の友人かどうか判定する　反应させたい角色IDを列挙しておく
;K17_GET_FRIENDS_TARGET			指定した角色の位置に爱丽丝の友人IDを取得
;K17_GET_FRIENDS_ROOM			指定した角色の位置が爱丽丝の友人の初始位置かを调べる
;K17_GET_TARGETS_ID				复数いるか捜查ついでに友人优先でIDを取る
;K17_GET_DOLL					角色クタに上海と蓬莱が含まなければ使えるという判定をする
;K17_GET_DOLL_CNAME				人形の名前を取得する
;K17_人形台词					口上中に人形の文本カラーで台词打ちする
;K17_GT							时间带に对応したあいさつ
;K17_CALL_G						この口上（爱丽丝）の性器の呼び方を返す(GNAMEだと见间违いそうなんで变え)
;K17_CALL_P						部位の呼び名　胸部とか
;K17_PS_TEXT					女性器表现地の文
;
;*********************************************************************************


;*********************************************************************************
;	关系性を查定する函数
;
;	GET_RELATION_TYPE(GRT)
;	每回フラグ见るの大变だから取得フラグに応じて关系性にランク付けする
;	每回IF文で分けずにSELECTできたり比较演算使える
;	返回值变量を操作してそのイベント（指令）内で一时的に关系性を上げたり下げたりもできる（K17_GRT_EX
;
;	09/06	ビット计算方式＆ヘッダーを追加　条件は面罩参照
;	09/08	K17_GET_RELATION_TYPEからK17_GRTに省略
;
;	参数	相手ID	省略で爱丽丝
;	返回值　关系性の值↓参照
;
;						陷落		   接吻合意 诶嘿嘿合意	イベント
;	0	知り合い		取得无
;	1	お友达			亲密5								会话指令から回收
;	2	思慕			思慕or炮友	无↑
;	3	思慕接吻						あり↓	无↑
;	4	思慕逢引											约会イベ済
;	5	思慕诶嘿嘿								あり↓		约会イベ省略可
;	6	恋慕			恋慕 or 爱欲	あり↓	あり↓		约会イベ省略可
;	7	恋慕告白											↓告白イベ済 ＆ 约会イベ済
;	8	恋慕允许无套		危险日允许无套
;	9	性癖												↓性癖取得イベ済
;	10	恋人			恋人								恋人or快乐刻印3
;*********************************************************************************
@K17_GRT(ARG=K17C_ID)
#FUNCTION
#DIM iRTF
#DIM iReRT
VARSET iReRT

iRTF = K17_RT_FG(ARG)
;iRTF = CFLAG:ARG:K17C_CF_RTF

;上から见ていって面罩を全部满たしたら返す
FOR i,K17C_RT_MAX-1,i<0,-1
	
	IF (K17C_RTM:i - (K17C_RTM:i & iRTF)) == 0
		iReRT = i
		BREAK
	ENDIF
NEXT

;DEBUGPRINTFORML 【K17_GRT ({iReRT})】

;番号返し
RETURNF iReRT

;*********************************************************************************
;	RELATION_TYPEのビットフラグ立て	细かい判定、实际のフラグチェックに使用
;	每回使用ものなので处理を考える
;
;	参数　：角色ID
;	返回值：全ての立ってるフラグを足した值
;*********************************************************************************
@K17_RT_FG(iID=K17C_ID)
#FUNCTION
#DIM iID	;参数0
#DIM iReRTF	;返回值

VARSET iReRTF

	;恋人	1p9
	SIF CFLAG:iID:既成事实 & 合意_恋人 || MARK:iID:快乐刻印 >= 3
		iReRTF += K17C_RTF_恋人
	
	;性癖取得	1p8
	SIF CFLAG:K17C_ID:K17C_CF_性癖取得
		iReRTF += K17C_RTF_性癖
	
	;告白イベ	1p7
	SIF CFLAG:K17C_ID:K17C_CF_告白イベント
		iReRTF += K17C_RTF_告白
	
	;允许无套		1p6
	SIF CFLAG:iID:允许无套 >= 2
		iReRTF += K17C_RTF_允许无套

	;恋慕or爱欲　同じ扱い	1p5
	SIF TALENT:iID:恋慕 || TALENT:iID:爱欲
		iReRTF += K17C_RTF_恋慕
	
	;诶嘿嘿成功	1p4
	SIF CFLAG:iID:既成事实 & 合意_诶嘿嘿 || (TCVAR:iID:初次拥抱 < 6 && !TCVAR:iID:初次拥抱 == 0)
		iReRTF += K17C_RTF_诶嘿嘿
	
	;约会イベ通过	1p3
	SIF CFLAG:K17C_ID:K17C_CF_约会イベント
		iReRTF += K17C_RTF_逢引
	
	;接吻成功	1p2
	SIF CFLAG:iID:既成事实 & 合意_接吻 || CFLAG:K17C_ID:K17C_CF_FK
		iReRTF += K17C_RTF_接吻
	
	;纯洁的交往	1p1
	SIF TALENT:iID:思慕 || TALENT:iID:恋慕 || TALENT:iID:爱欲 || TALENT:iID:炮友
		iReRTF += K17C_RTF_思慕
	
	;お友达		1p0
	SIF CFLAG:K17C_ID:K17C_CF_お友达イベント || ABL:iID:亲密 >= 5
		iReRTF += K17C_RTF_お友达

;DEBUGPRINTFORML 【K17_RT_FG ({iReRTF})



	;どれにも挂からなければ知り合い程度
RETURNF iReRTF

;*********************************************************************************
;	现在的关系性の名称取得
;*********************************************************************************
@K17_GET_RELATION_NAME(ARG)
SELECTCASE ARG
	CASE K17C_知り合い
		LOCALS = 熟人
	CASE K17C_お友达
		LOCALS = 朋友
	CASE K17C_思慕
		LOCALS = 喜欢……大概吧
	CASE K17C_思慕接吻
		LOCALS = 接吻关系
	CASE K17C_思慕逢引
		LOCALS = 喜欢！
	CASE K17C_思慕诶嘿嘿
		LOCALS = Ｈ的关系
	CASE K17C_恋慕
		LOCALS = 我爱你……
	CASE K17C_恋慕告白
		LOCALS = 我爱你！
	CASE K17C_恋慕允许无套
		LOCALS = 生小孩的关系
	CASE K17C_性癖
		LOCALS = 知道秘密的关系
	CASE K17C_恋人
		LOCALS = 最高の关系
ENDSELECT
RETURN 1


;*********************************************************************************
;	现在的关系性にステータスの变化に応じた揺らぎを与える
;	随机あり　实际のフラグ考虑无
;
;	心情、情绪、理性、愤怒、酒气、の状态に对応する
;	イベントなんかより何度も呼ばれる指令で使用
;
;	气に食わないので要调整
;	约会中无条件で下げ幅-1
;	恋人で+1にした
;	ゆらぎを与える条件を先のフラグ持ってる场合のみ限定にしました
;	取得しているフラグと关系性のランクが合致している场合
;	心情と反抗の值しか适応されない安定した状态になる
;	上位のフラグを持ちつつランクが下の场合、加えて愤怒、酒气、情绪、理性の割合が付く
;	例）现在思慕のランクだが恋人のフラグは持っている场合
;		思慕から+2～-3の值が随机で付く
;	ただしプラス值查定の方が多いので概ねプラスに倾く(反抗3とかついて怒ってなければ)
;	この例で情绪がよければ1/2の概率で+1か+2されるはず
;	どっちかっていうと下がった感觉か
;	身体接触など关系性で选择できるものが增える指令の判定に半分くらい胜てるはず
;	上げ幅上限を持ってる最高ランクのフラグにするか？
;	当面これで样子见
;
;	参数0	：角色ID　省略で爱丽丝
;	返回值	：变更されたRELATION_TYPE值
;*********************************************************************************
@K17_GRT_EX(ARG=K17C_ID)
#FUNCTION
#DIM iRT =0
#DIM iSum=0
#DIM iMinSum=0
#DIM TEMP =0
;上下限设定
#DIM CONST kMINLIMIT = 3
#DIM CONST kMAXLIMIT = 2

VARSET iSum
VARSET iMinSum
VARSET TEMP
iRT = K17_GRT(ARG)

;现在的ランク值のフラグと见比べて先のフラグを持っていればゆらぎを与える
IF K17_RT_FG(ARG) <= K17C_RTM:iRT
	;同等の场合心情と反抗だけ适応してそのまま返す(呼び方と同じ)
	iRT += TALENT:ARG:心情
	iRT += MARK:TARGET:反抗刻印
	RETURNF LIMIT(iRT,0,K17C_RT_MAX)
ENDIF

;----------------------
;先のフラグを持ってる时の处理
;----------------------
;心情そのまま　恶かったら-1
IF TALENT:ARG:心情 == 1
	iSum += 1
ELSEIF TALENT:ARG:心情 == -1
	iMinSum += 1
ENDIF

;反抗刻印
SIF MARK:TARGET:反抗刻印
	iMinSum += MARK:TARGET:反抗刻印


;态度の现在值セット
;iSum = CFLAG:ARG:态度
;情绪＆理性 割合平均值の1/3 实际は合计值を一绪に割って四舍五入か
iSum += K17_ROUND((BASE:ARG:情绪 / 100) + (1000 - BASE:ARG:理性) / 100,6)

;酒气割合の1/3足しちゃう
SIF BASE:ARG:酒气
	iSum += K17_ROUND(K17_ROUND((BASE:TARGET:酒气*10),(MAXBASE:TARGET:酒气)),3)
SIF BASE:ARG:愤怒
	iMinSum +=K17_ROUND( K17_ROUND((BASE:TARGET:愤怒*10),(MAXBASE:TARGET:愤怒)) ,3)

	
;约会中、下げ幅-1
SIF FLAG:约会的对象 == K17C_ID
	iMinSum--
;恋人なら无条件で+1
SIF TALENT:K17C_ID:恋人
	iSum++
;疑い
SIF CFLAG:ARG:K17C_CF_疑い && !CFLAG:ARG:时间停止暴露
	iMinSum += K17_ROUND(CFLAG:ARG:K17C_CF_疑い,30)

;大きい方一旦保存
SIF iMinSum > iSum
	TEMP = 1
;
;DEBUGPRINTFORML 【K17_GRT_EX 实值({iRT})】
;DEBUGPRINTFORML 【K17_GRT_EX プラス值({iSum})】
;DEBUGPRINTFORML 【K17_GRT_EX 减少值({iMinSum})】

;上下限设置
iMinSum=LIMIT(iMinSum, 0 ,kMINLIMIT)
iSum=LIMIT(iSum,0 ,kMAXLIMIT)

iMinSum=LIMIT(iRT-iMinSum, 0 ,K17C_RT_MAX-1)
iSum=LIMIT(iRT+iSum,0 ,K17C_RT_MAX-1)

;DEBUGPRINTFORML 【丸め后】
;DEBUGPRINTFORML 【K17_GRT_EX 实值({iRT})】
;DEBUGPRINTFORML 【K17_GRT_EX プラス适応值({iSum})】
;DEBUGPRINTFORML 【K17_GRT_EX 减少适応值({iMinSum})】


;振り幅が广阔方に2/4　狭窄方1/4　1/4でそのまま
SELECTCASE RAND:4
	CASE 0
		SIF iMinSum == 0
			RETURNF 0
		RETURNF RAND(iMinSum,iRT+1)
	CASE 1
		RETURNF RAND(iRT,iSum+1)
	CASE 2
		IF TEMP
			SIF iMinSum == 0
				RETURNF 0
			RETURNF RAND(iMinSum,iRT+1)
		ELSE
			RETURNF RAND(iRT,iSum+1)
		ENDIF
ENDSELECT

RETURNF iRT

;*********************************************************************************
;	日常指令で上升するであろう愤怒值を情绪理性で评价し
;	愤怒が点灯するようなら1　反抗刻印まで付きそうなら+刻印の数
;	愤怒が点灯不会うなら0
;	
;	完全は稍微无理くさいなーと思ったけどなんとかなるもんだな
;	本体SOURCE_CHECK函数中の口上呼び出しからFAVOR_CALCまでを必要分だけ再现
;	最终的な愤怒は自然减少がかかるけど、点灯は先に付くはずなので点灯フラグはとれるはず
;	本体のコピペ　实际に数值上げるのはまずいので变量で代用
;
;	参数0	：角色ID　省略で爱丽丝
;	返回值	：推定愤怒点灯フラグ
;			：RESULTに现在的愤怒值+愤怒上升值(0～1000丸め后)
;			：RESULT:1に今回の愤怒上升值
;*********************************************************************************
@K17_CHECK_ANGER_COM(ARG=K17C_ID)
#FUNCTION
#DIM DYNAMIC iValue = 0
#DIM DYNAMIC 愤怒上升值=0
#DIM DYNAMIC 情绪上升值=0
#DIM i理性
#DIM i情绪
#DIM i积攒度
#DIM i情欲
#DIM i心情

SIF TFLAG:102 >= 2 && FLAG:时间停止
	RETURNF 0

;-------------------------------------------------
;上升值コピペ　预测计算
;-------------------------------------------------
i心情 = TALENT:ARG:心情
i情欲 = PALAM:ARG:情欲
i情绪 = BASE:ARG:情绪
i理性 = BASE:ARG:理性
i积攒度 = CFLAG:ARG:积攒度
i情欲 += CUP:ARG:情欲 - CDOWN:ARG:情欲

SIF TCVAR:TARGET:欲求不满度管理 < GETPALAMLV(i情欲,15)
	i积攒度 += (GETPALAMLV(i情欲,15) - TCVAR:TARGET:欲求不满度管理) * (30 + TCVAR:TARGET:发情 * 20 + TCVAR:TARGET:媚药 * 20)


LOCAL = 0
LOCAL:1 = 0
;各种快感
;50上限、30000と+300 この数值が大きければSOURCE:ARGの增加に对して钝感になる
LOCAL += (100 - 30000 / (SOURCE:ARG:快Ｃ + SOURCE:ARG:快Ｖ + SOURCE:ARG:快Ａ + SOURCE:ARG:快Ｂ + 300)) * (1 + TALENT:ARG:快感应答)
;情爱
LOCAL += (100 - 100000 / (SOURCE:ARG:情爱 + 1000))
LOCAL:1 += (100 - 100000 / (SOURCE:ARG:情爱 + 1000))
;情欲
LOCAL += (20 - 20000 / (SOURCE:ARG:情欲 + 1000))
LOCAL:1 += (100 - 100000 / (SOURCE:ARG:情欲 + 1000))
;达成感
LOCAL += (30 - 30000 / (SOURCE:ARG:达成 + 1000))
;恭顺
LOCAL += (20 - 20000 / (SOURCE:ARG:恭顺 + 1000))
;屈服
LOCAL += (20 - 20000 / (SOURCE:ARG:屈从 + 1000))
;露出
LOCAL += (30 - 30000 / (SOURCE:ARG:露出 + 1000)) * (ABL:ARG:露出癖 - 3) / 3
;恐怖
LOCAL += (50 - 20000 / (SOURCE:ARG:恐怖 + 400)) * (ABL:ARG:顺从 - 3) / 3
;苦痛
LOCAL += (50 - 20000 / (SOURCE:ARG:苦痛 + 400)) * (ABL:ARG:受虐属性 - 3) / 3
;欢乐
LOCAL += (50 - 10000 / (SOURCE:ARG:欢乐 + 2000))
;征服
LOCAL += (30 - 90000 / (SOURCE:ARG:征服 + 3000))
;被动
LOCAL += (30 - 90000 / (SOURCE:ARG:被动 + 3000))
;不洁
LOCAL -= (50 - 25000 / (SOURCE:ARG:不洁 + 500)) * (2 - TALENT:ARG:污臭耐性) / 2
;郁闷
LOCAL -= (50 - 15000 / (SOURCE:ARG:郁闷 + 300))
;偏离正轨
LOCAL -= (50 - 25000 / (SOURCE:ARG:偏离正轨 + 500))
;反抗
LOCAL -= (50 - 10000 / (SOURCE:ARG:反感 + 200))



;情绪に好感度上升分追加
情绪上升值 = MIN(ABL:ARG:亲密 + 1,10) * LOCAL / 3
;路人がいると上升量半减
SIF WITH_MOB()
	情绪上升值 /= 2
i情绪 += MIN(情绪上升值,200)
i情绪 = MIN(i情绪 , MAXBASE:ARG:情绪)

;理性を快感度分减少
i理性 -= LOCAL:1 * (GETPALAMLV(i情欲,10) + 1) / 4 * (i积攒度 + 1000) / 2000
i理性 = MAX(i理性 , 0 )
;
;DEBUGPRINTFORML 【K17_CHECK_ANGER_COM 反感值({SOURCE:ARG:反感})】
;DEBUGPRINTFORML 【K17_CHECK_ANGER_COM 情欲值({i情欲})】
;DEBUGPRINTFORML 【K17_CHECK_ANGER_COM 积攒度({i积攒度})】
;DEBUGPRINTFORML 【K17_CHECK_ANGER_COM 情绪值({i情绪})】
;DEBUGPRINTFORML 【K17_CHECK_ANGER_COM 理性值({i理性})】
;DEBUGPRINTFORML 【K17_CHECK_ANGER_COM 反感({SOURCE:ARG:反感})
;DEBUGPRINTFORML 【K17_CHECK_ANGER_COM 愤怒{BASE:ARG:愤怒}


;反抗刻印つくか见る
SELECTCASE CUP:ARG:反感+CUP:ARG:不快
	CASE 500 TO 999
		SIF MARK:ARG:反抗刻印 < 1
			iValue = 1
	CASE 1000 TO 1999
		SIF MARK:ARG:反抗刻印 < 2
			iValue = 2
	CASE IS >= 2000
		SIF MARK:ARG:反抗刻印 < 3
			iValue = 3
ENDSELECT
SIF iValue
	i心情 = -1

;判定值コピペ
;日常ONかつ反感ソース有りで愤怒值上升
SIF SOURCE:ARG:反感 > 0 && TFLAG:102 == 1
	愤怒上升值 = SOURCE:ARG:反感 * 3 * (2000 - i情绪 ) * (500 + i理性) / ((2000 + i情绪) * (500 + MAXBASE:ARG:理性))
;振动棒胖次や触手胖次を是てる时は愤怒值が上升しない 逆ギレ防止
SIF SOURCE:ARG:反感 > 0 && TFLAG:102 == 1 && (EQUIP:ARG:下半身内衣２ == 28 || EQUIP:ARG:下半身内衣２ == 29 || EQUIP:ARG:下半身内衣２ == 30)
	愤怒上升值 = 0
;贞操带を是てる时は愤怒上升值が增加
SIF SOURCE:ARG:反感 > 0 && TFLAG:102 == 1 && EQUIP:ARG:下半身内衣２ == 27
	愤怒上升值 = 愤怒上升值 * 4 / 3
;心情で愤怒上升值が变化
IF i心情 == 1
	愤怒上升值 = 愤怒上升值 * 7 / 10
ELSEIF i心情 == -1
	愤怒上升值 = 愤怒上升值 * 3 / 2
ENDIF
SIF TCVAR:ARG:睡眠药强度 == 1 && CFLAG:ARG:恶作剧
	愤怒上升值 = 愤怒上升值 / 2
SIF TCVAR:ARG:睡眠药强度 == 2 && CFLAG:ARG:恶作剧
	愤怒上升值 = 愤怒上升值 / 5
SIF TCVAR:ARG:睡眠药强度 == 3 && CFLAG:ARG:恶作剧
	愤怒上升值 = 0

;强引かな
RESULT = LIMIT(BASE:ARG:愤怒+愤怒上升值,0,1000)
RESULT:1 = 愤怒上升值

;愤怒が有顶天で愤怒フラグONするか见る 既に反抗刻印ついたら怒ってるのでプラス
SIF BASE:ARG:愤怒+愤怒上升值 > MAXBASE:ARG:愤怒 || iValue
	iValue+=1
;
;DEBUGPRINTFORML 【K17_CHECK_ANGER_COM 上升值({愤怒上升值})】
;DEBUGPRINTFORML 【K17_CHECK_ANGER_COM 愤怒合计值({BASE:ARG:愤怒+愤怒上升值})】
;DEBUGPRINTFORML 【K17_CHECK_ANGER_COM 愤怒点灯({iValue})】

RETURNF iValue

;*********************************************************************************
;	反抗刻印を上げ、后のソースチェックで口上も呼べるように
;
;	12/14	履历追加
;
;	参数0	：反抗刻印に加える值　0で心情と愤怒だけつけたりできるはず
;	参数1	：角色ID 省略で爱丽丝
;*********************************************************************************
@K17_MARK_反抗刻印(iNum=0,iID=K17C_ID)
#DIM iNum
#DIM iID
IF MARK:iID:反抗刻印 != 3 || iNum
	;书き换えあり　书き换えた时だけ口上呼ぶ
	MARK:iID:反抗刻印 = LIMIT(MARK:iID:反抗刻印+iNum,0,3)
	MARK:iID:反抗取得履历 = LIMIT(MARK:iID:反抗刻印+iNum,0,3)
	TFLAG:反抗刻印取得 = MARK:iID:反抗刻印
ENDIF
CFLAG:iID:心情不快 = MARK:iID:反抗刻印
CFLAG:iID:勃然大怒 = 1
TALENT:iID:心情 = -1
RETURN 1
;*********************************************************************************
;	文字列ハートを返す 大きい方　2付いてる方は小さい方
;
;	メモ
;SETCOLOR COLOR("PINK") UNICODE(0x2665) RESETCOLOR
;HTML_PRINT "てすと<font color='red'>&#9829;&#9825;</font>"
;これ布丁ト函数から作らんとだめだな
;
;	参数0	：数字入れるとその数だけハートを返す　省略すると1个
;	返回值	：文字列で返ってくるので %K17_HEART(1)% とか书いて使用
;*********************************************************************************
@K17_HEART(ARG=1)
#FUNCTIONS
RETURNF UNICODE(0x2764)*ARG
@K17_HEART2(ARG=1)
#FUNCTIONS
RETURNF UNICODE(0x2665)*ARG

;*********************************************************************************
;	文字列///を返す　基本3本1セット
;
;	.config　FORM中の三连记号を展开しないに引っかかるため用意
;	eramakerのFORM构文は、///や+++などといった记号が3连续で表记された场合、
;	NAME:ASSIやCALLNAME:ASSIなどの文字列に展开されます。
;	
;
;	K17_RS使用时注意 参数2を/以外で指定してやる必要あり"\\"とか
;
;	参数0	：3+指定数分返す　省略で3本1セット
;	返回值	：文字列で返ってくるので%で囲って使用
;*********************************************************************************
@K17_BLUSH(ARG=0)
#FUNCTIONS
#DIMS rTxt
#DIM DYNAMIC iCount
#DIMS CONST tmpTxt = "/"
#DIM CONST iMAX = 3
VARSET rTxt
FOR iCount,0,ARG+iMAX
	rTxt += tmpTxt
NEXT
RETURNF rTxt

;*********************************************************************************
;	roundみたいなやつ こういうのなかったっけ
;	とりあえず小数点第一位まででやってみる
;	RELATIONのフレーム广げるのが正解だと思った（小并感
;
;	参数0	：割られる数
;	参数1	：割る数
;	返回值	：四舍五入した答え
;*********************************************************************************
@K17_ROUND(ARG:0=0,ARG:1=0)
#FUNCTION
SIF ARG == 0
	RETURNF 0
SIF ARG:1 == 0
	RETURNF 0
	
SIF (((ARG:0)*10) / ARG:1) - ((ARG:0)/ARG:1)*10 >= 5
	RETURNF (ARG:0/ARG:1)+1
RETURNF ARG:0/ARG:1
;*********************************************************************************
; 概率みたいなやつ こういうのなかったっけ
;
;	参数0	：％数值で代入
;	返回值	：100％中の随机值と参数0の胜败
;*********************************************************************************
@K17_PERCENT(ARG)
#FUNCTION
RETURNF (ARG-RAND:100)>0
;*********************************************************************************
;	下限设定值の随机
;
;	参数0が下限に达していなければそのまま返す
;	参数0が下限に达したら上限内で下限～参数0の随机值
;	上限を参数0の值を上下限差まで取るやつ
;
;	参数0	：判定数值
;	参数1	：下限
;	参数2	：上限基准值
;	返回值	：判定值
;*********************************************************************************
@K17_LOWER_LIMIT_RAND(iValue,iMin=0,iMax=1)
#FUNCTION
#DIM iValue
#DIM iMin
#DIM iMax
SIF iValue <= iMin
	RETURNF iValue
RETURNF RAND(iMin,LIMIT(iValue,iMin,iMax+(iMax-iMin))+1)
;*********************************************************************************
;	上限设定值の随机
;
;	参数0が上限に达していなければそのまま返す
;	参数0が上限に达していれば下限～上限值で随机
;	上限に达していたら下限と上限幅まで差基准で当选率を同概率になるようになるやつ
;
;	参数0	：判定数值
;	参数1	：下限
;	参数2	：上限
;	返回值	：判定值
;*********************************************************************************
@K17_MAX_LIMIT_RAND(iValue,iMin=0,iMax=1)
#FUNCTION
#DIM iValue
#DIM iMin
#DIM iMax
SIF iValue <= iMin
	RETURNF iValue
SIF iValue >= iMax
	RETURNF K17_RANGE_RAND(iValue,iMax-iMin,iValue-iMin)
RETURNF RAND(iMin,LIMIT(iValue,iMin,iMax)+1)

;*********************************************************************************
;	その值に振り幅をもたせた随机值
;
;	参数0から上下限に对する随机值を返す
;
;	参数0	：判定数值
;	参数1	：下限振り幅
;	参数2	：上限振り幅
;	返回值	：判定值
;*********************************************************************************
@K17_RANGE_RAND(iValue,iMin=1,iMax=1)
#FUNCTION
#DIM iValue
#DIM iMin
#DIM iMax
RETURNF RAND(MIN(iValue,iValue-iMin),MAX(iValue,iValue+iMax)+1)

;*********************************************************************************
;	条件付きRAND
;
;	对応フラグが伪の场合再抽选するため全ての条件が伪の场合エラー
;	なんかもうちょいマシなの考えよう
;
;	参数0	：返す值
;	参数1	：条件真伪
;	返回值	：
;*********************************************************************************
@K17_SELECT_RAND(iValue:0=0,iFlag:0=0,iValue:1=0,iFlag:1=0,iValue:2=0,iFlag:2=0,iValue:3=0,iFlag:3=0,iValue:4=0,iFlag:4=0,iValue:5=0,iFlag:5=0)
#FUNCTION
#DIM CONST kMAX = 6
#DIM iValue,6
#DIM iFlag,6
#DIM TEMP

SIF MATCH(iFlag,0) == VARSIZE("iValue")
	THROW 参数不正　K17_SELECT_RAND

WHILE 1
	TEMP = RAND:(kMAX)
	SIF iFlag:TEMP
		RETURNF iValue:TEMP
WEND

;*********************************************************************************
;	eratohoЯeverseよりお借りしました　元は女神转生なのかな？　制作者样达ありがとうございます
;	ライセスフリーだったのでコピペ
;	口上内で使用のでK17ついてます
;*********************************************************************************

;*********************************************************************************
;	/で区切った文字列を随机にチョイス
;	口上文章内で稍微口吻、单语、语尾だけ变えたいとかそういうときに使用
;	余计に/入れて空白が返されるのを利用とかもできる
;	多い时はPRINTDATA　STRDATA使えばいいんじゃね
;	使用频度によりK17_RSに改名
;*********************************************************************************
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:RAND_SPLIT.ERB
;	Facility	:参数として渡されたARGSから、特定の文字列ARGS:1で区切り
;				 その中からひとつを随机で返す（概率は常に等概率）
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;	Edit	Date			Author					Reason
;	001		2011/10/17		P						新规作成
;	002		2013/02/05		ぱ。					SPLITの修正を利用したRESULT保护
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;@RAND_SPLIT
;ARGS   操作する文字列
;ARGS:1 ARGSを区切る文字列
;ARG    返す数、2个以降はRESULTS:1以降に返していく
;================================================
@K17_RS, ARGS, ARGS:1 = "/", ARG = 1
#FUNCTIONS
#LOCALSSIZE 1000
#LOCALSIZE 1
#DIM TEMP
#DIM NUM

VARSET LOCALS

SPLIT ARGS, ARGS:1 , LOCALS, NUM

FOR LOCAL, 0, ARG
	IF NUM > 0
		TEMP = RAND:NUM
		NUM--
		RESULTS:LOCAL = %LOCALS:TEMP%
		ARRAYREMOVE LOCALS, TEMP, 1
	ENDIF
NEXT
RETURNF RESULTS

;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:CHANGE_SPLIT.ERB
;	Facility	:参数として渡されたARGSから、特定の文字列ARGS:1で区切った场合のARG番目の文字列をARGS:2へと变换した文字列を返す函数@CHANGE_SPLIT
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2011/09/25		Ｎ鸟					新规作成
;	002		2013/02/05		ぱ。					SPLITの修正を利用したRESULT保护
;	003		2016/10/04		妄想エロ乙女爱丽丝		空插入されるのが气になったので空の场合飞ばしに 要素番号管理はできなくなるね
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;@CHANGE_SPLIT
;ARGS   操作する文字列
;ARGS:1 ARGSを区切る文字列
;ARG 　 ARGSを区切った后で、操作する部位
;ARGS:2 变更する
;================================================
@K17_CHANGE_SPLIT(ARGS , ARGS:1 , ARG , ARGS:2 = "")
#FUNCTIONS
#LOCALSSIZE 200
#LOCALSIZE 3
VARSET LOCALS , ""
SIF ARG == -1
	RETURNF ""

SPLIT ARGS , ARGS:1 , LOCALS, LOCAL:2
LOCALS:ARG = %ARGS:2%
FOR LOCAL , 0 , 200
	SIF LOCALS:LOCAL != ""
		LOCAL:1 = LOCAL
NEXT
SIF LOCAL:1 == 0
	RETURNF LOCALS
FOR LOCAL , 1 , 200
	SIF LOCALS:LOCAL != ""
		LOCALS += ARGS:1 + LOCALS:LOCAL
	SIF LOCAL == LOCAL:1
		BREAK
NEXT
RETURNF LOCALS

;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:AUTO_SPLIT_NUM.ERB
;	Facility	:参数として渡されたARGSから、特定の文字列ARGS:1で区切った场合の、ARGS:2と一致する番号を返す函数@AUTO_SPLIT_NUM
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2011/09/25		Ｎ鸟					新规作成
;	002		2013/02/05		ぱ。					SPLITの修正を利用したRESULT保护
;	003		2016/10/04		妄想エロ乙女爱丽丝		検索フレーム广げ SPLITの参数4を削除 検索もFINDELEMENT任せに 配列尺寸を广げた
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;@AUTO_SPLIT_NUM
;ARGS   操作する文字列
;ARGS:1 ARGSを区切る文字列
;ARG 　 ARGSを区切った后で、操作する部位。ARGS:2が指定されている场合、发见したARGS:2の场所+ARG番目の文字列を返す。
;ARGS:2 検索する文字列
;================================================
@K17_AUTO_SPLIT_NUM(ARGS , ARGS:1 , ARGS:2 = "" , ARG = 0)
#FUNCTION
#DIM TEMP
#LOCALSSIZE 500
#LOCALSIZE 1
VARSET LOCALS
SPLIT ARGS , ARGS:1 , LOCALS

LOCAL = FINDELEMENT(LOCALS , ESCAPE(ARGS:2))
IF LOCAL != -1
	SIF ARG + LOCAL < 0
		THROW @"エラー：函数@AUTO_SPLIT_NUMにて{LOCAL}番目の文字列に%ARGS:2%を发见しましたが、第三参数{ARG}の指定が不正です。"
	RETURNF (LOCAL + ARG)
ENDIF
RETURNF -1


;*********************************************************************************
;	TWに入ってる函数のカスタ​舞​ズ
;	口上内で使用ことを想定してフォント色关系を整备
;*********************************************************************************
;-------------------------------------------------
;函数名:CHOICE
;概　要:２～４择函数
;参　数:ARGS:0…质问内容
;      :ARGS:1～4…选择肢の文字列(3,4は省略可)
;返回值:ユーザ入力结果(0～3)
;选择条件の无い、朴素的选择肢作成函数
;口上とかでも使えるかもしれない
;とりあえず4择まで。增やしてもいいけど
;
;	カスタ​舞​ズ
;口上主から质问用に色变え
;稍微气になったので色一回リセットして戻す仕样に
;-------------------------------------------------
@K17_CHOICE(ARGS:0, ARGS:1, ARGS:2, ARGS:3, ARGS:4)
VARSET LOCAL
PRINTL
PRINTFORML %ARGS:0%
PRINTL
RESETCOLOR
FOR LOCAL, 0, 4
	PRINTFORMDL [{LOCAL}] - %ARGS:(1 + LOCAL)%
	SIF LOCAL && !STRLENS(ARGS:(2 + LOCAL))
		BREAK
NEXT
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0 TO LOCAL - 1
	CASEELSE
		CALL SIMATTYAUOJISAN
		CLEARLINE 1
		GOTO INPUT_LOOP
ENDSELECT
LOCAL:1=RESULT
IF FLAG:口上文本设定 > 0
	IF FLAG:口上色
		TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
	ELSE
		RESETCOLOR
	ENDIF
ELSE
	RESETCOLOR
ENDIF

RETURN LOCAL:1
RETURN RESULT
;-------------------------------------------------
;选择肢を表示し入力结果を返す函数 条件を满たさない选择肢をグレー表示にして无效にすることが可能
;参数にセリフと条件(0で无效、非0で有效)を指定。最大5个まで
;返回值は选择した选择肢の番号
;
;	カスタ​舞​ズ
;RESULTの保护を追加
;11个にまで扩张
;-------------------------------------------------
@K17_ASK_M(ARGS:0, ARG:0, ARGS:1, ARG:1, ARGS:2, ARG:2, ARGS:3, ARG:3, ARGS:4, ARG:4, ARGS:5, ARG:5,ARGS:6, ARG:6,ARGS:7, ARG:7,ARGS:8, ARG:8,ARGS:9, ARG:9,ARGS:10, ARG:10)
CURRENTREDRAW
LOCAL:0 = RESULT
LOCAL:1 = 0

REDRAW 0
RESETCOLOR
FOR LOCAL:0, 0, 11
	IF ARGS:(LOCAL:0) != ""
		IF ARG:(LOCAL:0)
			PRINTBUTTON @"{LOCAL:0, 2, RIGHT}[%ARGS:(LOCAL:0)%]", LOCAL:0
		ELSE
			SETCOLOR 0x606060
			PRINTPLAINFORM {LOCAL:0, 2, RIGHT}[%ARGS:(LOCAL:0)%]
			RESETCOLOR
		ENDIF
		PRINTL 
	ENDIF
NEXT
$INPUT_LOOP
INPUT
IF RESULT < 0 || RESULT > 10 || ARGS:RESULT == "" || !ARG:RESULT
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
REDRAW LOCAL:0
LOCAL:1=RESULT
IF FLAG:口上文本设定 > 0
	IF FLAG:口上色
		TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
	ELSE
		RESETCOLOR
	ENDIF
ELSE
	RESETCOLOR
ENDIF

RETURN LOCAL:1

;*********************************************************************************
;	スプリットを用いた文字列の随机シャッフル函数
;	%K17_SPLIT_SHUFFLE("一番/二番/三番")%→二番三番一番　みたいに随机でシャッフルされる
;
;	参数0	：入れ变えを行う全体文字列 参数1の文字で区切ったもの
;	参数1	：区切り文字　省略で/
;	返回值	：入れ替え变更を施した文字列
;*********************************************************************************
@K17_SPLIT_SHUFFLE(ARGS="",ARGS:1="/")
#FUNCTIONS
#DIM DYNAMIC iNum = 0
#LOCALSSIZE 500
VARSET LOCALS

;解体してLOCALSに格纳
SPLIT ARGS,ARGS:1,LOCALS
;解体した数を确保
iNum = RESULT
;随机に入れ替え
FOR i,0,(iNum-1)
	r = RAND(i,iNum)
	SWAP LOCALS:i,LOCALS:r
NEXT

;入れ替えが终わったら顺番にLOCALSに格纳して戻す

FOR i,1,iNum
	LOCALS:0 += LOCALS:i
NEXT

RETURNF LOCALS
;*********************************************************************************
;	その角色のいる位置のMASTER以外の人数を调べる
;
;	なんかもっとスマートなやり方ないか
;	あったCMATCH
;
;	参数0	：角色ID　省略でMASTER
;	返回值	：人数　指定角色を含む
;*********************************************************************************
@K17_GET_CHARA_POSNUM(ARG=0)
#FUNCTION
RETURNF CMATCH(CFLAG:当前位置,CFLAG:ARG:当前位置,1,CHARANUM)

;VARSET LOCAL
;FOR i,1,CHARANUM
;	SIF CFLAG:ARG:当前位置 == CFLAG:i:当前位置
;		LOCAL ++
;NEXT
;RETURNF LOCAL
;*********************************************************************************
;	随机イベント发生チェック
;
;	自慰暴露发生の判定をコピペして必要なの付け加えたやつ
;	もういっそエロ系イベント发生判定として扱う
;	指定位置で一人で暇して闷々としてると概率で发生
;	なんか巧い名前思いつかん　あとで变える予定
;
;	参数0	：角色ID　省略で爱丽丝
;	参数1	：指定位置　省略で相手の当前位置
;	参数2	：他にTARGETがいるか考虑するか　0でしない　1で一人きり限定　省略で1
;	参数3	：开运大纹を使用かどうか　省略で无
;	返回值	：发生1 开运大纹で发生2 失败0
;*********************************************************************************
@K17_CHECK_RANDOM_EVENT(ARG=K17C_ID,ARG:1=-1,ARG:2=1,ARG:3=0)
#FUNCTION
#DIM DYNAMIC iPos=0

;隐密 恶作剧 时间停止 相手が愤怒は戻す
SIF CFLAG:MASTER:隐密中 && CFLAG:MASTER:恶作剧 && FLAG:时间停止 && CFLAG:ARG:勃然大怒
	RETURNF 0

;位置初期化
iPos = ARG:1
SIF ARG:1 == -1
	iPos = CFLAG:ARG:当前位置

;指定位置か
SIF CFLAG:ARG:当前位置 != iPos
	RETURNF 0

;その位置に相手が一人きりか
IF ARG:2 == 1
	;GET_TARGETNUMは当前位置しかとれなかったでござる 实际移动してないとだめみたいね
	;SIF GET_TARGETNUM() > 1
	SIF K17_GET_CHARA_POSNUM(ARG) > 1
		RETURNF 0
ENDIF


;弱ってないか 寝てないか
SIF BASE:ARG:体力 < 1000 || CFLAG:ARG:睡眠 || TCVAR:ARG:休憩中 || CFLAG:ARG:衰弱
	RETURNF 0
;溜まってなかったら戻す
SIF CFLAG:ARG:积攒度 <= 300
	RETURNF 0
	
;DEBUGPRINTFORML 【K17_CHECK_RANDOM_EVENT({CFLAG:ARG:积攒度 + TFLAG:幸运补正 })】
;通常の自慰暴露条件コピペ
SIF RAND:(CFLAG:ARG:积攒度 + TFLAG:幸运补正 * 10) >= RAND:500
	RETURNF 1
	
SIF ITEM:兔符「开运大纹」&& ARG:3==1
	RETURNF 2
RETURNF 0
;*********************************************************************************
;	随机イベント发生チェック
;	ラッキースケベ
;
;	K17_CHECK_RANDOM_EVENTを简素化
;	单纯に概率依存　幸运补正が10% 关系性值3%が效く
;
;	参数0	：％概率　省略で50
;	参数1	：他にTARGETがいるか考虑するか　0でしない　真省略で一人きり限定
;	参数2	：角色ID　省略で爱丽丝
;	返回值	：发生1 失败0
;*********************************************************************************
@K17_LUCKY_SUKEBE(ARG=50,ARG:1=1,ARG:2=K17C_ID)
#FUNCTION
#DIM DYNAMIC iPos=0

;隐密 恶作剧 时间停止 相手が愤怒は戻す
SIF CFLAG:MASTER:隐密中 && CFLAG:MASTER:恶作剧 && FLAG:时间停止 && CFLAG:(ARG:2):勃然大怒
	RETURNF 0

;その位置に相手が一人きりか
IF ARG:1
	;GET_TARGETNUMは当前位置しかとれなかったでござる 实际移动してないとだめみたいね
	;SIF GET_TARGETNUM() > 1
	SIF K17_GET_CHARA_POSNUM(ARG:2) > 1
		RETURNF 0
ENDIF

;弱ってないか 寝てないか
SIF BASE:(ARG:2):体力 < 1000 || CFLAG:(ARG:2):睡眠 || TCVAR:(ARG:2):休憩中 || CFLAG:(ARG:2):衰弱
	RETURNF 0

;通常の自慰暴露条件コピペ
SIF K17_PERCENT(ARG +TFLAG:幸运补正*10 + K17_GRT_EX(ARG:2)*3)
	RETURNF 1
RETURNF 0

;*********************************************************************************
;	输送中の胖次検索
;
;	指定した角色IDの胖次を现在所持しているか検索する
;	全种検索、持ってる枚数を返す
;
;	参数0	：角色ID
;	返回值	：所持している枚数
;*********************************************************************************
@K17_CHECK_输送胖次(iID=K17C_ID)
#FUNCTION
#DIM iID
#DIM iPants_Type
#DIM iPants_Num

SIF !FLAG:胖次输送
	RETURNF 0

iPants_Num = 0
FOR iPants_Type,0,MAXPANTS
	SIF PANTS_TEMP:iID:iPants_Type
		iPants_Num += PANTS_TEMP:iID:iPants_Type
NEXT
RETURNF iPants_Num

;*********************************************************************************
;	服装の名前を得る
;
;	服装の见た目の名前を得る
;	SHOW～だとはだけに对して脱がした后など高度过ぎてよくわからないので
;	これは完全に见た目から一番上のものを名前で返すやつ
;	それからはだけるとか判断する用
;	见るのは服だけなのでこれで空文字なら内衣を検索すればいい
;	もしくはARG:1を立てれば服无内衣丸见せでも取れるはず
;	服あり内衣无でも见た目からはわからないはずなので外套が得られる
;	そのうちSHOWと同じように名称整理する
;
;	EQUIPを下げ検索
;
;	参数0	：角色ID　省略で爱丽丝
;	参数1	：外套がなかった时に内衣まで见るかのフラグ 省略0で见ない 真で见る
;	返回值	：服装の名前　ないと空文字列になるはず　判定で使ったり名称得たりする
;	返回值1	：RESULTに服なら2　内衣なら1　裸0
;*********************************************************************************
@K17_GET_CLOTHNAME_TOP(ARG=K17C_ID,ARG:1=0)
#FUNCTIONS	
FOR i,GETNUM(EQUIP,"和服"),GETNUM(EQUIP,"上身衣服１")-1,-1
	IF EQUIP:TARGET:i
		RESULT = 2
		RETURNF CLOTHNAME(i,EQUIP:ARG:i)
	ENDIF
NEXT
IF ARG:1
	RESULT = 1
	RETURNF K17_GET_CLOTHNAME_TOP_UNDER(ARG)
ENDIF
RESULT = 0
RETURNF
;*********************************************************************************
;	上半身内衣
;*********************************************************************************
@K17_GET_CLOTHNAME_TOP_UNDER(ARG=K17C_ID)
#FUNCTIONS	
FOR i,GETNUM(EQUIP,"连体内衣"),GETNUM(EQUIP,"上半身内衣１")-1,-1
	IF EQUIP:TARGET:i
		RETURNF CLOTHNAME(i,EQUIP:ARG:i)
	ENDIF
NEXT
RETURNF

;*********************************************************************************
;	下半身
;*********************************************************************************
@K17_GET_CLOTHNAME_BOTTOM(ARG=K17C_ID,ARG:1=0)
#FUNCTIONS
FOR i,GETNUM(EQUIP,"和服"),GETNUM(EQUIP,"裤子")-1,-1
	;飞ばすやつ
	SELECTCASE i
		CASE GETNUM(EQUIP,"上身衣服１"),GETNUM(EQUIP,"上身衣服２")
			CONTINUE
	ENDSELECT
	
	IF EQUIP:TARGET:i
		RESULT = 2
		SIF K17_GET_SKIRT(ARG) != ""
			RETURNF CLOTHNAME(i,EQUIP:ARG:i)+"の"+K17_GET_SKIRT(ARG)
		RETURNF CLOTHNAME(i,EQUIP:ARG:i)
	ENDIF
NEXT
IF ARG:1
	RESULT = 1
	RETURNF K17_GET_CLOTHNAME_TOP_UNDER(ARG)
ENDIF
RESULT = 0
RETURNF
;*********************************************************************************
;	下半身内衣
;*********************************************************************************
@K17_GET_CLOTHNAME_BOTTOM_UNDER(ARG=K17C_ID)
#FUNCTIONS
FOR i,GETNUM(EQUIP,"连体内衣"),GETNUM(EQUIP,"下半身内衣１")-1,-1
	;飞ばすやつ
	SELECTCASE i
		CASE GETNUM(EQUIP,"上半身内衣１"),GETNUM(EQUIP,"上半身内衣２")
			CONTINUE
	ENDSELECT
	IF EQUIP:TARGET:i
		SIF i == GETNUM(EQUIP,"下半身内衣２")
			RETURNF "内衣"
		RETURNF CLOTHNAME(i,EQUIP:ARG:i)
	ENDIF
NEXT
RETURNF

;*********************************************************************************
;	めくる物体の名称
;
;	参数	：角色ID
;*********************************************************************************
@K17_GET_SKIRT(ARG=K17C_ID)
#FUNCTIONS
SIF EQUIP:ARG:和服
	RETURNF "裾"
IF EQUIP:ARG:裙子
	SIF EQUIP:ARG:裙子 != 3
		RETURNF "裙子"
	RETURNF
ENDIF
;ワンピのセレクト
SELECTCASE EQUIP:ARG:连衣裙
	CASE 1,2,3,7,8,9,10
		RETURNF "裙子"
	CASE 4,5,6,11,13
		RETURNF "裾"
	CASEELSE
		RETURNF
ENDSELECT
RETURNF

;*********************************************************************************
;	爱丽丝の二人称を返す
;
;	CALLNAMEだと被るかなCNAMEに
;	整地したい……
;
;	参数0	：呼び方を知りたい角色ID　省略でMASTER
;	参数1	：MASTERの装饰をつけるか(关系によって随机)どうか　省略で无
;	返回值	：名前と敬称付き文字列で返ってくるので%で囲って使用
;*********************************************************************************
@K17_CNAME(iID=K17C_マ斯塔ID,iDecFlag=0)
#FUNCTIONS
#DIM	iID			;参数0
#DIM	iDecFlag	;参数1
#DIM	iRT			;评价关系性EXじゃない
#DIMS	cCallName	;贵方、とかの呼び名
#DIMS	cReName		;返す名前っていう意味の
#DIMS	cHon		;敬称

VARSET cReName
VARSET cCallName
VARSET cHon
;扮演マ斯塔の场合角色呼びに变更
SIF FLAG:扮演 && iID == MASTER
	iID = FLAG:扮演

;多分MASTERが一番多いので先处理
IF iID == MASTER
	;关系性基本值
	iRT = K17_GRT(K17C_ID)
	;恋人付いてたら恋人扱いで
	SIF TALENT:MASTER:恋人 == K17C_ID
		iRT = K17C_恋人
	;愤怒状态なら下げる
	SIF CFLAG:K17C_ID:勃然大怒 == 1
		iRT -= 1
	;反抗　心情ついてたらその分上下
	SIF MARK:K17C_ID:反抗刻印
		iRT -= 1
	SIF TALENT:K17C_ID:心情
		iRT += TALENT:K17C_ID:心情

	;年龄性别判定　无駄处理多いな……
	IF TALENT:MASTER:年龄 != -1
		IF TALENT:MASTER:性别 & 1
			cCallName = 您
		ELSE
			cCallName = 您
		ENDIF
	ELSEIF TALENT:MASTER:年龄 == -1
		IF TALENT:MASTER:性别 & 1
			cCallName = 小姐
		ELSE
			cCallName = 先生
		ENDIF
	ENDIF
	
	;敬称　名前你なら略
	IF CALLNAME:MASTER != "你"
		IF TALENT:MASTER:年龄 < 1
			 IF TALENT:MASTER:性别 & 1
			 	;违和感あったのでちゃん付け无で
				cHon = 
			ELSE
				cHon = 君
			ENDIF
		ELSE
			cHon = 桑
		ENDIF
	ENDIF
	
	;关系性别选择
	SELECTCASE iRT
		
		CASE IS >= K17C_恋慕
			;恋慕↑で名前　你呼び　半々
			cReName = %K17_RS(@"%CALLNAME:MASTER%/你")%
			;ハートと///付き
			SIF iDecFlag
				cReName += K17_RS(K17_HEART()+"\\"+K17_HEART2()+"\\"+K17_BLUSH(),"\\")
			RETURNF cReName
		
		CASE IS >= K17C_思慕接吻
			;名前呼び
			cReName = %CALLNAME:MASTER%
			SIF iDecFlag
				cReName += K17_RS(K17_BLUSH()+"\\"+K17_HEART2(),"\\")
			RETURNF cReName
			
		
		CASE IS >= K17C_お友达
			;名前+敬称付きと知り合いの呼び方半々
			cReName = %K17_RS(@"%CALLNAME:MASTER%%cHon%/%cCallName%")%
			
			SIF iDecFlag
				cReName += K17_BLUSH()
			RETURNF cReName
		
		CASE IS >= K17C_知り合い
			;贵方or贵女
			RETURNF cCallName
			
		CASEELSE
			;减少になった时
			RETURNF "あんた"
	ENDSELECT
	
RETURNF cReName
ELSE

;--------------------------
;	他角色の呼び方
;--------------------------
	SELECTCASE iID
		CASE K17C_ID
			;自分　自分！？　ドッペルギャンガー
			RETURNF "爱丽丝"
			
		CASE K17C_神绮ID, K17C_魅魔ID, K17C_映姫ID
			;样付け
			RETURNF CALLNAME:iID+"大人"
			
		CASE K17C_梦子ID,K17C_露易兹ID,K17C_觉ID
			;さん付け
			RETURNF CALLNAME:iID+"桑"
			
		CASEELSE
			;名前呼び舍て
			RETURNF CALLNAME:iID
	ENDSELECT
ENDIF
RETURNF "\n\n　K17_CNAME　遇到意料之外的错误　记得报告哦\n等等啊、你\n\n"

;*********************************************************************************
;	爱丽丝の友人かどうか判定する
;	おい名前……
;
;	参数0	：友人か知りたい角色ID　-1で随机に返す　マ斯塔以外
;	返回值	：友人ID（反应させたい角色なら）そのID　それ以外0
;*********************************************************************************
@K17_GET_FRIENDS(ARG=0)
#FUNCTION
#DIM DYNAMIC iTmp=0
;そのまま返す
IF ARG >= 0
	iTmp = FINDELEMENT (K17C_友人ID,ARG,0,K17C_友人人数)
	SIF iTmp>=0
		RETURNF K17C_友人ID:iTmp
	RETURNF 0

;知り合いの中から随机
ELSEIF ARG == -1

	IF !FLAG:扮演
		RETURNF K17C_友人ID:(RAND:K17C_友人人数)
	ELSE
		;扮演と同角色の场合もあるのでその对応
		WHILE 1
			iTmp = RAND:K17C_友人人数
			SIF FLAG:扮演 != K17C_友人ID:iTmp
				RETURNF K17C_友人ID:iTmp
		WEND
	ENDIF
	
ENDIF

RETURNF 0

;*********************************************************************************
;	指定した角色の位置の爱丽丝の友人IDを取得
;	严密に言うとTARGETではない
;	基本的にMASTERの位置で取ってTARGETになり得るかを见る
;	最初に见つかったものを返すので配列で优先度を变えておく
;	TARGETで取ると优先度が效かないし何度も回すよりいいかな
;
;	参数0	：调べたい角色のID　その角色がいる位置で検索する　省略でMASTER
;	返回值	：见つかった知り合いのID　见つからなかったら0
;*********************************************************************************
@K17_GET_FRIENDS_TARGET(ARG=0)
#FUNCTION

FOR i,0,K17C_友人人数
	IF CFLAG:(K17C_友人ID:i):当前位置 ==  CFLAG:ARG:当前位置
		RETURNF K17C_友人ID:i
	ENDIF
NEXT
RETURNF 0

;*********************************************************************************
;	指定した角色の位置が爱丽丝の友人の初始位置かを调べる
;
;	参数0	：调べたい角色のID　その角色がいる位置で検索する　省略でMASTER
;	返回值	：见つかった知り合いのID　见つからなかったら0
;*********************************************************************************
@K17_GET_FRIENDS_ROOM(ARG=0)
#FUNCTION
FOR i,0,K17C_友人人数
	IF CFLAG:(K17C_友人ID:i):初始位置 ==  CFLAG:ARG:当前位置
		RETURNF K17C_友人ID:i
	ENDIF
NEXT
RETURNF 0
;*********************************************************************************
;	爱丽丝以外のTARGETを寻找
;	复数いても友人优先で返す
;	普通に寻找场合最初にでてきたID
;
;	返回值	：见つかった爱丽丝以外のID　友人＞他人＞无　いなかったら0
;*********************************************************************************
@K17_GET_TARGETS_ID()
#FUNCTION
#DIM TEMP = 0

;まず友达から
TEMP = K17_GET_FRIENDS_TARGET()
IF TEMP
	RETURNF TEMP
ELSE
	;普通に见て爱丽丝以外のID寻找
	FOR i,1,CHARANUM
		IF TARGET:i != K17C_ID
			RETURNF TARGET:i
		ENDIF
	NEXT
ENDIF
RETURNF 0
;*********************************************************************************
;	现在使える人形フラグを判定して返す
;
;	参数0	：知りたい人形のID　省略0なら全部の合计 -1で使えるものから随机
;			：1(0bit)で上海　2(1bit)で蓬莱　两方なら3　后で增设してもいいように
;	返回值0	：フラグ值で返す　だめなら0 表示チェックにも使える
;	返回值1	：使える数
;*********************************************************************************
@K17_GET_DOLL(ARG=0)
#FUNCTION
#DIM DYNAMIC rFlag =0,0	;RETURNFフラグ

;そもそも表示出来なきゃ返回
SIF CFLAG:K17C_ID:K17C_CF_人形台词表示设定<0
	RETURNF 0

;必要な分だけ検索する作りに
FOR i,1,K17C_人形数
	
	;検索回避
	SIF ARG != K17C_人形:i && ARG > 0
		CONTINUE
	;検索ヒットカウントUP
	IF ARG == K17C_人形:i || ARG <= 0
		FINDCHARA CALLNAME, K17C_人形名:i
		IF RESULT == -1
			rFlag:0 += K17C_人形:i
			rFlag:1++
		ENDIF
	ENDIF
NEXT
RESULT:1 = rFlag:1
SIF ARG >= 0 || rFlag:0 == 0
	RETURNF rFlag
	
;并び优先で返すパターン
IF ARG == -2
	FOR i,1,K17C_人形数
		IF K17C_人形:i & rFlag:0
			rFlag:0 = K17C_人形:i
			BREAK
		ENDIF
	NEXT
ENDIF
;随机选择
WHILE ARG==-1
	i=RAND:K17C_人形数
	IF K17C_人形:i & rFlag:0
		rFlag:0 = K17C_人形:i
		BREAK
	ENDIF
WEND
RETURNF rFlag

;*********************************************************************************
;	现在使える人形の名前を返す
;	人形の数增やしたら扩张设计 このベタ书きはやばい
;
;	参数0	：モード选择 省略0地の文　1ならセリフで使用 使える中から随机で持ってくる
;
;	返回值	：两方使えれば　上海たち
;			：片方ならその名前
;			：两方だめなら人形たち
;*********************************************************************************
@K17_GET_DOLL_CNAME(ARG=0)
#FUNCTIONS
#DIM DYNAMIC iDollFlag=0
#DIMS DYNAMIC cDoll

;人形表现处理　上海优先で
IF !ARG
	iDollFlag=K17_GET_DOLL(0)
	IF iDollFlag & K17C_上海&&iDollFlag & K17C_蓬莱
		cDoll = 上海它们
	ELSEIF iDollFlag & K17C_上海
		cDoll = %K17C_人形名:1%
	ELSEIF iDollFlag & K17C_蓬莱
		cDoll = %K17C_人形名:2%
	ELSE
		cDoll = %K17C_人形名:0%
	ENDIF
ELSE
	iDollFlag=K17_GET_DOLL(-1)
	RETURNF K17C_人形名:FINDELEMENT(K17C_人形,iDollFlag)
ENDIF
RETURNF cDoll

;*********************************************************************************
;	口上中に人形の文本カラーで台词打ちする
;
;		一文だけ使いたいときに使用　上海or蓬莱が追加or喋らせないでスキップ
;		爱丽丝の台词を改行しないで呼ぶといい感じ 横に右揃え
;		改行されてたら左揃え
;09/24	改行しないで呼んで判定失败したときも改行して返せば安心だな
;09/25	どちらが使えるか、判定が面倒なので随机选择时は人形として扱い、使える方で台词を出す
;		两生存の场合のみ随机ありで
;		めんどくせぇ　角色がいるかの判定はまた别に作ったので随机はそっちで取って
;09/26	结局／は胜手につく仕样に
;
;	ARG:0	：人形のフラグ　1省略で上海　2で蓬莱　-1で随机
;	ARG:1	：表示したいPRINTの形式　0无　1L 2W　省略でL
;	ARGS	：表示したい文字列 基本的に右揃え　空で自分の名前呼ぶ
;	返回值	：成功したら表示した人形のフラグ值　戻されたら0
;*********************************************************************************
@K17_人形台词(ARG=1,ARG:1=1,ARGS)
#DIM DYNAMIC DollFlag=0


;仕样チェック
DollFlag = K17_GET_DOLL(ARG)
;连续で人形に喋らせているときは改行しない
SIF !DollFlag && ARG:1 == 0
	RETURN 0

;描写判定
IF CFLAG:K17C_ID:K17C_CF_人形台词表示设定<0 || !DollFlag
	;空行判定
	SIF !LINEISEMPTY()
		PRINTL
	RETURN 0
ENDIF
;文本カラー初期化
IF FLAG:口上文本设定 > 0
	IF FLAG:口上色
		SETCOLOR DollFlag==K17C_上海 ? K17C_上海COLOR#K17C_蓬莱COLOR
	ELSE
		RESETCOLOR
	ENDIF
ELSE
	RESETCOLOR
ENDIF

;入力文字判定
IF ARGS == ""
	SELECTCASE DollFlag
		CASE K17C_上海
			ARGS += "上ー海"
		CASE K17C_蓬莱
			ARGS += "蓬ー莱"
	ENDSELECT
ENDIF

;文字出力
IF LINEISEMPTY()
	PRINTFORMLC ＼%ARGS%／
ELSE
	PRINTFORMC ＼%ARGS%／
;	PRINTFORM 
;	PRINTFORM ／
ENDIF
SELECTCASE ARG:1
	CASE 1
		PRINTL
	CASE 2
		PRINTW
ENDSELECT
;结束处理
IF FLAG:口上文本设定 > 0
	IF FLAG:口上色
		TRYCALLFORM M_KOJO_COLOR_K{NO:TARGET}
	ELSE
		RESETCOLOR
	ENDIF
ELSE
	RESETCOLOR
ENDIF
RETURN DollFlag

;*********************************************************************************
;	时间带に对する挨拶
;
;	参数0	：呼びたい时间带　省略で现在时间
;	返回值	：文字列
;*********************************************************************************
@K17_GT(ARG=0)
#FUNCTIONS
VARSET LOCALS
SIF ARG==0
 ARG=TIME:2

IF ARG == 1 || ARG == 2
	LOCALS += "早上好"
ELSEIF ARG == 3 || ARG == 4
	LOCALS += "你好"
ELSEIF ARG >= 5 && ARG <= 7
	LOCALS += "晚上好"
ENDIF
RETURNF LOCALS
;*********************************************************************************
;	爱丽丝の性器の呼び方を返す
;	今のとこ随机
;	そのうち呼び方の固定フラグでも作りたい
;
;	09/23	勃起度、润滑度追加
;
;	参数0	：角色ID 省略でMASTER
;	参数1	：0または省略で参数1の角色の性别に合わせた性器を呼ぶ
;			：扶她の场合优先して扶她の竿のほうを呼ぶ
;			：实数でも指し示せる 例：性别は扶她だけど今は女性器を呼びたい时、1
;	参数2	：装饰するか　省略でしない　淫语强制とかで使えんじゃね
;			：
;	返回值	：文字列で返ってくるので%で囲って使用
;*********************************************************************************
@K17_CALL_G(ARG=0,ARG:1=0,ARG:2=0)
#FUNCTIONS
VARSET LOCALS
LOCAL=0
;性器指定を见る
IF ARG==MASTER && (ARG:1) >= 2 && TALENT:ARG:性别 == 1 && ITEM:PBAND
	;女性MASTERの穿戴式假阴茎用
	RETURNF "穿戴式假阴茎"
ELSEIF ARG:1 != 0
	LOCAL=ARG:1
	;指定したけどその角色にはその性器ついてないって场合、本来のものに变える
	SIF (LOCAL >= 2 && !(TALENT:ARG:性别 & 2) )||( LOCAL == 1 && !(TALENT:ARG:性别 & 1))
		LOCAL=TALENT:ARG:性别
ELSEIF ARG:1 == 0
	LOCAL=TALENT:ARG:性别
ENDIF


;竿があるか见る MASTERの场合のみ
IF LOCAL>=2 && ARG:2 && ARG==0

	SIF BASE:ARG:勃起 >= 1000
		LOCALS += K17_RS("勃起的/崛起的/雄起的/变大的/硬硬的")
	
	;形状装饰
	SELECTCASE TALENT:ARG:形状
		;可爱い
		CASE 1
			LOCALS += K17_RS("可爱的/可爱的/儿童/幼小/未成熟")
		;粗チン
		CASE 2
			LOCALS += K17_RS("短小/可怜的/小小的/早漏/害羞的/包茎")
		;光る！
		CASE 4
			LOCALS += K17_RS("发光/闪闪发光/害羞的")
		;长疣的
		CASE 5
			LOCALS += K17_RS("起伏不平/凹凸不平/粗粗糙糙")
		;でかい！
		CASE 6
			LOCALS += K17_RS("巨根/巨物/极太/凶恶/巨大/超大")
		;普通　なんかほめとけ
		CASEELSE
			LOCALS += K17_RS("宏伟/出色/H/雄性")
	ENDSELECT
;女性器
ELSEIF LOCAL==1 && ARG:2
	IF PALAM:ARG:润滑 >= PALAMLV:5
		LOCALS += K17_RS("水灵灵的的/粘糊糊的/发洪水的")
	ELSEIF PALAM:ARG:润滑 >= PALAMLV:3
		LOCALS += K17_RS("湿漉漉的/黏哒哒的/湿湿黏黏的")
	ENDIF
	LOCALS += K17_RS("H的/下流的/色气的/晕乎乎的/弱弱的/贪婪的")
	
;自分の扶她
ELSEIF LOCAL>=2 && ARG:2
	SIF BASE:ARG:勃起 >= 1000
		LOCALS += K17_RS("勃起的/崛起的/坚挺的/变大的")
	LOCALS += K17_RS("和小孩一样/敏感的/可爱的/害羞的/弹弹的")
ENDIF

;男性器
SIF LOCAL >= 2 && TALENT:ARG:性别 == 2
	RETURNF LOCALS+K17_RS("肉棒/欧金金")
;扶她
SIF LOCAL >= 2 && TALENT:ARG:性别 == 3
	RETURNF LOCALS+K17_RS("小豆豆/扶她")+K17_RS("肉棒/欧金金")
SIF LOCAL == 1
	RETURNF LOCALS+K17_RS("小穴/蜜穴")
	
RETURNF "\n\n　抱歉、K17_CALL_G　遇到意料之外的错误　记得报告哦\n　报告时记得说一下遇到错误之前干了些什么"


;*********************************************************************************
;	爱丽丝の感觉部位の呼び名を取り出す
;
;	喘ぎ关系で使用用　基本自分用か……なんか混乱するなーそのうち统合するか
;	こっちはどの部位かが优先
;
;	参数0	：PALAM部位番号（ABLでもPALAMでも同じか
;	参数1	：装饰するか　省略でしない　淫语强制とかで使えんじゃね
;	参数2	：角色ID　省略で爱丽丝自身
;			：
;	返回值	：文字列で返ってくるので%で囲って使用
;*********************************************************************************
@K17_CALL_P(ARG,ARG:1=0,ARG:2=K17C_ID)
#FUNCTIONS
VARSET LOCALS

SELECTCASE ARG:0
	;クリ
	CASE K17C_部位C
		;竿判定
		IF TALENT:(ARG:2):性别 >= 2
			LOCALS += K17_CALL_G((ARG:2),3,ARG:1)
		ELSE
			SIF ARG:1 && BASE:ARG:勃起 >= 1000
				LOCALS += K17_RS("勃起的/崛起的/坚挺的/变大的")
			LOCALS += K17_RS("小豆子/小豆豆/阴蒂")
		ENDIF
	;ヴァギナ
	CASE K17C_部位V
		LOCALS += K17_CALL_G((ARG:2),1,ARG:1)
	;肛门
	CASE K17C_部位A
		LOCALS += K17_RS("菊花/屁股/后面")
	;胸围
	CASE K17C_部位B
		IF ARG:1 && ARG:2!=K17C_ID && TALENT:ARG:性别!=2
			
			;相手が女の场合乳比べ
			IF TALENT:MASTER:胸围 > TALENT:ARG:胸围
				LOCALS += K17_RS("摇摇欲坠/摇摇晃晃/弹来弹去")+"的"
			ELSEIF TALENT:MASTER:胸围 < TALENT:ARG:胸围
				LOCALS += K17_RS("水嫩水嫩的/小小的/可爱的")
			ELSE
				;自分と同じ大きさでえっちとかどういうことなの
				LOCALS += K17_RS("扑鼻的/下流的/水嫩水嫩的")
			ENDIF
			
			
		ENDIF
		LOCALS += K17_RS("胸部/胸")
	;マウス
	CASE K17C_部位M_接吻
		LOCALS += K17_RS("啾/接吻/啾ー")
	
	;↓追加
	CASE K17C_部位B_乳头
		IF ARG:1
			IF EX:(ARG:2):Ｂ绝顶 * 10000 + PALAM:(ARG:2):快Ｂ + PALAM:(ARG:2):情欲 >= 30000
				LOCALS += "变的"+K17_RS("坚挺起来/硬邦邦的/硬梆梆的")
			ELSEIF EX:(ARG:2):Ｂ绝顶 * 10000 + PALAM:(ARG:2):快Ｂ + PALAM:(ARG:2):情欲 >= 10000
				LOCALS += K17_RS("勃起的/变硬了/突了起来")
			ELSEIF EX:(ARG:2):Ｂ绝顶 * 10000 + PALAM:(ARG:2):快Ｂ + PALAM:(ARG:2):情欲 >= 3000
				LOCALS += K17_RS("变硬了/鼓鼓的/充血似的")
			ENDIF
		ENDIF
		LOCALS += K17_RS("奶头/乳头")
	CASE K17C_部位M_口交
		LOCALS += K17_RS("嘴巴/口")

ENDSELECT
RETURNF LOCALS


;*********************************************************************************
;	女性器表现地の文
;
;	参数0	：角色ID　省略で爱丽丝
;	参数1	：装饰フラグ　真でつける　省略伪で无
;	返回值	：文字列で返ってくるので%で囲って使用
;*********************************************************************************
@K17_PS_TEXT(iID=K17C_ID,iDecFlag=0)
#FUNCTIONS
#DIM iID		;参数0
#DIM iDecFlag	;参数1
#DIMS cReTxt	;返回值
VARSET cReTxt

;装饰
IF iDecFlag
	SIF TALENT:K17C_ID:处女 == 1
		cReTxt = 不识男人滋味的
	
	IF PALAM:K17C_ID:润滑 > PALAMLV:4
		cReTxt += K17_RS("咕咻咕咻充分湿润的/爱蜜都淌到大腿上的")
	ELSEIF PALAM:K17C_ID:润滑 > PALAMLV:2
		cReTxt += K17_RS("黏糊糊的被爱液打湿的/湿得一塌糊涂的")
	ENDIF
ENDIF

IF TALENT:iID:体型 < 0
	cReTxt += K17_RS("幼隙/小缝")
ELSE
	cReTxt += K17_RS("秘裂/秘所")
ENDIF
RETURNF cReTxt
