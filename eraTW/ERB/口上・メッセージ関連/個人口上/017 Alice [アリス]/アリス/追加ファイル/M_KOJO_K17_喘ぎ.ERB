;*********************************************************************************
;
;
;	K17口上内で使用诶嘿嘿关联の函数群
;
;	□目次	^[ \t]*(@)	でアウトライン绞込
;
;K17_SET_AUTO_PVOICE			グローバル保存する
;K17_ABL_RT_EX					感觉に对しRT、刻印の补正をかける
;K17_AUTO_PVOICE				グローバル保存したRT,系统で部位の喘ぎ声を1行出す
;K17_AP_NUM						グローバル保存したRT,系统で部位の喘ぎ声指定回数出す
;K17_AP_DEC						グローバル保存した系统で部位の繋ぎを取り出す
;K17_部位_CONVERT				文本の识别番号からABLの识别番号へ变换
;K17_AP_FINISH					AP对応单纯绝顶呼び出し
;K17_喘ぎ声						喘ぎ系统を统括したもの
;K17_侍奉声						参数增やすのも那个だったので新设
;K17_喘ぎ声_感度				感度に对する喘ぎ声
;K17_チュパ音					细かく指定する接吻音
;K17_喘ぎ_繋ぎ					细かく指定する喘ぎ声
;K17_快乐_喘ぎ声				快乐に对する文本
;K17_喘ぎ_侍奉					乳交と手コキの文本
;K17_喘ぎ_射精					相手の射精に关する文本
;K17_喘ぎ_绝顶					绝顶文本
;---------------------
;
;K17_SEXY_CONVERT				台词に吐息的なものを追加する
;K17_H妄想既读					妄想の既读判定を行う　グローバル保存する
;K17_H妄想						爱丽丝のHな妄想を取り出す
;K17_喘ぎタイプ_H妄想			↑の喘ぎタイプを取り出す
;K17_H妄想_喘ぎ					H妄想に对応した喘ぎ集
;
;*********************************************************************************


;*********************************************************************************
;	AUTO_PVOICEに对する设定をする
;
;	参数省略で变えない 强行があるので立ってたら强制
;
;	参数0	：RT值　再びセットするまでこれで呼ばれる指令呼ぶ度にセットすればいいんじゃね
;	参数1	：喘ぎ部位　-1でフレーバーに
;	参数2	：喘ぎタイプ
;*********************************************************************************
@K17_SET_AUTO_PVOICE(ARG=-1,ARG:1=-3,ARG:2=-1)
	SIF ARG:0 != -1
		CFLAG:K17C_ID:K17C_CF_喘ぎRT = ARG:0
	SIF ARG:1 != -3
		CFLAG:K17C_ID:K17C_CF_喘ぎ部位 = ARG:1
	SIF ARG:2 != -1
		CFLAG:K17C_ID:K17C_CF_喘ぎタイプ = ARG:2
	
	;状况变化
	SIF GET_TARGETNUM() > 1 && CFLAG:MASTER:诶嘿嘿 == 1
		CFLAG:K17C_ID:K17C_CF_喘ぎタイプ = K17C_受け_お手传い系
	SIF TEQUIP:绳
		CFLAG:K17C_ID:K17C_CF_喘ぎタイプ = K17C_受け_合意袭い系
	SIF TEQUIP:拘束
		CFLAG:K17C_ID:K17C_CF_喘ぎタイプ = K17C_受け_强行系
	SIF TEQUIP:新妻PLAY
		CFLAG:K17C_ID:K17C_CF_喘ぎタイプ = K17C_受け_おねだり系
	SIF TCVAR:强行
		CFLAG:K17C_ID:K17C_CF_喘ぎタイプ = K17C_受け_强行系
	
	;书き换え时のフラグ保存 
	IF ARG:2 != -1
		;経験の保存
{
		CFLAG:K17C_ID:(K17C_CF_喘ぎタイプ选择回数 + CFLAG:K17C_ID:K17C_CF_喘ぎタイプ) =
			MIN(CFLAG:K17C_ID:(K17C_CF_喘ぎタイプ选择回数 + CFLAG:K17C_ID:K17C_CF_喘ぎタイプ)+1,10000)
}
	ENDIF
RETURN 1


;*********************************************************************************
;	感觉にRT补正をかける
;	实数值は动かさないので喘ぎの补正です
;
;	参数0	：RT值
;	参数1	：感觉部位
;	参数2	：角色ID　通常省略で使用　爱丽丝
;*********************************************************************************
@K17_ABL_RT_EX(ARG:0=0,ARG:1=0,ARG:2=K17C_ID)
#FUNCTION
#DIM TEMP

;实数值に对して上下に随机かける
;下限に反抗と苦痛
;上限に屈服と快乐
TEMP = K17_RANGE_RAND((ARG:1>0?ABL:(ARG:2):(ARG:1)#ABL:(ARG:2):欲望) ,MARK:(ARG:2):反抗刻印+MARK:(ARG:2):苦痛刻印 ,MARK:(ARG:2):屈服刻印+MARK:(ARG:2):快乐刻印)
SELECTCASE ARG
	CASE IS >= K17C_恋慕
		;部位感觉　爱でカバー
		TEMP += RAND:2
		SIF TEMP < 1
			RETURNF -1
		RETURNF TEMP
	CASE IS >= K17C_思慕
		;部位感觉　爱でカバー　まだ初心者ならフレーバーあるので使用
		SIF TEMP < 1
			RETURNF -1
		RETURNF TEMP
ENDSELECT
;思慕以下は4までに抑える
RETURNF LIMIT(TEMP , 0 ,4)
;*********************************************************************************
;	RELATION_TYPEに对応した随机喘ぎ
;
;	グローバル保存したタイプで部位に对しRT补正をかけ、喘ぎ数回+快乐喘ぎ+喘ぎ数回を呼ぶ
;	この3つをいったんパッケしてシャッフルして戻す
;	终端が面倒なんでシャッフルした后1回喘ぎを入れればおｋ
;	爱抚に对して1行泛用の喘ぎ声として使用
;
;	参数0	：相手ID　基本MASTER
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_AUTO_PVOICE(ARG=0)
#FUNCTIONS
#DIM DYNAMIC iMinRand = 0
#DIM DYNAMIC iMaxRand = 0
VARSET LOCALS
SELECTCASE CFLAG:K17C_ID:K17C_CF_喘ぎRT
	CASE IS >= K17C_恋慕
		iMinRand = 2
		iMaxRand = 4
	CASE IS >= K17C_思慕
		iMinRand = 1
		iMaxRand = 3
	CASEELSE
		iMinRand = 1
		iMaxRand = 2
ENDSELECT

LOCALS = %K17_AP_NUM( RAND(iMinRand,iMaxRand) )%\\

IF CFLAG:K17C_ID:K17C_CF_喘ぎ部位 >= 50
	;性技别
	LOCALS += K17_喘ぎ_射精(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,0,ARG)+"\\"
ELSEIF CFLAG:K17C_ID:K17C_CF_喘ぎ部位 ==  K17C_部位无
	;部位无 情欲Lvに1000挂けたもの
	LOCALS += K17_快乐_喘ぎ声(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ, GETPALAMLV(PALAM:情欲,10)*1000 , -1)+"\\"
ELSEIF CFLAG:K17C_ID:K17C_CF_喘ぎ部位 == K17C_性技_无
	;性技无
	LOCALS += K17_喘ぎ_射精(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,0,ARG)+"\\"
ELSE

	IF CFLAG:K17C_ID:K17C_CF_喘ぎタイプ & K17C_喘ぎF_攻め && !(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ & K17C_喘ぎF_袭い)
		;攻めタイプ别
		LOCALS += K17_喘ぎ_射精(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,0,ARG)+"\\"
	
	ELSEIF CFLAG:K17C_ID:K17C_CF_喘ぎ部位 != K17C_部位B_乳头 && CFLAG:K17C_ID:K17C_CF_喘ぎ部位 != K17C_部位M_口交
		;感度别
		LOCALS += K17_快乐_喘ぎ声(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ, PALAM:K17C_ID:(CFLAG:K17C_ID:K17C_CF_喘ぎ部位) , CFLAG:K17C_ID:K17C_CF_喘ぎ部位)+"\\"
	ELSE
		;口交、乳头
		IF CFLAG:K17C_ID:K17C_CF_喘ぎ部位 == K17C_部位B_乳头
			;乳头
			LOCALS += K17_快乐_喘ぎ声(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ, PALAM:(K17C_部位B) , CFLAG:K17C_ID:K17C_CF_喘ぎ部位)+"\\"
		ELSE
			;口交
			LOCALS += K17_喘ぎ_射精(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,0,ARG)+"\\"
		ENDIF
	ENDIF
ENDIF
LOCALS += K17_AP_NUM( RAND(iMinRand,iMaxRand) )

RETURNF K17_SPLIT_SHUFFLE(LOCALS,"\\")+K17_AP_NUM(1,1)
;*********************************************************************************
;	参数で指定した回数あんあん言わすやつ
;	先にSET_AP使用こと　要するに参数を减らして文本の邪魔したくない
;
;	参数0	：鸣かせる回数　省略で1回　-1で1～5の随机
;	参数1	：终端整形　省略で0しない　1でする
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_AP_NUM(ARG:0=1,ARG:1=0)
#FUNCTIONS
#DIM iSense = 0
iSense = 0

;强制フレーバー 部位がないやつ　タイプ别に见て受け攻めで变える
IF CFLAG:K17C_ID:K17C_CF_喘ぎ部位 == K17C_部位无
	RETURNF K17_喘ぎ声(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,-1,ARG,ARG:1?1#0)
ELSEIF CFLAG:K17C_ID:K17C_CF_喘ぎ部位 == K17C_性技_无
	RETURNF K17_侍奉声(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,-1,ARG,ARG:1?1#0)
ENDIF

;补正かける
iSense = K17_部位_CONVERT(CFLAG:K17C_ID:K17C_CF_喘ぎ部位)
iSense = K17_ABL_RT_EX(CFLAG:K17C_ID:K17C_CF_喘ぎRT,iSense)

;性技か部位かを见る
IF CFLAG:K17C_ID:K17C_CF_喘ぎ部位 >= 50
	IF CFLAG:K17C_ID:K17C_CF_喘ぎ部位 != K17C_性技_舌
		RETURNF K17_侍奉声(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,iSense,ARG,ARG:1?1#0)
	ELSE
		RETURNF K17_チュパ音(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ|K17C_喘ぎF_袭い,iSense,ARG,ARG:1?1#0)
	ENDIF
ENDIF
;感觉部位
IF CFLAG:K17C_ID:K17C_CF_喘ぎ部位 != K17C_部位M_接吻 && CFLAG:K17C_ID:K17C_CF_喘ぎ部位 != K17C_部位M_口交
	RETURNF K17_喘ぎ声(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,iSense,ARG,ARG:1)
ELSE
	RETURNF K17_チュパ音(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,iSense,ARG,ARG:1)
ENDIF
RETURNF
;*********************************************************************************
;	グローバル保存したタイプと部位に对する数值にRT补正をかけた繋ぎ文本を返す
;	先にSET_AP使用こと
;
;	参数0	：终端フラグ
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_AP_DEC(ARG=0)
#FUNCTIONS
RETURNF K17_喘ぎ_繋ぎ(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,K17_ABL_RT_EX(CFLAG:K17C_ID:K17C_CF_喘ぎRT,K17_部位_CONVERT(CFLAG:K17C_ID:K17C_CF_喘ぎ部位)),ARG)
;*********************************************************************************
;	文本の部位番号とABLの番号の违うものをコンバートする
;	口交と乳头だけ
;
;	参数0	：部位番号
;	返回值	：ABLなどの部位番号
;*********************************************************************************
@K17_部位_CONVERT(ARG)
#FUNCTION
SELECTCASE ARG
	CASE K17C_部位B_乳头
		RETURNF K17C_部位B
	CASE K17C_部位M_口交
		RETURNF K17C_部位M
	CASEELSE
		RETURNF ARG
ENDSELECT
RETURNF
;*********************************************************************************
;	单纯绝顶呼び出し　AP
;	ex上から数えて最初に见つけた部位の呼び出し
;	そんなに重くないはずだから绝顶部位があるかのチェックにも使って
;
;	参数0	：-1でフレーバーを呼ぶ
;	返回值	：どこかが绝顶してれば文本
;	RESULT	：最初に见つけた部位番号
;*********************************************************************************
@K17_AP_FINISH(ARG=0)
#FUNCTIONS
#DIM iEX_Num
VARSET iEX_Num

SIF ARG < 0
	RETURNF K17_喘ぎ_绝顶(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,-1)

FOR i,0,GETNUM(EX,"Ｍ绝顶")
	IF NOWEX:i
		iEX_Num = i
		BREAK
	ENDIF
NEXT
RESULT = iEX_Num
SIF !iEX_Num
	RETURNF
RETURNF K17_喘ぎ_绝顶(CFLAG:K17C_ID:K17C_CF_喘ぎタイプ,iEX_Num)

;*********************************************************************************
;	随机喘ぎ声
;
;	喘ぎ声の系统で判断して各等する声を指定回数出力　随机に决定付き
;
;	10/24	同じのが连续不会うにしてみた
;
;	参数0	：喘ぎ声系统　-1で随机　省略随机
;	参数1	：ABL感觉数值　省略-1で性骚扰时のフレーバー
;	参数2	：鸣かせる回数　省略で1回　-1で1～5の随机
;	参数3	：终端整形　省略で0しない　1でする
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_喘ぎ声(ARG:0=-1,ARG:1=-1,ARG:2=1,ARG:3=0)
#FUNCTIONS
#DIMS DYNAMIC cTEMP
#DIMS DYNAMIC cHis
#DIM DYNAMIC iAPCount
VARSET LOCALS

;各种初期化
SIF ARG:0 == -1
	ARG:0=RAND:8
SIF ARG:2 == -1
	ARG:2=(RAND:5)+1

FOR iAPCount,0,ARG:2
	WHILE 1
		cTEMP = %K17_喘ぎ声_感度(ARG,ARG:1)%
		SIF cTEMP != cHis
			BREAK
	WEND
	LOCALS += cTEMP
	cHis = %cTEMP%
	LOCALS += K17_喘ぎ_繋ぎ(ARG:0,ARG:1,iAPCount==(ARG:2)-1&&ARG:3 ? 1 # 0)
NEXT
RETURNF LOCALS

;*********************************************************************************
;	随机侍奉声
;
;	喘ぎ声の系统で判断して各等する声を指定回数出力　随机に决定付き
;
;	参数0	：喘ぎ声系统　-1で随机　省略随机
;	参数1	：ABL性技数值
;	参数2	：鸣かせる回数　省略で1回　-1で1～5の随机
;	参数3	：终端整形　省略で0しない　1でする
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_侍奉声(ARG:0=-1,ARG:1=-1,ARG:2=1,ARG:3=0)
#FUNCTIONS
#DIMS DYNAMIC cTEMP
#DIMS DYNAMIC cHis
#DIM DYNAMIC iAPCount
VARSET LOCALS

;各种初期化
SIF ARG:0 == -1
	ARG:0=RAND:8
SIF ARG:2 == -1
	ARG:2=(RAND:5)+1

FOR iAPCount,0,ARG:2
	WHILE 1
		cTEMP = %K17_喘ぎ声_侍奉(ARG,ARG:1)%
		SIF cTEMP != cHis
			BREAK
	WEND
	LOCALS += cTEMP
	cHis = %cTEMP%
	LOCALS += K17_喘ぎ_繋ぎ(ARG:0,ARG:1,iAPCount==(ARG:2)-1&&ARG:3 ? 1 # 0)
NEXT
RETURNF LOCALS
;*********************************************************************************
;	随机喘ぎ声　感度别
;
;	喘ぎ声の系统で判断して各等する声を感度别に随机取得
;	单发 合意による装饰无繋ぎは别途用意
;
;09/26	性骚扰系で使用稍微した喘ぎ声を感度-1に追加
;
;	参数0	：喘ぎ声系统
;	参数1	：ABL感觉数值
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_喘ぎ声_感度(ARG:0,ARG:1)
#FUNCTIONS
VARSET LOCALS

IF !(ARG & K17C_喘ぎF_攻め)
	IF !(ARG & K17C_喘ぎF_袭い)
		;通常の喘ぎ声
		SELECTCASE ARG:1
			;最高度
			CASE IS >= 6
				LOCALS += K17_RS("啊啊嗯/咿呀嗯/还想要/好棒/可以哦/舒服吗/呀嗯/喜欢吗/好喜欢这个/咿咿/继续多做/还要更多/呐再多来一些/哈啊哈啊")
			;いい感度
			CASE IS >= 4
				LOCALS += K17_RS("啊啊嗯/咿呀/好棒/可以哦/呀嗯/咿呜唔/喜欢/这个好喜欢/咿咿/哈唔/哈啊哈啊")
			;惯れてきた
			CASE IS >= 2
				LOCALS += K17_RS("啊啊嗯/呀嗯/咿呀/好棒/可以哦/嗯嗯/咿呜唔/哈唔")
			;初心者
			CASE IS >= 0
				LOCALS += K17_RS("啊嗯/呼/唔咕/咿呀/咿/呀嗯/可以哦/那里/哈/哈啊")
			;实数指定のフレーバー喘ぎ声
			CASE -1
				LOCALS += K17_RS("啊嗯/啊啊嗯/呀嗯/咿呀/呀嗯/啊嗯/呀嗯/好棒")
		ENDSELECT
		
	ELSEIF ARG & K17C_喘ぎF_袭い
		;受けで袭われ系
		SELECTCASE ARG:1
			;最高度
			CASE IS >= 6
				LOCALS += K17_RS("不行啊/不要啊/不要啊啊/不可以咿咿咿/咕行/说了不行/咕行咕行/不行不行/不要不要/已经不行了呜/不可以咿")
			;いい感度
			CASE IS >= 4
				LOCALS += K17_RS("不行啊/不要啊啊/不要啊/不可以咿咿/咕行/说了不行/咕行咕行/不行不行/不要不要/已经不行了呜/不可以咿")
			;惯れてきた
			CASE IS >= 2
				LOCALS += K17_RS("不行/呀嗯/不要啊/不要/别这样/咕行/说了不行/咕行咕行/不行不行/不要不要")
			;初心者
			CASE IS >= 0
				LOCALS += K17_RS("不可以/不要/不要啊/咕呜/讨厌/咕/不行/别这样/快停下")
				
			;实数指定のフレーバー喘ぎ声
			CASE -1
				LOCALS += K17_RS("不——行/呀嗯/不要啊唔/真是的/讨厌啊/不行")
		ENDSELECT
	ENDIF
ELSEIF ARG & K17C_喘ぎF_攻め
	;IF !(ARG & K17C_喘ぎF_袭い)
		;とりあえず攻め系はまだ同じでいいかな
		SELECTCASE ARG:1
			;最高度
			CASE IS >= 6
				LOCALS += K17_RS("舒服吗？/哈啊哈啊/怎么样/这个好棒/很舒服吧/可以哦/呀嗯/很喜欢吧/啊哈/兴奋起来了/还要更多/呐/你看/你看你看")
			;いい感度
			CASE IS >= 4
				LOCALS += K17_RS("舒服吗？/哈啊哈啊/怎么样/这个好棒/很舒服吧/可以哦/呀嗯/很喜欢吧/啊哈/还要更多/呐/你看")
			;惯れてきた
			CASE IS >= 2
				LOCALS += K17_RS("舒服吗？/哈啊哈啊/怎么样/这个好棒/可以哦/呀嗯/很喜欢吧/啊哈/唔咕/咿呀/咿/呀/这里/你看")
			;初心者
			CASEELSE
				LOCALS += K17_RS("舒服吗？/哈啊哈啊/怎么样/唔咕/咿呀/咿/呀嗯/好棒/这里")
		ENDSELECT
	;ELSEIF ARG & K17C_喘ぎF_袭い
	
	
	;ENDIF
ENDIF
RETURNF LOCALS



;*********************************************************************************
;	随机チュパ音（チュパ音
;
;	感觉に对する反应
;	まとめすぎじゃね
;
;	参数0	：喘ぎ声系统　-1で随机　省略随机
;	参数1	：ABL感觉数值　省略-1でフレーバー
;	参数2	：鸣かせる回数　省略で1回　-1で1～5の随机
;	参数3	：终端整形　省略で0しない　1でする
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_チュパ音(ARG:0=-1,ARG:1=-1,ARG:2=1,ARG:3=0)
#FUNCTIONS
#DIM iCount
VARSET LOCALS

;各种初期化
SIF ARG:0 == -1
	ARG:0=RAND:8
;SIF ARG:1 == -1
;	ARG:1=(RAND:6)+1
SIF ARG:2 == -1
	ARG:2=(RAND:5)+1


FOR iCount,0,ARG:2

	IF !(ARG & K17C_喘ぎF_袭い)
		;普通のちゅっちゅ音
		SELECTCASE ARG:1
			;いい感度
			CASE IS >= 4
				LOCALS += K17_RS("唔啾/啾噜/呣啾/啾/噗啾/咕啾/唔啾/啾噜/呣啾/噼啾/舔/啾/啾/啾啾")
				LOCALS += K17_喘ぎ_繋ぎ(ARG:0,ARG:1,iCount==(ARG:2)-1&&ARG:3 ? 1 # 0)
				SIF !(ARG:3 && iCount==(ARG:2)-1)
					LOCALS += K17_RS("…/―/")
			;惯れてきた
			CASE IS >= 2
				LOCALS += K17_RS("唔啾/啾噜/呣啾/啾/呼啾/咕啾/噼啾/舔/啾/啾/啾啾")
				LOCALS += K17_喘ぎ_繋ぎ(ARG:0,ARG:1,iCount==(ARG:2)-1&&ARG:3 ? 1 # 0)
				SIF !(ARG:3 && iCount==(ARG:2)-1)
					LOCALS += K17_RS("…/……/―/――/")
			;初心者
			CASE IS >= 0
				LOCALS += K17_RS("哈呣/唔啾/啾/噼哟/舔/啾/啾/啾啾")
				LOCALS += K17_喘ぎ_繋ぎ(ARG:0,ARG:1,iCount==(ARG:2)-1&&ARG:3 ? 1 # 0)
				SIF !(ARG:3 && iCount==(ARG:2)-1)
					LOCALS += K17_RS("…/……/―/――")
				
			;フレーバーの接吻音で使ってディープなら↑使用
			CASE -1
				LOCALS += K17_RS("唔/哈呣/啾/唔啾")
				LOCALS += K17_喘ぎ_繋ぎ(ARG:0,ARG:1,iCount==(ARG:2)-1&&ARG:3 ? 1 # 0)
				SIF !(ARG:3 && iCount==(ARG:2)-1)
					LOCALS += K17_RS("…/―/")
		ENDSELECT
	ELSEIF ARG & K17C_喘ぎF_袭い
		;じゅぼじゅぼ系　口交もこれで代用できるかな
		SELECTCASE ARG:1
			;いい感度
			CASE IS >= 4
				LOCALS += K17_RS("唔啾/唔唔/呣啾/啾噜/啾啪/啾啵/啾啾/啾啵啾/啾啾啵/啾噜")
				LOCALS += K17_喘ぎ_繋ぎ(ARG:0,ARG:1,iCount==(ARG:2)-1&&ARG:3 ? 1 # 0)
				SIF !(ARG:3 && iCount==(ARG:2)-1)
					LOCALS += K17_RS("…/―/")
			;惯れてきた
			CASE IS >= 2
				LOCALS += K17_RS("呼咕/唔啾/唔唔/呣啾/啾噜/啾啪/啾啵/啾啾")
				LOCALS += K17_喘ぎ_繋ぎ(ARG:0,ARG:1,iCount==(ARG:2)-1&&ARG:3 ? 1 # 0)
				SIF !(ARG:3 && iCount==(ARG:2)-1)
					LOCALS += K17_RS("…/……/―/――/")
			;初心者
			CASE IS >= 0
				LOCALS += K17_RS("呼咕/唔咕/唔唔/呣咕/啾噜")
				LOCALS += K17_喘ぎ_繋ぎ(ARG:0,ARG:1,iCount==(ARG:2)-1&&ARG:3 ? 1 # 0)
				SIF !(ARG:3 && iCount==(ARG:2)-1)
					LOCALS += K17_RS("…/……/―/――")
				
				;フレーバーの接吻音で使ってディープなら↑使用
			CASE -1
				LOCALS += K17_RS("んっ/唔咕/唔唔/呣咕")
				LOCALS += K17_喘ぎ_繋ぎ(ARG:0,ARG:1,iCount==(ARG:2)-1&&ARG:3 ? 1 # 0)
				SIF !(ARG:3 && iCount==(ARG:2)-1)
					LOCALS += K17_RS("/―/…")
		ENDSELECT
	ENDIF
NEXT
RETURNF LOCALS


;*********************************************************************************
;	随机受け喘ぎ声↑の繋ぎ
;
;	感觉に对する反应の繋ぎ　喘ぎ声系统　合意で分岐
;
;	参数0	：喘ぎ声系统　合意フラグを见る
;	参数1	：ABL感觉数值
;	参数2	：终端フラグ（スペース消し 1でする　省略で0しない
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_喘ぎ_繋ぎ(ARG:0,ARG:1,ARG:2=0)
#FUNCTIONS
VARSET LOCALS

;合意あり（はーと
IF ARG:0 & K17C_喘ぎF_合意

	SELECTCASE ARG:1
		;最高度 うるさそうだなぁ
		CASE IS >= 4
			;LOCALS += K17_RS(K17_HEART(2)+"/"+K17_HEART2(2)+"/"+K17_HEART()+"/"+K17_HEART2())
			LOCALS += K17_RS(K17_HEART(2)+"/"+K17_HEART())
			SIF ARG:2!=1
				LOCALS += "　"
		;いい感度
		CASE IS >= 2
			;LOCALS += K17_RS(K17_HEART()+"\\"+K17_HEART2()+"\\"+K17_BLUSH(),"\\")
			LOCALS += K17_RS(K17_HEART2()+"\\"+K17_HEART()+"\\"+K17_BLUSH(),"\\")
			SIF ARG:2!=1
				LOCALS += "　"
		;初心者
		CASE IS >= 0
			LOCALS += K17_RS(K17_HEART2()+"\\"+K17_BLUSH()+"\\"+K17_BLUSH()+"\\…唔\\……\\…","\\")
			SIF ARG:2!=1
				LOCALS += "　"
		;フレーバー
		CASE -1
			LOCALS += K17_RS(K17_HEART2()+"\\"+K17_HEART()+"\\"+K17_BLUSH(),"\\")
			SIF ARG:2!=1
				LOCALS += "　"
	ENDSELECT

;合意无系
ELSE
	SELECTCASE ARG:1
		;最高度 うるさそうだなぁ
		CASE IS >= 4
			LOCALS += K17_RS("…！/…唔！/…！！/…唔！！")
			SIF ARG:2!=1
				LOCALS += "　"
		;いい感度
		CASE IS >= 2
			LOCALS += K17_RS("…/…唔/…！/…唔！")
			SIF ARG:2!=1
				LOCALS += "　"
		;初心者
		CASEELSE
			LOCALS += K17_RS("…/…唔/…！")
			SIF ARG:2!=1
				LOCALS += "　"
	ENDSELECT
ENDIF

RETURNF LOCALS


;*********************************************************************************
;	自分の快乐に对する喘ぎ声
;
;	部位快乐に对する声　绝顶までの数值
;	部位に合わせて使用
;	そのうち相手も作るか
;	「そろそろイッちゃう？　「おちんぽびくびくしてきた　「中に出しちゃ不行啊とか
;
;	参数0	：喘ぎ系统　袭いフラグを见る
;	参数1	：快乐数值　繋ぎは1/1000したもの
;	参数2	：自身部位 省略-1で无
;	参数3	：终端フラグ（スペース消し 1でする　省略で0しない
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_快乐_喘ぎ声(ARG:0=0,ARG:1=0,ARG:2=0)
#FUNCTIONS
VARSET LOCALS

IF ARG:2 != -1
	;部位判定
	LOCALS:1 = %K17_CALL_P(ARG:2)%
	LOCALS:1 += "唔"
ENDIF

IF !(ARG & K17C_喘ぎF_袭い) || ARG & K17C_喘ぎF_攻め
	SELECTCASE ARG:1
		;そろそろイケそう
		CASE IS >= 6000
			LOCALS += K17_RS("差不多…/已经…/快要…/")
			LOCALS += LOCALS:1
			LOCALS += K17_RS("快要去了/要去了/去了/舒服过头了/要咕行了/一抽一抽地要来了/要绝顶了")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,(ARG:1)/1000,ARG:3)
		;感じてきた
		CASE IS >= 3000
			LOCALS += LOCALS:1
			LOCALS += K17_RS("感觉要来了/还要更多/给我更多/可以哦/把我填满吧/感觉到了")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,(ARG:1)/1000,ARG:3)
			
		CASEELSE
			LOCALS += LOCALS:1
			LOCALS += K17_RS("…啊呼/…更多/…啊啊嗯/…那个…做吧/还…唔咕/…可以…哦")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,(ARG:1)/1000,ARG:3)
	ENDSELECT
ELSE
	SELECTCASE ARG:1
		;そろそろイかされそう
		CASE IS >= 6000
			LOCALS += K17_RS("呀啊/已经/差不多要/")
			LOCALS += LOCALS:1
			LOCALS += K17_RS("要被弄到去了/去了/被弄到去了//舒服过头了/要咕行了/一抽一抽地要来了/要绝顶了")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,(ARG:1)/1000,ARG:3)
		
		;感じてきた
		CASE IS >= 3000
			LOCALS += LOCALS:1
			LOCALS += K17_RS("不行/还做…吗/不行不行/咕行")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,(ARG:1)/1000,ARG:3)
		
		CASEELSE
			LOCALS += LOCALS:1
			LOCALS += K17_RS("…咕啊/…说了不行/…呀嗯/…不要/…不要嘛")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,(ARG:1)/1000,ARG:3)
	ENDSELECT
ENDIF
RETURNF LOCALS

;*********************************************************************************
;	侍奉の喘ぎ声
;
;	手コキと乳交时の吐息
;	射精と组み合わせる　受けでも侍奉は攻め系になる
;
;	参数0	：喘ぎ系统　袭いフラグを见る
;	参数1	：性技の数值　ABL50～55　基本爱丽丝は最初から指が凶恶
;	参数2	：终端フラグ（スペース消し 1でする　省略で0しない
;*********************************************************************************
@K17_喘ぎ声_侍奉(ARG,ARG:1=0,ARG:2=0)
#FUNCTIONS
VARSET LOCALS
IF !(ARG & K17C_喘ぎF_袭い) || ARG & K17C_喘ぎF_攻め
	;通常の喘ぎ声
	SELECTCASE ARG:1
		;最高度
		CASE IS >= 6
			LOCALS += K17_RS("舒服吗？/哈啊哈啊/怎么样/很棒吧/喜欢对吧/啊哈/兴奋起来了/呐/你看啊/你看你看")
		;いい感度
		CASE IS >= 4
			LOCALS += K17_RS("舒服吗？/哈啊哈啊/怎么样/很棒吧/喜欢对吧/啊哈/你看啊")
		;惯れてきた
		CASE IS >= 2
			LOCALS += K17_RS("舒服吗？/哈啊哈啊/怎么样/喜欢对吧/啊哈/唔咕/咿呀嗯/你看啊")
		;初心者
		CASEELSE
			LOCALS += K17_RS("舒服吗？/哈啊哈啊/怎么样/唔咕/咿呀嗯/嗯/呀嗯")
	ENDSELECT
	
ELSEIF ARG & K17C_喘ぎF_袭い
	;袭われ系
	SELECTCASE ARG:1
		;最高度
		CASE IS >= 6
			LOCALS += K17_RS("咕/不行~/咿呀啊啊/不要啊啊/不可以咿咿咿/咕行/说了不行/咕行咕行/不行不行/不要不要/已经不行了呜/不可以咿")
		;いい感度
		CASE IS >= 4
			LOCALS += K17_RS("咕/不行~/咿呀啊啊/不要啊/不可以咿咿/咕行/说了不行/咕行咕行/不行不行/不要不要/已经不行了呜/不可以咿")
		;惯れてきた
		CASE IS >= 2
			LOCALS += K17_RS("咕/不可以/呀嗯/不要啊/不要/快停下/咕行/说了不行/咕行咕行/不行不行/不要不要")
		;初心者
		CASE IS >= 0
			LOCALS += K17_RS("不可以/不要/不要啊/咕呜/讨厌/咕/不行/别这样/快停下")
			
		;实数指定のフレーバー喘ぎ声
		CASE -1
			LOCALS += K17_RS("不——行/呀嗯/不要啊唔/真是的/讨厌啊/不行")
	ENDSELECT
ENDIF
RETURNF LOCALS
;*********************************************************************************
;	相手の射精に对する喘ぎ声
;
;	射精に对する声　绝顶までの数值
;	「そろそろイッちゃう？　「おちんぽびくびくしてきた　「中に出しちゃ不行啊とか
;	侍奉系で使用
;
;	参数0	：喘ぎ系统　袭いフラグを见る
;	参数1	：终端フラグ（スペース消し 1でする　省略で0しない
;	参数2	：相手のID　基本MASTERか
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_喘ぎ_射精(ARG:0=0,ARG:1=0,ARG:2=0)
#FUNCTIONS
#DIM DYNAMIC iABLnum=0
VARSET LOCALS

;女性
SIF TALENT:(ARG:2):性别 == 1
	RETURNF

iABLnum = BASE:(ARG:2):射精
;射精确定で0になるのでこっちでみる
SIF NOWEX:(ARG:2):射精
	iABLnum = MAXBASE:(ARG:2):射精

LOCALS:1 = %K17_CALL_G(ARG:2)%

IF !(ARG & K17C_喘ぎF_袭い) || ARG & K17C_喘ぎF_攻め
;合意系
	SELECTCASE iABLnum
		CASE IS >= 10000
			LOCALS += K17_RS("快点…/真是的…/来吧…")
			LOCALS += LOCALS:1
			LOCALS += K17_RS("咻咻的出来吧/射出来吧/精子出来吧")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,iABLnum/(MAXBASE:(ARG:2):射精/10),ARG:1)
		;そろそろイケそう
		CASE IS >= 6000
			LOCALS += K17_RS("差不多…/真是的…/来吧…/")
			LOCALS += LOCALS:1
			LOCALS += K17_RS("精子要出来了么？/射出来也可以哦/要去了么？")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,iABLnum/(MAXBASE:(ARG:2):射精/10),ARG:1)
		;感じてきた
		CASE IS >= 3000
			LOCALS += LOCALS:1
			LOCALS += K17_RS("呯咕呯咕的/哔咕哔咕的/呯呯的")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,iABLnum/(MAXBASE:(ARG:2):射精/10),ARG:1)
			
		CASEELSE
			LOCALS += LOCALS:1
			LOCALS += K17_RS("越来越硬了/变得更大吧/来吧、加油哦")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,iABLnum/(MAXBASE:(ARG:2):射精/10),ARG:1)
	ENDSELECT
ELSE
;拒否系
	SELECTCASE iABLnum
		;そろそろイケそう
		CASE IS >= 6000
			LOCALS += K17_RS("呀啊/不行/不要啦/")
			LOCALS += LOCALS:1
			LOCALS += K17_RS("不要射出来/不可以射出来/不可以射精")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,iABLnum/(MAXBASE:(ARG:2):射精/10),ARG:1)
		;感じてきた
		CASE IS >= 3000
			LOCALS += LOCALS:1
			LOCALS += K17_RS("硬邦邦的、/一抖一抖的、/一抖一抖、的/一颤一颤的,")+K17_RS("不能这样/不要/不可以/不行/咕行")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,iABLnum/(MAXBASE:(ARG:2):射精/10),ARG:1)
		
		CASEELSE
			LOCALS += LOCALS:1
			LOCALS += K17_RS("…咕啊/…说了不行/…呀嗯/…不要/…不要嘛")
			LOCALS += K17_喘ぎ_繋ぎ(ARG,iABLnum/(MAXBASE:(ARG:2):射精/10),ARG:1)
	ENDSELECT
ENDIF
RETURNF LOCALS
;*********************************************************************************
;	随机喘ぎ_绝顶
;
;	部位に对応した绝顶
;
;	参数0	：系统 
;	参数1	：自身部位 省略-1で无
;	参数2	：相手の部位文字列　省略で无
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;	备考	：参数2　～で」まで入れる　%K17_CNAME()%の%K17_CALL_P()%でっ　とか、见られてっとか
;			：参数1を省いて参数2で文字列设定してもいい
;*********************************************************************************
@K17_喘ぎ_绝顶(ARG:0=0,ARG:1=-1,ARGS="")
#FUNCTIONS
VARSET LOCALS

IF ARG:1 >= 0
	;部位判定　V意外接续付きあり
	LOCALS:1 = %K17_CALL_P(ARG:1)%
	SIF ARG:1 != K17C_部位V && ARG:1 != K17C_部位C
		LOCALS:1 += K17_RS("里面/")
	LOCALS:1 += "唔"
ENDIF

IF !(ARG & K17C_喘ぎF_攻め)
	IF !(ARG & K17C_喘ぎF_袭い)
		LOCALS += "要去……"+K17_RS("去了去了/去了呜/去了啊啊啊")
		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("去了啊啊啊/去了啊啊啊啊/要去了哦哦哦")
	ELSE
		LOCALS += "不要啊……"+K17_RS("不要不要/明明不行呜/别这样")
		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("不可以咿咿咿咿咿/不行呜呜呜呜/不要啊啊啊啊啊")
	ENDIF
ELSEIF ARG & K17C_喘ぎF_攻め
	IF !(ARG & K17C_喘ぎF_袭い)
		LOCALS += "咿呀……"+K17_RS("去了去了/去了呜/被弄得去了呜呜呜")
		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("被弄得去了呜呜呜呜/去了哦哦哦/被弄得去了呜呜呜")
	ELSE
		LOCALS += "咕行……"+K17_RS("不行不行/说了咕行的/不可以哦")
		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("被弄得去了呜呜呜呜/去了哦哦哦/明明咕行哦哦哦哦哦/咕行咿咿咿咿咿")
	ENDIF
ENDIF


;统合整备计画迹地
;IF ARG:0 == 1	;おねだり系
;	IF ARG:1 == K17C_受け
;		LOCALS += "いくっ……"+K17_RS("去了去了/去了呜/去了啊啊啊")
;		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("去了啊啊啊/去了啊啊啊啊/要去了哦哦哦")
;	ELSE
;		LOCALS += "くるぅ……"+K17_RS("要来了要来了/来了啊啊/来了呢")
;		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("被弄得去了呜呜呜呜/去了哦哦哦/明明咕行哦哦哦哦哦/咕行咿咿咿咿咿")
;	ENDIF
;ELSEIF ARG:0 == 2	;合意袭われ系
;	IF ARG:1 == K17C_受け
;		LOCALS += "呀嗯……"+K17_RS("去了去了/去了呜/被弄得去了呜呜呜")
;		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("被弄得去了呜呜呜呜/去了哦哦哦/被弄得去了呜呜呜")
;	ELSE
;		LOCALS += "咕行……"+K17_RS("不行不行/说了咕行的/不可以哦")
;		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("被弄得去了呜呜呜呜/去了哦哦哦/明明咕行哦哦哦哦哦/咕行咿咿咿咿咿")
;	ENDIF
;ELSEIF ARG:0 == 3	;强行系
;	IF ARG:1 == K17C_受け
;		LOCALS += "不要啊……"+K17_RS("不要不要/明明不行呜/别这样")
;		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("不可以咿咿咿咿咿/不行呜呜呜呜/不要啊啊啊啊啊")
;	ELSE
;		LOCALS += "咕行……"+K17_RS("不行不行/说了咕行的/不可以哦")
;		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("被弄得去了呜呜呜呜/被弄得去了呜呜呜/明明咕行哦哦哦哦哦/咕行咿咿咿咿咿")
;	ENDIF
;ELSEIF ARG:0 == 4	;お手传い系
;	IF ARG:1 == K17C_受け
;		LOCALS += "きてっ……"+K17_RS("去了去了/去了呜/被弄得去了呜呜呜")
;		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("去了啊啊啊/去了啊啊啊啊/要去了哦哦哦")
;	ELSE
;		LOCALS += "くるぅ……"+K17_RS("要来了要来了/来了啊啊/来了呢")
;		LOCALS += "……"+ARGS+LOCALS:1+K17_RS("要来了哦哦哦/去了呜呜呜呜/去了哦哦哦哦")
;	ENDIF




LOCALS+="～～～～"
IF ARG:1==-1
	;各等部位がない场合随意
	IF ARG & K17C_喘ぎF_合意
		LOCALS+=K17_HEART(3)
	ELSE
		LOCALS+="！！！"
	ENDIF
ELSE
	;感度の半分の装饰くっつける
	IF ARG & K17C_喘ぎF_合意
		;LOCALS+=K17_HEART(ABL:(ARG:1)/2)
		LOCALS+=K17_HEART(LIMIT( (ABL:(ARG:1)+NOWEX:(ARG:1)) /2,1,5))
	ELSE
		LOCALS+=UNICODE(0xFF01)*(LIMIT(ABL:(ARG:1)/2,1,5))
	ENDIF
ENDIF
RETURNF LOCALS


;*********************************************************************************
;	日本语文字列に随意吐息？を混ぜる
;
;	こういうのなかったっけ随意作った美しくないっ　要改良
;	CONVERTってなんかちがくね？
;	例）今日もいい天气　→　今…日っもいぃっっいぃっっ天っ气…（はーと　みたいな感じ
;	通常台词をCONVERTして跳蛋付け徘徊とかできるな！
;
;	参数0	：加工したい文字列
;	参数1	：付け加え周期　省略で5文字周期の随机
;	参数2	：语尾オプションの文字列（ハートとか
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;*********************************************************************************
@K17_SEXY_CONVERT(ARGS,ARG=5,ARGS:1="")
#FUNCTIONS
#DIM iCount = 0
VARSET LOCALS
VARSET LOCAL
;长さ测定
x=STRLENSU(ARGS)
iCount=0
FOR i,0,x

	;一文字だけ取り出す
	SUBSTRINGU ARGS,i,1
	
	;句读点上书き
	IF RESULTS == "、" || RESULTS == "。"
		LOCALS += "…"
		SIF ARGS:1 != ""
			LOCALS += ARGS:1+"　"
			
	ELSE
		LOCALS += RESULTS
	ENDIF
	
	IF ARG!=0 && iCount == 0
		LOCAL=RAND:ARG
		iCount = ARG
	ENDIF
	
	;无视したい文字
	SIF STRCOUNT(RESULTS,"[\W『』「」っぁぃぅぇぉゃゅょ\"　\"]|\\n")
		LOCAL=1
	
	IF LOCAL == 0 && !(i==x-1)
		RESULT = 0
		SELECTCASE RESULTS
			CASE "あ"
				LOCALS += "啊嗯"
				RESULT=1
			CASE "い"
				LOCALS += "咿"
				RESULT=1
			CASE "う"
				LOCALS += "呜嗯"
				RESULT=1
			CASE "え"
				LOCALS += "唔"
				RESULT=1
			CASE "の"
				LOCALS += "哦唔"
				RESULT=1
			CASE "ん"
				LOCALS += "唔"
				RESULT=1
		ENDSELECT
		SIF STRCOUNT(RESULTS,"[かさたなはまやらわがざだばぱ]")
			LOCALS += ""
		SIF STRCOUNT(RESULTS,"[きしちにひみりぎじぢびぴ]")
			LOCALS += "咿"
		SIF STRCOUNT(RESULTS,"[くすつぬふむゆるぐずづぶぷ]")
			LOCALS += ""
		SIF STRCOUNT(RESULTS,"[けせてねへめれげぜでべぺ]")
			LOCALS += ""
		SIF STRCOUNT(RESULTS,"[おこそとほもよろごぞどぼぽ]")
			LOCALS += "唔"
		SIF !RESULT
			LOCALS += K17_RS("…/…唔/～唔/、")
		IF ARGS:1 !="" && RAND:2
			LOCALS += ARGS:1+"　"
		ELSE
			;LOCALS += K17_RS("…/…唔/～唔/、")
		ENDIF
	ENDIF
	
	SIF iCount!=0
		iCount--
	LOCAL--
	;终端
	SIF ARGS:1!="" && i==x-1
		LOCALS += ARGS:1*2
NEXT

RETURNF LOCALS

;*********************************************************************************
;	妄想の既读判定をする
;
;	参数0	：判定したい妄想种类の番号　决めてから呼ぶ
;	参数1	：判定したい妄想の番号　决めてから呼ぶ
;	参数2	：既读判定变更フラグ　0の场合入れられた物を保存しそのまま返す　1なら既读を见て读んでたら别のへ变更
;	返回值	：判定后既读なら未读の番号を返す　未读ならそのまま
;	备考	：K17C_CF_受け既读 + ARGでグローバル保存の番号を呼ぶ
;*********************************************************************************
@K17_H妄想既读(ARG,ARG:1=-1,ARG:2=1)
#FUNCTION
;こういうのどう书いたらいいんですかねっ 7bit目を立てて-1して6bit目までを全て立てる
#DIM CONST 既读面罩 = (1<<K17C_受け妄想数)-1,(1<<K17C_攻め妄想数)-1
#DIM iMask				;种类に合わせて实际判定する面罩の入れ物
#DIM DYNAMIC iValue = 0	;妄想の番号　お返し用


;种类选择
iMask = 既读面罩:(ARG)

iValue = ARG:1

;とりあえず入れられたものは立てる
IF ARG:2==0
;DEBUGPRINTFORML 【H妄想既读 iValue({(iValue)}) ARG:1({(ARG:1)})
	SETBIT CFLAG:K17C_ID:(K17C_CF_受け既读 + ARG),iValue
	RETURNF iValue
ENDIF

;全て既读してたらリセット
;SIF CFLAG:K17C_ID:(K17C_CF_受け既读 + ARG) - iMask == 0
;	VARSET CFLAG:K17C_ID:(K17C_CF_受け既读 + ARG)

;既读判定
IF (CFLAG:K17C_ID:(K17C_CF_受け既读 + ARG) < iMask)
	;立ってないフラグを寻找
	WHILE GETBIT(CFLAG:K17C_ID:(K17C_CF_受け既读 + ARG),iValue)
		iValue = RAND:(K17C_妄想:ARG)
	WEND
ELSE
	;全取得しらた普通に随机
	iValue = RAND:(K17C_妄想:ARG)
ENDIF
;保存フラグ那个ば既读保存して返す
SIF ARG:2
	SETBIT CFLAG:K17C_ID:(K17C_CF_受け既读 + ARG),iValue
RETURNF iValue


;*********************************************************************************
;	爱丽丝のHな妄想を取り出します
;
;	なるべく头恶そうな感じで书きたい
;	使い方
;	基本的に%K17_H妄想%～（设定、妄想、想像～でしちゃう）と使用、うん使いづらい书きづらい
;	就寝あとの自慰、强制自慰などで使用
;
;	10/05	后々のため参数增やして既读值持っておく
;	11/20	调教自慰に伴い返回值のRESULTに参数0と参数1を保存
;
;	参数0	：取り出す种类　1受け　2攻め　3其他シチュ　省略-1、溢れても随机
;	参数1	：指定した番号のシチュ文字列を返す　0だと随机
;	参数2	：既读变更フラグ　读んだものだったら别のに变える　省略でON　全部读んだら随机
;	返回值	：文字列を返す PRINTFORML文の中で%で囲って使用
;	RESULT	：既读保存で番号变えた场合はその番号
;*********************************************************************************
@K17_H妄想(ARG:0=-1,ARG:1=-1,ARG:2=1)
#FUNCTIONS

VARSET LOCALS	;返す文字列

;妄想种类数　溢れてたら随机
SIF !INRANGE(ARG:0,0,K17C_妄想种类数-1)
	ARG:0 = RAND:K17C_妄想种类数
;妄想　溢れてたら随机
SIF !INRANGE(ARG:1,0,(K17C_妄想:(ARG:0))-1)
	ARG:1 = RAND:(K17C_妄想:(ARG:0))
	
;既读フラグ那个ば保存　读める番号に变えてくれる
IF ARG:2
	ARG:1 = K17_H妄想既读(ARG,ARG:1,ARG:2)
ENDIF
;----------------------------------------------------------
;受け想像　7种がんばって埋める
;どんな风に+ どんなこと + されちゃう/しちゃう/される
;
;1姫受け　2わんこ受け　3袭い受　4强气受け　5健气受け　6小恶魔受け　7诱い受け
;----------------------------------------------------------
IF ARG:0 == K17C_受け
	
	;DEBUGPRINTFORML 【选择番号 {ARG:1}】
	;DEBUGPRINTFORML 【グローバル保存 {CFLAG: K17C_CF_受け既读}】
	
	SELECTCASE ARG:1
	
		;姫受け
		CASE K17C_姫受け
			LOCALS += K17_RS("就像公主一样/就像小猫一样/就像可爱的人偶一样")+"被"
			LOCALS += K17_RS("温柔珍惜地/溺爱地/娇惯地/温柔地抚摸")+"对待着\n　"
			LOCALS += "在"+K17_CNAME(MASTER)+"的面前"
			LOCALS += K17_RS("张开双腿/掀起裙子/被拉下胖次")+"、\n　"
			LOCALS += "在" + K17_CALL_G(TARGET,0,1)+"被"+\@ GETBIT(TALENT:TARGET:性别,1) ? 啪啪啪 # 库啪库啪 \@+"的同时、\n　"
			LOCALS += K17_RS("祈求着/央求着")+K17_RS("继续做下去/变得更舒服/绝顶")
			
		;わんこ受け
		CASE K17C_わんこ受け
			LOCALS += "一边不停地"+K17_RS(@"向%K17_CNAME(MASTER)%撒娇/蹭着%K17_CNAME(MASTER)%/像小狗狗一样对向%K17_CNAME(MASTER)%")+"、\n　"
			LOCALS += K17_RS("啾啾亲亲地/舔来舔去地/说着喜欢你喜欢你/说着最喜欢你了")+"、"
			LOCALS += "一边不停吻着"+K17_RS("脸上/"+K17_CALL_G(,,1)+"/胸上/嘴巴/全身上下")+"地做爱"
			
		;袭い受
		CASE K17C_袭い受け
			LOCALS += "被"
			LOCALS += K17_RS("正处于发情期的/欲火正旺的/脑子里只剩下交尾的")+K17_CNAME(MASTER)
			LOCALS += "像"+K17_RS("犬/雄兽/野兽")+"一般"
			LOCALS += K17_RS("激烈地/不断地/粗暴地")+"、\n"
			
			;MASTERに竿があるか
			IF TALENT:MASTER:性别 >= 2
				;ある场合受けであることを考虑して女性器で受けます
				LOCALS += "『我的"+K17_CALL_G(TARGET,1,1)+"、向"
				LOCALS += "比平时"+K17_RS("更坚挺的/还要大的/更健壮的/更雄状的/更兴奋的")+K17_CALL_G(MASTER,3,1)
			ELSEIF TALENT:MASTER:性别 == 1 && TALENT:TARGET:性别 == 3
				;MASTERが女性で爱丽丝が扶她の场合
				LOCALS += "『我的"+K17_CALL_G(TARGET,3,1)+"、向"
				LOCALS += "比平时"+"还要"+K17_RS("湿乎乎/湿湿滑滑/咕啾咕啾")+"的"+K17_CALL_G(MASTER,1,1)
			ELSEIF TALENT:MASTER:性别 == 1 && TALENT:TARGET:性别 == 1
				;どちらも女性の场合
				LOCALS += "『我的"+K17_CALL_G(TARGET,1,1)+"、在"
				LOCALS += K17_RS("出色的/激烈的/熟练的/爽快的/快乐的")+"调教下"
			ENDIF
			LOCALS += K17_RS("屈服了/投降了/投降得心服口服了/认输了/屈服且深刻体会了")+K17_HEART(RAND:3+1)+"』\n　"
			LOCALS += K17_RS("一边做着啊嘿颜剪刀手/一边一脸痴态的做着剪刀手/一边高潮/")+"一边被"
			LOCALS += K17_RS("被侵犯了/被强行H了/强制交尾了/无责任H了/强暴了")
			
		;强气受け
		CASE K17C_强气受け
			LOCALS += "被"+K17_CNAME(MASTER)
			LOCALS += K17_RS("紧紧绑了起来/用力按住/抓着头发/撕破了衣服/折服了")+"、"
			LOCALS += "就算"+K17_RS("很讨厌/说着不行/说着住手/挣扎着/反抗着/发出悲鸣")+"、\n　还是强行被"
			SIF TALENT:MASTER:性别 >= 2
				LOCALS += K17_RS("健壮的/雄壮的/令人舒服的")+K17_CALL_G(MASTER,3,1)
			LOCALS += "进行" + K17_RS("屈服/苦痛快乐/强制快乐")+"调教了、\n　"
			LOCALS += "被像"+K17_RS("人偶/物品/性欲处理道具/性奴隷")+"一样对待、被搞得乱七八糟"
			
		;健气受け
		CASE K17C_健气受け
			LOCALS += K17_CNAME(MASTER)
			LOCALS += "一"+K17_RS("变得色情起来/旦起了欲火/发情起来/勃起")+"就立刻"
			LOCALS += K17_RS("用手/用嘴巴/将胖次拉了下来/将我的裙子掀起")+"、\n　"
			LOCALS += K17_RS(@"直到%K17_CALL_G(MASTER,,1)%利索地/连前戏都没有、直到%K17_CALL_G(MASTER,,1)%/直到%K17_CALL_G(MASTER,,1)%胡乱地/直到%K17_CALL_G(MASTER,,1)%迅速地")
			IF TALENT:MASTER:性别 >= 2
				LOCALS += K17_RS("哔咕哔咕地去了/绝顶/精液biubiu的出来/精子射出来/射精")
			ELSE
				LOCALS += K17_RS("哔咕哔咕地去了/绝顶/抽搐着潮吹/颤抖着高潮了/潮吹")
			ENDIF
			LOCALS += "为止、\n　"+K17_RS("都被拿去当作性欲处理道具/身体都被随心所欲的玩弄着/都一直被随意着玩弄身体")
			
		;小恶魔受け
		CASE K17C_小恶魔受け
			LOCALS += "在我故意的、"
			STRDATA
				DATAFORM 掀开裙子给他看的
				DATAFORM 直接让他摸胸部的
				DATAFORM 摔倒的时候张开腿,露出胖次的
				DATAFORM 一绊倒就用胸部压上去的
				DATAFORM 假装不知道的崛起屁股的
				DATAFORM 向前弯屈露出胸谷的
				DATAFORM 穿着内衣装睡的
				DATAFORM 让他偷看换衣服的
				DATAFORM 假装喝醉黏着他的
			ENDDATA
			LOCALS += RESULTS
			LOCALS += K17_RS("诱惑/色诱/勾引")+"下、\n　"
			LOCALS += K17_RS("兴奋/发情/亢奋")+"的"+K17_CNAME(MASTER)+"、"
			LOCALS += K17_RS("像野兽一般扑了过来/喘着粗气的从后面袭了过来/把我搞的乱七八糟了/撕破了衣服顺势扑了上来")
			
		;诱い受け
		CASE K17C_诱い受け
			LOCALS += "被"+K17_CNAME(MASTER)
			STRDATA
				DATAFORM 看见了正在自慰的场景
				DATAFORM 在浴室撞倒了
				DATAFORM 把上海劫持为了人质
				DATAFORM 知道了胖次丢失的事
				DATAFORM 灌了很多酒
				DATAFORM 灌了安眠药
			ENDDATA
			LOCALS += RESULTS
			LOCALS += "、\n　就这样"+K17_RS("逐渐顺势地/无可奈何地/被抓住把柄地")
			LOCALS += K17_RS("一边悔恨/一边被侮辱")
			LOCALS += K17_RS("一边被侵犯了/一边被强行H了/一边被H了")
			
		;ここから下にCASE追加で书く
		CASEELSE
			;再抽选会场
			ARG:1 = RAND:K17C_受け妄想数
	ENDSELECT
	
ENDIF
;----------------------------------------------------------
;攻め妄想　7种がんばって埋める
;
;1年上攻め　2强气攻め　3鬼畜攻め　4わんこ攻め　5ヘタレ攻め　6女王样(俺样)攻め　7言叶攻め
;----------------------------------------------------------
IF ARG:0 == K17C_攻め

	
	SELECTCASE ARG:1
		
		;年上攻め　おねショタですねわかります
		CASE K17C_年上攻め
			LOCALS += "我将"+K17_CNAME(MASTER)+K17_RS("推倒/紧紧抱住")+"\n　"
			LOCALS += K17_RS("抚摸着的同时/宠爱着的同时/像对待小孩子一样/像疼爱小猫一样")+"、"
			LOCALS += "变成"+K17_RS("色情的/色气的/下流的")+K17_RS(CALLNAME:TARGET+"/姐姐/大姐姐/少女/小姑娘")+"、\n　"
			LOCALS += K17_RS(@"向%K17_CNAME(MASTER)%的身体教会了/向%K17_CNAME(MASTER)%的身体教育了/让%K17_CNAME(MASTER)%的身体深深体会了/让%K17_CNAME(MASTER)%的身体牢牢记住了")
			LOCALS += K17_RS("如何做舒服的事情/女人的身体的秘密/甜蜜的H方法")
			
		
		;强气攻め
		CASE K17C_强气攻め
			LOCALS += "我将"+K17_CNAME(MASTER)
			LOCALS += K17_RS("的衣服脱了下来/推到在床上/带进卧室")+"后"
			LOCALS += K17_RS(@"骑了上来/压在身下/在%K17_CNAME(MASTER)%身上跨坐着/骑在%K17_CNAME(MASTER)%上面")
			LOCALS += "\n在" + K17_CALL_G(MASTER,0,1) + "说出"
			LOCALS += "『"+CALLNAME:TARGET+K17_RS("女王大人/大人/公主殿下")
			LOCALS += K17_RS("、请饶了我吧/、已经不行了/、最喜欢了/、我爱你/、我认输了/、我投降了")+"』之前\n　就"
			LOCALS += K17_RS("强行侵犯了一番/好好玩弄了一番/彻底地侵犯了一番")
			
		
		;鬼畜攻め
		CASE K17C_鬼畜攻め
			LOCALS += K17_CNAME(MASTER)
			LOCALS += "被魔法"+K17_RS("的丝线绑住/压制着/控制着")+"拘束了起来、"
			LOCALS += K17_RS("不悦的/抵抗着的/挣扎着的")+K17_CNAME(MASTER)+"被\n　"
			;MASTERに竿があるか
			IF TALENT:MASTER:性别 >= 2 && ABL:MASTER:断袖属性
				;断袖属性が那个ば肛门を攻める
				LOCALS += "我用"+ K17_CALL_G(TARGET,3,1)+"把"+K17_RS("肛门/菊花/后穴")
			ELSEIF TALENT:MASTER:性别 >= 2
				;攻めであることを考虑してMASTERの竿を攻める
				LOCALS += "我用"+K17_RS("手/脚/胸部/腋下/"+K17_CALL_G(TARGET,1,1))+"把"+K17_CALL_G(MASTER,3,1)
			ELSEIF TALENT:MASTER:性别 == 1 && TALENT:TARGET:性别 == 3
				;MASTERが女性で爱丽丝が扶她の场合
				LOCALS += "我用"+K17_CALL_G(TARGET,3)+"、把"
				LOCALS += "变得比平时"+"还要更加"+K17_RS("湿乎乎的/湿湿滑滑/濡湿不堪")+"的"+K17_CALL_G(MASTER,1,1)
			ENDIF
			LOCALS += K17_RS("随心所欲地玩弄了/搞得乱七八糟的/强行侵犯了")
			
		
		;わんこ攻め　伊东ライフ
		CASE K17C_わんこ攻め
			IF TALENT:MASTER:性别 >= 2
				LOCALS += "主动跨上了"+K17_CNAME(MASTER)+"的"+K17_CALL_G(,,1)+"、"
			ELSE
				LOCALS += "主动摩擦着"+K17_CNAME(MASTER)+"的"+K17_CALL_G(,,1)+"、"
			ENDIF
			LOCALS += "\n『"
			LOCALS:1 += K17_RS("加油/Fight/好厉害/了不起")
			;挂け声2回の掟
			FOR LOCAL,1,2+1
				LOCALS += LOCALS:1
				LOCALS += K17_HEART(LOCAL)+"　"
			NEXT
			LOCALS += K17_HEART()+K17_CNAME(MASTER)+K17_HEART()
			LOCALS += "』\n　"
			IF LOCALS:1 == "加油" || LOCALS:1 == "Fight" 
				LOCALS += "这样"+K17_RS("应援/鼓励")
			ELSE
				LOCALS += "这样"+K17_RS("褒美/鼓励")
			ENDIF
			LOCALS += "着的同时、"
			IF TALENT:MASTER:性别 >= 2
				LOCALS += K17_RS("坐在大腿上/一边拥抱着一边")
				LOCALS += "用我的"+K17_CALL_G(TARGET,1,1)+"、\n　"
				LOCALS += K17_RS("温柔地包容着/啾啾地吸着/咕啾咕啾地吸着/整个吞了进去")+"、"
			ENDIF
			LOCALS += K17_RS("努力地H/加油地H/啪啪啪/开始H的舞蹈/让"+K17_CALL_G()+"舒服起来")+"吧"
			
		
		;ヘタレ攻め　これもうわかんねぇな
		CASE K17C_ヘタレ攻め
			LOCALS += "赤身裸体地在"+K17_CNAME(MASTER)+"面前"
			LOCALS += K17_RS("跪了下来/土下座/摆出了服从的姿势")+"\n"
			;ん？
			LOCALS += "『"+K17_RS("我什么都会听从的/我会成为你的雌奴隶的/我会成为你的性欲处理人偶的/我会成为你的性奴隷的/我会成为你的狗的/我会百依百顺的")
			LOCALS += K17_RS("请不要厌倦我/请不要抛弃我/请责罚我吧/请宠爱我吧/请喜欢上我吧")+"』\n　"
			LOCALS += "这样"+K17_RS("哭着的同时/哭着纠缠的同时/恳求的同时")+"、"
			LOCALS += K17_RS("狼吞虎咽地用/全身侍奉着/谄媚用")
			LOCALS += K17_CNAME(MASTER)+"的"+K17_RS("身体/胸/双脚/"+K17_CALL_G(MASTER,0,1))+"来H"
			
		
		;女王样(俺样)攻め　わがままな感じ　束缚か
		CASE K17C_女王样攻め
			LOCALS += "将"+K17_CNAME(MASTER)+K17_RS("拘束/监禁/软禁/束缚")+"了起来"
			LOCALS += K17_RS("强行//强硬地/随心所欲地")+"用我的"
			LOCALS += K17_RS(K17_CALL_G(TARGET,0,1)+"摩擦着/胸部蹭着/用屁股蹭着/用脚踩着")+"、\n　"
			IF TALENT:MASTER:性别 >= 2
				LOCALS += K17_CALL_G()+"兴奋地"+K17_RS("勃起了/勃起了/硬了起来/站了起来/挺立了起来")+"、"
			ELSE
				LOCALS += K17_CALL_G()+"兴奋地"+K17_RS("湿了/做好了H的准备/湿得一塌糊涂了/变得乱七八糟了")+"、"
			ENDIF
			LOCALS += "到那里"+K17_RS("就停下/停下/先等一下/就变成痛感了")+"吧、"+K17_CNAME(MASTER)+"\n　"
			LOCALS += "『"+CALLNAME:TARGET+K17_RS("是世界第一可爱的/是最美的/、要娶你为妻/、向你服从/、一生只爱你")+"！』\n　"
			LOCALS += "直到向我这么"+K17_RS("宣誓忠诚/宣言/发誓")+"之前"
			LOCALS += K17_RS("就干着急/就好好欺负你//就先干等着/就寸止")+"吧"
			
			
		;言叶攻め
		CASE K17C_言叶攻め
			LOCALS += K17_CNAME(MASTER)+"的"+K17_RS("自慰/一人H/自慰")+"被看到了、\n　"
			LOCALS += "到底在想"+K17_RS("幻想/妄想/想像/妄想")+"着多么"+K17_RS("下流的/色情的/性感的")+"我的样子、"
			LOCALS += "询问着是否把我当成了"+K17_RS("自慰材料/自慰对象/幻想对象")+"、"
			LOCALS += "\n『"
			FOR LOCAL,1,2+1
				LOCALS += K17_RS("H/变态/笨蛋/色狼")
				LOCALS += K17_HEART(LOCAL)+"　"
			NEXT
			LOCALS += K17_HEART()+K17_RS("变态桑/变态先生")+K17_HEART()
			LOCALS += "』\n　"
			LOCALS += "一边这样"+K17_RS("责骂着/责备着/在耳边低声细语着")+"一边"
			LOCALS += K17_RS("将胸部押了上来/将屁股翘了起来/将裙子掀了起来/啾啾的亲了上去")+"、\n　如果要帮忙的话、我的"
			IF TALENT:TARGET:性别 >= 2
				LOCALS += K17_CALL_G(TARGET)+"也"+K17_RS("勃起了/勃起了/硬了起来/站了起来/挺立了起来")+"、"
			ELSE
				LOCALS += K17_CALL_G(TARGET)+"也"+K17_RS("湿了/做好H的准备了/湿得一塌糊涂了/已经乱七八糟了")+"、"
			ENDIF
			LOCALS += "已经无法忍耐了\n　于是就这样"+K17_RS("妄想实现地H起来了/不由自主的H起来了/强行H起来了/顺势H起来了")
			
		CASEELSE
			;再抽选会场
			ARG:1 = RAND:K17C_攻め妄想数
	ENDSELECT
ENDIF	

RESULT:0 = ARG:0
RESULT:1 = ARG:1
RETURNF LOCALS

;
;WHILE ARG:0 == 3
;	;Cosplayやフェチなど　异常・过激なのはこっち　そのうち作る
;	SELECTCASE ARG:1
;		CASE 1
;			LOCALS += K17_RS("强迫/强行/强制的")+"、"
;			LOCALS += "在"+K17_RS("外面/可能会被他人发现的场所/镜子前/人偶们的面前")
;			LOCALS += "被"+K17_RS("强上/强迫/侵犯")+"了"
;		
;			LOCALS += "和"+K17_RS("约会/在一起/出门的时候")+"的时候"
;			LOCALS += K17_RS("变硬了/勃起/貂ト张ってる/宣示着那里的存在感/发情")+"のに气づいたら、"
;			LOCALS += "牵起手带去了"+K17_RS("小巷子/洗手间/暗处/灌木丛")
;			RETURNF
;			
;		CASEELSE
;			;再抽选会场
;			ARG:1 = RAND:妄想MAX +1
;	ENDSELECT
;WEND

RETURNF "\n\n　ごめんなさい、K17_H妄想　想定外エラーよ　报告してね\n　前は仕舞いなさいよ"


;*********************************************************************************
;	H妄想に对応する喘ぎ声の种类を取得する
;	
;	参数0	：取り出す种类　0受け　1攻め　2其他シチュ　-1省略で随机
;	参数1	：妄想番号　-1省略で随机
;	返回值	：番号ｈ参照
;*********************************************************************************
@K17_喘ぎタイプ_H妄想(ARG:0=-1,ARG:1=-1)
#FUNCTION
#DIM CONST タイプMAX = 4	;参数1の最大数定义　あとで追加する事考えて随意
#DIM iReValue				;返回值
VARSET iReValue

;取り出し方を见る　随机初期化
SIF !INRANGE(ARG:0,0,K17C_妄想种类数-1)
	ARG:0 = RAND:K17C_妄想种类数


IF ARG:0 == K17C_受け
	SIF !INRANGE(ARG:1,0,K17C_受け妄想数-1)
		ARG:1 = RAND:K17C_受け妄想数
	SELECTCASE ARG:1
		
		;おねだり系　これが通常予定
		CASE K17C_姫受け,K17C_わんこ受け
			iReValue=K17C_受け_おねだり系
			
		;合意袭われ系（激烈地されちゃう系
		CASE  K17C_袭い受け,K17C_小恶魔受け
			iReValue=K17C_受け_合意袭い系
			
		;强行系（袭われちゃう系
		CASE K17C_诱い受け,K17C_强气受け
			iReValue=K17C_受け_强行系
			
		;お手传い系　性欲处理
		CASE K17C_健气受け
			iReValue=K17C_受け_お手传い系
	ENDSELECT
	
ELSEIF ARG:0 == K17C_攻め
	SIF !INRANGE(ARG:1,0,K17C_攻め妄想数-1)
		ARG:1 = RAND:K17C_攻め妄想数
	SELECTCASE ARG:1
		;おねだり系　お愿いしてえっちしてもらう系
		CASE K17C_ヘタレ攻め,K17C_言叶攻め
			iReValue=K17C_攻め_おねだり系
		;合意攻め系　激烈地しちゃう系
		CASE  K17C_强气攻め,K17C_年上攻め
			iReValue=K17C_攻め_合意袭い系
		;强行袭い系　袭っちゃう系
		CASE K17C_鬼畜攻め,K17C_女王样攻め
			iReValue=K17C_攻め_强行系
		;お手传い系（がんばれがんばれ
		CASE K17C_わんこ攻め
			iReValue=K17C_攻め_お手传い系
	ENDSELECT
ENDIF

RETURNF iReValue

;*********************************************************************************
;	H妄想に对応した喘ぎ文本
;	调教はまた别に作る
;
;	书き方なんとかならんかったのかね……
;
;	参数0	：取り出す喘ぎ声系统
;	参数1	：おもちゃフラグ　省略0で无
;	返回值	：成否判定
;*********************************************************************************
@K17_H妄想_喘ぎ(ARG:0=0,ARG:1=0)

;仕样函数メモ
;K17_喘ぎ声(系统(ARG),感觉值,回数,终端)
;K17_喘ぎ_繋ぎ(系统(ARG),感觉值,终端)
;ここは自慰なんで感觉值は爱丽丝の思うがまま

IF !(ARG & K17C_喘ぎF_攻め)
	;受け
	IF !(ARG & K17C_喘ぎF_袭い)
		IF ARG & K17C_喘ぎF_合意
			;おねだり系
			PRINTFORML 「来吧%K17_喘ぎ_繋ぎ(ARG,4)%……%K17_喘ぎ声(ARG,0,4,1)%」
			;こっちは指主导
			IF ARG:1 == 0
				PRINTFORMDL %CALLNAME:TARGET%%K17_RS("怯生生地/唯唯诺诺地/慢慢地")%把手伸向了密壶
			ELSE
				PRINTFORMDL %CALLNAME:TARGET%%K17_RS("怯生生地/唯唯诺诺地/慢慢地")%把玩具按在了秘裂上
			ENDIF
			PRINTFORML 「%K17_喘ぎ声(ARG,3,5,1)%」
			PRINTW
			PRINTFORML 「继续……%K17_喘ぎ声(ARG,2,1)%多多地、%K17_喘ぎ声(ARG,2,1)%侵犯我……%K17_RS("可、可以哦/没、没关系哦/侵、侵犯我吧")%、%K17_喘ぎ声(ARG,2,1,1)%」
			PRINTFORML 「%K17_喘ぎ声(ARG,4,2)%多多地、%K17_RS("兴奋起来/加油/爱我")%%K17_喘ぎ_繋ぎ(ARG,4)%%K17_喘ぎ声(ARG,4,2,1)%」
		ELSE
			;お手传い系
			PRINTFORML 「要做、吗？…%K17_喘ぎ声(ARG,0,4,1)%」
			IF ARG:1 == 0
				PRINTFORMDL %CALLNAME:TARGET%%K17_RS("怯生生地/唯唯诺诺地/慢慢地")%把手伸向了密壶
			ELSE
				PRINTFORMDL %CALLNAME:TARGET%%K17_RS("怯生生地/唯唯诺诺地/慢慢地")%把玩具按在了秘裂上
			ENDIF
			PRINTFORML 「%K17_喘ぎ声(ARG,3,5,1)%」
			PRINTW
			IF TALENT:MASTER:性别 >= 2
				PRINTFORML 「快点……%K17_喘ぎ声(ARG,2,1)%结束、%K17_喘ぎ声(ARG,2,1)%快点……%K17_RS("射、射出来/射、射精/咻咻射出来")%、%K17_喘ぎ声(ARG,2,1,1)%」
				PRINTFORML 「%K17_喘ぎ声(ARG,4,2)%咕呜、%K17_RS("出来/射精/高潮")%%K17_喘ぎ_繋ぎ(ARG,4)%%K17_喘ぎ声(ARG,4,2,1)%」
			ELSE
				PRINTFORML 「快点……%K17_喘ぎ声(ARG,2,1)%结束、%K17_喘ぎ声(ARG,2,1)%快点……%K17_RS("去了/结束/绝顶")%、%K17_喘ぎ声(ARG,2,1,1)%」
				PRINTFORML 「%K17_喘ぎ声(ARG,4,2)%咕呜、%K17_RS("去了/结束/绝顶")%%K17_喘ぎ_繋ぎ(ARG,4)%%K17_喘ぎ声(ARG,4,2,1)%」
			ENDIF
		ENDIF
	ELSE
		IF ARG & K17C_喘ぎF_合意
			;合意袭い系　こっちは腰主导
			PRINTFORML 「不行%K17_喘ぎ_繋ぎ(ARG,4)%……%K17_喘ぎ声(ARG,0,4,1)%」
			IF ARG:1 == 0
				PRINTFORMDL 一边趴着一边把枕头夹在两脚之间、%K17_RS("轻飘飘地/蹭来蹭去")%的%K17_RS("不停摇晃着腰/不停晃着屁股")%
			ELSE
				PRINTFORMDL 一边趴着一边把玩具按在秘部上、%K17_RS("轻飘飘地/蹭来蹭去")%的%K17_RS("不停摇晃着腰/不停晃着屁股")%
			ENDIF
			
			PRINTFORML 「%K17_喘ぎ声(ARG,3,5,1)%」
			PRINTW
			PRINTFORML 「这、么、激烈%K17_喘ぎ_繋ぎ(ARG,4)%%K17_喘ぎ声(ARG,2,1)%……%K17_RS("得冷静下来/要加油/要坏掉了")%……%K17_喘ぎ声(ARG,2,1)%呀啊……」
			PRINTFORML 「%K17_喘ぎ声(ARG,4,1)%%K17_RS("温柔一点/慢一点/很痛啊不要")%%K17_喘ぎ_繋ぎ(ARG,4)%%K17_喘ぎ声(ARG,4,2,1)%」
		ELSE
			;强行系
			PRINTFORML 「不要啊%K17_喘ぎ_繋ぎ(ARG,4)%……%K17_喘ぎ声(ARG,0,4,1)%」
			IF ARG:1 == 0
				PRINTFORMDL 一边趴着一边把枕头夹在两脚之间、%K17_RS("轻飘飘地/蹭来蹭去")%的%K17_RS("不停摇晃着腰/不停晃着屁股")%
			ELSE
				PRINTFORMDL 一边趴着一边把玩具按在秘部上、%K17_RS("轻飘飘地/蹭来蹭去")%的%K17_RS("不停摇晃着腰/不停晃着屁股")%
			ENDIF
			PRINTFORML 「%K17_喘ぎ声(ARG,3,5,1)%」
			PRINTW
			PRINTFORML 「不要、不要这样强行%K17_喘ぎ_繋ぎ(ARG,4)%%K17_喘ぎ声(ARG,2,1)%……%K17_RS("等等/别侵犯我了/别再侵犯了")%……%K17_喘ぎ声(ARG,2,1)%求你了……」
			PRINTFORML 「%K17_喘ぎ声(ARG,4,1)%%K17_RS("侵犯哦哦/强行咿/乱来啊")%%K17_喘ぎ_繋ぎ(ARG,4)%%K17_喘ぎ声(ARG,4,2,1)%」
		ENDIF
	ENDIF
ELSE
	;攻め
	IF !(ARG & K17C_喘ぎF_袭い)
		IF ARG & K17C_喘ぎF_合意
			;お手传い系
			PRINTFORML 「做了、啊…%K17_喘ぎ声(ARG,0,4,1)%」
			IF ARG:1 == 0
				PRINTFORMDL 骑在枕头上、%K17_RS("轻飘飘地/蹭来蹭去")%的%K17_RS("不停摇晃着腰/不停晃着屁股")%
			ELSE
				PRINTFORMDL 坐着一动不动地把玩具压在秘处的同时、%K17_RS("轻飘飘地/蹭来蹭去")%的%K17_RS("不停摇晃着腰/不停晃着屁股")%
			ENDIF
			PRINTFORML 「%K17_喘ぎ声(ARG,3,5,1)%」
			PRINTW
			PRINTFORML 「来啊……%K17_喘ぎ声(ARG,2,1)%加油、%K17_喘ぎ声(ARG,2,1)%快点……%K17_RS("射、射出来/射、射精吧/咻咻地射出来")%、%K17_喘ぎ声(ARG,2,1,1)%」
			PRINTFORML 「%K17_喘ぎ声(ARG,4,1)%%K17_RS("好厉害好厉害/加油/射出来")%%K17_喘ぎ_繋ぎ(ARG,4)%%K17_喘ぎ声(ARG,4,2,1)%」
		ELSE
			;おねだり系
		ENDIF
			PRINTFORML 「求你了……」%K17_喘ぎ声(ARG,0,4,1)%」
			IF ARG:1 == 0
				PRINTFORMDL 骑在枕头上、%K17_RS("轻飘飘地/蹭来蹭去")%的%K17_RS("不停摇晃着腰/不停晃着屁股")%
			ELSE
				PRINTFORMDL 坐着一动不动地把玩具压在秘处的同时、%K17_RS("轻飘飘地/蹭来蹭去")%的%K17_RS("不停摇晃着腰/不停晃着屁股")%
			ENDIF
			PRINTFORML 「%K17_喘ぎ声(ARG,3,5,1)%」
			PRINTW
			PRINTFORML 「呐？　…%K17_喘ぎ声(ARG,2,1)%再来、%K17_喘ぎ声(ARG,2,1)%做吧……%K17_RS("呐？　呐？/求你了/很舒服不是吗？")%、%K17_喘ぎ声(ARG,2,1,1)%」
			PRINTFORML 「%K17_喘ぎ声(ARG,4,1)%%K17_RS("做嘛做嘛/来做吧/来嘛来嘛——")%%K17_喘ぎ_繋ぎ(ARG,4)%%K17_喘ぎ声(ARG,4,2,1)%」
		
	ELSE
		;合意袭い 强行
		PRINTFORML 「呼ー%K17_喘ぎ_繋ぎ(ARG,4)%……%K17_喘ぎ声(ARG,0,4,1)%」
		IF ARG:1 == 0
			PRINTFORMDL 骑在枕头上、%K17_RS("轻飘飘地/蹭来蹭去")%的%K17_RS("不停摇晃着腰/不停晃着屁股")%
		ELSE
			PRINTFORMDL 坐着一动不动地把玩具压在秘处的同时、%K17_RS("轻飘飘地/蹭来蹭去")%的%K17_RS("不停摇晃着腰/不停晃着屁股")%
		ENDIF
		PRINTFORML 「%K17_喘ぎ声(ARG,3,5,1)%」
		PRINTW
		PRINTFORML 「呵呵……%K17_喘ぎ声(ARG,2,1)%怎么了、%K17_喘ぎ声(ARG,2,1)%呐……%K17_RS("很舒服吧/很喜欢这样子吧/明明就很喜欢")%、%K17_喘ぎ声(ARG,2,1,1)%」
		PRINTFORML 「%K17_喘ぎ声(ARG,4,1)%%K17_RS("要高潮了吗？/快去了吗？/还不行哦")%%K17_喘ぎ_繋ぎ(ARG,4)%%K17_喘ぎ声(ARG,4,2,1)%」
			
	ENDIF
ENDIF

PRINTFORMDL %CALLNAME:TARGET%以平时绝对无法想象的、%K17_RS("绝不可能表现出来的/放荡的/淫乱的")%样子沉溺于自慰之中

;;绝顶处理
;PRINTFORMDW %CALLNAME:TARGET%以一副平时无法想象的不成体统的样子沉浸在自慰中
;PRINTFORML 「%K17_喘ぎ_声(6,1,ARG)%%K17_喘ぎ_快乐(ARG & K17C_喘ぎF_受攻,8000)%%K17_喘ぎ_声(6,1,ARG,1)%」
;PRINTFORML 「%K17_喘ぎ_绝顶(ARG,RG)%」

PRINTW
RETURN 0



