
;### 心形のみ别の色でPRINTするための函数 ##################
;ew6样のコードを参考にさせていただきました
;文字列中に含まれる"HPH"をホット粉色色のハートとしてPRINTし、それ以外の文字列は通常の色で普通にPRINTする
;ARGS "W"=PRINTSWで表示。"L"=PRINTSLで表示。"W"でも"L"でもなければPRINTSで表示
@K2_HPH_PRINT,对象文字列,行书式
#DIMS 对象文字列
#DIMS 行书式
#DIM 出力文字数
#DIM HPHインデクス值

DO
	HPHインデクス值 = STRFIND(对象文字列, "HPH")
	
	;对象文字列がHPHで始まらないが后ろにHPHがある场合
	IF HPHインデクス值 >= 1
		;HPHの直前まで文字表示
		LOCALS = %SUBSTRING(对象文字列, 0, HPHインデクス值)%
		PRINTS @"%LOCALS%"
		出力文字数 = STRLENS(LOCALS)
	;对象文字列がHPHで始まる场合
	ELSEIF HPHインデクス值 == 0
		LOCAL = GETCOLOR()
		SETCOLOR 0xFF69B4
		PRINTFORM %UNICODE(0x2764)%
		SETCOLOR LOCAL
		出力文字数 = 3
	;对象文字列中にもはやHPHが含まれない场合
	ELSE
		;ここで行书式による分岐
		SELECTCASE 行书式
		CASE "W"
			PRINTSW @"%对象文字列%"
		CASE "L"
			PRINTSL @"%对象文字列%"
		CASEELSE
			PRINTS @"%对象文字列%"
		ENDSELECT
		BREAK
	ENDIF

	;表示済みの文字分を削除して对象文字列を更新する
	对象文字列 = %SUBSTRING(对象文字列, 出力文字数, -1)%
	
LOOP 1


;### 行为を容认してくれるか判定用式中函数 ################
;意识のある状态で行为を怒らず受け入れていれば１を返す。そうでなければ０
;ARG=角色番号
@K2_CONSCIOUS_AND_TOLERANT(ARG)
#FUNCTION
#DIM 反抗系パラメータ上升值

SIF ARG == 0
	ARG = TARGET
	
SIF !SHIRAHU(ARG) || CFLAG:ARG:勃然大怒 || TCVAR:ARG:强行
	RETURNF 0
	
反抗系パラメータ上升值 = CUP:ARG:反感 + CUP:ARG:不快

SIF MARK:ARG:反抗刻印 == 0 && 反抗系パラメータ上升值 >= 500
	RETURNF 0
SIF MARK:ARG:反抗刻印 == 1 && 反抗系パラメータ上升值 >= 1001
	RETURNF 0
SIF MARK:ARG:反抗刻印 == 2 && 反抗系パラメータ上升值 >= 2001
	RETURNF 0
SIF MARK:ARG:反抗刻印 >= 3
	RETURNF 0

RETURNF 1


;### 大きな体格差の有无判定用式中函数 ################
;对象角色と主人との体型差によって返り值が决まる
;比较して主人が大きすぎると１、主人が非常に大きすぎると２、主人が小さすぎると－１、主人が非常に小さすぎると－２
;ARG=角色番号
@K2_EXCESSIVE_STATURE_GAP(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET

IF TALENT:MASTER:体型 <= -5
	LOCAL = -8
ELSE
	LOCAL = TALENT:MASTER:体型
ENDIF

IF TALENT:ARG:体型 <= -5
	LOCAL:1 = -8
ELSE
	LOCAL:1 = TALENT:ARG:体型
ENDIF

SELECTCASE LOCAL - LOCAL:1
CASE IS > 5
	RETURNF 2
CASE 5
	RETURNF 1
CASE -5
	RETURNF -1
CASE IS < -5
	RETURNF -2
CASEELSE
	RETURNF 0
ENDSELECT


;### 诶嘿嘿时基本状态判定用式中函数 ################
;K2_CONSCIOUS_AND_TOLERANTに无知とK2_EXCESSIVE_STATURE_GAPの条件を加えたもの
;ARG=角色番号
@K2_BASIC_CONDITION_IN_MATING(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET

SIF !K2_CONSCIOUS_AND_TOLERANT(ARG)
	RETURNF 0

SIF TALENT:ARG:无知
	RETURNF 0
	
SIF K2_EXCESSIVE_STATURE_GAP(ARG)
	RETURNF 0
	
RETURNF 1


;### 快感に积极的か判定用式中间数 ################
;返り值１は积极的、０は消极的
;ARG=角色番号
@K2_SEXUAL_FRANKNESS(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET

IF MARK:ARG:快乐刻印 >= 3 || TALENT:ARG:快感应答 >= 0 || TALENT:ARG:风骚
	RETURNF 1
ELSE
	RETURNF 0
ENDIF


;### 表现バリエーション用式中函数 ################
@K2_EXPRESSION_VARIATION(ARGS)
#FUNCTIONS

VARSET LOCALS

IF ARGS == "SPERM"
	LOCALS = %TEXTR("白浊液/精液/精液/子种汁")%

ELSEIF ARGS == "VULVA"
	LOCALS = %TEXTR("秘处/秘裂/阴门/女阴")%

ELSEIF ARGS == "ANUS"
	LOCALS = %TEXTR("尻穴/肛门/菊门")%

ELSEIF ARGS == "CROTCH"
	LOCALS = %TEXTR("股间/臀沟/股沟")%

ELSEIF ARGS == "PENIS"
	LOCALS = %TEXTR("肉棒/肉棒/肉棒/肉棒")%

ELSE
	LOCALS = ※K2_EXPRESSION_VARIATION()の文字列参数が不正※

ENDIF

RETURNF LOCALS


;### 乳房描写用式中函数 ################
;ARG=角色番号
@K2_BREASTS_DESCRIPTION(ARG)
#FUNCTIONS

SIF ARG == 0
	ARG = TARGET

SELECTCASE TALENT:ARG:胸围
CASE IS >= ( TALENT:ARG:体型 == -2 ?3 #2 )
	LOCALS = %TEXTR("快要装不下的/令人瞠目结舌的大小的/沉甸甸很有重量感的/丰满的/非常巨大的/DuangDuang的/大得下流的")%
CASE TALENT:ARG:体型 == -2 ?2 #1
	LOCALS = %TEXTR("丰满的/比较大的/丰腴的/圆润的")%
CASE TALENT:ARG:体型 == -2 ?1 #0
	LOCALS = %TEXTR("柔软的/有女性魅力的隆起的/水灵灵的")%
CASE TALENT:ARG:体型 == -2 ?0 #-1
	LOCALS = %TEXTR("适中的/小不点/比较小的")%
CASEELSE
	LOCALS = %TEXTR("单薄的/平缓的/没肉的")%
ENDSELECT
SELECTCASE TALENT:ARG:胸围
CASE IS >= ( TALENT:ARG:体型 == -2 ?3 #2 )
	LOCALS += @"%TEXTR("乳/乳房/胸部/爆乳")%"
CASE TALENT:ARG:体型 == -2 ?2 #1
	LOCALS += @"%TEXTR("乳/乳房/胸部")%"
CASE TALENT:ARG:体型 == -2 ?1 #0
	LOCALS += @"%TEXTR("乳/乳房/胸部")%"
CASE TALENT:ARG:体型 == -2 ?0 #-1
	LOCALS += @"%TEXTR("胸/乳/胸部")%"
CASEELSE
	LOCALS += @"%TEXTR("胸/胸部")%"
ENDSELECT

RETURNF LOCALS

