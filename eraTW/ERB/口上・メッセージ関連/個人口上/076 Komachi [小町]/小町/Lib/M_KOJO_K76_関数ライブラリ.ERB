;
;### 函数 ###################################
;以下の函数は「映姫样口上」内の「M_KOJO_K30_函数・お说教ライブラリ」を拝借したものです
;小町口上作者が作成したものではないのでご注意ください
;同ライブラリの利用に关しては、映姫样口上の作者样のライセンスに准じます

;拝借ここから
;==================================================

;==================================================
;MASTERの恋人がその场に居るかどうか返す式中函数
;これは力技でなんとか实装できたっぽいので、使わなくても问题ない气がする（小町口上作者）
;でも绝对こっちのがスマート
;=-1 小町が恋人 =0 元から恋人无 =1 その场に居ない =2 恋人と同席
@K76_FIND_LOVER()
#FUNCTION
SIF TALENT:MASTER:恋人 == 0
	RETURNF 0
SIF TALENT:恋人
	RETURNF -1
LOCAL = TALENT:MASTER:恋人
SIF CFLAG:LOCAL:当前位置 == CFLAG:MASTER:当前位置
	RETURNF 2
RETURNF 1
;==================================================


;==================================================
;同房间の角色を拾う式中函数。寻找顺位は[恋人]持ち→知り合い→TARGET内随机
;爱丽丝口上の函数お借りして平文化した映姫口上の函数を拝借しました。つまり丸パクり
;丸コピする以外の技术がないので先人たちの知惠に感谢をし继续しかない
@K76_FIND_AROUND()
#FUNCTION
#DIM 知り合い, 3 = 30, 43, 1
#DIM 总人数
#DIM 抽选
#LOCALSIZE 200
;小町の知り合いは3人,映姫>华扇>灵梦の顺に(胜手に)设定
;一応理由としては
;映姫→言わずもがな
;华扇→茨歌仙で络みが多かった
;灵梦→茨歌仙で灵梦がどんな人物か割と詳しく语っていた描写があった（と感じた）
;冥界组とか三月精コミックで交流あったやつおるやろ、という方もいるでしょうがとりあえず口上に关系しそうな角色优先で拨片UP
VARSET LOCAL
抽选 = 0
总人数 = GET_TARGETNUM() 
;房间に一人なら当然0
SIF 总人数 == 1
	RETURNF 0
;[恋人]持ちがいたら优先してIDを返す
SIF K76_FIND_LOVER() == 2
	RETURNF TALENT:MASTER:恋人
;知り合い优先度顺に当前位置を调べMASTERと同じならIDを返す
FOR COUNT, 0, 2
	SIF CFLAG:(知り合い:COUNT):当前位置 == CFLAG:MASTER:当前位置
		RETURNF 知り合い:COUNT
NEXT
;知り合いがいなければ同室のTARGET_ID全部を拾う
FOR COUNT,1, 总人数 + 1
	;地狱の断头台withジェロニモ状态を防ぐため口上主はスキップ
	SIF TARGET:COUNT == TARGET:0
		CONTINUE
	;MASTERとこまっちゃんの当前位置にいなければスキップ
	SIF (CFLAG:(TARGET:COUNT):当前位置 != CFLAG:MASTER:当前位置) || (CFLAG:(TARGET:COUNT):当前位置 != CFLAG:76:当前位置)
		CONTINUE
	;配列のカウントは别で取りながら代入
	LOCAL:抽选 = TARGET:COUNT
	抽选 ++
NEXT
;抽选してIDを决定
RETURNF LOCAL:(RAND(抽选))
;==================================================


;==================================================
;醉っぱらい度を数字で返す式中函数
;=0 清醒 =1 微醺 =2 醉酒 3=不省人事
@K76_DRUNK()
#FUNCTION
SIF BASE:76:酒气 <= 2
	RETURNF 0
SELECTCASE BASE:76:酒气
	;不省人事
	CASE IS > (MAXBASE:76:酒气 / 10) * 8
		RETURNF 3
	;醉酒
	CASE IS > (MAXBASE:76:酒气 / 10) * 5
		RETURNF 2
	;微醺
	CASE IS <= (MAXBASE:76:酒气 / 10) * 5
		RETURNF 1
ENDSELECT
;==================================================
;拝借ここまで

;==================================================
;本体函数を加工して自作函数に
;==================================================
;勃起状态を数字で返す式中函数
;=0 通常 =1 石更 =2 梆硬 =3 Erection!!
@K76_BOKKI()
#FUNCTION
#DIM CHARA
IF BASE:CHARA:勃起 == MAXBASE:CHARA:勃起
	RETURNF 3
ELSEIF BASE:CHARA:勃起 >= 1000
	RETURNF 2
ELSEIF BASE:CHARA:勃起 >= 500
	RETURNF 1
ELSE
	RETURNF 0
ENDIF
