
;### 心形のみ别の色でPRINTするための函数 ##################
;ew6样のコードを参考にさせていただきました
;文字列中に含まれる"HPH"をホット粉色色のハートとしてPRINTし、それ以外の文字列は通常の色で普通にPRINTする
;ARGS "W"=PRINTSWで表示。"L"=PRINTSLで表示。"W"でも"L"でもなければPRINTSで表示
@K78_HPH_PRINT,对象文字列,ARGS
#DIMS 对象文字列
#DIM 出力文字数
#DIM HPHインデクス值

DO
	HPHインデクス值 = STRFIND(对象文字列, "HPH")
	
	;对象文字列がHPHで始まらないが后ろにHPHがある场合
	IF HPHインデクス值 >= 1
		;HPHの直前まで文字表示
		LOCALS = %SUBSTRING(对象文字列, 0, HPHインデクス值)%
		PRINTS @"%LOCALS%"
		出力文字数 = STRLENS(LOCALS)
	;对象文字列がHPHで始まる场合
	ELSEIF HPHインデクス值 == 0
		LOCAL = GETCOLOR()
		SETCOLOR 0xFF69B4
		PRINTFORM %UNICODE(0x2764)%
		SETCOLOR LOCAL
		出力文字数 = 3
	;HPHがもうない场合
	ELSE
		;ここでARGSによる分岐
		SELECTCASE ARGS
		CASE "W"
			PRINTSW @"%对象文字列%"
		CASE "L"
			PRINTSL @"%对象文字列%"
		CASEELSE
			PRINTS @"%对象文字列%"
		ENDSELECT
		BREAK
	ENDIF

	;表示済みの文字分を削除して对象文字列を更新する
	对象文字列 = %SUBSTRING(对象文字列, 出力文字数, -1)%
	
LOOP 1


;### 行为を容认してくれるか判定用式中函数 ################
;意识のある状态で行为を怒らず受け入れていれば１を返す。そうでなければ０
;ARG=角色番号
@K78_CONSCIOUS_AND_TOLERANT(ARG)
#FUNCTION
#DIM 反抗系パラメータ上升值

SIF ARG == 0
	ARG = TARGET
	
SIF !SHIRAHU(ARG) || CFLAG:ARG:勃然大怒 || TCVAR:ARG:强行
	RETURNF 0
	
反抗系パラメータ上升值 = CUP:ARG:反感 + CUP:ARG:不快

SIF MARK:ARG:反抗刻印 == 0 && 反抗系パラメータ上升值 >= 500
	RETURNF 0
SIF MARK:ARG:反抗刻印 == 1 && 反抗系パラメータ上升值 >= 1001
	RETURNF 0
SIF MARK:ARG:反抗刻印 == 2 && 反抗系パラメータ上升值 >= 2001
	RETURNF 0
SIF MARK:ARG:反抗刻印 >= 3
	RETURNF 0

RETURNF 1


;### 大きな体格差の有无判定用式中函数 ################
;对象角色と主人との体型差によって返り值が决まる
;比较して主人が大きすぎると１、主人が非常に大きすぎると２、主人が小さすぎると－１、主人が非常に小さすぎると－２
;ARG=角色番号
@K78_EXCESSIVE_STATURE_GAP(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET

IF TALENT:MASTER:体型 <= -5
	LOCAL = -8
ELSE
	LOCAL = TALENT:MASTER:体型
ENDIF

IF TALENT:ARG:体型 <= -5
	LOCAL:1 = -8
ELSE
	LOCAL:1 = TALENT:ARG:体型
ENDIF

SELECTCASE LOCAL - LOCAL:1
CASE IS > 5
	RETURNF 2
CASE 5
	RETURNF 1
CASE -5
	RETURNF -1
CASE IS < -5
	RETURNF -2
CASEELSE
	RETURNF 0
ENDSELECT


;### 诶嘿嘿时基本状态判定用式中函数 ################
;K78_CONSCIOUS_AND_TOLERANTに无知とK78_EXCESSIVE_STATURE_GAPの条件を加えたもの
;ARG=角色番号
@K78_BASIC_CONDITION_IN_MATING(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET

SIF !K78_CONSCIOUS_AND_TOLERANT(ARG)
	RETURNF 0

SIF TALENT:ARG:无知
	RETURNF 0
	
SIF K78_EXCESSIVE_STATURE_GAP(ARG)
	RETURNF 0
	
RETURNF 1


;### 乳头变色判定用式中函数 ################
;乳头が变色していれば１、变色してなければ０をかえす
;ARG=角色番号
@K78_DARKENED_NIPPLES(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET

IF CFLAG:ARG:1001 == 2 && TALENT:ARG:母乳体质 > 0
	RETURNF 2
ELSEIF CFLAG:ARG:1001 && ( CFLAG:ARG:妊娠经过日数 > 60 || ( CFLAG:ARG:分娩经过日 > 0 && CFLAG:ARG:分娩经过日 < CHILD_离乳 ) )
	RETURNF 2
ELSEIF CFLAG:ARG:1001 && ( CFLAG:ARG:妊娠经过日数 > 40 || ( CFLAG:ARG:分娩经过日 > 0 && CFLAG:ARG:分娩经过日 < CHILD_词汇 ) )
	RETURNF 1
ELSE
	RETURNF 0
ENDIF


;### 乳头描写用式中函数 ################
;ARG=角色番号
@K78_NIPPLE_MODIFIER(ARG)
#FUNCTIONS

SIF ARG == 0
	ARG = TARGET

IF K78_DARKENED_NIPPLES(ARG) == 2
	LOCALS = %TEXTR("黑乎乎的/颜色很深的/发黑的/乳腺发达的/大颗粒显眼的/乳晕肥大的/下流的大乳晕/被放射状的青筋包围")%
ELSEIF K78_DARKENED_NIPPLES(ARG) == 1
	LOCALS = %TEXTR("颜色稍深/茶色的/红豆色/怀孕了/乳晕腺/乳晕很大")%
ELSE
	LOCALS = %TEXTR("粉嫩的/粉红色的/色素单薄的/淡色的/可爱的/＿")%
ENDIF

RETURNF LOCALS


;### 腋が露出しているか判定用式中函数 ################
;现在的衣装で腋が露出していれば１を返し、露出していなければ０を返す
;袖あり无どちらか微妙な衣服の场合は露出してないあつかい
;衣服が追加されたら要变更
;ARG:0=角色番号
;ARG:1 脱衣口上用。０以外にすれば变成只穿内衣的样子とき用の腋露出判定になる（半脱は腋露出に影响ないので无视）
@K78_ARMPIT_EXPOSURE(ARG:0,ARG:1)
#FUNCTION

SIF ARG:0 == 0
	ARG:0 = TARGET
	
;「变成只穿内衣的样子」で脱掉衣服なので内衣姿になる际は判定をパスする
IF ARG:1 == 0
	;连衣裙系は背带裙衣、浴衣以外不可
	SIF !GROUPMATCH( EQUIP:(ARG:0):连衣裙, 0, CLOTHES_ID("连衣裙","背带裙衣"), CLOTHES_ID("连衣裙","浴衣") )
		RETURNF 0

	;上身衣服１は无袖外套と博丽的巫女服以外不可
	SIF !GROUPMATCH( EQUIP:(ARG:0):上身衣服１, 0, CLOTHES_ID("上身衣服１","无袖外套"), CLOTHES_ID("上身衣服１","博丽的巫女服") )
		RETURNF 0

	;上身衣服２は无袖女式衬衫、守矢的巫女服、抹胸、吊带背心、吊带衫以外不可
	SIF !GROUPMATCH( EQUIP:(ARG:0):上身衣服２, 0, CLOTHES_ID("上身衣服２","无袖女式衬衫"), CLOTHES_ID("上身衣服２","守矢的巫女服"), CLOTHES_ID("上身衣服２","抹胸"), CLOTHES_ID("上身衣服２","吊带背心"), CLOTHES_ID("上身衣服２","吊带衫") )
		RETURNF 0

	;和服系は全部不可
	SIF EQUIP:(ARG:0):和服
		RETURNF 0
		
	;外衣は全部不可
	SIF EQUIP:(ARG:0):外衣
		RETURNF 0
ENDIF

;以下は「变成只穿内衣的样子」で脱がないので必ず判定する

;紧身内衣系は紧身内衣、贴身长筒袜（赤と黑）、保暖内衣、锁帷子のどれかだと不可
SIF GROUPMATCH( EQUIP:(ARG:0):紧身内衣, CLOTHES_ID("紧身内衣","紧身内衣"), CLOTHES_ID("紧身内衣","贴身长筒袜（赤）"), CLOTHES_ID("紧身内衣","贴身长筒袜（黑）"), CLOTHES_ID("紧身内衣","保暖内衣"), CLOTHES_ID("紧身内衣","锁帷子") )
	RETURNF 0
	
;连体内衣系は连体内衣以外不可
SIF !GROUPMATCH( EQUIP:(ARG:0):连体内衣, 0, CLOTHES_ID("连体内衣","连体内衣") )
	RETURNF 0

;上半身内衣２は有色衬衫のみ不可
SIF EQUIP:(ARG:0):上半身内衣２ == CLOTHES_ID("上半身内衣２","有色衬衫")
	RETURNF 0

RETURNF 1


;### 腋汗判定用式中函数 ################
;ARG=角色番号
@K78_ARMPIT_SWEAT(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET
	
IF TCVAR:ARG:媚药 > 0 || TCVAR:ARG:发情
	RETURNF 2
ELSEIF CFLAG:ARG:澡堂 >= 2500 || ( DAY:2 == 2 && CFLAG:ARG:澡堂 >= 1000 )
	RETURNF 1
ELSE
	RETURNF 0
ENDIF


;### 腋汗ジミ判定用式中函数 ################
;现在的衣装で腋汗ジミが见えるなら１を返し、见えないなら０を返す
;衣服が追加されたら要变更
;ARG=角色番号
@K78_SWEATY_UNDERARMS(ARG)
#FUNCTION

SIF ARG == 0
	ARG = TARGET
	
;腋汗がなければダメ
SIF !K78_ARMPIT_SWEAT(ARG)
	RETURNF 0

;腋が露出していたらダメ
SIF K78_ARMPIT_EXPOSURE(ARG)
	RETURNF 0
	
;外衣は全部ダメ
SIF EQUIP:ARG:外衣
	RETURNF 0
	
;连体内衣系は胶衣と机车夹克だとダメ
SIF GROUPMATCH( EQUIP:ARG:连体内衣, CLOTHES_ID("连体内衣","胶衣"), CLOTHES_ID("连体内衣","机车夹克") )
	RETURNF 0

;和服系は和服、千早、浴衣、道着、白装束だとダメ
SIF GROUPMATCH( EQUIP:ARG:和服, CLOTHES_ID("和服","和服"), CLOTHES_ID("和服","千早"), CLOTHES_ID("和服","浴衣"), CLOTHES_ID("和服","道着"), CLOTHES_ID("和服","白装束") )
	RETURNF 0
	
;上身衣服１は外套、西装上衣、水干、狩衣だとダメ
SIF GROUPMATCH( EQUIP:ARG:上身衣服１, CLOTHES_ID("上身衣服１","外套"), CLOTHES_ID("上身衣服１","西装上衣"), CLOTHES_ID("上身衣服１","水干"), CLOTHES_ID("上身衣服１","狩衣") )
	RETURNF 0

;上身衣服２は运动夹克衫、道着、小纹、长着、白小袖だとダメ
SIF GROUPMATCH( EQUIP:ARG:上身衣服２, CLOTHES_ID("上身衣服２","运动夹克衫"), CLOTHES_ID("上身衣服２","道着"), CLOTHES_ID("上身衣服２","小纹"), CLOTHES_ID("上身衣服２","长着"), CLOTHES_ID("上身衣服２","白小袖") )
	RETURNF 0

;紧身内衣系は锁帷子のみダメ
SIF EQUIP:ARG:紧身内衣 == CLOTHES_ID("紧身内衣","锁帷子")
	RETURNF 0
	
;连衣裙系は法衣、道士服、罩衫、布偶装だとダメ
SIF GROUPMATCH( EQUIP:ARG:连衣裙, CLOTHES_ID("连衣裙","法衣"), CLOTHES_ID("连衣裙","道士服"), CLOTHES_ID("连衣裙","罩衫"), CLOTHES_ID("连衣裙","布偶装") )
	RETURNF 0

RETURNF 1


;++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
;以下、体毛关系の函数

;### 体毛变更用メイン函数 ################
;しらふかつ恋慕or爱欲のある状态で浴场で会话すると呼び出される
@K78_HAIR_TREATMENT
#DIM 阴毛or腋毛 ;1=阴毛について 2=腋毛について
#DIM CFL番号变换 ;CFLAG番号用 0=阴毛 4=腋毛

$HAIR_TREATMENT_LOOP

CALL ASK_M("关于阴毛的请求", 1, "关于腋毛的请求", 1, "普通地对话", 1)
IF RESULT < 2
	阴毛or腋毛 = RESULT + 1
	CFL番号变换 = (阴毛or腋毛 - 1) * 4
	;毛の状态を表示
	CALL K78_HAIR_STATUS,阴毛or腋毛
	;どの毛に变えてもらうか选择肢
	IF 阴毛or腋毛 == 1
		CALL ASK_M("变得光溜溜", CFLAG:1004 != -2, "留下剃痕", CFLAG:1004 != -1, "普通地修整", CFLAG:1004 != 0, "变得茂密", CFLAG:1004 != 1, "变得非常茂密", CFLAG:1004 != 2, "果然还是放弃", 1)
		SIF RESULT == 5
			GOTO HAIR_TREATMENT_LOOP
		;どの阴毛に变更するかが决まる
		CFLAG:78:1004 = RESULT - 2
	ELSE
		CALL ASK_M("变得光溜溜", CFLAG:1008 != 0, "留下剃痕", CFLAG:1008 != 1, "变得茂密", CFLAG:1008 != 2, "变得非常茂密", CFLAG:1008 != 3, "果然还是放弃", 1)
		SIF RESULT == 4
			GOTO HAIR_TREATMENT_LOOP
		;どの腋毛に变更するかが决まる
		CFLAG:78:1008 = RESULT
	ENDIF
	;毛を伸ばす场合
	IF CFLAG:(1003 + CFL番号变换) < CFLAG:(1004 + CFL番号变换)
		;情景描写
		CALL K78_HAIR_TREATMENT_DESCRIPTIVE_TEXTS,阴毛or腋毛,1
		;毛の生长の计算のために现时点の毛の状态と现时点の总日数を保存
		CFLAG:78:(1005 + CFL番号变换) = CFLAG:(1003 + CFL番号变换)
		CFLAG:78:(1006 + CFL番号变换) = DAY:0
	;伸ばすのをやめて现状维持に切り替える场合
	ELSEIF CFLAG:(1003 + CFL番号变换) == CFLAG:(1004 + CFL番号变换)
		;情景描写
		CALL K78_HAIR_TREATMENT_DESCRIPTIVE_TEXTS,阴毛or腋毛,-1
	;毛を剃る场合
	ELSE
		;主人が剃るか调教对象に剃らせるか选择
		CALL ASK_YN(@"%CALLNAME:MASTER%来剃","让%CALLNAME:TARGET%来剃")
		;选择に応じて情景描写
		CALL K78_HAIR_TREATMENT_DESCRIPTIVE_TEXTS,阴毛or腋毛,(RESULT + 2)
		;剃るときは伸ばす场合と违い变更がすぐに反映される
		CFLAG:78:(1003 + CFL番号变换) = CFLAG:(1004 + CFL番号变换)
	ENDIF
	RETURN 1
ENDIF

;「普通に会话する」を选んだばあい返り值を０にして通常の会话口上に移行する
RETURN 0


;### 体毛の状态表示用函数 ##################
;ARG 1=阴毛 2=腋毛
@K78_HAIR_STATUS,ARG

VARSET LOCALS
VARSET LOCAL

PRINTL
IF ARG == 1
	;阴毛の状态の名称を设定（现在的分と目标の分のふたつ）
	FOR LOCAL,0,2
		SELECTCASE CFLAG:TARGET:(1003 + LOCAL)
		CASE -2
			LOCALS:LOCAL = 光溜溜的
		CASE -1
			LOCALS:LOCAL = 剃的整整齐齐
		CASE 0
			LOCALS:LOCAL = 普通（默认）
		CASE 1
			LOCALS:LOCAL = 自然的
		CASE 2
			LOCALS:LOCAL = 茂密的
		CASEELSE
			LOCALS:LOCAL = ※CFLAG:{TARGET}:{1003 + LOCAL}の值が不正※
		ENDSELECT
	NEXT
	;现在的阴毛の状态を表示する。いま伸ばしてもらっているところなら、目标の阴毛と生えきるまでの残り日数も表示する。
	PRINTFORMDL 现在的阴毛：%LOCALS:0%　　\@ CFLAG:1003 == CFLAG:1004 ? 你的指定：维持现在的阴毛 # 你的指定：%LOCALS:1%　　完全长好还需要{(CFLAG:1004 - CFLAG:1005) * 2 - (DAY:0 - CFLAG:1006)}天 \@
ELSEIF ARG == 2
	;腋毛の状态の名称を设定（现在的分と目标の分のふたつ）
	FOR LOCAL,0,2
		SELECTCASE CFLAG:TARGET:(1007 + LOCAL)
		CASE 0
			LOCALS:LOCAL = 光溜溜的（默认）
		CASE 1
			LOCALS:LOCAL = 剃的整整齐齐
		CASE 2
			LOCALS:LOCAL = 自然的
		CASE 3
			LOCALS:LOCAL = 茂密的
		CASEELSE
			LOCALS:LOCAL = ※CFLAG:{TARGET}:{1007 + LOCAL}の值が不正※
		ENDSELECT
	NEXT
	;现在的腋毛の状态を表示する。いま伸ばしてもらっているところなら、目标の腋毛と生えきるまでの残り日数も表示する。
	PRINTFORMDL 现在的腋毛：%LOCALS:0%　　\@ CFLAG:1007 == CFLAG:1008 ? 你的指定：维持现在的腋毛 # 你的指定：%LOCALS:1%　　完全长好还需要{(CFLAG:1008 - CFLAG:1009) * 2 - (DAY:0 - CFLAG:1010)}天 \@
ENDIF
PRINTL


;### 体毛变更の情景描写用函数 ##################
;ARG:0 1=阴毛 2=腋毛
;ARG:1 -1=伸ばすのをやめて现状维持に切り替える 1=伸ばす 2=主人が剃る 3=调教对象が自己剃る
@K78_HAIR_TREATMENT_DESCRIPTIVE_TEXTS,ARG:0,ARG:1

DRAWLINE

SIF FLAG:口上色
	TRYCALLFORM M_KOJO_COLOR_K{TARGET}

;阴毛关系
IF ARG:0 == 1
	SELECTCASE ARG:1
	;现状维持に切り替える场合
	CASE -1
		PRINTFORMDW 向%CALLNAME:TARGET%拜托了保持现状阴毛的样子。
	;伸ばす场合
	CASE 1
		PRINTFORMD 阴毛
		;指定した阴毛による分岐
		SELECTCASE CFLAG:1004
		CASE -1
			PRINTFORMD 弄成光溜溜的样子
		CASE 0
			PRINTFORMD 弄成普通的样子
		CASE 1
			PRINTFORMD 不要弄得太浓密
		CASE 2
			PRINTFORMD 自然生长像丛林的样子
		ENDSELECT
		PRINTFORMDL 向%CALLNAME:TARGET%拜托了。
		PRINTW
		;台词表示（伸ばすお愿い）
		TRYCCALLFORM DIALOGUE_K{TARGET}_HAIR_TREATMENT,1,1
			PRINTL
		CATCH
		ENDCATCH
		PRINTFORMDW 看来完全长好还需要{(CFLAG:1004 - CFLAG:1003) * 2}天左右。
	;主人が剃る场合
	CASE 2
		PRINTFORMDL 向%CALLNAME:TARGET%拜托了能不能让其剃阴毛。
		PRINTW
		;台词表示（剃らせてもらうお愿い）
		TRYCCALLFORM DIALOGUE_K{TARGET}_HAIR_TREATMENT,1,2,1
			PRINTL
		CATCH
		ENDCATCH
		PRINTFORMD %CALLNAME:MASTER%
		SELECTCASE RAND:3
		CASE 1
			PRINTFORMD 钻入了%CALLNAME:TARGET%的股间。
		CASE 2
			PRINTFORMD 对着仰面躺着的%CALLNAME:TARGET%的股间。
		CASEELSE
			PRINTFORMD 对着四肢趴着的%CALLNAME:TARGET%的跨间。
		ENDSELECT
		;剃毛前の阴毛による分岐
		SELECTCASE CFLAG:1003
		CASE -1
			PRINTFORMD 将一点点地长着的阴毛
		CASE 0
			PRINTFORMD 将整洁的阴毛
		CASE 1
			PRINTFORMD 将不经修理浓密茂盛的阴毛
		CASE 2
			PRINTFORMD 将肛门周围长得密密麻麻的刚毛
		ENDSELECT
		;剃毛后の阴毛による分岐
		SELECTCASE CFLAG:1004
		CASE -2
			PRINTFORMDL 刮完，像儿童一样股间变得光溜溜的。
		CASE -1
			PRINTFORMDL 为了留下自己喜欢的剃须痕，精心地剃掉了。
		CASE 0
			PRINTFORMDL 给整理得干干净净了。
		CASE 1
			PRINTFORMDL 虽然已经修理过了，但仍然有浓厚阴毛覆盖着。
		ENDSELECT
		PRINTW
		;台词表示（MASTERが剃ったあと）
		TRYCALLFORM DIALOGUE_K{TARGET}_HAIR_TREATMENT,1,2,2
	;调教对象が自己剃る场合
	CASE 3
		PRINTFORMDL 向%CALLNAME:TARGET%拜托了让我看刮阴毛。
		PRINTW
		;台词表示（剃るのを见せてもらうお愿い）
		TRYCCALLFORM DIALOGUE_K{TARGET}_HAIR_TREATMENT,1,3,1
			PRINTL
		CATCH
		ENDCATCH
		PRINTFORMD 跨过放在浴室里的手镜蹲了下来，
		;剃毛前の阴毛による分岐
		SELECTCASE CFLAG:1003
		CASE -1
			PRINTFORMD 把稍微露出一点儿的硬毛刮了上去
		CASE 0
			PRINTFORMD 刮掉了阴毛
		CASE 1
			PRINTFORMD 把覆盖阴部太多的毛刮掉了
		CASE 2
			PRINTFORMD 修剪了像密林一样茂密的阴毛
		ENDSELECT
		PRINTFORMDL 因为%CALLNAME:TARGET%的样子%CALLNAME:MASTER%更加兴奋了。
		PRINTW
		;台词表示（TARGETが剃ったあと）
		TRYCALLFORM DIALOGUE_K{TARGET}_HAIR_TREATMENT,1,3,2
	ENDSELECT
;腋毛关系
ELSEIF ARG:0 == 2
	SELECTCASE ARG:1
	;现状维持に切り替える场合
	CASE -1
		PRINTFORMDW 向%CALLNAME:TARGET%拜托了维持腋毛现在的样子。
	;伸ばす场合
	CASE 1
		PRINTFORMD 对着镜子仔细观察了剃掉了腋毛
		;指定した腋毛による分岐
		SELECTCASE CFLAG:1008
		CASE 1
			PRINTFORMD 剃完后染上淡淡的颜色
		CASE 2
			PRINTFORMD 让它长出来
		CASE 3
			PRINTFORMD 自由生长
		ENDSELECT
		PRINTFORMDL 向%CALLNAME:TARGET%拜托了。
		PRINTW
		;台词表示（伸ばすお愿い）
		TRYCCALLFORM DIALOGUE_K{TARGET}_HAIR_TREATMENT,2,1
			PRINTL
		CATCH
		ENDCATCH
		PRINTFORMDW 看来完全长好还需要{(CFLAG:1008 - CFLAG:1007) * 2}天左右。
	;主人が剃る场合
	CASE 2
		PRINTFORMDL 向%CALLNAME:TARGET%请求为其腋毛。
		PRINTW
		;台词表示（剃らせてもらうお愿い）
		TRYCCALLFORM DIALOGUE_K{TARGET}_HAIR_TREATMENT,2,2,1
			PRINTL
		CATCH
		ENDCATCH
		PRINTFORMD %CALLNAME:MASTER%将%CALLNAME:TARGET%的手臂举了起来
		;剃毛前の腋毛による分岐
		SELECTCASE CFLAG:1007
		CASE 1
			PRINTFORMD 将粗乱的腋毛
		CASE 2
			PRINTFORMD 对着镜子仔细观察了剃掉了腋毛
		CASE 3
			PRINTFORMD 把不检点地长长的腋毛
		ENDSELECT
		;剃毛后の腋毛による分岐
		SELECTCASE CFLAG:1008
		CASE 0
			PRINTFORMDL 完全剃掉了
		CASE 1
			PRINTFORMDL 小心地剃了，留下了适当的痕迹。
		CASE 2
			PRINTFORMDL 随意剃了一下。可是毛还残留着。
		ENDSELECT
		PRINTW
		;台词表示（MASTERが剃ったあと）
		TRYCALLFORM DIALOGUE_K{TARGET}_HAIR_TREATMENT,2,2,2
	;调教对象が自己剃る场合
	CASE 3
		PRINTFORMDL 向%CALLNAME:TARGET%拜托想看刮腋毛。
		PRINTW
		;台词表示（剃るのを见せてもらうお愿い）
		TRYCCALLFORM DIALOGUE_K{TARGET}_HAIR_TREATMENT,2,3,1
			PRINTL
		CATCH
		ENDCATCH
		PRINTFORMD %CALLNAME:MASTER%看着%CALLNAME:TARGET%举起一只手
		;剃毛前の腋毛による分岐
		SELECTCASE CFLAG:1007
		CASE 1
			PRINTFORMD 对着镜子仔细观察了剃掉了腋下汗毛
		CASE 2
			PRINTFORMD 对着镜子仔细观察了剃掉了腋毛
		CASE 3
			PRINTFORMD 对着镜子仔细观察了剃掉了腋下刚毛
		ENDSELECT
		PRINTFORMDL 的样子。
		PRINTW
		;台词表示（TARGETが剃ったあと）
		TRYCALLFORM DIALOGUE_K{TARGET}_HAIR_TREATMENT,2,3,2
	ENDSELECT
ENDIF


;### 体毛变更の台词用函数 ##################
;ARG:0 1=阴毛 2=腋毛
;ARG:1 1=伸ばす 2=主人が剃る 3=调教对象が自己剃る
;ARG:2 表示タイミング 1=剃毛をお愿いしたとき 2=剃ったとき
@DIALOGUE_K78_HAIR_TREATMENT,ARG:0,ARG:1,ARG:2

;--------------------------------------------------
;记入チェック（=0, 非表示、1, 表示）
LOCAL = 1
;--------------------------------------------------
IF LOCAL
	;-------------------------------------------------
	;阴毛の变更
	;记入チェック（=0, 非表示、1, 表示）
	LOCAL:1 = 1
	;-------------------------------------------------
	IF LOCAL:1 && ARG:0 == 1
		;-------------------------------------------------
		;伸ばすお愿い
		;记入チェック（=0, 非表示、1, 表示）
		LOCAL:2 = 1
		;-------------------------------------------------
		IF LOCAL:2 && ARG:1 == 1
			SELECTCASE CFLAG:1004
			CASE -1
				PRINTFORML 「要求还真是详细呢……」
			CASE 1
				PRINTFORML 「浓一点比较好吗？」
			CASE 2
				IF FIRSTTIME("阴毛をボーボーにする",0,78)
					PRINTFORMDW %CALLNAME:TARGET%虽然有点害羞，但在%CALLNAME:MASTER%的纠缠不休的请求总算是答应了。
					PRINTFORMW 「那么随便自由生长就可以了吗…？嘛，我可是个丰收之神，也不是不能做到……」
					PRINTFORMW 「但，但不是说本来就会长的很浓密哦，是用了我的力量才变成这样的」
					RETURN 1
				ENDIF
				PRINTFORML 「把我都弄晕了， 你还真是喜欢这种…」
			ENDSELECT
			PRINTFORMW 「知道了。但是要花时间，等着吧」
			RETURN 1
		ENDIF
		;-------------------------------------------------
		;MASTERが剃る
		;记入チェック（=0, 非表示、1, 表示）
		LOCAL:2 = 1
		;-------------------------------------------------
		IF LOCAL:2 && ARG:1 == 2
			;-------------------------------------------------
			;剃らせてもらうお愿い
			;记入チェック（=0, 非表示、1, 表示）
			LOCAL:3 = 1
			;-------------------------------------------------
			IF LOCAL:3 && ARG:2 == 1
				IF FIRSTTIME("主人に剃毛される（阴毛）",0,78)
					PRINTFORMW 「那边毛的修建？是没什么关系……。哎？你要现在开始刮？」
					PRINTFORMDW %CALLNAME:TARGET%虽然觉得很羞耻还是为%CALLNAME:MASTER%拿来了刮刀
					RETURN 1
				ENDIF
				PRINTFORML 「不用你来了啦，我自己就可以了啊……」
				PRINTFORMDL %CALLNAME:MASTER%没有听%CALLNAME:TARGET%说的话，取出了刮毛刀。
				RETURN 1
			ENDIF
			;-------------------------------------------------
			;MASTERが剃ったあと
			;记入チェック（=0, 非表示、1, 表示）
			LOCAL:3 = 1
			;-------------------------------------------------
			IF LOCAL:3 && ARG:2 == 2
				SELECTCASE CFLAG:1004
				CASE -2
					PRINTFORMW 「虽然很舒畅，但是总觉得有点不安心」
				CASE -1
					PRINTFORMW 「这种齐刷刷的感触」
				CASE 0
					PRINTFORMW 「嗯，不错。剃得很好啊」
				CASE 1
					PRINTFORMW 「还是太浓密了吧」
				ENDSELECT
				RETURN 1
			ENDIF
		ENDIF
		;-------------------------------------------------
		;TARGETが剃る
		;记入チェック（=0, 非表示、1, 表示）
		LOCAL:2 = 1
		;-------------------------------------------------
		IF LOCAL:2 && ARG:1 == 3
			;-------------------------------------------------
			;剃るのを见せてもらうお愿い
			;记入チェック（=0, 非表示、1, 表示）
			LOCAL:3 = 1
			;-------------------------------------------------
			IF LOCAL:3 && ARG:2 == 1
				IF FIRSTTIME("自己剃毛する（阴毛）",0,78)
					PRINTFORMW 「想看那边的剃的毛吗？不要。不会感到羞耻的吗」
					PRINTFORMDW %CALLNAME:MASTER%拼命说服了看起来十分嫌弃的%CALLNAME:TARGET%。
					PRINTFORMW 「哇，知道了啦。……哈啊，这到底哪里有意思了啊」
					RETURN 1
				ENDIF
				PRINTFORML 「还再想看吗？好吧。赶紧去做吧」
				RETURN 1
			ENDIF
			;-------------------------------------------------
			;TARGETが剃ったあと
			;记入チェック（=0, 非表示、1, 表示）
			LOCAL:3 = 1
			;-------------------------------------------------
			IF LOCAL:3 && ARG:2 == 2
				PRINTFORML 「好的，刮掉了。这样就可以了吗？」
				PRINTFORMW 「真是的。对这种奇怪的事这么讲究」
				RETURN 1
			ENDIF
		ENDIF
	ENDIF
	;-------------------------------------------------
	;腋毛の变更
	;记入チェック（=0, 非表示、1, 表示）
	LOCAL:1 = 1
	;-------------------------------------------------
	IF LOCAL:1 && ARG:0 == 2
		;-------------------------------------------------
		;伸ばすお愿い
		;记入チェック（=0, 非表示、1, 表示）
		LOCAL:2 = 1
		;-------------------------------------------------
		IF LOCAL:2 && ARG:1 == 1
			IF FIRSTTIME("腋毛を生やす",0,78)
				PRINTFORMW 「腋毛？喜欢那种东西吗？」
				PRINTFORMW 「唔，被\@ FLAG:扮演 == 77 ?其他孩子#姐姐 \@看见的话很羞耻…」
				PRINTFORMDW %CALLNAME:TARGET%在%CALLNAME:MASTER%的一再请求下终于同意了。
				PRINTFORMW 「就这么想要它长出来吗？ 既然%CALLNAME:MASTER%这样说了的话…」
				RETURN 1
			ENDIF
			SELECTCASE CFLAG:1008
			CASE 1
				PRINTFORML 「我要做点什么好吗?其他还有什么特别的讲究呢……」
			CASE 2
				PRINTFORML 「果然长出来还是有点害羞的…」
			CASE 3
				PRINTFORML 「就这样茫然自得还真是奇怪啊……」
			ENDSELECT
			PRINTFORMW 「好吧。我想会稍微花点时间，等一下吧」
			RETURN 1
		ENDIF
		;-------------------------------------------------
		;MASTERが剃る
		;记入チェック（=0, 非表示、1, 表示）
		LOCAL:2 = 1
		;-------------------------------------------------
		IF LOCAL:2 && ARG:1 == 2
			;-------------------------------------------------
			;剃らせてもらうお愿い
			;记入チェック（=0, 非表示、1, 表示）
			LOCAL:3 = 1
			;-------------------------------------------------
			IF LOCAL:3 && ARG:2 == 1
				IF FIRSTTIME("主人に剃毛される（腋毛）",0,78)
					PRINTFORMW 「想刮腋毛？……真是个怪人」
					PRINTFORMDW %CALLNAME:TARGET%虽然有点被吓到了还是取来了刮刀。
					PRINTFORMW 「你的脸太近了……稍微有点不好意思…」
					RETURN 1
				ENDIF
				PRINTFORML 「知道了知道了，又要刮了是吧」
				PRINTFORMDL %CALLNAME:TARGET%露出了无奈的表情，露出了腋下。
				RETURN 1
			ENDIF
			;-------------------------------------------------
			;MASTERが剃ったあと
			;记入チェック（=0, 非表示、1, 表示）
			LOCAL:3 = 1
			;-------------------------------------------------
			IF LOCAL:3 && ARG:2 == 2
				SELECTCASE CFLAG:1008
				CASE 0
					PRINTFORMW 「嗯，很舒畅」
				CASE 1
					PRINTFORMW 「没刮完……会让别人觉得你很散漫」
				CASE 2
					PRINTFORMW 「可以再帮我刮一点吗」
				ENDSELECT
				RETURN 1
			ENDIF
		ENDIF
		;-------------------------------------------------
		;TARGETが剃る
		;记入チェック（=0, 非表示、1, 表示）
		LOCAL:2 = 1
		;-------------------------------------------------
		IF LOCAL:2 && ARG:1 == 3
			;-------------------------------------------------
			;剃るのを见せてもらうお愿い
			;记入チェック（=0, 非表示、1, 表示）
			LOCAL:3 = 1
			;-------------------------------------------------
			IF LOCAL:3 && ARG:2 == 1
				IF FIRSTTIME("自己剃毛する（腋毛）",0,78)
					PRINTFORMW 「又说些奇怪的话……」
					PRINTFORMW 「……好吧，又不是什么值得欣赏的东西」
					RETURN 1
				ENDIF
				PRINTFORML 「看是可以看但不要妨碍我哦」
				RETURN 1
			ENDIF
			;-------------------------------------------------
			;TARGETが剃ったあと
			;记入チェック（=0, 非表示、1, 表示）
			LOCAL:3 = 1
			;-------------------------------------------------
			IF LOCAL:3 && ARG:2 == 2
				PRINTFORML 「好了、弄完了」
				PRINTFORMW 「%CALLNAME:MASTER%一直在看着的样子，还真是不会厌倦呢」
				RETURN 1
			ENDIF
		ENDIF
	ENDIF
ENDIF
RETURN 1


;### 体毛生长处理用函数 ##################
;遭遇、起床、すれ违いの口上で呼び出される
;ARG=角色番号（省略不可）
@K78_HAIR_GROWTH,ARG

FOR LOCAL,0,8,4
	;现在的体毛が目标の体毛に满たない场合、经过日数に応じた增加后の值を计算し现在的体毛に代入する
	IF CFLAG:ARG:(1003 + LOCAL) < CFLAG:ARG:(1004 + LOCAL)
		CFLAG:ARG:(1003 + LOCAL) = CFLAG:ARG:(1005 + LOCAL) + (DAY:0 - CFLAG:ARG:(1006 + LOCAL)) / 2
		;目标の体毛を超えることはない
		SIF CFLAG:ARG:(1003 + LOCAL) > CFLAG:ARG:(1004 + LOCAL)
			CFLAG:ARG:(1003 + LOCAL) = CFLAG:ARG:(1004 + LOCAL)
	ENDIF
NEXT


;### 阴毛描写用式中函数 ##################
;ARG=角色番号
@K78_GENITAL_HAIR_MODIFIER(ARG)
#FUNCTIONS

VARSET LOCALS

SIF ARG == 0
	ARG = TARGET

SELECTCASE CFLAG:ARG:1003
CASE -2
	SELECTCASE RAND:3
	CASE 1
		LOCALS = 无毛的
	CASE 2
		LOCALS = 像小孩子一般光溜溜的
	CASEELSE
		LOCALS = 没长毛
	ENDSELECT
CASE -1
	SELECTCASE RAND:3
	CASE 1
		LOCALS = 长着整整齐齐的毛
	CASE 2
		LOCALS = 长了毛绒绒的卷毛
	CASEELSE
		LOCALS = 有剃过残留下来的毛
	ENDSELECT
CASE 0
	LOCALS = 
CASE 1
	SELECTCASE RAND:3
	CASE 1
		LOCALS = 浓密的毛
	CASE 2
		LOCALS = 大量的毛
	CASEELSE
		LOCALS = 茂密的毛
	ENDSELECT
CASE 2
	SELECTCASE RAND:3
	CASE 1
		LOCALS = 密林之下的
	CASE 2
		LOCALS = 刚毛藏在里面几乎看不见
	CASEELSE
		LOCALS = 被毛绒绒的卷毛覆盖了
	ENDSELECT
CASEELSE
	LOCALS = ※CFLAG:{ARG}:1003の值が不正※
ENDSELECT

RETURNF LOCALS


;### 腋毛描写用式中函数 ##################
;ARG=角色番号
@K78_ARMPIT_HAIR_MODIFIER(ARG)
#FUNCTIONS

VARSET LOCALS

SIF ARG == 0
	ARG = TARGET

SELECTCASE CFLAG:ARG:1007
CASE 0
	SELECTCASE RAND:3
	CASE 1
		LOCALS = 光溜溜的
	CASE 2
		LOCALS = 光滑的
	CASEELSE
		LOCALS = 光滑的
	ENDSELECT
CASE 1
	SELECTCASE RAND:3
	CASE 1
		LOCALS = 留下淡淡的痕迹
	CASE 2
		LOCALS = 修整过
	CASEELSE
		LOCALS = 剃完有残留
	ENDSELECT
CASE 2
	SELECTCASE RAND:3
	CASE 1
		LOCALS = 长毛了
	CASE 2
		LOCALS = 有蓬松的毛
	CASEELSE
		LOCALS = 没有处理过毛
	ENDSELECT
CASE 3
	SELECTCASE RAND:4
	CASE 1
		LOCALS = 自由生长的毛
	CASE 2
		LOCALS = 浓密的毛
	CASE 3
		LOCALS = 被丰满的毛覆盖了
	CASEELSE
		LOCALS = 毛茸茸的
	ENDSELECT
CASEELSE
	LOCALS = ※CFLAG:{ARG}:1007の值が不正※
ENDSELECT

RETURNF LOCALS

