;-----------------------------------------------------------
;角色选择用函数
;-----------------------------------------------------------
;-----------------------------------------------------------
;CALLNAMEによる选择肢
;你と路人以外の全部を列表UP
;-----------------------------------------------------------
@ASK_CALLNAME
;生成
FOR LOCAL, 0, CHARANUM
	IF !IS_NAMED(LOCAL)
		CONTINUE
	ENDIF
	PRINTFORMLC [{LOCAL, 3}] %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%
NEXT
PRINTL
DRAWLINE
PRINTL [999] 返回
;入力受付
DO
	INPUT
	SIF RESULT == 999 || IS_NAMED(RESULT)
		RETURN RESULT
	CLEARLINE 1
LOOP 1
;-----------------------------------------------------------
;路人と你と出禁以外なら1を返す
;-----------------------------------------------------------
@IS_NAMED(ARG)
#FUNCTION
	IF ARG == MASTER || TALENT:ARG:路人 || CFLAG:ARG:出禁
		RETURNF 0
	ENDIF
RETURNF 1
;-----------------------------------------------------------
;10个までの选择肢
;-----------------------------------------------------------
@ASK_CHOICES10(argChoices:0="", argChoices:1="", argChoices:2="", argChoices:3="", argChoices:4="", argChoices:5="", argChoices:6="", argChoices:7="", argChoices:8="", argChoices:9="")
#DIMS argChoices, 10
CALL ASK_CHOICES(argChoices)
RETURN RESULT

;-----------------------------------------------------------
;参照渡しで数无制限の选择肢
;空欄は改行するが连续して改行しない、全て空欄ならTHROW
;-----------------------------------------------------------
@ASK_CHOICES(refChoices, argCanselNo=-1,argCanselText="中止")
#DIMS REF refChoices, 0
#DIM DYNAMIC argCanselNo
#DIMS DYNAMIC argCanselText
#DIMS html
#DIM skip
;生成
VARSET skip
VARSET html
FOR LOCAL, 0, VARSIZE("refChoices")
	;空欄
	IF refChoices:LOCAL == ""
		;空欄改行は连续しない
		IF !skip
			html += "<br>"
		ENDIF
		skip ++
	;中身あり
	ELSE
		html += HTML_SPACE(2) +HTML_BUTTON(refChoices:LOCAL, LOCAL, 3) + "<br>"
		skip = 0
	ENDIF
NEXT
SIF !skip
	html += "<br>"
SIF argCanselNo >= 0
	html += HTML_SPACE(2) + HTML_BUTTON(argCanselText, argCanselNo, 3) + "<br>"
SIF html == "<br>"
	THROW 选择肢が全て空です。
;表示
HTML_PRINT "<p align='left'><nobr>"+ "<br>" + html + "<br>" + "</nobr></p>"
;入力受付
DO
	INPUT
	SIF argCanselNo || (RESULT < VARSIZE("refChoices") && refChoices:RESULT != "")
		RETURN RESULT
	CLEARLINE 1
LOOP 1
;-----------------------------------------------------------
;参照渡しで选择肢をボタンで横にならべる
;空欄は无视、全て空欄ならTHROW
;-----------------------------------------------------------
@ASK_BUTTONS(refChoices)
#DIMS REF refChoices, 0
#DIMS DYNAMIC html
;生成
FOR LOCAL, 0, VARSIZE("refChoices")
	IF refChoices:LOCAL != ""
		html += HTML_SPACE(2) + HTML_BUTTON(refChoices:LOCAL, LOCAL)
	ENDIF
NEXT
SIF html == ""
	THROW 选择肢が全て空です。
;表示
HTML_PRINT "<p align='left'>" + html + "<br>" + "</p>"
;入力受付
DO
	INPUT
	SIF RESULT < VARSIZE("refChoices") && refChoices:RESULT != ""
		RETURN RESULT
	CLEARLINE 1
LOOP 1