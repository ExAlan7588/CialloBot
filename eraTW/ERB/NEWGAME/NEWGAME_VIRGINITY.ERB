;-----------------------------------------------------------
;处女设定
;-----------------------------------------------------------
;-----------------------------------------------------------
;处女设定メニュー
;-----------------------------------------------------------
@VIRGIN_SET
#DIMS choices, 11
PRINTL 
PRINTL 在游戏开始前可以变更对角色的处女・非处女 ＋ futa化与否＆形状进行个别设定（若体型设定里选全员futa化,此处的形状设定会失效）
PRINTL 全体设定即可对所有人生效
DRAWLINE
VARSET choices
choices:0 '= "个别设定"
choices:1 '= "全员都不知道什么叫做爱"
choices:2 '= "你是笨蛋吗肯定得全员处女啊"
choices:3 '= "幻想少女很长寿所以肯定经验丰富吧"
choices:4 '= "干脆全部都变的风骚吧"
choices:5 '= "虽然全员都是处女但是裤腰带都很松"
choices:9 '= "不管是不是处女都很想受种的样子"
SIF 全员の贞操
	choices:全员の贞操 '= "<font color='#67e47e'>(有效)</font>" + choices:全员の贞操
SIF 全员の贞操 ||SUMCARRAY(初期贞操)
	choices:10 '= "全员恢复初期设定"

CALL ASK_CHOICES(choices, 100, "结束")
SELECTCASE RESULT
	;システム
	CASE 0
		CALL SET_VIRGINITY
	CASE 10
		CVARSET 初期贞操
		VARSET 全员の贞操
		PRINTW 在全员初始设定的状态下开始游戏。
	CASE 100
		PRINTW 设定完毕。
		RETURN
	CASE IS == 全员の贞操
		全员の贞操 = 0
		PRINTW 全体设定无效了。
	;贞操
	CASE 1
		全员の贞操 = RESULT
		PRINTW 小兔姫在此。
		PRINTW 在全员无知处女的状态下开始游戏。
	CASE 2
		全员の贞操 = RESULT
		PRINTW 你个处女厨！
		PRINTW 在全员处女的状态下开始游戏。
	CASE 3
		全员の贞操 = RESULT
		PRINTW 在全员非处女的状态下开始游戏。
	CASE 4
		全员の贞操 = RESULT
		PRINTW 在全员风骚的状态下开始游戏。
	CASE 5
		PRINTFORMW 麻烦的家伙。
		全员の贞操 = RESULT
		PRINTW 在全员风骚处女的状态下开始游戏。
	CASE 9
		全员の贞操 = RESULT
		PRINTW 在全员渴望受种的状态下开始游戏。	
	CASEELSE
		THROW 无效输入。
ENDSELECT
RESTART

;-----------------------------------------------------------
;个别设定
;-----------------------------------------------------------
@SET_VIRGINITY
#DIM CHARA_ID
FOR CHARA_ID, 0, CHARANUM
	SIF !IS_NAMED(CHARA_ID)
		CONTINUE
	SIF TALENT:CHARA_ID:处女
		SETCOLOR C_PINK
	SIF TALENT:CHARA_ID:风骚
		SETCOLOR C_P_BLUE
	LOCALS '= @"[{CHARA_ID, 3}] %SET_VIRGINITY_KEY(初期贞操:CHARA_ID:0)%%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA_ID)%"
	PRINTFORMLC %LOCALS%
	RESETCOLOR
NEXT
PRINTL
DRAWLINE
PRINTL [999] 设定结束
DO
	INPUT
	CHARA_ID = RESULT
	SELECTCASE CHARA_ID
		CASE 999
			PRINTW 个别设定完毕。
			RETURN
		CASEELSE
			IF !IS_NAMED(CHARA_ID)
				CLEARLINE 1
				CONTINUE
			ENDIF
			BREAK
	ENDSELECT
LOOP 1

初期贞操:CHARA_ID:0 ++
IF 初期贞操:CHARA_ID:0 >= 8 && 初期贞操:CHARA_ID:0 < 10
	初期贞操:CHARA_ID:0 = 9
ELSEIF 初期贞操:CHARA_ID:0 >= 10
	初期贞操:CHARA_ID:0 = 0
ENDIF

SELECTCASE 初期贞操:CHARA_ID:0
	CASE 0
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA_ID)%的能力恢复为了初期设定
	CASE 1
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA_ID)%变成无知处女了
	CASE 2
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA_ID)%变成处女了
	CASE 3
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA_ID)%变成非处了
	CASE 4
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA_ID)%变得风骚了
	CASE 5
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA_ID)%变成风骚处女了
	CASE 6
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA_ID)%变成扶她
	CASE 7
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA_ID)%变成扶她处女
	CASE 9 
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA_ID)%变得渴望受种了
	CASEELSE
		THROW 贞操设定不适当
ENDSELECT
RESTART
;-----------------------------------------------------------
;设定略语
;-----------------------------------------------------------
@SET_VIRGINITY_KEY(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 0
		RETURNF ""
	CASE 1
		RETURNF "[无知]"
	CASE 2
		RETURNF "[处女]"
	CASE 3
		RETURNF "[熟练]"
	CASE 4
		RETURNF "[风骚]"
	CASE 5
		RETURNF "[骚处]"
	CASE 6
		RETURNF "[ふた]"
	CASE 7
		RETURNF "[双处]"
	CASE 9
		RETURNF "[受种渴求]"
	CASEELSE
		THROW 贞操设定不适当
ENDSELECT
;-----------------------------------------------------------
;处女设定适用
;-----------------------------------------------------------
@VIRGIN_SET_APPLY
FOR LOCAL, 1, CHARANUM
	IF 初期贞操:LOCAL:0
		CALL VIRGIN_SET_APPLY_PART(LOCAL, 初期贞操:LOCAL:0)
	ELSE
		CALL VIRGIN_SET_APPLY_PART(LOCAL, 全员の贞操)
	ENDIF
NEXT

;-----------------------------------------------------------
;-----------------------------------------------------------
@VIRGIN_SET_APPLY_PART(ARG, ARG:1)
SELECTCASE ARG:1
	CASE 0
	CASE 1
		CALL SET_VIRGIN(ARG:1)
		TALENT:ARG:无知 = 1
	CASE 2
		CALL SET_VIRGIN(ARG)
	CASE 3
		CALL SET_WELL_EXP(ARG)
	CASE 4
		CALL SET_YARIMAN(ARG)
	CASE 5
		CALL SET_VIRGIN(ARG)
		SIF TALENT:ARG:性别嗜好 == 1
			TALENT:ARG:性别嗜好 = 0
		ABL:ARG:欲望 = 3
		TALENT:ARG:风骚 = 1
		TALENT:ARG:贞操 = -1
	CASE 6
		CALL SET_FUTANARI(ARG)
		SIF 全员扶她化 != 1
			CALL SET_FUTANARI_SHAPE(ARG)
	CASE 7
		CALL SET_VIRGIN(ARG)
		CALL SET_FUTANARI(ARG)
		SIF 全员扶她化 != 1
			CALL SET_FUTANARI_SHAPE(ARG)
	CASE 9
		TALENT:ARG:无知 = 0
		SIF TALENT:ARG:性别嗜好 == 1
			TALENT:ARG:性别嗜好 = 0
		TALENT:ARG:怀孕愿望 = 1
		SIF TALENT:ARG:幼稚 == 1
			TALENT:ARG:初潮 = 1
		TALENT:ARG:难以越过的底线 = 0
		TALENT:ARG:献身的 = 1
		ABL:ARG:膣射中毒 = 3
CASEELSE
	THROW 贞操设定不适当
ENDSELECT

;-----------------------------------------------------------
;体型变更
;-----------------------------------------------------------
@BODY_SET
#DIMS choices, 11
PRINTL 
PRINTL 游戏开始前可以变更角色的体型素質
PRINTL 可以同时选择复数的设定
DRAWLINE
VARSET choices
;ロリ
choices:1 '= "全员都变成萝莉吧"
SIF 全员ロリ化
	choices:1 '= "<font color='#67e47e'>(有效)</font>" + choices:1
;小人
choices:2 '= "全员都变成小人吧"
SIF 全员小人化
	choices:2 '= "<font color='#67e47e'>(有效)</font>" + choices:2
;动物耳
choices:3 '= "全员兽耳"
SIF 全员动物耳化
	choices:3 '= "<font color='#67e47e'>(有效)</font>" + choices:3
;扶她
choices:4 '= "全员长出小鸡鸡"
SIF 全员扶她化
	choices:4 '= "<font color='#67e47e'>(有效)</font>" + choices:4
;母乳体质
choices:5 '= "全员分泌乳汁"
SIF 全员母乳体质化
	choices:5 '= "<font color='#67e47e'>(有效)</font>" + choices:5
;リセット
IF 全员ロリ化 || 全员小人化 || 全员动物耳化 || 全员扶她化 || 全员母乳体质化
	choices:10 '= "全员恢复初期设定"
ELSE
	choices:10 '= "全员萝莉+小人+兽耳"
ENDIF
CALL ASK_CHOICES(choices, 100, "结束")
SELECTCASE RESULT
	CASE 10
IF 全员ロリ化 || 全员小人化 || 全员动物耳化 || 全员扶她化 || 全员母乳体质化
			全员ロリ化 = 0
			全员小人化 = 0
			全员动物耳化 = 0
			全员扶她化 = 0
			全员母乳体质化 = 0
			PRINTW 设定重置了。
		ELSE
			全员ロリ化 = 1
			全员小人化 = 1
			全员动物耳化 = 1
			全员扶她化 = 1
			PRINTW 以全员都变成长兽耳的萝莉小人的状态下开始游戏。
		ENDIF
	CASE 100
		PRINTW 设定完毕。
		RETURN
	CASE 1
		IF 全员ロリ化
			全员ロリ化 = 0
		ELSE
			全员ロリ化 = 1
			IF TALENT:MASTER:形状 < 6
				PRINTFORML 偶尔当当萝莉控也不错呢！
				PRINTFORML
				PRINTW 以全员都是幼女的状态下开始游戏。
			ELSE
				PRINTFORML 你个禽兽！
				PRINTFORML
				PRINTW 以全员都是幼女的状态下开始游戏。
			ENDIF
		ENDIF
	CASE 2
		IF 全员小人化
			全员小人化 = 0
		ELSE
			全员小人化 = 1
			IF TALENT:MASTER:形状 < 3
				PRINTW 以全员都是小人的状态下开始游戏。
			ELSE
				PRINTFORML 你个畜生！
				PRINTFORML 
				PRINTW 以全员都是小人的状态下开始游戏。
			ENDIF
		ENDIF
	CASE 3
		IF 全员动物耳化
			全员动物耳化 = 0
		ELSE
			全员动物耳化 = 1
			PRINTFORMW 以全员都长兽耳的状态下开始游戏。
		ENDIF
	CASE 4
		IF 全员扶她化
			全员扶她化 = 0
		ELSE
			全员扶她化 = 1
			PRINTFORMW 以全员都是扶她的状态下开始游戏。
		ENDIF
	CASE 5
		IF 全员母乳体质化
			全员母乳体质化 = 0
		ELSE
			全员母乳体质化 = 1
			PRINTFORMW 以全员都会分泌乳汁的状态下开始游戏。
		ENDIF
ENDSELECT
RESTART

@SET_BODY_APPLY
;ロリの后に小人でないと小人が上书きされる
IF 全员ロリ化
	FOR LOCAL,1,CHARANUM
		CALL SET_LOLI(LOCAL)
	NEXT
ENDIF
IF 全员小人化
	FOR LOCAL,1,CHARANUM
		TALENT:LOCAL:体型 = -5
	NEXT
ENDIF
IF 全员动物耳化
	FOR LOCAL,1,CHARANUM
		TALENT:LOCAL:143 = 1
	NEXT
ENDIF
IF 全员扶她化
	FOR LOCAL,1,CHARANUM
		CALL SET_FUTANARI(LOCAL)
	NEXT
ENDIF
IF 全员母乳体质化
	FOR LOCAL,1,CHARANUM
		TALENT:LOCAL:母乳体质 = 2
		MAXBASE:LOCAL:母乳 = 1000
	NEXT
ENDIF

;-----------------------------------------------------------
;初期化
;强行开始新的游戏でも使用される
;CFLAGのリセットは继承に支障があるので禁止
;-----------------------------------------------------------
@RESET_CHARA(ARG)
FOR LOCAL, 0, VARSIZE("TALENTNAME")
	TALENT:ARG:LOCAL = CSVTALENT(ARG, LOCAL)
NEXT
FOR LOCAL, 0, VARSIZE("EXPNAME")
	EXP:ARG:LOCAL = CSVEXP(ARG, LOCAL)
NEXT
FOR LOCAL, 0, VARSIZE("ABLNAME")
	ABL:ARG:LOCAL = CSVABL(ARG, LOCAL)
NEXT
FOR LOCAL, 0, VARSIZE("EXPNAME")
	EXP:ARG:LOCAL = CSVEXP(ARG, LOCAL)
NEXT
FOR LOCAL, 0, VARSIZE("BASENAME")
	BASE:ARG:LOCAL = CSVBASE(ARG, LOCAL)
	MAXBASE:ARG:LOCAL = BASE:ARG:LOCAL
NEXT

;-----------------------------------------------------------
;处女化
;-----------------------------------------------------------
@SET_VIRGIN(ARG)
TALENT:ARG:处女 = 1
EXP:ARG:Ｃ经验 = 0
EXP:ARG:Ｂ经验 = 0
EXP:ARG:Ｖ经验 = 0
EXP:ARG:Ａ经验 = 0
ABL:ARG:Ｖ感觉 = 0
ABL:ARG:Ａ感觉 = 0
EXP:ARG:Ｖ性交经验 = 0
EXP:ARG:Ｖ扩张经验 = 0
EXP:ARG:子宫口开发经验 = 0
EXP:ARG:Ａ性交经验 = 0
EXP:ARG:Ａ扩张经验 = 0
EXP:ARG:手淫经验 = 0
EXP:ARG:口淫经验 = 0
EXP:ARG:乳交经验 = 0
EXP:ARG:接吻经验 = 0
EXP:ARG:侍奉快乐经验 = 0
EXP:ARG:爱情经验 = 0
EXP:ARG:苦痛快乐经验 = 0
EXP:ARG:嗜虐快乐经验 = 0
EXP:ARG:露出快乐经验 = 0
EXP:ARG:紧缚经验 = 0
EXP:ARG:绝顶经验 = 0
EXP:ARG:射精经验 = 0
EXP:ARG:喷乳经验 = 0
EXP:ARG:放尿经验 = 0
EXP:ARG:精液经验 = 0
EXP:ARG:精饮经验 = 0
EXP:ARG:分娩经验 = 0
TALENT:ARG:无接吻经验 = 1
TALENT:ARG:风骚 = 0
TALENT:ARG:性别嗜好 = CSVTALENT(ARG,81)
TALENT:ARG:贞操 = CSVTALENT(ARG,30)
ABL:ARG:腰 = CSVABL(ARG, GETNUM(ABL, "腰"))
ABL:ARG:欲望 = CSVABL(ARG, GETNUM(ABL, "欲望"))

;-----------------------------------------------------------
;経験丰富
;-----------------------------------------------------------
@SET_WELL_EXP(ARG)
TALENT:ARG:处女 = 0
SIF TALENT:ARG:性别嗜好 == 1
	TALENT:ARG:性别嗜好 = 0
EXP:ARG:Ｖ经验 = MAX(EXP:ARG:Ｖ经验, 50)
ABL:ARG:Ｖ感觉 = MAX(ABL:ARG:Ｖ感觉, 2)
ABL:ARG:腰 = MAX(ABL:ARG:腰, 2)
EXP:ARG:Ｖ性交经验 = MAX(EXP:ARG:Ｖ经验, 50)
EXP:ARG:Ｖ扩张经验 = MAX(EXP:ARG:Ｖ扩张经验, 3)
EXP:ARG:精液经验 = MAX(EXP:ARG:精液经验, 10)

;-----------------------------------------------------------
;风骚
;-----------------------------------------------------------
@SET_YARIMAN(ARG)
TALENT:ARG:处女 = 0
SIF TALENT:ARG:性别嗜好 == 1
	TALENT:ARG:性别嗜好 = 0
EXP:ARG:Ｖ经验 = MAX(EXP:ARG:Ｖ经验, 100)
ABL:ARG:Ｖ感觉 = MAX(ABL:ARG:Ｖ感觉, 3)
ABL:ARG:腰 = MAX(ABL:ARG:腰, 3)
ABL:ARG:欲望 = 3
EXP:ARG:Ｖ性交经验 = MAX(EXP:ARG:Ｖ经验, 100)
EXP:ARG:Ｖ扩张经验 = MAX(EXP:ARG:Ｖ扩张经验, 3)
EXP:ARG:精液经验 = MAX(EXP:ARG:精液经验, 50)
TALENT:ARG:风骚 = 1
TALENT:ARG:贞操 = -1

;-----------------------------------------------------------
;ロリ
;-----------------------------------------------------------
@SET_LOLI(ARG)
TALENT:ARG:胸围 = MAX(TALENT:ARG:胸围 - 2, -2)
TALENT:ARG:体型 = -2

;-----------------------------------------------------------
;扶她
;-----------------------------------------------------------
@SET_FUTANARI(ARG)
;PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%のふたなり函数呼び出し
TALENT:ARG:性别 = 3
IF RAND:100 == 1
	TALENT:ARG:形状 = 10
ELSE
	IF RAND:5 == 0
		SELECTCASE RAND:3
			CASE 0
				TALENT:ARG:形状 = 4
			CASE 1
				TALENT:ARG:形状 = 5
			CASE 2
				TALENT:ARG:形状 = 6
		ENDSELECT
	ELSE 
		SELECTCASE RAND:6
			CASE 0
				TALENT:ARG:形状 = 1
			CASE 1 TO 2
				TALENT:ARG:形状 = 2
			CASE 3 TO 5
				TALENT:ARG:形状 = 3			
		ENDSELECT
	ENDIF
ENDIF
MAXBASE:ARG:勃起 = 1500
MAXBASE:ARG:精力 = 1000


;-----------------------------------------------------------
;ふたなり形状
;-----------------------------------------------------------
@SET_FUTANARI_SHAPE(ARG)
;PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%のふたなり形状函数呼び出し
$LOOP
PRINTFORML 请设定%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的男性器形状
PRINTL [1] 未成熟
PRINTL [2] 短小的
PRINTL [3] 普通
PRINTL [4] 发光的
PRINTL [5] 长疣的
PRINTL [6] 巨根
INPUT
IF RESULT == 5
	PRINTW 就算被塞入那种东西也只会觉得痛，不会舒服的哦？
	PRINTL [0] 那还是算了吧
	PRINTL [1] 多管闲事
	INPUT
	IF RESULT == 1
		PRINTW 你这抖M！
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的男性器上长出了疣
		TALENT:ARG:形状 = 5
	ELSE
		GOTO LOOP
	ENDIF
ELSEIF RESULT == 4
	PRINTL 我不太明白你在说什么
	PRINTL [0] 抱歉，我搞错了
	PRINTL [1] 会像萤火鱿一样发光
	INPUT
	IF RESULT == 1
		PRINTW 你真是个怪人。
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的男性器开始发光了
		TALENT:ARG:形状 = 4
	ELSE
		GOTO LOOP
	ENDIF
ELSEIF RESULT == 6
	PRINTL 你是不是简单地认为越大越好？太大了血液循环会变差，反而会变软哦。
	PRINTL 而且重要的是和你的洞穴是否相配，而不是尺寸的大小。尺寸不合适只会让彼此痛苦哦。
	PRINTL [0] 我有点没自信，还是算了吧
	PRINTL [1] 我想看到即使自己感到痛苦，也要为了冲动而扭曲的少女的脸
	INPUT
	IF RESULT == 1
		PRINTW 你真是个麻烦的人。
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的男性器变大了
		TALENT:ARG:形状 = 6
	ELSE
		GOTO LOOP
	ENDIF
ELSEIF RESULT == 3
	PRINTL 果然普通是最好的，对吧？
	PRINTL [0] 总觉得少了点什么
	PRINTL [1] 这样就好
	INPUT
	IF RESULT == 1
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的男性器是很普通的形状
		TALENT:ARG:形状 = 3
	ELSE
		GOTO LOOP
	ENDIF
ELSEIF RESULT == 2
	PRINTL 也为长出那种东西的身体着想一下啊。
	PRINTL [0] 果然还是有点可怜
	PRINTL [1] 这样才好
	INPUT
	IF RESULT == 1
		PRINTW 明明好不容易才长出来的……。
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的男性器变得可怜兮兮的
		TALENT:ARG:形状 = 2
	ELSE
		GOTO LOOP
	ENDIF
ELSEIF RESULT == 1
	PRINTL 只有那里发育也怪怪的，对吧。
	PRINTL [0] 不平衡也挺好的
	PRINTL [1] 正好合适才相配
	INPUT
	IF RESULT == 1
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的男性器变得可爱起来了
		TALENT:ARG:形状 = 1
	ELSE
		GOTO LOOP
	ENDIF
ENDIF