;-------------------------------------------------
;HTML_PRINT关联函数
;-------------------------------------------------
;Emuera1810以降向け
;とりあえずこんな感じに。割と口上向けのつもり
;大体いい感じになったかな？
;-------------------------------------------------
;主な使い方(ざっくり。眠い时に书いたので少し意味不明)
;まず@HTMLPRINT(|D)(L|W)の使い方を觉えます
;例1)CALL HTMLPRINTW(@"「ドーモ、%NAME_OUTPUT_TRANSLATION("NAME", MASTER)%＝サン。古明地こいしです」")
;次に、<b><i><s><u>タグの使い方を觉えます
;例2)CALL HTMLPRINTW(@"「ドーモ、%NAME_OUTPUT_TRANSLATION("NAME", MASTER)%＝サン。<b>古明地こいし</b>です」")
;<font>タグは面倒なので代わりに@HTMLFONTの使い方を觉えます
;例3)CALL HTMLPRINTW(@"「ドーモ、%NAME_OUTPUT_TRANSLATION("NAME", MASTER)%＝サン。%HTMLFONT("<b>古明地こいし</b>", "ＭＳ 明朝", 0xFFFFFF)%です」")
;余裕があれば文字参照の使い方を觉えるとなお良し(この例だと"&#x2661;"の代わりにUNICODE(0x2661)でも同じ)
;例4)CALL HTMLPRINTW(@"「ドーモ、%NAME_OUTPUT_TRANSLATION("NAME", MASTER)%＝サン。<b>古明地こいし</b>です%HTMLFONT("&#x2661;", "Verdana", 0xC07070)%」")
;よく使う记号とかは<font>タグごと函数に缠めておくと便利かも
;最后に、<や>や&などの记号は文字参照使って书かないとおかしな事になるから气を付けて
;これで大体のことが后腐れなく一行で记述出来ます。やったね！
;-------------------------------------------------
;・大前提として、HTMLを新规に习得してまで无理に使う必要は全く无い
;・HTML_PRINT命令は基本的にHTML仕样に准拠。これらの函数も比较的HTMLを齧ったことのある人向けかもしれない
;・HTML未经验でも、ここで扱う范围を齧るだけなら简单なので性欲を持て余す方はどうぞ
;-------------------------------------------------
;函数の概要
;～～PRINT函数～～
;・HTMLPRINT(|D)(L|W)…PRINT(|FORM)(|D)(L|W)命令に近い挙动の函数。HTML_PRINT命令同样のタグがほぼ使用可能
;・HTMLPRINTSINGLE(|D)…PRINTSINGLE(|D)命令に近い挙动の函数。HTML_PRINT命令同样のタグがほぼ使用可能
;・HTMLBIGPRINT(L|W)…BIGPRINT函数同样の性质を持つHTMLPRINT函数。HTML_PRINT命令同样のタグがほぼ使用可能
;・HTMLSMALLPRINT(L|W)…SMALLPRINT函数同样の要素を持つHTMLPRINT函数。HTML_PRINT命令同样のタグがほぼ使用可能
;～～タグ处理函数～～
;・HTMLAUTOBUTTON…<button>、<nonbutton>、タグ无しを自动で切り替えられる式中函数。ボタンの有效/无效状态を管理する场合に
;・HTMLBUTTON…<button>タグを付加する式中函数。ボタンを作る际に必要。ボタンの值、ツールチップ文字列に关するタグ
;・HTMLFONT…<font>タグを付加する式中函数。SETFONT、SETCOLOR命令相当に加え、ボタン选择色も定义できる
;・HTMLNOBR…<nobr>タグを付加する式中函数。画面端で折り返さなくなる、PRINTSINGLE命令相当
;・HTMLNONBUTTON…<nonbutton>タグを付加する式中函数。非ボタンにツールチップ文字列を设定できる
;・HTMLP…<p>タグを付加する式中函数。文字の表示位置を指定するALIGNMENT命令相当
;・HTMLSTYLE…<b><i><s><u>タグを付加する式中函数。FONTSTYLE命令相当
;～～ユーティリティ函数～～
;・CHKFACE…'/'区切りのフォント名の中から使えるものを返す式中函数。<font>タグのface属性で复数指定出来ない仕样をカバー
;・HTMLCOLOR…色值を#RRGGBB形式の文字列に变更して返す式中函数。<font>タグのcolor属性に对応
;-------------------------------------------------

;*************************************************
;PRINT函数
;*************************************************
;・FORM文字列を渡す际は""の前に@を付けてください
;　　　文字列 → "あなたは射精した"
;　FORM文字列 → @"%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%は射精した"
;-------------------------------------------------

;-------------------------------------------------
;函数名:HTMLPRINT(|D)(L|W)
;概　要:HTML_PRINT命令をPRINT命令风にするラッパー函数
;参　数:ARGS:0…HTML文字列。FORM文字列を渡す场合は""の前に@を付けてください
;　　　 ARG:0 …[省略可]文字の表示色。省略した场合はSETCOLORの指定そのまま
;　　　         ※キーワード"D"を使用した场合はARG:0を指定することは出来ません
;　　　 ARGS:1…[省略可]文字列の表示位置。(left/左/center/中央/right/右)のいずれかを指定可
;　　　         省略した场合は、ALIGNMENTの指定そのまま。大文字小文字の区别はしません
;返回值:なし(多分RESULT保护)
;备　考:この函数では<p>タグが使用できません。<nobr>タグは文头文末でのみ使用可能
;・HTML_PRINTによる描画はALIGNMENT、SETFONT、SETCOLOR、FONTSTYLEとその类似命令の影响を受けない
;・HTML_PRINTにはWAITさせる机能が无い
;上记は口上で使う际に少し面倒な部分なので、影响を受けたかのようにタグを付加するように细工
;この处理により、一部タグが使用できなくなりますがご了承ください
;また、HTML_PRINTは自动的に改行するのでPRINTやPRINTDは作れません。あしからず
;(そのうちBuffer机能付きのHTMLPRINTB系とかHTMLPRINTBC系作るかもしれないけど…)
;表示位置と色を制御できれば口上的にさらに使いやすい气がしたのでちょっと细工しておいた
;きっと口上で使う分にはこのくらいでいい气がする。何も考えなくていい
;-------------------------------------------------
@HTMLPRINTL(ARGS:0, ARG:0 = -1, ARGS:1, ARGS:2 = "VOID")
HTML_PRINT HTMLP(HTMLFONT(HTMLSTYLE(ARGS:0), ARGS:2 == "VOID" ? GETFONT() # ARGS:2, ARG:0 < 0 ? GETCOLOR() # ARG:0), ARGS:1)
RETURN RESULT

@HTMLPRINTW(ARGS:0, ARG:0 = -1, ARGS:1)
CALL HTMLPRINTL(ARGS:0, ARG:0, ARGS:1)
WAIT
RETURN RESULT

@HTMLPRINTDL(ARGS:0, ARGS:1)
HTML_PRINT HTMLP(HTMLFONT(HTMLSTYLE(ARGS:0), GETFONT()), ARGS:1)
RETURN RESULT

@HTMLPRINTDW(ARGS:0, ARGS:1)
CALL HTMLPRINTDL(ARGS:0, ARGS:1)
WAIT
RETURN RESULT


;-------------------------------------------------
;函数名:HTMLPRINTSINGLE(|D)
;概　要:HTML_PRINT命令をPRINTSINGLE命令风にするラッパー函数
;参　数:ARGS:0…HTML文字列。FORM文字列を渡す场合は""の前に@を付けてください
;　　　 ARG:0 …[省略可]文字の表示色。省略した场合はSETCOLORの指定そのまま
;　　　         ※キーワード"D"を使用した场合はARG:0を指定することは出来ません
;　　　 ARGS:1…[省略可]文字列の表示位置。(left/左/center/中央/right/右)のいずれかを指定可
;　　　         省略した场合は、ALIGNMENTの指定そのまま。大文字小文字の区别はしません
;返回值:なし(多分RESULT保护)
;备　考:この函数では<p><nobr>タグが使用できません。(<nobr>タグは自动で付加されます)
;本家PRINTSINGLE命令と同样、自动的に改行が付加されるため、(|L|W)キーワードはありません
;多分全く使わない。なんで作っちゃったかなぁ
;-------------------------------------------------
@HTMLPRINTSINGLE(ARGS:0, ARG:0 = -1, ARGS:1)
HTML_PRINT HTMLP(HTMLNOBR(HTMLFONT(HTMLSTYLE(ARGS:0), GETFONT(), ARG:0 < 0 ? GETCOLOR() # ARG:0)), ARGS:1)
RETURN RESULT

@HTMLPRINTSINGLED(ARGS:0, ARGS:1)
HTML_PRINT HTMLP(HTMLNOBR(HTMLFONT(HTMLSTYLE(ARGS:0), GETFONT())), ARGS:1)
RETURN RESULT


;-------------------------------------------------
;函数名:HTMLBIGPRINT(L|W)
;概　要:オオキイHTMLPRINT函数
;参　数:ARGS:0…HTML文字列。FORM文字列を渡す场合は""の前に@を付けてください
;　　　 ARG:0 …[省略可]文字の表示色。省略した场合はSETCOLORの指定そのまま
;　　　 ARGS:1…[省略可]文字列の表示位置。(left/左/center/中央/right/右)のいずれかを指定可
;　　　         省略した场合は、ALIGNMENTの指定そのまま。大文字小文字の区别はしません
;返回值:なし(多分RESULT保护)
;备　考:この函数では<p><br>タグが使用できません。<nobr>タグは文头文末でのみ使用可能
;とりあえず用意したHTML_PRINT版BIGPRINT函数
;タグを使用出来るとはいえ、より慎重な运用が求められるかも
;例えば途中で色を变えても问题は无いだろうけど、フォントを变えると台无しに
;BIGPRINTは特に2行分使うので、\nとか<br>入れるときっとおかしくなる
;-------------------------------------------------
@HTMLBIGPRINTL(ARGS:0, ARG:0 = -1, ARGS:1)
#LOCALSIZE 1
#LOCALSSIZE 1
IF CHKFONT("Raavi")
	LOCAL:0 = CURRENTREDRAW()
	LOCALS:0 = %GETFONT()%
	REDRAW 0
	SETFONT "Raavi"
	CALL HTMLPRINTL(" " + ARGS:0 + " ", ARG:0, ARGS:1)
	SETFONT LOCALS:0
	PRINTL 
	REDRAW LOCAL:0
ELSE
	CALL HTMLPRINTL(ARGS:0, ARG:0, ARGS:1)
ENDIF
RETURN RESULT

@HTMLBIGPRINTW(ARGS:0, ARG:0 = -1, ARGS:1)
CALL HTMLBIGPRINTL(ARGS:0, ARG:0, ARGS:1)
WAIT
RETURN RESULT


;-------------------------------------------------
;函数名:HTMLSMALLPRINT(L|W)
;概　要:チイサイHTMLPRINT函数
;参　数:ARGS:0…HTML文字列。FORM文字列を渡す场合は""の代わりに@""で括ってください
;　　　 ARG:0 …[省略可]文字の表示色。省略した场合はSETCOLORの指定そのまま
;　　　 ARGS:1…[省略可]文字列の表示位置。(left/左/center/中央/right/右)のいずれかを指定可
;　　　         省略した场合は、ALIGNMENTの指定そのまま。大文字小文字の区别はしません
;返回值:なし(多分RESULT保护)
;备　考:この函数では<p>タグが使用できません。<nobr>タグは文头文末でのみ使用可能
;とりあえず用意したHTML_PRINT版SMALLPRINT函数
;タグを使用出来るとはいえ、より慎重な运用が求められるかも
;例えば途中で色を变えても问题は无いだろうけど、フォントを变えると台无しに
;-------------------------------------------------
@HTMLSMALLPRINTL(ARGS:0, ARG:0 = -1, ARGS:1)
#LOCALSSIZE 1
IF CHKFONT("Estrangelo Edessa")
	LOCALS:0 = %GETFONT()%
	SETFONT "Estrangelo Edessa"
	CALL HTMLPRINTL(" " + ARGS:0 + " ", ARG:0, ARGS:1)
	SETFONT LOCALS:0
ELSE
	CALL HTMLPRINTL(ARGS:0, ARG:0, ARGS:1)
ENDIF
RETURN RESULT

@HTMLSMALLPRINTW(ARGS:0, ARG:0 = -1, ARGS:1)
CALL HTMLSMALLPRINTL(ARGS:0, ARG:0, ARGS:1)
WAIT
RETURN RESULT


;*************************************************
;タグ处理函数
;*************************************************
;・<b><i><s><u><br>タグは属性值无いし自分で书いて、どうぞ
;・自分で书いた方が、函数入れ子にするよりは短く书けて处理も早いはず
;・これらの函数はHTML_PRINT命令を直に呼ぶ场合も使えるし、PRINT函数内でも使える
;・大体のことは、これらの函数を入れ子にすればなんとかなる…かも
;-------------------------------------------------

;-------------------------------------------------
;函数名:HTMLAUTOBUTTON
;概　要:HTML_PRINT命令および关联函数で使用する<button><nonbutton><font>タグを自动で付加する函数
;参　数:ARGS:0…ボタンにする文字列
;　　　 ARGS:1…ボタンの值(数值变量などは组み込み函数TOSTR(int)で文字列化してください)
;　　　 ARGS:2…[省略可][Emuera1812beta001以降のみ指定可]ツールチップに表示する文字列
;　　　 ARG:0 …[省略可]表示色。省略した场合は现在の文字色
;　　　 ARG:1 …[省略可]ボタンの选择中表示色。省略した场合はデフォルトの选择色
;　　　 ARG:2 …[省略可]ボタンが有效かどうかを指定する真伪值。省略した场合は有效(1)
;　　　 ARGS:3…[省略可]ボタンが无效の场合の文字列。省略した场合はARGS:0
;　　　 ARGS:4…[省略可][Emuera1812beta001以降のみ指定可]ボタンが无效の场合のツールチップ文字列。省略した场合はARGS:2
;　　　 ARG:3 …[省略可]ボタンが无效の场合の表示色。省略した场合はARG:0
;返回值:ARGS:0を<button><nonbutton><font><b><i><s><u>タグで囲ったり囲わなかったりするHTML文字列
;备　考:式中函数
;★★★ツールチップ机能はEmuera1812alpha001以降で使用できます★★★
;★★★Button无效Tooltip有りで使えるのはEmuera1812beta001以降★★★
;ボタンガーvalueガーという电波を受信したので作ったのだけど…
;Emuera1812beta001でvalue属性の仕样变わったので割といらなくなった。ついでに<nonbutton>も殆ど要らない子に
;alpha版はさくっと切り舍てて、beta版の仕样にそって手直し
;基本は<button>に丸投げして、各属性やタグそのものの必要性を判别
;ついでに有效/无效时で表示文字列/ツールチップ文字列/表示色も变えられるように
;参数分けなくても三项演算子使えば済む话かもしれないが、使い手が书きやすい方を选んで使えばいいと思う
;SETFONT/FONTSTYLEは胜手に自动继承する有难迷惑な设计。理由は面倒だったから
;气にいらなければ改造するか别に似たような函数组むかすればいいと思うよ
;正直この函数单体は、予め作っておいた函数を组み合わせただけなので大したことは无いのだけど…
;とりあえず私は使わない…と思う。参数の种类が多いとIDEでも无い限り意味不明になりがち
;-------------------------------------------------
@HTMLAUTOBUTTON(ARGS:0, ARGS:1, ARGS:2, ARG:0 = -1, ARG:1 = -1, ARG:2 = 1, ARGS:3, ARGS:4, ARG:3 = -1)
#FUNCTIONS
IF !ARG:2	;ボタンが无效な场合、使用する参数を变える
	ARG:0 = ARG:3 >= 0 ? ARG:3 # ARG:0
	ARGS:0 = \@ ARGS:3 != "" ? %ARGS:3% # %ARGS:0% \@
	ARGS:2 = \@ ARGS:4 != "" ? %ARGS:4% # %ARGS:2% \@
ENDIF
RETURNF HTMLBUTTON(HTMLFONT(HTMLSTYLE(ARGS:0), GETFONT(), ARG:0 < 0 ? GETCOLOR() # ARG:0, ARG:1), ARGS:1, ARGS:2, ARG:2)


;-------------------------------------------------
;函数名:HTMLBUTTON
;概　要:HTML_PRINT命令および关联函数で使用する<button>タグを付加する函数
;参　数:ARGS:0…ボタンにする文字列
;　　　 ARGS:1…ボタンの值(数值变量などは组み込み函数TOSTR(int)で文字列化してください)
;　　　 ARGS:2…[省略可][Emuera1812alpha001以降のみ指定可]ツールチップに表示する文字列
;　　　 ARG:0 …[省略可]ボタンが有效かどうかを指定する真伪值。省略した场合は有效(1)
;返回值:ARGS:0を<button>タグで囲ったHTML文字列
;备　考:式中函数
;★★★ツールチップ机能はEmuera1812alpha001以降で使用できます★★★
;★★★Button无效Tooltip有りで使えるのはEmuera1812beta001以降★★★
;一応空文字列返すボタンはアリなので、作れる
;空文字列のツールチップ出しても仕方ないので、そういうときはtitle属性省く
;ボタンが无效な场合はvalue属性省く。ツールチップも空文字ならタグそのものをつけない
;-------------------------------------------------
@HTMLBUTTON(ARGS:0, ARGS:1, ARGS:2, ARG:0 = 1)
#FUNCTIONS
#LOCALSSIZE 1
SIF !ARG:0 && ARGS:2 == ""
	RETURNF ARGS:0
LOCALS:0 = <button
SIF ARG:0
	LOCALS:0 += @" value='%ARGS:1%'"
SIF ARGS:2 != ""
	LOCALS:0 += @" title='%ARGS:2%'"
RETURNF LOCALS:0 + @">%ARGS:0%</button>"


;-------------------------------------------------
;函数名:HTMLFONT
;概　要:HTML_PRINT命令および关联函数で使用する<font>タグを付加する函数
;参　数:ARGS:0…付加する对象の文字列
;　　　 ARGS:1…[省略可]フォント名。省略した场合および、指定したフォントが存在しない场合はタグに含まれない
;　　　 ARG:0 …[省略可]表示色。省略した场合はタグに含まれない
;　　　 ARG:1 …[省略可]ボタンの选择中表示色。省略した场合はタグに含まれない
;　　　 ※ARGS:1/ARG:0/ARG:1の全てを省略した场合、GETFONT()とGETCOLOR()の情报を元にタグが生成されます
;返回值:ARGS:0を<font>タグで囲ったHTML文字列
;备　考:式中函数
;<font>タグはSETFONT命令とSETCOLOR命令に相当します
;何气にボタンの选择中表示色を变えられるのは新机能。HTML_PRINTだけ！
;-------------------------------------------------
@HTMLFONT(ARGS:0, ARGS:1, ARG:0 = -1, ARG:1 = -1)
#FUNCTIONS
#LOCALSSIZE 1
LOCALS:0 = <font
SIF !CHKFONT(ARGS:1) && ARG:0 < 0 && ARG:1 < 0
	RETURNF @"<font face='%GETFONT()%' color='%HTMLCOLOR()%'>%ARGS%</font>"
SIF CHKFONT(ARGS:1)
	LOCALS:0 += @" face='%ARGS:1%'"
SIF ARG:0 >= 0
	LOCALS:0 += @" color='%HTMLCOLOR(ARG:0)%'"
SIF ARG:1 >= 0
	LOCALS:0 += @" bcolor='%HTMLCOLOR(ARG:1)%'"
RETURNF @"%LOCALS:0%>%ARGS:0%</font>"


;-------------------------------------------------
;函数名:HTMLNOBR
;概　要:HTML_PRINT命令および关联函数で使用する<nobr>タグを付加する函数
;参　数:ARGS:0…付加する对象の文字列
;返回值:ARGS:0を<nobr>タグで囲ったHTML文字列
;备　考:式中函数
;<nobr>タグを使用すると描画领域を越えたことによる暗黙の改行が行われなくなります(PRINTSINGLE命令に相当)
;その场合でも、<br>タグによる明示的な改行は可能です
;<nobr>タグは最初のテ接吻トよりも外侧に置く必要があります
;この函数もそのように使用してください
;要らないと思うんだけど…气分の问题ってヤツ
;-------------------------------------------------
@HTMLNOBR(ARGS:0)
#FUNCTIONS
RETURNF @"<nobr>%ARGS:0%</nobr>"


;-------------------------------------------------
;函数名:HTMLNONBUTTON
;概　要:HTML_PRINT命令および关联函数で使用する<nonbutton>タグを付加する函数
;参　数:ARGS:0…ツールチップ付きにする文字列
;　　　 ARGS:1…[Emuera1812alpha001以降のみ指定可]ツールチップに表示する文字列
;返回值:ARGS:0を<nonbutton>タグで囲ったHTML文字列
;备　考:式中函数 Emuera1812alpha001以降でのみ使用可
;★★★ツールチップ机能はEmuera1812alpha001以降で使用できます★★★
;空文字列のツールチップ出しても仕方ないので、そういうときはタグ付けないよ
;Emuera1812beta001で要らなくなった感が强い。消えるのかな？
;-------------------------------------------------
@HTMLNONBUTTON(ARGS:0, ARGS:1)
#FUNCTIONS
SIF ARGS:1 == ""
	RETURNF ARGS:0
RETURNF @"<nonbutton title='%ARGS:1%'>%ARGS:0%</nonbutton>"


;-------------------------------------------------
;函数名:HTMLP
;概　要:HTML_PRINT命令および关联函数で使用する<p>タグを付加する函数
;参　数:ARGS:0…付加する对象の文字列
;　　　 ARGS:1…[省略可]文字列の表示位置。(left/左/center/中央/right/右)のいずれかを指定可
;　　　         省略した场合は、CURRENTALIGN()の情报を元にタグが生成されます。大文字小文字の区别はしません
;返回值:ARGS:0を<p>タグで囲ったHTML文字列
;备　考:式中函数
;<p>タグを使用すると文字列の表示位置を制御できます(ALIGNMENT命令に相当)
;<p>タグは一番外侧に置く必要があります
;この函数も一番外侧で使用してください
;-------------------------------------------------
@HTMLP(ARGS:0, ARGS:1)
#FUNCTIONS
#DIMS CONST ALIGN = "", "left", "center", "right", "左", "中央", "右"
SIF !MATCH(ALIGN, TOLOWER(ARGS:1))	;念のため参数チェック
	THROW @HTMLPに不明なalign属性值が渡されました（%ARGS:1%）
ARGS:1 '= MATCH(ALIGN, ARGS:1, 4) ? ALIGN:(FINDELEMENT(ALIGN, ARGS:1, , , 1) - 3) # ARGS:1
RETURNF "<p align='" + TOLOWER(\@ ARGS:1 == "" ? %CURRENTALIGN()% # %ARGS:1% \@) + @"'>%ARGS:0%</p>"


;-------------------------------------------------
;函数名:HTMLSTYLE
;概　要:HTML_PRINT命令および关联函数で使用する<b><i><s><u>タグを付加する函数
;参　数:ARGS:0…付加する对象の文字列
;　　　 ARG:0 …[省略可]文字のスタイル。太字(1)/斜体(2)/打ち消し线(4)/下线(8)の组み合わせを指定可能
;　　　         指定方法はFONTSTYLE命令と同样で、各数字の和で复数指定できます
;　　　         省略した场合はGETSTYLE()の情报を元にタグが生成されます
;返回值:ARGS:0を必要な<b><i><s><u>タグで囲ったHTML文字列
;备　考:式中函数
;FONTSTYLE命令相当にでっち上げた函数。缠まっていた方が便利な时もある…かも
;この函数で付加されるタグの效果は以下の通り
;<b>タグ…太字。FONTBOLD、またはFONTSTYLE 1に相当
;<i>タグ…斜体。FONTITALIC、またはFONTSTYLE 2に相当
;<s>タグ…打消。FONTSTYLE 4に相当
;<u>タグ…下线。FONTSTYLE 8に相当
;-------------------------------------------------
;FONTSTYLE命令と异なり、タグ内で既に适用中の效果を消すことは出来ません
;例1)HTML_PRINT @"TEST0<b>TEST1%HTMLSTYLE("TEST2", 2)%TEST3</b>TEST4"
;　　この场合、TEST2には太字と斜体の两方が适用されます。斜体のみにするには、
;例2)HTML_PRINT @"TEST0<b>TEST1</b>%HTMLSTYLE("TEST", 2)%<b>TEST3</b>TEST4"
;　　のように、一旦<b>タグを闭じる必要があります
;-------------------------------------------------
@HTMLSTYLE(ARGS:0, ARG:0 = -1)
#FUNCTIONS
#LOCALSSIZE 2
VARSET LOCALS
ARG:0 = ARG:0 < 0 ? GETSTYLE() # ARG:0
IF GETBIT(ARG:0, 0)
	LOCALS:0 = <b>%LOCALS:0%
	LOCALS:1 += "</b>"
ENDIF
IF GETBIT(ARG:0, 1)
	LOCALS:0 = <i>%LOCALS:0%
	LOCALS:1 += "</i>"
ENDIF
IF GETBIT(ARG:0, 2)
	LOCALS:0 = <s>%LOCALS:0%
	LOCALS:1 += "</s>"
ENDIF
IF GETBIT(ARG:0, 3)
	LOCALS:0 = <u>%LOCALS:0%
	LOCALS:1 += "</u>"
ENDIF
RETURNF LOCALS:0 + ARGS:0 + LOCALS:1


;*************************************************
;ユーティリティ函数
;*************************************************
;・直で<font>タグ书きたい人とか
;-------------------------------------------------

;-------------------------------------------------
;函数名:CHKFACE
;概　要:复数フォント使用可否判定函数
;参　数:ARGS:0…フォント名(/区切りで复数指定可。とりあえず10种まで)
;返回值:前から顺にCHKFONTして最初に使用可能と判断されたフォント名
;　　　 使用できるフォントが无い场合は现在のフォント名
;备　考:式中函数
;<font>タグにおけるface属性の,区切り(优先フォント指定)机能の代替
;のはずだったんだけど…使いどころは他にもあるのかも…？
;标准以外のフォントだけど、あれば使う场合とか…
;あとは…その…WinXP/VISTA/7/8以外の…
;べ、べつにMac对応のために作ったわけじゃないんだから…かかか勘违いしないでよね！
;-------------------------------------------------
@CHKFACE(ARGS:0)
#FUNCTIONS
#LOCALSIZE 2
#LOCALSSIZE 10
SPLIT ARGS:0, "/", LOCALS, LOCAL:0
FOR LOCAL:1, 0, LOCAL:0
	SIF CHKFONT(LOCALS:(LOCAL:1))
		RETURNF LOCALS:(LOCAL:1)
NEXT
RETURNF GETFONT()


;-------------------------------------------------
;函数名:HTMLCOLOR
;概　要:HTML_PRINT命令のfontタグに渡す色值に变换する函数
;参　数:ARG:0…[省略可]色值
;　　　        省略した场合はGETCOLOR()の情报を元に自动生成
;返回值:#RRGGBB形式の16进色值を表现する文字列
;备　考:式中函数
;普通に10进渡せたらこんなの要らないのに…HTML仕样ゆえ致し方なし
;ありえない数值が来ても下24bitしか见ないので问题なし
;TOSTR使ってるのは性格の问题。桁合わせと大文字化をしたかった
;实际はCONVERTでもいいんだろうけど
;-------------------------------------------------
@HTMLCOLOR(ARG:0 = -1)
#FUNCTIONS
RETURNF "#" + TOSTR((ARG:0 < 0 ? GETCOLOR() # ARG:0) & 0xFFFFFF, "X6")
