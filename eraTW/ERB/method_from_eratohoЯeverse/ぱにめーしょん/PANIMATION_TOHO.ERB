;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;文字列表示・アニメーション函数ファイル「ぱにめーしょん」0527版 同梱、东方Projectローカル函数ファイル
;东方系バリアントで使えそうなビジュアル・雰囲气向上系の函数群です。东方系以外では…どうだろう。あっても邪魔はしませんが。
;こちらは、ぱにめーしょん本体がないと动作しません
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡

;-------------------------------------------------
;东方Project风BGMカットイン函数@BGM_CUTIN
;	文字列参数0：曲名
;	文字列参数1：フォント（等幅じゃないと机能しません。省略したらDFKai-SB）
;	参数0：省略すると头に「♪（红・文・DS・妖战タイプ）」、正にすると「ＢＧＭ. （永・风以降タイプ）」、负にすると何もつけない
;	参数1：スクロール速度（デフォルトだと5）
;	参数2：画面端まで入る文字数（デフォルトだと117）
;	参数3：1にすると消え方がサイドアウトに（デフォルトだとフェードアウト）
;
;Windows标准フォントではないHG创英ﾌﾟﾚｾﾞﾝｽEBは望ましくないので、独断と偏见でデフォのフォントをDFKai-SBに变更@/L(14/09/28)
;-------------------------------------------------
@BGM_CUTIN(ARGS, ARG, ARGS:1 = "DFKai-SB", ARG:1 = 5, ARG:2 = 117, ARG:3)
#LOCALSIZE 2
#LOCALSSIZE 2
IF ARG >= 0
	LOCALS = %\@ ARG ? %"ＢＧＭ. "% # ♪ \@+ARGS%
ELSE
	LOCALS = %ARGS%
ENDIF
;色保存
LOCAL:1 = GETCOLOR()
RESETCOLOR
ARGS = %" "*ARG:2+LOCALS%
LOCALS:1 = %GETFONT()%
SIF CHKFONT(ARGS:1)
	SETFONT ARGS:1
;表示オン
IF ANIME_CONFIG("BGM_CUTIN")
	;スクロール速度がウィンドウ幅に对し速すぎる场合、矯正する
	ARG:1 = ARG:1 >= ARG:2 ? ARG:2-1 # ARG:1
	REDRAW 0
	LOCAL = 0
	;サイドイン
	FOR LOCAL, 0, STRLENS(LOCALS)+4, ARG:1
		PRINTSINGLEFORM %SUBSTRING(ARGS , LOCAL)%
		TWAIT 2, 0
		CLEARLINE 1
	NEXT
	PRINTFORML %" "*(ARG:2-STRLENS(LOCALS)-4)+LOCALS%
	TWAIT 800, 0
	CLEARLINE 1

	IF ARG:3 == 1
		;サイドアウト
		FOR LOCAL, 0, STRLENS(LOCALS)+4, ARG:1*2
			PRINTSINGLEFORM %SUBSTRING(ARGS , STRLENS(LOCALS)+4-LOCAL)%
			TWAIT 2 , 0
			CLEARLINE 1
		NEXT
	ELSE
		;BGMアニメオンだとフェードを强制表示する
		CALLF ANIME_CONFIG("ONCE")
		;フェードアウト
		CALL FADEOUT(8, " "*(ARG:2-STRLENS(LOCALS)-4)+LOCALS)
		CLEARLINE 1
	ENDIF
	REDRAW 1
;表示オフ
ELSE
	PRINTFORMW %" "*(ARG:2-STRLENS(LOCALS)-4)+LOCALS%
ENDIF
SETFONT LOCALS:1
SETCOLOR LOCAL:1
RETURN RESULT

;-------------------------------------------------
;东方Project风キャラ登场カットイン函数@CHARA_CUTIN
;	文字列参数0：肩书き
;	文字列参数1：名前
;	文字列参数2：ローマ字读み
;	参数0：省略すると左、正だと右寄りに表示。表示位置の逆からクロールインする
;	参数1：スクロール速度（デフォルトだと30）
;	参数2：画面端まで入る文字数（デフォルトだと117）
;			…バウンドいらんよね？
;神灵庙风の中央揃えです。需要があれば一行目左寄せ・三行目右寄せのバージョンも作る？
;-------------------------------------------------
;二つ名のフォントをHG教科书体からDFKai-SBへ
;ローマ字读みのフォントをＭＳ 明朝からGungsuhCheへ、それぞれ变更
;HG教科书体の粛清はWindows标准のフォントではない为。ＭＳ 明朝からGungsuhCheへの变更はついでに何か变えたかった、ということで
;Win7であれば环境に依存しないはずだけど、Win8/8.1での検证ができていないので、そこら边はなんともなところ@/L(2014/09/28)
;-------------------------------------------------
@CHARA_CUTIN(ARGS, ARGS:1, ARGS:2, ARG, ARG:1 = 30, ARG:2 = 117)
#LOCALSIZE 11
#LOCALSSIZE 11
;フォント保存
LOCALS:10 = %GETFONT()%
;色保存
LOCAL:10 = GETCOLOR()
RESETCOLOR

;まず、３つの文字列を偶数にする
ARGS   = %\@ STRLENS(ARGS)/2 ? %ARGS+" "% # %ARGS% \@%
ARGS:1 = %\@ STRLENS(ARGS:1)/2 ? %ARGS:1+" "% # %ARGS:2% \@%
ARGS:2 = %\@ STRLENS(ARGS:2)/2 ? %ARGS:2+" "% # %ARGS:2% \@%

;一番长い文字列の字数を保存して、4を足す
LOCAL:3 = MAX(STRLENS(ARGS), STRLENS(ARGS:1), STRLENS(ARGS:2))+4

;文字列整理
FOR LOCAL, 0, 3
	LOCAL:1 = (LOCAL:3-STRLENS(ARGS:LOCAL))/2
	;インデントがてらに头としっぽをつけます
	ARGS:LOCAL = %(" "*LOCAL:1)+ARGS:LOCAL+(" "*LOCAL:1)%
	;原本を保存
	LOCALS:LOCAL = %ARGS:LOCAL%
	;スペースを付加
	IF !ARG
		LOCALS:LOCAL = %"  "+LOCALS:LOCAL%
		ARGS:LOCAL = %(" "*ARG:2)+LOCALS:LOCAL%
	ELSE
		ARGS:LOCAL = %" "*(ARG:2 - 2 - STRLENS(ARGS:LOCAL))+ARGS:LOCAL%
	ENDIF
NEXT

;罫线も用意しないとね
LOCALS:4 = %"-="*(LOCAL:3/2)%
LOCALS:5 = %"=-"*(LOCAL:3/2)%
;スペースを付加
IF !ARG
	LOCALS:4 = %"  "+LOCALS:4%
	LOCALS:5 = %"  "+LOCALS:5%
	LOCALS:6 = %(" "*ARG:2)+LOCALS:4%
	LOCALS:7 = %(" "*ARG:2)+LOCALS:5%
ELSE
	LOCALS:6 = %" "*(ARG:2 - 3 - STRLENS(LOCALS:4))+LOCALS:4%
	LOCALS:7 = %" "*(ARG:2 - 3 - STRLENS(LOCALS:5))+LOCALS:5%
ENDIF

;表示オン
IF ANIME_CONFIG("CHARA_CUTIN")
	;スクロール速度がウィンドウ幅に对し速すぎる场合、矯正する
	ARG:1 = ARG:1 >= ARG:2 ? ARG:2-1 # ARG:1
	REDRAW 0
	LOCAL = 0
	FOR LOCAL, 0, ARG:2-2, ARG:1
		IF !ARG
			;罫线は元フォント
			SETFONT LOCALS:10
			PRINTSINGLEFORM %SUBSTRING(LOCALS:6 , LOCAL)%
			;二つ名はDFKai-SB
			;SIF CHKFONT("HG教科书体")
			;	SETFONT "HG教科书体"
			SIF CHKFONT("DFKai-SB")
				SETFONT "DFKai-SB"
			PRINTSINGLEFORM %SUBSTRING(ARGS , LOCAL)%
			;名前は元フォント
			SETFONT LOCALS:10
			PRINTSINGLEFORM %SUBSTRING(ARGS:1 , LOCAL)%
			;ローマ字读みはGungsuhChe
			SIF CHKFONT("GungsuhChe")
				SETFONT "GungsuhChe"
			;SIF CHKFONT("ＭＳ 明朝")
			;	SETFONT "ＭＳ 明朝"
			PRINTSINGLEFORM %SUBSTRING(ARGS:2 , LOCAL)%
			;罫线は元フォント
			SETFONT LOCALS:10
			PRINTSINGLEFORM %SUBSTRING(LOCALS:7 , LOCAL)%
		ELSE
			SETFONT LOCALS:10
			PRINTSINGLEFORM %SUBSTRING(LOCALS:6 , (ARG:2 - 2) - LOCAL)%
			SIF CHKFONT("DFKai-SB")
				SETFONT "DFKai-SB"
			PRINTSINGLEFORM %SUBSTRING(ARGS , (ARG:2 - 2) - LOCAL)%
			SETFONT LOCALS:10
			PRINTSINGLEFORM %SUBSTRING(ARGS:1 , (ARG:2 - 2) - LOCAL)%
			SIF CHKFONT("GungsuhChe")
				SETFONT "GungsuhChe"
			PRINTSINGLEFORM %SUBSTRING(ARGS:2 , (ARG:2 - 2) - LOCAL)%
			SETFONT LOCALS:10
			PRINTSINGLEFORM %SUBSTRING(LOCALS:7 , (ARG:2 - 2) - LOCAL)%
		ENDIF
		TWAIT 2 , 0
		SIF !ARG || LOCAL
			CLEARLINE 5
	NEXT
ENDIF

IF !ARG
	SETFONT LOCALS:10
	PRINTFORML %LOCALS:4%
	SIF CHKFONT("DFKai-SB")
		SETFONT "DFKai-SB"
	PRINTFORML %LOCALS%
	SETFONT LOCALS:10
	PRINTFORML %LOCALS:1%
	SIF CHKFONT("GungsuhChe")
		SETFONT "GungsuhChe"
	PRINTFORML %LOCALS:2%
	SETFONT LOCALS:10
	PRINTFORML %LOCALS:5%
ELSE
	SETFONT LOCALS:10
	PRINTFORML %LOCALS:6%
	SIF CHKFONT("DFKai-SB")
		SETFONT "DFKai-SB"
	PRINTFORML %ARGS%
	SETFONT LOCALS:10
	PRINTFORML %ARGS:1%
	SIF CHKFONT("GungsuhChe")
		SETFONT "GungsuhChe"
	PRINTFORML %ARGS:2%
	SETFONT LOCALS:10
	PRINTFORML %LOCALS:7%
ENDIF
REDRAW 1
SETCOLOR LOCAL:10
RETURN RESULT


;-------------------------------------------------
;幻想暦函数@GENSOU_CALENDAR
;	参数0：2000年1月1日（115季睦月一日）からの经过日数
;参数に经过日数を与えることで、幻想卿の暦を文字列で返す函数です
;-------------------------------------------------
@GENSOU_CALENDAR(ARG)
#FUNCTIONS
#LOCALSIZE 3
;LOCALに年数
LOCAL = GETYEAR(ARG)
;LOCAL:1に月数
LOCAL:1 = GETDATE(GETYEAR(ARG, 1), GETURUU(LOCAL))
;LOCAL:2に日数
LOCAL:2 = GETDATE(GETYEAR(ARG, 1), GETURUU(LOCAL), 1)

RETURNF @"第%TOKANJI(LOCAL+115)%季　%GETMONTHNAME(LOCAL:1)%の%TOKANJI(LOCAL:2)%"


;-------------------------------------------------
;古典时刻函数@CLASSICAL_TIME
;	参数0：对象の变量（デフォでは秒）
;	参数1：1なら参数0を秒で、2なら时で计算する
;	参数2：表示形式（デフォ「○の○つ」、1=「○の刻、○つ」、2=「○の○つ　-○○-」）
;式中で使用する函数です。参数に记录されている数字を秒として、现在の时刻を文字列で返します
;古典时刻表记なので、30分单位未满の端数は切り舍てます。
;-------------------------------------------------
;すでに本体同梱してあるのでコメントアウト
;@CLASSICAL_TIME(ARG, ARG:1, ARG:2)
;#FUNCTIONS
;;分に变换する
;SELECTCASE ARG:1
;	;时で渡す场合
;	CASE 2
;		ARG *= 60
;	;秒で渡す场合
;	CASE 1
;		ARG /= 60
;ENDSELECT
;;0:00が子の三つなので、60分を足す
;ARG += 60
;;日期も管理しているケースを考え、24时间（1440分）で割ってあまりを取る（子の一つ～二つもここで对応される）
;ARG %= 1440
;;表示
;SIF GET_CLASSICAL_HOUR(ARG, 1) == "丑" && (ARG%120)/30 == 2
;	RETURNF "丑三つ时"
;RETURNF @"%GET_CLASSICAL_HOUR(ARG, 1)%の\@ ARG:2 == 1 ? 刻、 # \@%TOKANJI(((ARG%120)/30)+1)%つ\@ ARG:2 == 2 ? 　-%GET_CLASSICAL_HOUR(ARG, 1, 1)%时- # \@"

;时を取得
;参数2を真にすると、大まかな时间の样子を取る
;すでに本体同梱してあるのでコメントアウト
;@GET_CLASSICAL_HOUR(ARG, ARG:1, ARG:2)
;#FUNCTIONS
;;分に变换する
;SELECTCASE ARG:1
;	;时で渡す场合
;	CASE 2
;		ARG *= 60
;	;分で渡す场合（处理なし）
;	CASE 1
;	;秒で渡す场合（デフォルト）
;	CASEELSE
;		ARG /= 60
;ENDSELECT
;
;SELECTCASE ARG/120
;	CASE 0
;		RETURNF \@ ARG:2 ? 深更（しんこう） # 子 \@
;	CASE 1
;		RETURNF \@ ARG:2 ? 未明（みめい） # 丑 \@
;	CASE 2
;		RETURNF \@ ARG:2 ? 东云（しののめ） # 寅 \@
;	CASE 3
;		RETURNF \@ ARG:2 ? 暁（あかつき） # 卯 \@
;	CASE 4
;		RETURNF \@ ARG:2 ? 早天（そうてん） # 辰 \@
;	CASE 5
;		RETURNF \@ ARG:2 ? 小昼（こひる） # 巳 \@
;	CASE 6
;		RETURNF \@ ARG:2 ? 昼中（ひるなか） # 午 \@
;	CASE 7
;		RETURNF \@ ARG:2 ? 日盛り（ひざかり） # 未 \@
;	CASE 8
;		RETURNF \@ ARG:2 ? 终日（ひねもす） # 申 \@
;	CASE 9
;		RETURNF \@ ARG:2 ? 黄昏（たそがれ） # 酉 \@
;	CASE 10
;		RETURNF \@ ARG:2 ? 宵（よい） # 戌 \@
;	CASE 11
;		RETURNF \@ ARG:2 ? 夜半（よわ） # 亥 \@
;ENDSELECT
;

;-------------------------------------------------
;数值→汉数字变换函数@TOKANJI
;TOSTR函数チックに、参数の数值を汉数字の文字列で返します
;变量の最大值は约922京であるため、一、万、亿、兆、京の5回处理します
;-------------------------------------------------
;すでに本体同梱してあるのでコメントアウト
;@TOKANJI(ARG)
;#FUNCTIONS
;#LOCALSIZE 1
;#LOCALSSIZE 1
;#DIM LCOUNT, 1
;VARSET LOCALS
;FOR LCOUNT, 0, 5
;	LOCAL = ARG/POWER(10000, (4-LCOUNT))
;	ARG %= POWER(10000, (4-LCOUNT))
;	IF LOCAL
;		LOCALS += TOKANJIT(LOCAL)
;		SELECTCASE LCOUNT
;			CASE 0
;				LOCALS += "京"
;			CASE 1
;				LOCALS += "兆"
;			CASE 2
;				LOCALS += "亿"
;			CASE 3
;				LOCALS += "万"
;		ENDSELECT
;	ENDIF
;NEXT
;RETURNF \@ LOCALS != "" ? %LOCALS% # 零 \@

;内部用or简易用
;四桁以内の汉字变换。万以上与えるとバグ
;すでに本体同梱してあるのでコメントアウト
;@TOKANJIT(ARG)
;#FUNCTIONS
;SIF ARG >= 10000
;	RETURNF "ばぐ"
;LOCALS = \@ ARG/1000 ? %TOKANJIS(ARG/1000, 1)%千 # \@
;ARG %= 1000
;LOCALS += \@ ARG/100 ? %TOKANJIS(ARG/100, 1)%百 # \@
;ARG %= 100
;LOCALS += \@ ARG/10 ? %TOKANJIS(ARG/10, 1)%十 # \@
;ARG %= 10
;RETURNF LOCALS + TOKANJIS(ARG)


;一桁の变换。参数1を真にすると一を省略する。2以上にすると零を表示する
;すでに本体同梱してあるのでコメントアウト
;@TOKANJIS(ARG, ARG:1)
;#FUNCTIONS
;SELECTCASE ARG
;	CASE 0
;		RETURNF \@ ARG:1 > 1 ? 零 #  \@
;	CASE 1
;		RETURNF \@ ARG:1 ?  # 一 \@
;	CASE 2
;		RETURNF "二"
;	CASE 3
;		RETURNF "三"
;	CASE 4
;		RETURNF "四"
;	CASE 5
;		RETURNF "五"
;	CASE 6
;		RETURNF "六"
;	CASE 7
;		RETURNF "七"
;	CASE 8
;		RETURNF "八"
;	CASE 9
;		RETURNF "九"
;	CASE 10
;		RETURNF "十"
;	CASEELSE
;		RETURNF "ばぐ"
;ENDSELECT


;-------------------------------------------------
;閏年判定函数@GETURUU
;	参数0：年数
;西暦で年数を与えるとその年の日数を返します
;-------------------------------------------------
@GETURUU(ARG)
#FUNCTION
RETURNF (!(ARG % 4) && (ARG % 100)) || !(ARG % 400) ? 366 # 365


;-------------------------------------------------
;年数判定函数@GETYEAR
;	参数0：2000年1月1日からの经过日数
;	参数1：1にすると、端日数を返します
;经过日数から、经过年数を返します。
;参数1を真にすると、现在の年で何日经过しているかを返します。
;2000年を0年として返すため、西暦表记する际は2000を足してください。
;单纯に经过年数を数える场合は意识しなくてもＯＫ
;-------------------------------------------------
@GETYEAR(ARG, ARG:1)
#FUNCTION
#LOCALSIZE 1
VARSET LOCAL
;2000年を0年目として扱う
;2000年から顺に回して、年数LOCALを增やす
WHILE ARG > GETURUU(LOCAL)
	LOCAL++
	ARG -= GETURUU(LOCAL)
WEND
RETURNF ARG:1 ? ARG # LOCAL


;-------------------------------------------------
;日期判定函数@GETDATE
;	参数0：年内经过日数（年内总日数を超えるときはエラー…にはせず、总日数で割った余りで计算する。でもやめてね？）
;	参数1：年内总日数（GETURUU并用推奖）
;	参数2：省略すると○月、真にすると○日を返します
;-------------------------------------------------
@GETDATE(ARG, ARG:1 = 365, ARG:2)
#FUNCTION
#LOCALSIZE 2
;念のための处理
ARG %= ARG:1
;月ごとの日数计算
FOR LOCAL, 1, 13
	SELECTCASE LOCAL
		;30日の月
		CASE 4, 6, 9, 11
			LOCAL:1 = 30
		;2月
		CASE 2
			LOCAL:1 = 28 + (ARG:2 == 366)
		;31日の月
		CASEELSE
			LOCAL:1 = 31
	ENDSELECT
	IF ARG > LOCAL:1
		ARG -= LOCAL:1
	ELSE
		BREAK
	ENDIF
NEXT
RETURNF ARG:2 ? ARG # LOCAL-1


;-------------------------------------------------
;月名变换函数@GETMONTHNAME
;	参数0：ｎ月のｎ
;旧暦の月名に变换するだけ！
;12を超えていたら胜手に12で割って计算します。
;-------------------------------------------------
@GETMONTHNAME(ARG)
#FUNCTIONS
;念のための处理
ARG %= 12
SELECTCASE ARG
	CASE 1
		RETURNF "睦月"
	CASE 2
		RETURNF "如月"
	CASE 3
		RETURNF "弥生"
	CASE 4
		RETURNF "卯月"
	CASE 5
		RETURNF "皋月"
	CASE 6
		RETURNF "水无月"
	CASE 7
		RETURNF "文月"
	CASE 8
		RETURNF "叶月"
	CASE 9
		RETURNF "长月"
	CASE 10
		RETURNF "神无月"
	CASE 11
		RETURNF "霜月"
	CASEELSE
		RETURNF "师走"
ENDSELECT
