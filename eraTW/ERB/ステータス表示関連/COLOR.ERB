;----------------------------------------------------------
;@COLOR(ARGS)
;----------------------------------------------------------
@COLOR, ARGS
#FUNCTION
SELECTCASE TOUPPER(ARGS)
	CASE "RED", "赤"
		RETURNF 0xFF0000
	CASE "DARKRED"
		RETURNF 0x990000
	CASE "BLUE"
		RETURNF 0x0000FF
	CASE "P-RED", "PASTEL-RED", "パ赤"
		RETURNF 0xC07070
	CASE "P_BLUE", "PASTEL-BLUE", "パ青"
		RETURNF 0x7070C0
	CASE "P_GREEN", "PASTEL-GREEN", "パ绿"
		RETURNF 0x70C070
	CASE "MEDSPRINGGREEN"
		RETURNF 0x00FA9A
	CASE "GREEN","绿"
		RETURNF 0x00FF00
	CASE "P_PURPLE", "PASTEL-PURPLE", "パ紫"
		RETURNF 0xC070C0
	CASE "P-YELLOW", "PASTEL-YELLOW", "パ黄"
		RETURNF 0x505020
	CASE "P-BGREEN", "P_BLUEGREEN", "PASTEL-BLUEGREEN", "パ青绿"
		RETURNF 0x205050
	CASE "WHITE", "白"
		RETURNF 0xC0C0C0
	CASE "BLACK", "黑"
		RETURNF 0x000000
	CASE "AQUA", "水色"
		RETURNF 0x66FFFF
	CASE "GRAY", "灰色"
		RETURNF 0x404040
	CASE "LIGHT-GRAY", "明灰色"
		RETURNF 0x777777
	CASE "PINK", "桃"
		RETURNF 0xFFCCFF
	CASE "DARK-PINK", "黑桃"
		RETURNF 0x990099
	CASE "YELLOW", "黄"
		RETURNF 0xFFFF00
	CASE "ORANGE","橙"
		RETURNF 0xFFA500
	CASE "CREAM"
		RETURNF 0xFFFFCC
ENDSELECT

;----------------------------------------------------------
;着色布丁ト函数
;直前まで使用していた色とフォントを保存し、使用后はそちらに复归するように
;表示形式           0=通常,1=PRINTL,2=PRINTW,3=PRINTPLAIN
;フォントス磚瓦   0=通常,1=太字,2=斜体,4=打ち消し线,8=下线
;(ビットで管理されているため例えば3なら太字＋斜体)
;----------------------------------------------------------
@COLORMESSAGE(ARGS, 色, 表示形式, フォントス磚瓦)
#DIM 现在指定の色
#DIM 现在指定のフォントス磚瓦
#DIM 色
#DIM フォントス磚瓦
#DIM 表示形式

现在指定の色 = GETCOLOR()
现在指定のフォントス磚瓦 = GETSTYLE()

SIF フォントス磚瓦
	FONTSTYLE フォントス磚瓦
SETCOLOR 色
SELECTCASE 表示形式
	CASE 0
		PRINTFORM %ARGS%
	CASE 1
		PRINTFORML %ARGS%
	CASE 2
		PRINTFORMW %ARGS%
	CASE 3
		PRINTPLAINFORM %ARGS%
ENDSELECT
SETCOLOR 现在指定の色
FONTSTYLE 现在指定のフォントス磚瓦

;条件を满たしていた场合に黄色をつける
@COND_COLORMESSAGE(COND,MESSAGE)
#DIM COND
#DIMS MESSAGE
SIF COND
	SETCOLOR C_YELLOW
PRINTFORML %MESSAGE%
RESETCOLOR

;-------------------------------------------------------------------------------
;    BARの色违え版
;-------------------------------------------------------------------------------
;    例  PRINTFORM %PRINT_COLORBAR(40,100,8,%UNICODE(0x2584)%,%UNICODE(0x2584)%,,0x404040)% 
;        [===     ]
;    参数はBARとほぼ一绪 + ARGSで描写文字の变更が可能(指定しない场合はBARと同じ仕样)
;    ARG:3はバーの色(0xFFFFFF形式で指定),ARG:4はバーの背景色(0xFFFFFF形式で指定)
;===============================================================================
@PRINT_COLORBAR(ARG, ARG:1, ARG:2, ARGS = "*", ARGS:1 = ".", ARG:3, ARG:4)
#LOCALSIZE 5
SIF ARG:1 <= 0
	ARG:1 = 1
LOCAL:1 = ARG * ARG:2 / ARG:1
LOCAL:2 = GETCOLOR()
FOR LOCAL, 1, ARG:2 + 1
	IF LOCAL:1 >= LOCAL
		IF !LOCAL:3
			SETCOLOR ARG:3
			LOCAL:3 = 1
		ENDIF
		PRINTFORM %ARGS%
	ELSE
		IF !LOCAL:4
			SETCOLOR ARG:4
			LOCAL:4 = 1
		ENDIF
		PRINTFORM %ARGS:1%
	ENDIF
NEXT
SETCOLOR LOCAL:2
VARSET LOCAL

;-------------------------------------------------
;カスタムバー用色セット函数@BARCOLORSET
;PRINT_COLORBARのARG:3に利用し、ARG;4にRESULTを使用
;-------------------------------------------------
@BARCOLORSET, ARGS
#FUNCTION
 ;LOCALに色を代入
SELECTCASE ARGS
	CASE "赤"
		RESULT:1 = 0x502020
		RETURNF 0xC07070
	CASE "薄赤"
		RESULT:1 = 0x803040
		RETURNF 0xF07090
	CASE "青"
		RESULT:1 = 0x202050
		RETURNF 0x7070C0
	CASE "绿"
		RESULT:1 = 0x205020
		RETURNF 0x70C070
	CASE "紫"
		RESULT:1 = 0x502050
		RETURNF 0xC070C0
	CASE "黄色"
		RESULT:1 = 0x505020
		RETURNF 0xC0C070
	CASE "青绿"
		RESULT:1 = 0x205050
		RETURNF 0x70C0C0
	CASE "白"
		RESULT:1 = 0x202020
		RETURNF 0xC0C0C0
	CASE "オレンジ"
		RESULT:1 = 0x504010
		RETURNF 0xFF8000
	CASE "水色"
		RESULT:1 = 0x203030
		RETURNF 0x5090FF
	CASE "EXTRA"
		RESULT:1 = 0x505020
		RETURNF 0xFFFFDD
	CASE "EXTRA2"
		RESULT:1 = 0x505020
		RETURNF 0xFFFFDD
	 ;白
	 CASEELSE
		RESULT:1 = 0x202020
		RETURNF 0xCCFFCC
ENDSELECT

;-------------------------------------------------
;ハートマーク函数@HEART
;	参数0：心形の数（省略すると1つ）
;	参数1：使用するフォント名（省略するとVerdana）
;中抜きのハートマークを表示します
;-------------------------------------------------
@HEART(ARG = 1, ARGS)
LOCALS = %GETFONT()%
SETFONT "Times New Roman"
PRINTFORM %UNICODE(0x2665)*ARG%
;PRINTFORM %UNICODE(0x2661)*ARG%
SETFONT LOCALS


@HEARTMARK(ARG, ARGS)
GETFONT
LOCAL = GETCOLOR()
SIF ARGS != "PLAIN"
	SETCOLOR C_H_PINK
SETFONT "Times New Roman"
;PRINTFORM %UNICODE(0x2665) * ARG%
PRINTFORM %UNICODE(0x2764) * ARG%

SETFONT RESULTS
SETCOLOR LOCAL

;ハート文字を返す
;フォントは考虑していない
@GET_HEART()
#FUNCTIONS
RETURNF UNICODE(0x2665)


;ハート文字で挟む
@CLIP_HEART(文字列)
#FUNCTIONS
#DIMS 文字列
文字列 = %GET_HEART() + 文字列 + GET_HEART()%
RETURNF 文字列


;穣子口上からパクリ
;### 心形のみ别の色でPRINTするための函数 ##################
;文字列中に含まれる"HPH"をホット粉色カラーのハートとしてPRINTし、それ以外の文字列は通常の色で普通にPRINTする
;ARGS "W"=PRINTSWで表示。"L"=PRINTSLで表示。"W"でも"L"でもなければPRINTSで表示
@HPH_PRINT(对象文字列,ARGS)
#DIMS 对象文字列
#DIM 出力文字数
#DIM HPHインデクス值
#DIM BPBインデクス值
#DIM NEXTインデクス值

DO
	HPHインデクス值 = STRFIND(对象文字列, "HPH")
		BPBインデクス值 = STRFIND(对象文字列, "BPB")
	IF HPHインデクス值 >= 0 & BPBインデクス值 < 0
		NEXTインデクス值 = HPHインデクス值
	ELSEIF HPHインデクス值 < 0 & BPBインデクス值 >= 0
		NEXTインデクス值 = BPBインデクス值
	ELSE
		NEXTインデクス值 = MIN(HPHインデクス值, BPBインデクス值)
	ENDIF

	;对象文字列がHPHで始まらないが后ろにHPHがある场合
		;对象文字列がHPHまたBPBで始まらないが后ろにHPHまたBPBがある场合
	IF NEXTインデクス值 >= 1
		;HPHの直前まで文字表示
		LOCALS = %SUBSTRING(对象文字列, 0, NEXTインデクス值)%
		IF GROUPMATCH(ARGS, "DW", "DL", "dw", "dl")
			PRINTSD @"%LOCALS%"
		ELSE
			PRINTS @"%LOCALS%"
		ENDIF
		出力文字数 = STRLENS(LOCALS)
	;对象文字列がHPHで始まる场合
	ELSEIF HPHインデクス值 == 0
		LOCALS:1 = %GETFONT()%
		SETFONT "Times New Roman"
		LOCAL = GETCOLOR()
		SETCOLOR C_H_PINK
		PRINTFORM %UNICODE(0x2764)%
		SETCOLOR LOCAL
		SETFONT LOCALS:1
		出力文字数 = 3
	;对象文字列がBPBで始まる场合
	ELSEIF BPBインデクス值 == 0
		LOCALS:1 = %GETFONT()%
		SETFONT "Times New Roman"
		LOCAL = GETCOLOR()
		SETCOLOR C_H_PINK
		PRINTFORM ///
		SETCOLOR LOCAL
		SETFONT LOCALS:1
		出力文字数 = 3
	;HPHとBPBがもうない场合
	ELSE
		;ここでARGSによる分岐
		SELECTCASE ARGS
		CASE "DW", "dw"
			PRINTSDW @"%对象文字列%"
		CASE "DL", "dl"
			PRINTSDL @"%对象文字列%"
		;----------
		CASE "W", "w"
			PRINTSW @"%对象文字列%"
		CASE "L", "l"
			PRINTSL @"%对象文字列%"
		CASEELSE
			PRINTS @"%对象文字列%"
		ENDSELECT
		BREAK
	ENDIF

	;表示済みの文字分を削除して对象文字列を更新する
	对象文字列 = %SUBSTRING(对象文字列, 出力文字数, -1)%
	
LOOP 1
