;FileName_INFO.ERB --------------------------------- Rev1.01
;ターン处理
;CALL		SYSTEM
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@SHOW_STATUS
#DIM 天气非表示
#DIM 元の天气
VARSET LOCAL

VARSET DOUBLE_MESSAGE_BLOCKED
TFLAG:SKIPASSIKOJO = 0

IF STR:(8000 + (CFLAG:MASTER:当前位置)) == "" && AT_HOME(MASTER)
	PRINTFORML 现在位置很异常（当前位置＞{CFLAG:MASTER:当前位置}）
	PRINTFORMW 为了避免报错而往%NAME_FROM_PLACE(CFLAG:MASTER:初始位置)%移动
	CFLAG:MASTER:当前位置 = CFLAG:MASTER:初始位置
	CFLAG:MASTER:约会中 = MAIN_MAP
ENDIF
;-------------------------------------------------
;TFLAG:调教中COMABLE管理表示制御
;-------------------------------------------------
TFLAG:调教中COMABLE管理 = TFLAG:调教自动实行管理
SIF TFLAG:调教自动实行管理 == 1
	TFLAG:调教中COMABLE管理 = 0
SIF TFLAG:调教自动实行管理
	TFLAG:调教自动实行管理 --
;ウフフ中なら接触部位表示
SIF TFLAG:102 == 2
	CALL SHOW_TOUCH

;-------------------------------------------------
;料理の时间经过
;-------------------------------------------------
SIF DISH_NAME != ""
	CALL DISH_FRESHNESS
;-------------------------------------------------
;畑の时间经过
;-------------------------------------------------
SIF TIME:5 >= 4
	FLAG:803 = 0
	FLAG:807 = 0

;-------------------------------------------------
;角色の态度
;-------------------------------------------------
CALL CHARA_ATTITUDE

;-------------------------------------------------
;角色の思考
;-------------------------------------------------
;CALL CHARA_MIND

;-------------------------------------------------
;时间经过、角色の移动
;-------------------------------------------------
CALL INFO_TIME_PASSING

;-------------------------------------------------
;约会结束タイムUP处理
;-------------------------------------------------
SIF CHK_DATENOW(CFLAG:MASTER:约会中)
	CALL 约会结束タイムUP处理
;-------------------------------------------------
;TARGET关联の角色变量の更新
;-------------------------------------------------
CALL INFO_RENEW_TARGET

;-------------------------------------------------
;ウフフ结束TEQUIP处理
;EQUIPの更新もここに仕込んでおく
;-------------------------------------------------
CALL INFO_RENEW_EQUIP

;-------------------------------------------------
;MASTERの同行フラグチェック
;-------------------------------------------------
SIF !同行人数()
	CFLAG:MASTER:同行 = 0

;-------------------------------------------------
;背景色设定
;-------------------------------------------------
IF FLAG:时间停止
	SETBGCOLOR 1, 24, 60
ELSE
	;(天候パッチ)时刻・天候に応じて背景色を变更
	CALL SET_MAP_WEATHER_BGCOLOR
ENDIF

;-------------------------------------------------
;情报表示
;-------------------------------------------------
PRINTL
;天气の表示/非表示の更新
;天界への行き来の际の天气の更新
CALL INFO_CULC_WEATHER(天气非表示)

;「一日が终わりました」表示と宴会关联の更新处理など
CALL INFO_GO_NEXTDAY
CALL CHANGE_TIMEZONE

;儿童の描写
CALL CHILD_DESCRIPTION

;回家时间等の予告通知
CALL TIMEUP_NOTICE(FLAG:约会的对象)

;钟表画像表示
CALL INFO_CLOCK_IMAGE(TIME)

;ここからゲーム画面に表示されるインターフェイス
DRAWLINEFORM *
;日时と祝日の表示（改行无）
CALL INFO_TIME_WATCH

;各种フラグ确认用 デ背包中以外は非表示で
;PRINTFORM 诶嘿嘿:{CFLAG:TARGET:诶嘿嘿}
;PRINTFORM 恶作剧:{CFLAG:恶作剧}
;PRINTFORM 睡眠深度:{TCVAR:TARGET:睡眠深度}

;天气と月龄の表示
SIF !天气非表示
	CALL INFO_SHOW_WEATHER

;现在の气温を表示
CALL INFO_现在の气温
PRINTPLAIN
;MASTERの眠气など
CALL INFO_SITUATION
;时间停止关联
CALL INFO_TIMESTOP

;-------------------------------------------------
;当前位置情报
;-------------------------------------------------
CALL INFO_GPS
PRINTL
;-------------------------------------------------
;TARGET情报とモード切り替え
;-------------------------------------------------
VARSET LOCAL
;TARGET关联の简易情报表示函数群
CALL INFO_TARGET_DATAGROUP
CALL PRINTBUTTON_EX(TFLAG:Status非表示 ? "▽[＋][Status]--------" # "▼[－][Status]--------", 3001, TFLAG:Status非表示)
PRINTBUTTON "[能力表示]",803
PRINTL 
IF !TFLAG:Status非表示
	CALL INFO_BASE(MASTER)
	SIF TARGET
		CALL INFO_BASE(TARGET)
	CALL INFO_STATUS
ENDIF

SIF TARGET
	CALL SHOW_INFO_PALAM

IF !TARGET && TFLAG:抱住模式
	TFLAG:抱住模式 = 0
	TFLAG:102 = 1
ENDIF

IF CFLAG:MASTER:诶嘿嘿 == 2
	SIF !TFLAG:PALAM非表示
		PRINTL 
	DRAWLINE
	CALL Counter呼び出し处理
ENDIF
IF CFLAG:MASTER:诶嘿嘿 > 0 && !TARGET
	FOR LOCAL,0,CHARANUM
		SIF CFLAG:LOCAL:出禁
			CONTINUE
		CALL ENDUFUFU(LOCAL)
		CALL SET_PRANK(0,LOCAL)
	NEXT
ENDIF
;FileName_INFO.ERB --------------------------------- Rev1.00
;污渍显示处理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@STAIN_INFO
;主人的污垢
CALL INFO_CHARA_STAIN(MASTER)
SIF TARGET > 0
	CALL INFO_CHARA_STAIN(TARGET)
SIF ASSI > 0
	CALL INFO_CHARA_STAIN(ASSI)
WAIT


;污渍显示处理の泛用化
@INFO_CHARA_STAIN(C_ID)
#DIM        C_ID
#DIM        BODY_PARTS
#DIMS CONST PARTS_NAME = "口", "手", "Ｐ", "Ｖ", "Ａ", "Ｂ", "膣内", "肠内"
#DIM        TYPE_STAIN
#DIMS CONST STAIN_NAME = "爱液", "阴茎", "精液", "肛门", "母乳", "粘液", "破瓜之血", "巧克力"
PRINTFORML 【%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的污垢】
FOR BODY_PARTS, 0, 8
	SIF PARTS_NAME:BODY_PARTS == "Ｐ" && !(TALENT:C_ID:2 & 2)
		CONTINUE
	SIF GROUPMATCH(PARTS_NAME:BODY_PARTS, "Ｖ", "Ｂ") && !(TALENT:C_ID:2 & 1)
		CONTINUE
	PRINTFORM 　%PARTS_NAME:BODY_PARTS%：
	FOR TYPE_STAIN, 0, 7
		SIF GETBIT(STAIN:C_ID:BODY_PARTS, TYPE_STAIN)
			PRINTFORM <%STAIN_NAME:TYPE_STAIN%>
	NEXT
	IF GETBIT(STAIN:C_ID:BODY_PARTS, FINDELEMENT(STAIN_NAME, "巧克力"))
		IF GETBIT(STAIN:C_ID:BODY_PARTS, FINDELEMENT(STAIN_NAME, "母乳"))
			PRINT <巧克力奶汁>
		ELSE
			PRINT <巧克力>
		ENDIF
	ENDIF
	PRINTL 
NEXT
PRINTL 



@SAVEINFO
;场所は妖怪之山（山麓）で半角16文字分、名前は克劳恩皮丝で14文字分必要
PUTFORM %createSaveInfo(0)%

;生成存档信息
@createSaveInfo(qSave = 0)
	#FUNCTIONS
	#DIM qSave
	RETURNF @"{DAY,3,RIGHT}日目 %GET_MAPNAME(MAIN_MAP),16,LEFT% %NAME_OUTPUT_TRANSLATION("NAME", MASTER),14,LEFT% Ver %SAVESTR:本体バージョン,9,LEFT% \@ qSave == 1 ? ⭐即时存档⭐ # \@\@ qSave == 2 ? *自动存档* # \@"

;FileName_INFO.ERB --------------------------------- Rev1.00
;プレイ状态表示处理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	まとめて表示するように改良,TARGETの＜道具や使用状态＞以外の状态はここ
;-----------------------------------------------------------
@SHOW_EQUIP_2
;状况の表示
;调教对象には不使用、特殊な着脱式の道具を追加した场合はここに追加をしてください
;（场所变更、マ斯塔や助手が使用する道具等）
LOCALS = （

IF TALENT:TARGET:心情 == 1
	LOCALS += "・好心情"
ELSEIF TALENT:TARGET:心情 == -1
	LOCALS += "・不高兴"
ENDIF

SIF TCVAR:TARGET:祝福
	LOCALS += "・祝福"
SIF TCVAR:TARGET:魔性
	LOCALS += "・ＰUP"
SIF TCVAR:TARGET:加护
	LOCALS += "・加护"

IF CFLAG:TARGET:睡眠
	SELECTCASE TCVAR:TARGET:睡眠药强度
	CASE 1
		LOCALS += "・深度睡眠"
	CASE 2
		LOCALS += "・极深睡眠"
	CASE 3
		LOCALS += "・昏睡中"
	CASEELSE
		LOCALS += "・睡眠中"
	ENDSELECT
ELSEIF TCVAR:TARGET:睡眠药强度
	SELECTCASE TCVAR:TARGET:睡眠药强度
	CASE 1
		LOCALS += "・睡意"
	CASE 2
		LOCALS += "・强烈睡意"
	CASE 3
		LOCALS += "・朦胧中"
	ENDSELECT
ENDIF

SIF TCVAR:TARGET:发情
	LOCALS += "・发情中"
SIF TCVAR:TARGET:烂醉
	LOCALS += "・烂醉中"

SELECTCASE TCVAR:TARGET:祈愿补正
CASE 23
	LOCALS += "・性的兴趣上升中"
CASE 31
	LOCALS += "・解放上升中"
CASE 41
	LOCALS += "・弄湿难易上升中"
CASE 70
	LOCALS += "・快感肯定上升中"
CASE 80
	LOCALS += "・倒错的付与中"
CASE 82
	LOCALS += "・施虐狂付与中"
CASE 83
	LOCALS += "・受虐狂付与中"
CASE 101
	LOCALS += "・Ｃ感度上升中"
CASE 102
	LOCALS += "・Ｖ感度上升中"
CASE 103
	LOCALS += "・Ａ感度上升中"
CASE 104
	LOCALS += "・Ｂ感度上升中"
CASE 106
	LOCALS += "・Ｍ感度上升中"
ENDSELECT

;カラー表示,上に行くほど优先度が高い
IF CFLAG:TARGET:睡眠
	SETCOLOR C_BLUE
ELSEIF TCVAR:TARGET:发情
	SETCOLOR C_H_PINK
ELSEIF TCVAR:TARGET:烂醉
	SETCOLOR C_ORANGE
ELSEIF TCVAR:TARGET:睡眠药强度
	SETCOLOR C_BLUE
ELSEIF TALENT:TARGET:心情 == 1
	SETCOLOR C_AQUA
ELSEIF TALENT:TARGET:心情 == -1
	SETCOLOR C_RED
ENDIF

;表示部分
PRINTFORM %NAME_OUTPUT_TRANSLATION("NAME", TARGET)%

;通常时
IF LOCALS== "（"
	RETURN
;状态时
ELSE
	LOCALS += "）"
	PRINTFORM %REPLACE(LOCALS, "（・", "（")%
ENDIF
RESETCOLOR

;FileName_INFO.ERB --------------------------------- Rev1.00
;道具状态表示处理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	TARGETの使用状态
;-----------------------------------------------------------
@SHOW_EQUIP_1
;使用中の道具を表示する
;一度使用したら解除するまで止まらない道具や调教を
;追加した场合はここに追加をしてください
SIF !CHARA_EQUIP(1)
	RETURN
PRINTFORM 使用中(%NAME_OUTPUT_TRANSLATION("NAME", TARGET)%) 

SIF TCVAR:TARGET:口服避孕药 == 1
	PRINTFORM <口服避孕药>
SIF TEQUIP:TARGET:避孕套
	PRINTFORM <避孕套>
	;魔改假避孕药
SIF TCVAR:TARGET:口服避孕药（假） == 1
	PRINTFORM <口服避孕药（假）>
SIF TEQUIP:TARGET:阴蒂夹
	PRINTFORM <阴蒂夹>
SIF TEQUIP:TARGET:飞机杯
	PRINTFORM <飞机杯>
SIF TEQUIP:TARGET:振动棒
	PRINTFORM <振动棒>
SIF TEQUIP:TARGET:肛用振动棒
	PRINTFORM <肛用振动棒>
SIF TEQUIP:TARGET:后庭拉珠
	PRINTFORM <后庭拉珠>
SIF TEQUIP:TARGET:乳头夹
	PRINTFORM <乳头夹>
SIF TEQUIP:TARGET:搾乳机
	PRINTFORM <搾乳器>
SIF TEQUIP:TARGET:眼罩
	PRINTFORM <眼罩>
SIF TEQUIP:TARGET:绳
	PRINTFORM <用绳子捆绑>
SIF TEQUIP:TARGET:口球
	PRINTFORM <口球>
SIF TEQUIP:TARGET:浣肠
	PRINTFORM <浣肠＋肛塞>
SIF TEQUIP:TARGET:扩张气球
	PRINTFORM <扩张气球>
SIF TEQUIP:TARGET:肛门电极
	PRINTFORM <肛门电极>
;SIF TEQUIP:TARGET:24
;	PRINTFORM <乳房电极装着中>
SIF TEQUIP:TARGET:拘束
	PRINTFORM <拘束中>
;TEQUIPの方は使われていない模样
;SIF TEQUIP:TARGET:媚药
;	PRINTFORM <媚药效果中>
;SIF TEQUIP:TARGET:利尿剂
;	PRINTFORM <利尿剂效果中>
SIF TCVAR:TARGET:媚药
	PRINTFORM <媚药效果中>
SIF TCVAR:TARGET:利尿剂
	PRINTFORM <利尿剂效果中>
SIF TCVAR:TARGET:ポイズンゲージ
	PRINTFORM <媚毒浸透中>
SIF TEQUIP:TARGET:视频拍摄
	PRINTFORM <视频拍摄中({TEQUIP:TARGET:视频拍摄})>
SIF TEQUIP:TARGET:野外PLAY
	PRINTFORM <野外PLAY中>
SIF TEQUIP:TARGET:羞耻PLAY == 1
	PRINTFORM <羞耻PLAY中>
SIF TEQUIP:TARGET:浴室PLAY
	PRINTFORM <浴室PLAY中>
SIF TEQUIP:TARGET:淋浴
	PRINTFORM <淋浴>
SIF TEQUIP:TARGET:新妻PLAY
	PRINTFORM <新妻PLAY中>
SIF TEQUIP:TARGET:插入禁止
	PRINTFORM <插入禁止>
;SIF TEQUIP:TARGET:继续接吻
;	PRINTFORM <接吻中>

PRINTL 

;ココより↑にTEQUIPに登录したものを书き込む

;FileName_INFO.ERB --------------------------------- Rev1.00
;跳蛋表示处理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	你の使用状态
;-----------------------------------------------------------
@SHOW_EQUIP_3
#DIM 吻痕の相手

SIF !CHARA_EQUIP(3)
	RETURN
吻痕の相手 = TFLAG:吻痕
PRINTFORM 携行中(%NAME_OUTPUT_TRANSLATION("CALLNAME", PLAYER)%) 
;料理の情报
SIF DISH_NAME != ""
	CALL INFO_DISH

IF TFLAG:吻痕 && !TFLAG:绷带
	PRINTFORM <%NAME_OUTPUT_TRANSLATION("CALLNAME", 吻痕の相手)%弄的吻痕>
ELSEIF TFLAG:吻痕 && TFLAG:绷带
	PRINTFORM <%NAME_OUTPUT_TRANSLATION("CALLNAME", 吻痕の相手)%弄的吻痕(隐蔽中)>
ENDIF
SIF TCVAR:MASTER:口服避孕药 == 1
	PRINTFORM <口服避孕药>
SIF TEQUIP:MASTER:避孕套
	PRINTFORM <避孕套>
SIF FLAG:胖次输送
	PRINTFORM <别人的胖次>
SIF CFLAG:PLAYER:跳蛋插入
	PRINTFORM <跳蛋(插入中)>
SIF CFLAG:PLAYER:跳蛋A插入
	PRINTFORM <肛门跳蛋(插入中)>
SIF TFLAG:パール期限
	PRINTFORM <肛门拉珠({TFLAG:パール插入个数}玉插入中 \@ TFLAG:パール期限 > TIME ? 还有{TFLAG:パール期限 - TIME}分# \@)>
;围裙はTARGET（纷らわしい）
SIF TEQUIP:TARGET:围裙
	PRINTFORM <围裙>

;你に效果を发挥している药などを表记
IF TFLAG:仙香玉兔
	IF TFLAG:仙香玉兔 >= 120
		CALL COLORMESSAGE("<仙香玉兔>", C_PINK, 0, 1)
	ELSE
		CALL COLORMESSAGE("<仙香玉兔>", C_D_PINK, 0, 0)
	ENDIF
ENDIF
SIF TCVAR:MASTER:国士无双（药）
	PRINTFORM <国士无双（药）×{TCVAR:MASTER:国士无双（药）}>

IF TFLAG:剧毒身躯
	CALL COLORMESSAGE("<剧毒身躯>", C_D_PINK, 0, TFLAG:剧毒身躯 >= 120)
ENDIF

;幸运补正
IF TFLAG:幸运补正 > 0
	PRINTFORM <幸运>
ELSEIF TFLAG:幸运补正 < 0
	PRINTFORM <不幸>
ENDIF

;料理Buff
SIF TCVAR:MASTER:祝福
	PRINTFORM <祝福>
SIF TCVAR:MASTER:魔性
	PRINTFORM <ＰUP>
SIF TCVAR:MASTER:加护
	PRINTFORM <加护>
SIF TCVAR:MASTER:丰穣
	PRINTFORM <丰穣>

PRINTL 

;FileName_INFO.ERB --------------------------------- Rev1.00
;装备状态判定
;@SHOW_EQUIP_1、@SHOW_EQUIP_3で使用ので装备品追加したら追记のこと
;ARG = SHOW_EQUIP_{ARG} での参照元
;PLAYER_EQUIPを改良しました
;-----------------------------------------------------------
@CHARA_EQUIP(ARG)
#FUNCTION
IF ARG == 1
	SIF TCVAR:TARGET:口服避孕药 == 1
		RETURNF 1
	SIF TEQUIP:TARGET:避孕套
		RETURNF 1
	SIF TEQUIP:TARGET:阴蒂夹
		RETURNF 1
	SIF TEQUIP:TARGET:飞机杯
		RETURNF 1
	SIF TEQUIP:TARGET:振动棒
		RETURNF 1
	SIF TEQUIP:TARGET:肛用振动棒
		RETURNF 1
	SIF TEQUIP:TARGET:后庭拉珠
		RETURNF 1
	SIF TEQUIP:TARGET:乳头夹
		RETURNF 1
	SIF TEQUIP:TARGET:搾乳机
		RETURNF 1
	SIF TEQUIP:TARGET:眼罩
		RETURNF 1
	SIF TEQUIP:TARGET:绳
		RETURNF 1
	SIF TEQUIP:TARGET:口球
		RETURNF 1
	SIF TEQUIP:TARGET:浣肠
		RETURNF 1
	SIF TEQUIP:TARGET:扩张气球
		RETURNF 1
	SIF TEQUIP:TARGET:肛门电极
		RETURNF 1
;	SIF TEQUIP:TARGET:24
;		RETURNF 1
	SIF TEQUIP:TARGET:拘束
		RETURNF 1
	;TEQUIPは使われていない模样
;	SIF TEQUIP:TARGET:媚药
;		RETURNF 1
;	SIF TEQUIP:TARGET:利尿剂
;		RETURNF 1
	SIF TCVAR:TARGET:媚药
		RETURNF 1
	SIF TCVAR:TARGET:利尿剂
		RETURNF 1
	SIF TCVAR:TARGET:ポイズンゲージ
		RETURNF 1
	SIF TEQUIP:TARGET:视频拍摄
		RETURNF 1
	SIF TEQUIP:TARGET:野外PLAY
		RETURNF 1
	SIF TEQUIP:TARGET:羞耻PLAY == 1
		RETURNF 1
	SIF TEQUIP:TARGET:浴室PLAY
		RETURNF 1
	SIF TEQUIP:TARGET:淋浴
		RETURNF 1
	SIF TEQUIP:TARGET:新妻PLAY
		RETURNF 1
	SIF TEQUIP:TARGET:插入禁止
		RETURNF 1
;	SIF TEQUIP:TARGET:继续接吻
;		RETURNF 1
ELSEIF ARG == 3
	SIF TFLAG:吻痕
		RETURNF 1
	SIF FLAG:胖次输送
		RETURNF 1
	SIF CFLAG:PLAYER:跳蛋插入
		RETURNF 1
	SIF CFLAG:PLAYER:跳蛋A插入
		RETURNF 1
	SIF TFLAG:パール期限
		RETURNF 1
	SIF TFLAG:仙香玉兔
		RETURNF 1
	SIF TCVAR:MASTER:国士无双（药）
		RETURNF 1
	SIF TFLAG:剧毒身躯
		RETURNF 1
	SIF TCVAR:MASTER:口服避孕药 == 1
		RETURNF 1
	SIF TEQUIP:MASTER:避孕套
		RETURNF 1
	;围裙はTARGET（纷らわしい）
	SIF TEQUIP:TARGET:围裙
		RETURNF 1
	SIF DISH_NAME != ""
		RETURNF 1
	SIF TFLAG:幸运补正
		RETURNF 1
	SIF TCVAR:MASTER:祝福
		RETURNF 1
	SIF TCVAR:MASTER:魔性
		RETURNF 1
	SIF TCVAR:MASTER:加护
		RETURNF 1
	SIF TCVAR:MASTER:丰穣
		RETURNF 1
ENDIF
RETURNF 0

;FileName_INFO.ERB --------------------------------- Rev1.00
;パラメータ表示处理
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@SHOW_INFO_PALAM
CALL PRINTBUTTON_EX(TFLAG:PALAM非表示 ? "▽[＋][Palam]---------" # "▼[－][Palam]---------", 3000, TFLAG:PALAM非表示)
PRINTL 
VARSET LOCAL
IF !TFLAG:PALAM非表示
	FOR LOCAL,0,100
		SIF !STRLENS(PALAMNAME:LOCAL)
			CONTINUE
		; SIF LOCAL <= 4 && EX:TARGET:(LOCAL + 20)
		; 	SETCOLOR C_YELLOW
		; 若寸止重数 > 1，则用不同颜色表示
        IF LOCAL <= 4 && EX:TARGET:(LOCAL + 20)
            SELECTCASE (EX:TARGET:(LOCAL + 20))
                CASE 1
                    SETCOLOR 0xFFFF00  ; Yellow
                CASE 2
                    SETCOLOR 0xFFBF00  ; Yellow-Orange
                CASE 3
                    SETCOLOR 0xFF7F00  ; Orange
                CASE 4
                    SETCOLOR 0xFF3F00  ; Orange-Red
                CASE 5
                    SETCOLOR 0xFF0000  ; Red
                CASE 6
                    SETCOLOR 0xFF006F  ; Red-Pink
                CASE 7
                    SETCOLOR 0xFF00BF  ; Pink
                CASE 8
                    SETCOLOR 0xDF00BF  ; Pink-Purple
                CASEELSE
                    SETCOLOR 0xBF00BF  ; Purple
            ENDSELECT
        ENDIF
		PRINTFORM  %PALAMNAME:LOCAL%Lv{GETPALAMLV(PALAM:LOCAL,15),2}
		RESETCOLOR
		IF GETPALAMLV(PALAM:LOCAL,15) >= 15
			CALL PRINT_COLORBAR, 0, 1, 8, UNICODE(0x2584), UNICODE(0x2584), BARCOLORSET("青"), RESULT:1
		ELSE
			;现在的PALAMLV
			LOCAL:1 = PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) - PALAMLV:(GETPALAMLV(PALAM:LOCAL,15))
			LOCAL:4 = PALAM:LOCAL - PALAMLV:(GETPALAMLV(PALAM:LOCAL,15))
			IF LOCAL < 4
				CALL PRINT_COLORBAR, PALAM:LOCAL, 10000, 8, UNICODE(0x2584), UNICODE(0x2584), BARCOLORSET("青"), RESULT:1
			ELSE
				CALL PRINT_COLORBAR, LOCAL:4, LOCAL:1, 8, UNICODE(0x2584), UNICODE(0x2584), BARCOLORSET("青"), RESULT:1
			ENDIF
	;		NEXT
		ENDIF
		LOCAL:1 = PALAM:LOCAL
		LOCAL:2 = 0
		;これだけ特殊
		IF LOCAL == 9 && LOCAL:1 >= 100000
			PRINTFORM %"摩擦 0",6% 
		ELSE
			;桁合わせ
			FOR LOCAL:10,0,10
				IF LOCAL:1 > 999999
					LOCAL:1 /= 1000
					LOCAL:2 ++
				ENDIF
			NEXT
			PRINTFORM {LOCAL:1,6}
			SELECTCASE LOCAL:2
				CASE 0
					PRINT  
				CASE 1
					PRINT k
				CASE 2
					PRINT M
				CASE 3
					PRINT G
				CASE 4
					PRINT T
				CASE 5
					PRINT P
			ENDSELECT
		ENDIF
		LOCAL:3 ++
		SIF LOCAL:3 % PRINTCPERLINE() == 0
			PRINTL 
	NEXT
ENDIF

;-------------------------------------------------
;函数名:SORT_CFLAG
;CFLAG:ARGの升顺でRESULT:1に角色番号を代入する
;ARG CFLAGの番号
;-------------------------------------------------
@SORT_CFLAG(ARG)
VARSET LOCAL
VARSET RESULT
FOR LOCAL:10,1,CHARANUM
	LOCAL:1 = 0
	FOR LOCAL,1,CHARANUM
		SIF MATCH(RESULT,LOCAL)
			CONTINUE
		IF CFLAG:LOCAL:ARG >= LOCAL:1
			LOCAL:1 = CFLAG:LOCAL:ARG
			LOCAL:2 = LOCAL
		ENDIF
	NEXT
	RESULT:(LOCAL:10) = LOCAL:2
NEXT
;FileName_INFO.ERB --------------------------------- Rev1.00
;料理鲜度チェック
;CALL		USER
;ARG		VOID
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@DISH_FRESHNESS
#DIM FRESH_MESSAGE
SIF DISH_LIMIT >= DISH_MAXLIMIT
	FRESH_MESSAGE = 0
DISH_LIMIT -= TIME_PROGRESS(TIME:1)
SIF DISH_LIMIT <= DISH_MAXLIMIT * 4 / 5
	DISH_TASTE '= REPLACE(DISH_TASTE, "できたて\/", "")
IF DISH_LIMIT <= DISH_MAXLIMIT * -1
	CALL COLORMESSAGE(DISH_NAME + "实在是咽不下去！", C_RED, 2)
	PRINTL
	CALL COLORMESSAGE(CALLNAME:MASTER + "把" + DISH_NAME + "废弃了", C_RED, 2)
	CALL RESET_DISH
	FRESH_MESSAGE = 0
ELSEIF DISH_LIMIT <= 0 && FRESH_MESSAGE < 2
	CALL COLORMESSAGE(DISH_NAME + "散发出了恶臭…", C_ORANGE, 2)
	DISH_TASTE '= REPLACE(DISH_TASTE, "冷\/", "")
	DISH_TASTE '= REPLACE(DISH_TASTE, "温暖\/", "")
	DISH_TASTE += "劣化/" * 2
	FRESH_MESSAGE = 2
ELSEIF DISH_LIMIT <= DISH_MAXLIMIT * 1 / 2 && FRESH_MESSAGE < 1
	IF STRCOUNT(DISH_TASTE, "冰淇淋/")
		PRINTFORMW %DISH_NAME%融化了…
		DISH_TASTE '= REPLACE(DISH_TASTE, "冷\/", "")
	ELSEIF STRCOUNT(DISH_TASTE, "冷/")
		PRINTFORMW %DISH_NAME%变得黏糊糊的了
		DISH_TASTE '= REPLACE(DISH_TASTE, "冷\/", "")
	ELSE
		PRINTFORMW %DISH_NAME%冷掉了…
		DISH_TASTE '= REPLACE(DISH_TASTE, "温暖\/", "")
	ENDIF
	DISH_TASTE += "劣化/"
	FRESH_MESSAGE = 1
ENDIF

;-------------------------------------------------
;古典时刻函数@CLASSICAL_TIME
;	参数0：对象の变量（デフォでは秒）
;	参数1：1なら参数0を秒で、2なら时で计算する
;	参数2：表示形式（デフォ「○の○つ」、1=「○の刻、○つ」、2=「○の○つ　-○○-」）
;式中で使用する函数です。参数に记录されている数字を秒として、现在的时刻を文字列で返します
;古典时刻表记なので、30分单位未满の端数は切り舍てます。
;-------------------------------------------------
@CLASSICAL_TIME(ARG, ARG:1, ARG:2)
#FUNCTIONS
;0:00が子の三つなので、60分を足す
ARG += 60
;日期も管理しているケースを考え、24时间（1440分）で割ってあまりを取る（子の一つ～二つもここで对応される）
ARG %= 1440
;表示
SIF GET_CLASSICAL_HOUR(ARG, 1) == "丑" && (ARG%120)/30 == 2
	RETURNF "丑三つ时"
RETURNF @"%GET_CLASSICAL_HOUR(ARG, 1)%时\@ ARG:2 == 1 ? 刻、 # \@%TOKANJI(((ARG%120)/30)+1)%点\@ ARG:2 == 2 ?  -%GET_CLASSICAL_HOUR(ARG, 1, 1)%- # \@"

;时を取得
;参数2を真にすると、大まかな时间の样子を取る
@GET_CLASSICAL_HOUR(ARG, ARG:1, ARG:2)
#FUNCTIONS
;分に变换する
SELECTCASE ARG:1
	;时で渡す场合
	CASE 2
		ARG *= 60
	;分で渡す场合（处理无）
	CASE 1
	;秒で渡す场合（默认）
	CASEELSE
		ARG /= 60
ENDSELECT

SELECTCASE ARG/120
	CASE 0
		RETURNF \@ ARG:2 ? 深更 # 子 \@
	CASE 1
		RETURNF \@ ARG:2 ? 未明 # 丑 \@
	CASE 2
		RETURNF \@ ARG:2 ? 东云 # 寅 \@
	CASE 3
		RETURNF \@ ARG:2 ? 暁 # 卯 \@
	CASE 4
		RETURNF \@ ARG:2 ? 早天 # 辰 \@
	CASE 5
		RETURNF \@ ARG:2 ? 小昼 # 巳 \@
	CASE 6
		RETURNF \@ ARG:2 ? 昼中 # 午 \@
	CASE 7
		RETURNF \@ ARG:2 ? 日盛 # 未 \@
	CASE 8
		RETURNF \@ ARG:2 ? 终日 # 申 \@
	CASE 9
		RETURNF \@ ARG:2 ? 黄昏 # 酉 \@
	CASE 10
		RETURNF \@ ARG:2 ? 宵 # 戌 \@
	CASE 11
		RETURNF \@ ARG:2 ? 夜半 # 亥 \@
ENDSELECT

;-------------------------------------------------
;数值→汉数字变换函数@TOKANJI
;TOSTR函数チックに、参数の数值を汉数字の文字列で返します
;变量の最大值は约922京であるため、一、万、亿、兆、京の5回处理します
;-------------------------------------------------
@TOKANJI(ARG)
#FUNCTIONS
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LCOUNT, 1
VARSET LOCALS
FOR LCOUNT, 0, 5
	LOCAL = ARG/POWER(10000, (4-LCOUNT))
	ARG %= POWER(10000, (4-LCOUNT))
	IF LOCAL
		LOCALS += TOKANJIT(LOCAL)
		SELECTCASE LCOUNT
			CASE 0
				LOCALS += "京"
			CASE 1
				LOCALS += "兆"
			CASE 2
				LOCALS += "亿"
			CASE 3
				LOCALS += "万"
		ENDSELECT
	ENDIF
NEXT
RETURNF \@ LOCALS != "" ? %LOCALS% # 零 \@

;内部用or简易用
;四桁以内の汉字变换。万以上与えるとバグ
@TOKANJIT(ARG)
#FUNCTIONS
SIF ARG >= 10000
	RETURNF "ＢＵＧ"
LOCALS = \@ ARG/1000 ? %TOKANJIS(ARG/1000, 1)%千 # \@
ARG %= 1000
LOCALS += \@ ARG/100 ? %TOKANJIS(ARG/100, 1)%百 # \@
ARG %= 100
LOCALS += \@ ARG/10 ? %TOKANJIS(ARG/10, 1)%十 # \@
ARG %= 10
RETURNF LOCALS + TOKANJIS(ARG)

;一桁の变换。参数1を真にすると一を省略する。2以上にすると零を表示する
@TOKANJIS(ARG, ARG:1)
#FUNCTIONS
SELECTCASE ARG
	CASE 0
		RETURNF \@ ARG:1 > 1 ? 零 #  \@
	CASE 1
		RETURNF \@ ARG:1 ?  # 一 \@
	CASE 2
		RETURNF "二"
	CASE 3
		RETURNF "三"
	CASE 4
		RETURNF "四"
	CASE 5
		RETURNF "五"
	CASE 6
		RETURNF "六"
	CASE 7
		RETURNF "七"
	CASE 8
		RETURNF "八"
	CASE 9
		RETURNF "九"
	CASE 10
		RETURNF "十"
	CASEELSE
		RETURNF "ＢＵＧ"
ENDSELECT



;-------------------------------------------------
;时间经过、角色の移动
;-------------------------------------------------
@INFO_TIME_PASSING
#DIM MOODBREAKER
IF TFLAG:远距离移动
	;TFLAGを戻す（远距离移动フラグ）
	TFLAG:远距离移动 = 0
ELSEIF TIME_PROGRESS(TIME:1)
	;角色移动
	CALL CHARA_MOVEMENT
	IF TCVAR:MASTER:亲热 >= 1
		MOODBREAKER = IN_ROOM_MEMBER("MIN", CFLAG:MASTER:当前位置, "CFLAG", 6)
		LOCAL:2 = CFLAG:MOODBREAKER:态度
		;亲热强度０以下だと空气が变わる
		IF LOCAL:2 <= 0 && MOODBREAKER > 0 && CFLAG:MOODBREAKER:当前位置 != CFLAG:MOODBREAKER:前一次的位置 && !CFLAG:MOODBREAKER:睡眠
			SIF PREVCOM != 400
				PRINTFORMW 由于%NAME_OUTPUT_TRANSLATION("CALLNAME", MOODBREAKER)%的进入、现场的气氛改变了…
			FOR LOCAL, 0, CHARANUM
				;同室判定前なので同室フラグは使えない
				SIF CFLAG:LOCAL:当前位置 == CFLAG:MASTER:当前位置 && CFLAG:LOCAL:态度 >= 1 && !CFLAG:LOCAL:睡眠 && PREVCOM != 400
					CALL KOJO_MESSAGE_SEND("EVENT",5,LOCAL,MOODBREAKER)
				;恶作剧OFF
				CALL SET_PRANK(0,LOCAL,2)
				;隐密中OFF
				CALL SET_SNEAK(0,LOCAL,2)
			NEXT
		ENDIF
	ENDIF
	SIF WEATHERDLC
		CALL WEATHERCHANGESTAUS
ENDIF
TIME:1 = TIME + 1440 * DAY
CALL SET_SLEEP(0,MASTER,0)



;-------------------------------------------------
;TARGET关联の角色变量の更新
;-------------------------------------------------
@INFO_RENEW_TARGET
CALL TARGETSET_CHACK("「同室更新无し」")
;MASTERと同室フラグ
FOR LOCAL, 1, CHARANUM
	;口上の基本フラグ设定
	CALL KOJO_MESSAGE_SEND("FLAGSETTING", 0, LOCAL)

	;当前位置がMASTERと同じなら同室
	IF CFLAG:LOCAL:当前位置 == CFLAG:MASTER:当前位置
		RESULT = 0
		;まだ同室フラグが立っていない(现ターンで同室化）かつ眠っていない
		SIF !CFLAG:LOCAL:同室 && !CFLAG:LOCAL:睡眠
			CALL AFFAIR_DISCLOSURE(LOCAL, 0)
		;情事の发觉 AFFAIR_DISCLOSURE で立ち去った场合は同室が立たない
		SIF !RESULT
			CFLAG:LOCAL:同室 = 1
	ELSE
		;相手からこちらが见えていて、相手かこちらかが移动したかこちらが推倒た
		IF CAN_MOVE(CFLAG:LOCAL:当前位置, CFLAG:MASTER:当前位置) & 2 && !CFLAG:LOCAL:睡眠 && PREVCOM >= 0 && FLAG:70 != 1
			SIF (CFLAG:MASTER:前一次的位置 != CFLAG:MASTER:当前位置 || CFLAG:LOCAL:前一次的位置 != CFLAG:LOCAL:当前位置 || TRAINNAME:PREVCOM == "推倒")
				CALL AFFAIR_DISCLOSURE(LOCAL, 1)
		ENDIF
		CFLAG:LOCAL:同室 = 0
	ENDIF
NEXT
CFLAG:MASTER:你前一次的位置 = CFLAG:MASTER:当前位置

;亲热强度决定
;TARGET侧
FOR LOCAL, 1, GET_TARGETNUM() + 1
	;合意か把柄がないと3にならない
	IF GETBIT(CFLAG:(TARGET:LOCAL):既成事实, 0) || CFLAG:(TARGET:LOCAL):被掌握把柄
		LOCAL:1 = CFLAG:(TARGET:LOCAL):态度
	ELSE
		LOCAL:1 = MIN(CFLAG:(TARGET:LOCAL):态度, 2)
	ENDIF
	TCVAR:(TARGET:LOCAL):亲热 = LOCAL:1
	;TARGET:2以降は、MIN(直前のTARGETのTCVAR:130,自己的CFLAG:6)
	SIF LOCAL >= 2
		TCVAR:(TARGET:LOCAL):亲热 = MIN(TCVAR:(TARGET:(LOCAL - 1)):亲热, TCVAR:(TARGET:LOCAL):亲热)
NEXT

;MASTERはTARGET:1～のCFLAG:6の最低值
TCVAR:MASTER:亲热 = TCVAR:(IN_ROOM_MEMBER("MIN", CFLAG:MASTER:当前位置, "TCVAR", 130)):亲热


;-------------------------------------------------
;ウフフ结束TEQUIP处理
;EQUIPの更新もここに仕込んでおく
;-------------------------------------------------
@INFO_RENEW_EQUIP
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:诶嘿嘿 && !BATHROOM(CFLAG:LOCAL:当前位置) && !睡眠时间(LOCAL)
		CONTINUE
	SIF LOCAL == TARGET && CFLAG:LOCAL:当前位置 != CFLAG:LOCAL:前一次的位置
		CALL ENDUFUFU(LOCAL)
	;服装更新系
	;メンドイのでまとめて
	SIF !BATHROOM(CFLAG:LOCAL:当前位置)
		TEQUIP:(LOCAL):浴室PLAY = 0
	;澡堂の中なら全裸
	IF BATHROOM(CFLAG:LOCAL:当前位置)
		SIF EQUIP:LOCAL:0 && !(TFLAG:泡澡堂 && TALENT:LOCAL:风俗娘)  
			CALL 脱衣处理(LOCAL, "全裸", "「表示无」")
		IF CFLAG:LOCAL:诶嘿嘿
			; || TCVAR:LOCAL:亲热 > 2
			TEQUIP:(LOCAL):浴室PLAY = 1
		ELSE
			TEQUIP:(LOCAL):浴室PLAY = 0
		ENDIF
	;诶嘿嘿/恶作剧中は复原衣服を抑制
	ELSEIF !(CFLAG:LOCAL:诶嘿嘿 || CFLAG:LOCAL:恶作剧)
		CALL CHANGE_CLOTHES(LOCAL, "复原衣服")
	ENDIF
	CALL CLOTHES_SETTING_TRAIN(LOCAL)
	
	;共用雨伞フラグ（同行依存）
	;共用雨伞できるのはターゲットだけ
	SIF LOCAL != 0 && !CHK_DATENOW(CFLAG:LOCAL:约会中) && (!CFLAG:LOCAL:同行 || LOCAL != TARGET)
		TEQUIP:LOCAL:201 = 0
	;共用雨伞フラグ（同行依存）
	;约会中持续させる处理＋ターゲットと共用雨伞する处理＋连れ出した后外出了时雨が降っていたら共用雨伞する处理
	SIF LOCAL != 0 && (CHK_DATENOW(CFLAG:LOCAL:约会中) || CFLAG:LOCAL:同行) && LOCAL == TARGET && TIME:5 > 3 && TIME:5 < 14 && OUTROOF(CFLAG:MASTER:当前位置) && (ITEM:36 || (TARGET == 16 || TARGET == 26 || TARGET == 40 || TARGET == 68))
		TEQUIP:LOCAL:201 = 1
NEXT


;天气の表示/非表示の更新
;天界への行き来の际の天气の更新
@INFO_CULC_WEATHER(天气非表示)
#DIM REF 天气非表示
#DIM     元の天气
天气非表示 = 0
IF CFLAG:MASTER:约会中 == 9 || CFLAG:MASTER:当前位置 == 槐安通路
	天气非表示 = 1
ELSEIF CFLAG:MASTER:当前位置 == OMANEKIBEYA() && GROUPMATCH(CFLAG:MASTER:招待, 36, 37, 38, 49, 60, 64, 83, 84)
	天气非表示 = 1
ENDIF
IF CFLAG:MASTER:当前位置 == 天界 && !元の天气
	元の天气 = TIME:5
	TIME:5 = 1
ELSEIF CFLAG:MASTER:当前位置 != 天界 && 元の天气
	TIME:5 = 元の天气
	元の天气 = 0
ENDIF



;「一日が终わりました」表示と宴会关联の更新处理など
@INFO_GO_NEXTDAY
SIF TIME <= 1440
	RETURN
PRINTL  
IF !CFLAG:MASTER:诶嘿嘿
	IF FESTIVAL() == "大晦日"
		PRINTW HAPPY NEW YEAR!!
		PRINTW 因为是海螺小姐时空，所以今年也是20XX年！
		FOR LOCAL, 1, CHARANUM
			;ハッピーニューイヤー口上
			SIF CFLAG:LOCAL:当前位置 == CFLAG:MASTER:当前位置
				CALL KOJO_MESSAGE_SEND("EVENT", 33, LOCAL)
		NEXT
	ELSE
		PRINTW 一天结束了
	ENDIF
ENDIF
PRINTL  
;宴会当日に一日が终わると宴会フラグOFF
IF DAY > FLAG:开始日 && FLAG:宴会举办FLAG
	CALL ENKAI_END()
ENDIF
;宴会の举办判定
SIF FLAG:3 && FLAG:宴会举办FLAG == 0
	CALL ENKAI_SETTING
;---------------------------
;时间の经过
DAY ++
DAY:3 ++
TIME -= 1440
CALL CHANGE_MONTH
;(天候パッチ)短期予报を行う
CALL WEATHER_FORECAST_1TERM
;(天候パッチ)1日予报を行う
CALL WEATHER_FORECAST_1DAY

;---------------------------
;宴会の参加者决定
IF FLAG:3 && 宴会开始日判定()
	CALL ENKAI_ENTRY
	SIF EATDLC
		CALL MY_ENKAI_ENTRY
	FOR LOCAL, 1, CHARANUM
		;自宅在住角色なら参加者来访しない
		SIF CFLAG:LOCAL:神社在住 > 0
			CONTINUE
		IF CFLAG:LOCAL:宴会参加
			CFLAG:LOCAL:据点来访 = 0
			CFLAG:LOCAL:遭遇位置 = 0
		ENDIF
	NEXT
ENDIF


;日时と祝日の表示（改行无）
@INFO_TIME_WATCH
PRINTFORM %GET_MONTH()% {DAY:3}日目(
;%GET_DAY()%)
CALL COLOR_YOUBI
PRINTFORM )
SETCOLOR C_RED
PRINTFORM %FESTIVAL()%
RESETCOLOR
PRINTFORM %时刻表示(TIME)% 
PRINTFORM (%CLASSICAL_TIME(TIME, 1, 2)%)

;色付きの曜日表示
@COLOR_YOUBI
IF GET_DAY() == "日"
	SETCOLOR C_FLAME
ELSEIF GET_DAY() == "土"
	SETCOLOR C_ROYAL_BLUE
ENDIF
PRINTFORM %GET_DAY()%
RESETCOLOR
;-------------------------------------------------
;	钟表画像を表示
;
;	ARG		：时刻
;	iSize	：フォントに对する尺寸％数值　纵基准　纵横比维持　省略で1000(10倍)
;	sAlign	：align属性　省略で右寄せ　left, center, rightの三种が指定できます
;-------------------------------------------------
@INFO_CLOCK_IMAGE(ARG, iSize=500,sAlign="right")
#DIM iSize
#DIMS sAlign
#DIMS sPrint_Text
#DIM iEnter_Con
#DIM iFont_Hei_mag
#DIM YPOS

iEnter_Con = 4
iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5?1#0)
YPOS = iEnter_Con * iFont_Hei_mag / 10+1
sPrint_Text =  <p align='%sAlign%'><nobr>
sPrint_Text += @"<img src='%GET_CLOCK_IMAGE_NAME(ARG)%' height='{iFont_Hei_mag}'"
sPrint_Text += @"ypos='{YPOS}'>"
sPrint_Text += @"</p>"

;PRINTFORM %sPrint_Text%
HTML_PRINT sPrint_Text

;-------------------------------------------------
;	钟表画像名を取得
;
;	ARG	：时刻
;-------------------------------------------------
@GET_CLOCK_IMAGE_NAME(ARG)
#FUNCTIONS
#DIM HOUR
#DIM MINUTE
#DIMS IMAGE_NAME
HOUR   = ARG / 60 % 12
MINUTE = ARG % 60 / 5 * 5
IMAGE_NAME = 钟表_%TOSTR(HOUR,"00")%_%TOSTR(MINUTE,"00")%
RETURNF IMAGE_NAME

;天气と月龄の表示
@INFO_SHOW_WEATHER
SIF FLAG:异常气象 >= 1 && FLAG:异常气象 <= 8
	SETCOLOR C_PINK
;(天候パッチ)豪雨を追加
SIF TIME:5 == 14
	SETCOLOR C_P_PURPLE
PRINTFORM ― %GET_WEATHER()% ― 
RESETCOLOR
;06时から18时までは昼间
IF WEATHERDLC
	CALL WEATHERSTAUSSHOW
	GOTO SKIP
ENDIF

IF TIME:5 == 0 || TIME:5 == 1
	IF TIME:2 >= 2 && TIME:2 <= 4
		SETCOLOR 0xFF9640
		PRINTFORM %UNICODE(0x2600)%
	ELSE
		;19时から06时までは夜间
		SETCOLOR 0xFFFF40
		PRINTFORM %UNICODE(0x262A)%
			;俗气のので保留
			;IF MOON_WATCH(DAY:3) > 0
			;PRINTFORM %UNICODE(0x262A)%
			;ELSE
			;UNICODE 0x2728
			;PRINT ★
			;ENDIF
	ENDIF
ELSEIF TIME:5 == 2 || TIME:5 == 3
	SETCOLOR 0x9C9C9C
	PRINTFORM %UNICODE(0x2601)%
ELSEIF (TIME:5 == 4 || TIME:5 == 5 || TIME:5 == 6 || TIME:5 == 7 || TIME:5 == 14 || (TIME:5 == 13 && GET_HEAT_THUNDERSTORM() == 1)) && TEQUIP:TARGET:201 && OUTROOF(CFLAG:MASTER:当前位置)
	SETCOLOR 0xFF99CC
	PRINTFORM %UNICODE(0x2602)% 
	SETCOLOR 0xFFCCFF
	CALL HEART
	PRINTFORM 共用雨伞
	CALL HEART
ELSEIF TIME:5 == 4 || TIME:5 == 5 || TIME:5 == 6 || TIME:5 == 7
	SETCOLOR 0x0000FF
	PRINTFORM %UNICODE(0x2602)%
;(天候パッチ)豪雨を追加
ELSEIF TIME:5 == 14
	SETCOLOR C_气象_泥石流灾害警戒情报
	PRINTFORM %UNICODE(0x2602)%
;(天候パッチ)ひょうを追加
ELSEIF TIME:5 == 15
	SETCOLOR C_AQUA
	PRINTFORM ◆
;(天候パッチ)冰雹を修正
ELSEIF TIME:5 == 13
	SETCOLOR C_AQUA
	PRINTFORM ∵
ELSEIF (TIME:5 >= 8 && TIME:5 <= 13) && TEQUIP:TARGET:201 && OUTROOF(CFLAG:MASTER:当前位置)
	SETCOLOR 0xFFCCFF
	PRINTFORM %UNICODE(0x2603)% 
	SETCOLOR 0xFF99CC
	CALL HEART
	PRINTFORM 共用雨伞
	CALL HEART
ELSEIF TIME:5 >= 8 && TIME:5 <= 13
	SETCOLOR 0xFFFFFF
	PRINTFORM %UNICODE(0x2603)%
ENDIF
;(天候パッチ)雷
IF GET_FRONTAL_THUNDERSTORM() == 1 && GET_HEAT_THUNDERSTORM() == 1 && GET_WEATHER_THUNDERSTORM() == 1
	SETCOLOR C_YELLOW
	PRINTFORM %UNICODE(0x26A1)%
	PRINTFORM %UNICODE(0x26A1)%
ELSEIF (GET_FRONTAL_THUNDERSTORM() == 1 || GET_HEAT_THUNDERSTORM() == 1) && GET_WEATHER_THUNDERSTORM() == 1
	SETCOLOR C_YELLOW
	PRINTFORM %UNICODE(0x26A1)%
ENDIF
RESETCOLOR

$SKIP

IF TIME:7 == 1
	SETCOLOR 0xFF0000
	PRINT /
	SETCOLOR 0xFFCC00
	PRINT /
	SETCOLOR 0xFFFF00
	PRINT /
	SETCOLOR 0x66CC00
	PRINT /
	SETCOLOR 0x3399FF
	PRINT /
	SETCOLOR 0x0033CC
	PRINT /
	SETCOLOR 0x990099
	PRINT /
ELSEIF TIME:7 == 2
	SETCOLOR 0xFFFFFF
	PRINTFORM +。*:゜+:
ENDIF
RESETCOLOR
CALL INFO_MOON

;(天候パッチ)风速の表示
SELECTCASE WIND_VELOCITY
	CASE 1
		PRINT 　
		SETCOLOR C_气象_早期警报
		PRINT 强风
	CASE 2
		PRINT 　
		SETCOLOR C_气象_早期警报
		PRINTFORM %UNICODE(0x26A0)%
		SETCOLOR C_气象_警报
		PRINT 暴风
	CASE 3
		PRINT 　
		SETCOLOR C_气象_早期警报
		PRINTFORM %UNICODE(0x26A0)%
		SETCOLOR C_气象_泥石流灾害警戒情报
		PRINT 飓风
ENDSELECT
RESETCOLOR

;月龄の表示
@INFO_MOON
#DIM MOON
MOON = MOON_WATCH(DAY:3)
SIF MOON <= 0
	RETURN
SETCOLOR 0xFFFF40
PRINTFORM %NAME_MOON(DAY:3)%
RESETCOLOR

;(天候パッチ)气温
@INFO_现在の气温
#DIM 现在气温
现在气温 = GET_现在气温()
SELECTCASE 现在气温
	CASE IS <= -50
		PRINT 　
		SETCOLOR C_气温_酷寒
		PRINTFORM 气温{现在气温/10}.{ABS(现在气温%10)}℃
	CASE IS < 0
		PRINT 　
		SETCOLOR C_气温_冬
		PRINTFORM 气温{现在气温/10}.{ABS(现在气温%10)}℃
	CASE IS < 50
		PRINT 　
		SETCOLOR C_气温_寒
		PRINTFORM 气温{现在气温/10}.{ABS(现在气温%10)}℃
	CASE IS < 100
		PRINT 　
		SETCOLOR C_气温_冷
		PRINTFORM 气温{现在气温/10}.{ABS(现在气温%10)}℃
	CASE IS < 150
		PRINT 　
		SETCOLOR C_气温_凉
		PRINTFORM 气温{现在气温/10}.{ABS(现在气温%10)}℃
	CASE IS < 200
		PRINT 　
		SETCOLOR C_气温_温
		PRINTFORM 气温{现在气温/10}.{ABS(现在气温%10)}℃
	CASE IS < 250
		PRINT 　
		SETCOLOR C_气温_暖
		PRINTFORM 气温{现在气温/10}.{ABS(现在气温%10)}℃
	CASE IS < 300
		PRINT 　
		SETCOLOR C_气温_夏
		PRINTFORM 气温{现在气温/10}.{ABS(现在气温%10)}℃
	CASE IS < 350
		PRINT 　
		SETCOLOR C_气温_真夏
		PRINTFORM 气温{现在气温/10}.{ABS(现在气温%10)}℃
	CASE IS >= 350
		PRINT 　
		SETCOLOR C_气温_猛暑
		PRINTFORM 气温{现在气温/10}.{ABS(现在气温%10)}℃
ENDSELECT
RESETCOLOR

;时间停止关联
;TSPバーと指令の位置はどちらが良い？
;暂定に倣って停止解除の位置とX轴が近いので指令を右侧にしていますが、スペース（"　"）があるので左侧にもできます。
@INFO_TIMESTOP
CALL BASE_BAR(MASTER, "TSP", 30, "水色", "「无种类表记」")
;时间停止指令を参考に条件追记
IF BASE:MASTER:气力 <= 0 || BASE:MASTER:TSP <= 0 || (!FLAG:70 && (CFLAG:诶嘿嘿 || CFLAG:恶作剧 || CFLAG:陪睡中))
	SETCOLOR C_L_GRAY
	PRINT [无法停止时间]
	RESETCOLOR
ELSEIF !FLAG:70
	IF TFLAG:妄想中 == 1
		PRINT [妄想中]
	ELSEIF CFLAG:MASTER:恶作剧
		SETCOLOR C_L_GRAY
		PRINT [无法停止时间]
		RESETCOLOR
	ELSE
		CALL PRINTBUTTON_EX("[时间停止可]",355,0,FLAG:70,)
	ENDIF
ELSEIF CFLAG:诶嘿嘿
	CALL PRINTBUTTON_EX("[时间停止恶作剧中]",999,0,FLAG:70)
ELSE
	CALL PRINTBUTTON_EX("[时间停止中]",999,0,FLAG:70)
ENDIF

;催眠发动指令を参考に条件追记
IF FLAG:催眠强度
	IF FLAG:70 || CFLAG:MASTER:诶嘿嘿 || CFLAG:MASTER:恶作剧 || CFLAG:MASTER:陪睡中
		SETCOLOR C_L_GRAY
		PRINT  [催眠发动不可]
		RESETCOLOR
	ELSE
		CALL PRINTBUTTON_EX(@" [常识改变中(强度:{FLAG:催眠强度})]",666,0,FLAG:催眠强度)
	ENDIF
ELSEIF BASE:MASTER:TSP < 500 || FLAG:70
	SETCOLOR C_L_GRAY
	PRINT  [催眠发动不可]
	RESETCOLOR
ELSEIF BASE:MASTER:TSP >= 1000 && !TARGET
	CALL PRINTBUTTON_EX(" [常识改变发动可]",666,0,FLAG:催眠强度)
ELSE
	CALL PRINTBUTTON_EX(" [催眠发动可]",666,0,FLAG:催眠强度)
ENDIF

SIF TATTO_SYSTEM
	CALL PRINTBUTTON_EX(" [烙印系统测试]",900,0,0)

PRINTL 
;MASTERの眠气など
@INFO_SITUATION
SIF TEQUIP:挑逗模式
	PRINT 　<挑逗中>
PRINTFORM 　<%HUNGER(MASTER)%>　
RESETCOLOR
IF NEMUKE() > 720
	LOCALS = 睡眠可
	IF NEMUKE() > 960
		SETCOLOR C_RED
		LOCALS = 要睡眠
	ELSEIF NEMUKE() > 840
		SETCOLOR C_YELLOW
	ELSE
		SETCOLOR C_P_YELLOW
	ENDIF
	SIF CFLAG:MASTER:清醒剂
		LOCALS += "・清醒剂"
	PRINTFORML <%LOCALS%>
	RESETCOLOR
ELSEIF FLAG:约会的对象
	SETCOLOR 255,51,153
	IF CFLAG:(FLAG:约会的对象):当前位置 == OMANEKIBEYA()
		PRINTFORM   <\@ TFLAG:泡澡堂 ? 接受%NAME_OUTPUT_TRANSLATION("CALLNAME", (FLAG:约会的对象))%的私人服务 # 拜访%NAME_OUTPUT_TRANSLATION("CALLNAME", (FLAG:约会的对象))%的房间 \@中>
	ELSE
		PRINTFORM   <和%NAME_OUTPUT_TRANSLATION("CALLNAME", (FLAG:约会的对象))%约会中>
	ENDIF
	RESETCOLOR
	;MASTERの眠气が限界に近いなら警告
	IF NEMUKE() > 660
		PRINT (差不多到了该结束的时候了)
	ENDIF
	PRINTL
ELSEIF CFLAG:MASTER:清醒剂
	SELECTCASE CFLAG:MASTER:清醒剂
		CASE IS > 240
			PRINTL <清醒剂・极>
		CASE IS > 180
			PRINTL <清醒剂・强>
		CASE IS > 120
			PRINTL <清醒剂・中>
		CASEELSE
			PRINTL <清醒剂・弱>
	ENDSELECT
ELSE
	PRINTL
ENDIF



;当前位置情报
@INFO_GPS
IF TFLAG:约会道中
	PRINTFORMS "%DATENAME_PLACE()%的道中　　　(距离到达还有{TFLAG:约会道中}分 )"
ELSEIF CHK_DATENOW(CFLAG:MASTER:约会中) && CFLAG:MASTER:当前位置 != OMANEKIBEYA()
	PRINTFORMS "%DATENAME_PLACE()%  - %DATENAME_SPOT()% -"
ELSE
	PRINTFORMS NAME_FROM_PLACE(CFLAG:MASTER:当前位置)
IF FamilyHotel > 1 && GROUPMATCH(CFLAG:MASTER:当前位置,P61地の间,P62水の间,P63火の间,P64风の间,P65光の间,P66暗の间)
	PRINTFORM   住宿旅客: 
	FOR LOCAL, 1, CHARANUM - 1
		SIF CFLAG:LOCAL:311 == CFLAG:MASTER:当前位置
			PRINTFORM ❤%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)% 
	NEXT
ENDIF
	;木浴缸设置场所はそれとわかるように
	SIF NEAR_BATH() == CFLAG:MASTER:当前位置
		PRINTFORMS " <木浴缸Lv{ITEM:木浴缸}>"
	;私室にいる时、家具シリーズの表记
	IF CFLAG:MASTER:当前位置 == CFLAG:MASTER:初始位置
		SIF ITEM:桐木床
			PRINTFORMS " <桐木床Lv{ITEM:桐木床}>"
		SIF ITEM:榉木桌椅
			PRINTFORMS " <榉木桌椅Lv{ITEM:榉木桌椅}>"
		SIF ITEM:栎木沙发
			PRINTFORMS " <栎木沙发Lv{ITEM:栎木沙发}>"
	ENDIF
ENDIF
;ウフフ中は气が散るので表示させない,待客室98は表示させるので99未满
IF TFLAG:102 != 2 && !FLAG:1002 && AT_HOME(MASTER) == 1
	PRINTPLAIN  
	PRINTFORM %SEIKETU(CFLAG:MASTER:当前位置)%
	RESETCOLOR
ENDIF



;-------------------------------------------------
;料理の情报
;-------------------------------------------------
@INFO_DISH
PRINTFORM <%EX_DISH_NAME%
SIF STRCOUNT(DISH_TASTE, "できたて") == 1
	PRINT (新鲜制作)
SIF STRCOUNT(DISH_TASTE, "劣化") == 1
	PRINT (劣)
SIF STRCOUNT(DISH_TASTE, "劣化") >= 2
	PRINT (腐)
SIF DISH_DRG
	PRINTFORM (%ITEMNAME:DISH_DRG%)
SIF DISH_DRG_2
	PRINTFORM (%ITEMNAME:DISH_DRG_2%)
PRINT >


;-------------------------------------------------
;空腹具合の表示函数
;-------------------------------------------------
@HUNGER(ARG)
#FUNCTIONS
LOCAL = TCVAR:ARG:空腹时刻 - TIME:1
SELECTCASE LOCAL
	CASE IS > 270
	SETCOLOR C_GREEN
		RETURNF "满腹"
	CASE IS > 180
	SETCOLOR C_P_GREEN
		RETURNF "八分饱"
	CASE IS > 0
	SETCOLOR C_P_YELLOW
		RETURNF "普通"
	CASEELSE
		RETURNF "可吃饭"
ENDSELECT

;TARGET关联の简易情报表示函数群
@INFO_TARGET_DATAGROUP
IF TARGET <= 0
	TFLAG:102 = 0
	TFLAG:胖次收夺 = 0
	RETURN
ENDIF
IF !CFLAG:睡眠
	CALL 被逆推
ELSEIF CFLAG:恶作剧 <= 1
	;诶嘿嘿解除
	CVARSET CFLAG, GETNUM(CFLAG, "诶嘿嘿"), 0
ENDIF
;ターゲット切替ボタンの设置＆同行者と携带の表示
CALL INFO_TARGET_BUTTON
;TARGET本人の简易情报
CALL INFO_TARGET_SITUATION
IF CFLAG:睡眠
	IF CFLAG:恶作剧 == 1
		TFLAG:102 = 1
	ELSEIF CFLAG:恶作剧 == 2
		CFLAG:TARGET:诶嘿嘿 = 1
		CFLAG:MASTER:诶嘿嘿 = 1
		TFLAG:102 = 2
	ELSE
		TFLAG:102 = 0
	ENDIF
ENDIF



;ターゲット切替ボタンの设置＆同行者と携带の表示
;旧@INFO_TARGET_GROUPと统合して一行にまとめる
@INFO_TARGET_BUTTON
#DIM 同行人数
#DIM 表示済み, 200
;同行者と携带の表示部分

同行人数 = 0
VARSET 表示済み
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF !CFLAG:(TARGET:LOCAL):同行
		CONTINUE
	同行人数 ++
	表示済み:LOCAL = 1
	SIF 同行人数 > 1
		PRINT 、
	IF TARGET == TARGET:LOCAL
		SETCOLOR C_AQUA
	ELSEIF FLAG:约会的对象
		SETCOLOR C_PINK
	ELSE
		SETCOLOR 255, 200, 100
	ENDIF
	PRINTBUTTON "[" + @"%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET:LOCAL)%" + "]", (1000 + TARGET:LOCAL)
	RESETCOLOR
NEXT
IF 同行人数
	IF CHK_DATENOW(CFLAG:MASTER:约会中)
		PRINTFORM \@ TFLAG:泡澡堂 ? 服务 # 约会 \@中
	ELSE
		PRINT 同行中
	ENDIF
ENDIF
IF TFLAG:运搬 > 0
	IF TARGET == TFLAG:运搬
		SETCOLOR C_AQUA
	ELSE
		SETCOLOR 200, 50, 200
	ENDIF
	IF FINDELEMENT(TARGET, TFLAG:运搬, 1) != -1
		PRINTBUTTON "[" + @"%NAME_OUTPUT_TRANSLATION("CALLNAME", TFLAG:运搬)%" + "]", (1000 + TARGET:FINDELEMENT(TARGET, TFLAG:运搬, 1))
	ELSE
		PRINTFORM [%NAME_OUTPUT_TRANSLATION("CALLNAME", TFLAG:运搬)%]
	ENDIF
	RESETCOLOR
	PRINT 带走中
	表示済み:FINDELEMENT(TARGET, TFLAG:运搬, 1) = 1
ENDIF
SIF MATCH(表示済み, 1) && GET_TARGETNUM() != MATCH(表示済み, 1)
	PRINT ／

;ターゲット切替ボタン部分
TFLAG:胖次收夺 = 0
FOR LOCAL, 1, GET_TARGETNUM() + 1
	SIF CFLAG:(TARGET:LOCAL):当前位置 != CFLAG:MASTER:当前位置
		CONTINUE
	SIF 表示済み:LOCAL
		CONTINUE
	SIF TARGET == TARGET:LOCAL
		SETCOLOR C_AQUA
	PRINTBUTTON "[" + @"%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET:LOCAL)%" + "]", (1000 + TARGET:LOCAL)
	RESETCOLOR
	SIF FLAG:70 == 1 && EQUIP:(TARGET:LOCAL):0 && EQUIP:(TARGET:LOCAL):下半身内衣２ != 0 && FLAG:700 >= 100
		TFLAG:胖次收夺++
	SIF LOCAL < GET_TARGETNUM()
		PRINT 、
NEXT
PRINTL

;TARGET本人の简易情报
@INFO_TARGET_SITUATION
LOCAL = TARGET + 4000
CALL SHOW_EQUIP_2
PRINTFORM (好感度:
CALL PRINT_RANK(CFLAG:2, "MAX_50000")
PRINTFORM  {CFLAG:2}, 信赖度:
CALL PRINT_RANK(CFLAG:信赖度, "MAX_20000")
PRINTFORM  {CFLAG:信赖度}
IF NOTICE_LUST(TARGET)
	PRINTFORM ,　欲求不满度:{CFLAG:TARGET:积攒度 / 10}％)　
ENDIF
PRINTFORM  孩子个数:{CFLAG:TARGET:儿童人数}, 受孕:　
IF INRANGE(CFLAG:TARGET:分娩经过日, 1, 59) || TALENT:TARGET:妊娠 ;1で种付け不可
	PRINT ×)　
ELSE
	PRINT 〇)　
ENDIF
;IF NOTICE_LUST(TARGET)
;	PRINTFORM ,　欲求不满度:{CFLAG:TARGET:积攒度 / 10}％)　
;ELSE
;	PRINT )　
;ENDIF

;PRINTFORM [%HUNGER(TARGET)%]　
DEBUGPRINTFORML  合意判定值:{CFLAG:合意判定}

SIF BASE:愤怒 < 0
	BASE:愤怒 = 0
LOCAL = MIN(BASE:愤怒 / 200, 5)
IF CFLAG:睡眠
	IF CFLAG:勃然大怒
		SETCOLOR 255,0,0
		PRINTFORM 怒<{EXP:TARGET:睡眠奸经验}>{TCVAR:TARGET:睡眠深度,5,RIGHT}
		RESETCOLOR
	ELSEIF CFLAG:MASTER:恶作剧 == 2
		SELECTCASE TCVAR:TARGET:睡眠深度
			CASE 0 TO 500
				SETCOLOR 255,0,0
				PRINTFORM 就快醒了<{EXP:TARGET:睡眠奸经验}>{TCVAR:TARGET:睡眠深度,5,RIGHT}
			CASE 501 TO 1500
				SETCOLOR 255,255,0
				PRINTFORM 翻来覆去<{EXP:TARGET:睡眠奸经验}>{TCVAR:TARGET:睡眠深度,5,RIGHT}
			CASE 1501 TO 3000
				SETCOLOR 0,255,0
				PRINTFORM 睡眠中❤<{EXP:TARGET:睡眠奸经验}>{TCVAR:TARGET:睡眠深度,5,RIGHT}
			CASE 3001 TO 5000
				SETCOLOR 0,255,255
				PRINTFORM 睡得很香<{EXP:TARGET:睡眠奸经验}>{TCVAR:TARGET:睡眠深度,5,RIGHT}
			CASE IS > 5000
				SETCOLOR 0,0,255
				PRINTFORM 十分安稳<{EXP:TARGET:睡眠奸经验}>{TCVAR:TARGET:睡眠深度,5,RIGHT}
			CASEELSE
		ENDSELECT
		RESETCOLOR
	ELSE
		PRINTFORM 觉醒:{BASE:TARGET:愤怒,4,RIGHT}‰
	ENDIF
ELSE
	IF CFLAG:勃然大怒
		IF TALENT:胆量 < 0
			SETCOLOR C_BLUE
			PRINT 泪
		ELSE
			SETCOLOR 255,0,0
			PRINT 怒
		ENDIF
		RESETCOLOR
	ELSEIF CFLAG:恶作剧 || TCVAR:烂醉
		PRINTFORM 觉醒:%"！" * LOCAL%
	ELSE
		PRINTFORM 愤怒:%"！" * LOCAL%
		PRINTFORM   %"　" * (5 - LOCAL)%
		SELECTCASE CFLAG:TARGET:清醒剂
			CASE IS > 240
				PRINT <清醒剂・极>
			CASE IS > 180
				PRINT <清醒剂・强>
			CASE IS > 120
				PRINT <清醒剂・中>
			CASE IS > 0
				PRINT <清醒剂・弱>
			CASEELSE
				PRINT
		ENDSELECT
	ENDIF
ENDIF
PRINTL 
IF TARGET
	PRINT 工作情报：
	PRINTFORM %CSTR:TARGET:工作情报%　
	PRINT 　职场　：
	IF TALENT:TARGET:风俗娘
		PRINTFORM 旧地狱温泉　
	ELSE
		PRINTFORM %CSVCSTR(TARGET, 2)%　
	ENDIF
	PRINTFORM 　%ABLNAME:42%:
	CALL PRINT_RANK(ABL:TARGET:42, "MAX_6")
	PRINTFORM  {ABL:TARGET:42} 
	PRINTL
ENDIF
;SHOW_STATUS用のバー函数と状态をまとめて表示する函数
@INFO_BASE(CHARA)
#DIM CHARA
PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA), 20, LEFT%
;BASE_BAR
IF CFLAG:CHARA:睡眠 && CHARA != MASTER
	CALL BASE_BAR_2(CHARA, "体力", "体", 10, "水色")
ELSE
	CALL BASE_BAR_2(CHARA, "体力", "体", 10, "绿")
ENDIF
CALL BASE_BAR_2(CHARA, "气力", "气", 10, "青")
IF TCVAR:CHARA:烂醉 && BASE:CHARA:酒气 < (MAXBASE:CHARA:酒气 / 10) * 4
	CALL BASE_BAR_2(CHARA, "酒气", "酒", 10, "紫")
ELSE
	CALL BASE_BAR_2(CHARA, "酒气", "酒", 10, "赤")
ENDIF
IF HAS_PENIS(CHARA)
	CALL BASE_BAR_2(CHARA, "精力", "精", 10, "黄")
ELSE
	PRINTFORM %" " * 21%
ENDIF
;其他の状态
CALL COLORMESSAGE(ALCOHOL_FACE(CHARA), C_ORANGE, 0)
SIF HAS_PENIS(CHARA)
	CALL INFO_SHOW_BOKKI(CHARA)
PRINTL 

;勃起状态のアナログ表示
@INFO_SHOW_BOKKI(CHARA)
#DIM CHARA
SETCOLOR C_P_PURPLE
IF BASE:CHARA:勃起 == MAXBASE:CHARA:勃起
	FONTBOLD
	PRINTFORM Erection!!
	FONTREGULAR
ELSEIF BASE:CHARA:勃起 >= 1000
	PRINTFORM 梆硬.{BASE:CHARA:勃起}
ELSEIF BASE:CHARA:勃起 >= 500
	PRINTFORM 石更.{BASE:CHARA:勃起}
ELSE
	PRINTFORM 通常.{BASE:CHARA:勃起}
ENDIF
RESETCOLOR

;TARGETおよび你の情报・状态
@INFO_STATUS
IF TARGET
	CALL INFO_EMOTION
	SIF FLAG:生理周期可视化
		CALL CYCLE_PRINT(TARGET)
	SIF EX:TARGET:膣内精液 > 0
		PRINTFORM 膣内精液量:{EX:TARGET:膣内精液,5,LEFT}
	SIF EX:TARGET:肛门内精液 > 0
		PRINTFORM 肛内精液量:{EX:TARGET:肛门内精液,5,LEFT}
	PRINTL
ENDIF
;魔改功能
IF TARGET != 0
	IF EX:TARGET:膣内精液 > 0
		SETCOLOR C_PINK
		PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的腔内：
		FOR LOCAL, 0, CHARANUM
			SIF CFLAG:TARGET:(LOCAL + 3000) > 0
				PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%{CFLAG:TARGET:(LOCAL + 3000),5,LEFT}
		NEXT
		PRINTL
	ENDIF
	IF EX:TARGET:子宫内精液 > 0
		SETCOLOR C_CREAM
		PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的宫内❤：
		FOR LOCAL, 0, CHARANUM
			SIF CFLAG:TARGET:(LOCAL + 2000) > 0
				PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%{CFLAG:TARGET:(LOCAL + 2000),5,LEFT}
		NEXT
		RESETCOLOR
		PRINTL
	ENDIF
	IF EX:TARGET:肛门内精液 > 0
		SETCOLOR C_ORANGE
		PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的肛内：
		FOR LOCAL, 0, CHARANUM
			SIF CFLAG:TARGET:(LOCAL + 3500) > 0
				PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%{CFLAG:TARGET:(LOCAL + 3500),5,LEFT}
		NEXT
		RESETCOLOR
		PRINTL
	ENDIF
ENDIF
IF EX:MASTER:膣内精液 > 0
	SETCOLOR C_PINK
	PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的腔内：
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:MASTER:(LOCAL + 3000) > 0
			PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%{CFLAG:MASTER:(LOCAL + 3000),5,LEFT}
	NEXT
	PRINTL
ENDIF
IF EX:MASTER:子宫内精液 > 0
	SETCOLOR C_CREAM
	PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的宫内❤：
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:MASTER:(LOCAL + 2000) > 0
			PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%{CFLAG:MASTER:(LOCAL + 2000),5,LEFT}
	NEXT
	RESETCOLOR
	PRINTL
ENDIF
IF EX:MASTER:肛门内精液 > 0
	SETCOLOR C_ORANGE
	PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的肛内：
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:MASTER:(LOCAL + 3500) > 0
			PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%{CFLAG:MASTER:(LOCAL + 3500),5,LEFT}
	NEXT
	RESETCOLOR
	PRINTL
ENDIF
IF TATTO_SYSTEM == 1
	IF STRLENS(st_name:TARGET:0) > 0
		CLEARLINE 1
		SETCOLOR C_HEARTPINK
		PRINTFORM 烙印：　　
		PRINTFORML %st_name:TARGET:0%
		RESETCOLOR
	ENDIF
ENDIF
;魔改完成
;你の状态
CALL SHOW_EQUIP_3
;TARGETの状态
CALL SHOW_EQUIP_1
;射精/母乳ゲージ表示函数
CALL INFO_GAUGEGROUP

;情绪や理性などの表示
@INFO_EMOTION
LOCAL = LIMIT(BASE:TARGET:情绪 / 200,0,5)
PRINTFORM 情绪:
SIF BASE:TARGET:情绪 == MAXBASE:TARGET:情绪
	SETCOLOR 255,51,153
IF BASE:TARGET:情绪 == MAXBASE:TARGET:情绪 && CHK_DATENOW(CFLAG:TARGET:约会中)
	FONTBOLD
	PRINTFORM OverDrive!!
	FONTREGULAR
ELSE
	CALL HEARTMARK(LOCAL)
	PRINTFORM %" " * (13 - LOCAL * 2)%
ENDIF
RESETCOLOR

LOCAL = MAX(BASE:TARGET:理性 / 200,1)
PRINT 理性:
SIF BASE:TARGET:理性 < 100
	SETCOLOR 255,255,0
IF BASE:TARGET:理性 == 0 && CHK_DATENOW(CFLAG:TARGET:约会中)
	FONTBOLD
	PRINTFORM LimitBreak!!
	FONTREGULAR
ELSE
	PRINTFORM %"★" * LOCAL%
	PRINTFORM %" " * (14 - LOCAL * 2)%
ENDIF
RESETCOLOR


;射精/母乳ゲージ表示函数
@INFO_GAUGEGROUP
SIF !MAXBASE:2
	MAXBASE:2 = 10000
IF ASSI > 0
	SIF !MAXBASE:ASSI:2
		MAXBASE:ASSI:2 = 10000
ENDIF
IF TALENT:MASTER:精力超群
	MAXBASE:MASTER:2 = 5000
ELSE
	MAXBASE:MASTER:2 = 10000
ENDIF
SIF !GROUPMATCH(TFLAG:102, 2, 3)
	RETURN

CALL SHOW_SAMEN_GAUGE(MASTER)
CALL SHOW_SAMEN_GAUGE(TARGET)

IF TALENT:MASTER:母乳体质
	IF TALENT:MASTER:淫乳
		MAXBASE:MASTER:3 = 5000
	ELSE
		MAXBASE:MASTER:3 = 10000
	ENDIF
	CALL INFO_GAUGE_BAR(MASTER, "母乳", "EXTRA")
	PRINTFORML [{BASE:MASTER:3,5}/{MAXBASE:MASTER:3,5}]
ENDIF

IF TALENT:TARGET:母乳体质
	IF TALENT:TARGET:淫乳
		MAXBASE:3 = 5000
	ELSE
		MAXBASE:3 = 10000
	ENDIF
	CALL INFO_GAUGE_BAR(TARGET, "母乳", "EXTRA")
	PRINTFORML [{BASE:3,5}/{MAXBASE:3,5}]
ENDIF



;SHOW_STATUS用のバー函数の共通设定、射精/母乳ゲージ用
@INFO_GAUGE_BAR(C_ID, NAME_BASE, NAME_COLOR)
#DIM C_ID
#DIMS NAME_BASE
#DIMS NAME_COLOR
LOCALS = 　%NAME_BASE%(%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%)
PRINTFORM %LOCALS,12,LEFT%
RESETCOLOR
CALL BASE_BAR(C_ID, NAME_BASE, 18, NAME_COLOR, "「无种类表记」「无数字表记」")

@SHOW_SAMEN_GAUGE(ARG)
SIF !HAS_PENIS(ARG)
	RETURN
CALL INFO_GAUGE_BAR(ARG, "射精", "水色")
IF TCVAR:ARG:要去了
	SETCOLOR C_RED
	PRINTFORM [！要去了！]
	RESETCOLOR
ELSE
	PRINTFORM [{BASE:ARG:2,5}/{MAXBASE:ARG:2,5}]
ENDIF
LOCALS = 忍耐
SELECTCASE TCVAR:ARG:射精我慢
	CASE 0
		LOCALS = 通常
	CASE 1
		SETCOLOR C_AQUA
	CASE 2
		SETCOLOR C_M_GREEN
	CASE 3
		SETCOLOR C_YELLOW
	CASE 4
		SETCOLOR C_RED
ENDSELECT
PRINTBUTTON "["+LOCALS+"]", 5000 + ARG
RESETCOLOR
PRINTFORML 

;------------------------------------------
;回家予告とか
;1,外出时の你回家予告
;2,约会时の相手の回家予告
;3,要睡眠通知
;------------------------------------------
@TIMEUP_NOTICE(DATE)
#DIM DATE
#DIM TIMEUP_MESSAGE, 4
IF NEMUKE() > 660 && CFLAG:MASTER:约会中 != MAIN_MAP && !TIMEUP_MESSAGE:1
	CALL COLORMESSAGE("差不多是回去的时间了…", C_ORANGE, 2)
	TIMEUP_MESSAGE:1 = 1
ELSEIF NEMUKE() < 660
	TIMEUP_MESSAGE:1 = 0
ENDIF
IF DATE && !TIMEUP_MESSAGE:2
	IF CFLAG:DATE:神社在住
		IF INRANGE(TIME, CFLAG:DATE:就寝时间 - 60, CFLAG:DATE:就寝时间)
			CALL COLORMESSAGE(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", DATE)%好像\@ TIMEUP_MESSAGE:1 ? 也 # \@已经很困了…", C_ORANGE, 2)
			TIMEUP_MESSAGE:2 = 1
		ENDIF
	ELSE
		IF INRANGE(TIME, CFLAG:DATE:回家时间 - 60, CFLAG:DATE:回家时间)
			CALL COLORMESSAGE(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", DATE)%差不多\@ TIMEUP_MESSAGE:1 ? 也 # \@该到回去的时间了…", C_ORANGE, 2)
			TIMEUP_MESSAGE:2 = 1
		ENDIF
	ENDIF
ELSEIF !DATE
	TIMEUP_MESSAGE:2 = 0
ENDIF
IF CFLAG:TARGET:340 >= 3500 && TARGET!= MASTER
	CALL COLORMESSAGE(@"面色绯红的%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%正在竭力站稳...", C_RED, 2)
ELSEIF CFLAG:TARGET:340 >= 1500 && TARGET!= MASTER
	CALL COLORMESSAGE(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的身体正在微微颤抖...", C_ORANGE, 2)
ENDIF
IF NEMUKE() > 960 && !TIMEUP_MESSAGE:3 && !CFLAG:MASTER:诶嘿嘿
	CALL COLORMESSAGE("想睡觉了…", C_RED, 2)
	TIMEUP_MESSAGE:3 = 1
ELSEIF NEMUKE() <= 960
	TIMEUP_MESSAGE:3 = 0
ENDIF
