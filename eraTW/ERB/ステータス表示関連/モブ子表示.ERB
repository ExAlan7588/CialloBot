;*******************************************************************************************************
;路人子画像表示用
;*******************************************************************************************************
;削除しました＠v.4.730.INFO改良パッチ

;*******************************************************************************************************
;路人子画像生成用函数
;*******************************************************************************************************
@路人子生成(角色番号, 立绘_Flag)
#DIM 角色番号
#DIM 立绘_Flag
#DIM GID
#DIMS 生成文字列
#DIMS 合成文字列配列, 50
#DIMS 色合成
#DIM 合成数
#DIM リソースカウント

;别绘専用函数
IF STRFIND(CSTR:角色番号:路人子素材CSTR开始位置, "别绘描画") > -1
	CALL 别绘路人子画像生成(角色番号, 立绘_Flag)
	RETURN 1
ENDIF

;リソース作成
;=====================================
FOR リソースカウント, 0, 路人子素材リソース数
	;初期化
	GID = 0
	VARSET LOCAL
	VARSET 合成文字列配列,
	合成数 = 0
	;画像合成下地用のダミーを配置する
	CALL 画像合成(GID,"ダミー")
	;使用リソースの名称を配列に取得する
	;DEBUGPRINTFORML {角色番号} - 路人子リソース探索(%合成文字列配列%, {合成数}, %CSTR:角色番号:(路人子素材CSTR开始位置 + リソースカウント)%), 立绘_Flag
	CALL 路人子リソース探索(合成文字列配列, 合成数, CSTR:角色番号:(路人子素材CSTR开始位置 + リソースカウント), 立绘_Flag)
	;路人子画像は高い番号のリソースから先に合成していく必要があるので降顺にソートする
	ARRAYSORT 合成文字列配列, BACK, 0, 合成数
	;路人子用画像を顺番に合成していく
	FOR LOCAL, 0, 合成数
		色合成 '= ""
		IF 合成文字列配列:LOCAL != ""
			;色合成确认
			FOR LOCAL:1, 0 ,路人子素材色种别数
				IF STRFIND(合成文字列配列:LOCAL, @"_%路人子素材色种别:(LOCAL:1)%_") != -1 && DIC_CONTAINSKEY(CSTR:角色番号:(路人子素材CSTR开始位置 + リソースカウント), @"%路人子素材色种别:(LOCAL:1)%色")
					色合成 = %DIC_GET(CSTR:角色番号:(路人子素材CSTR开始位置 + リソースカウント), @"%路人子素材色种别:(LOCAL:1)%色")%
					LOCAL:1 = 路人子素材色种别数
				ENDIF
			NEXT
			CALL 画像合成(GID, 合成文字列配列:LOCAL, 0, 0, 0, 0, 色合成)
		ENDIF
	NEXT
	;合成した画像をリソースに登录する
	CFLAG:角色番号:(路人子素材CFLAG开始位置 + リソースカウント) = GID
	;泛用テンプレートで使用できるように名前を统一する
	IF !立绘_Flag
		SELECTCASE リソースカウント
			CASE 0
				CALL リソース登录(@"立绘_服_通常_{角色番号}", GID)
			CASE 1
				CALL リソース登录(@"立绘_服_笑颜_{角色番号}", GID)
			CASE 2
				CALL リソース登录(@"立绘_服_愤怒_{角色番号}", GID)
			CASE 3
				CALL リソース登录(@"立绘_裸_通常_{角色番号}", GID)
			CASE 4
				CALL リソース登录(@"立绘_裸_笑颜_{角色番号}", GID)
			CASE 5
				CALL リソース登录(@"立绘_裸_愤怒_{角色番号}", GID)
		ENDSELECT
	ELSE
		SELECTCASE リソースカウント
			CASE 0
				CALL リソース登录(@"颜绘_服_通常_{角色番号}", GID)
			CASE 1
				CALL リソース登录(@"颜绘_服_笑颜_{角色番号}", GID)
			CASE 2
				CALL リソース登录(@"颜绘_服_愤怒_{角色番号}", GID)
			CASE 3
				CALL リソース登录(@"颜绘_裸_通常_{角色番号}", GID)
			CASE 4
				CALL リソース登录(@"颜绘_裸_笑颜_{角色番号}", GID)
			CASE 5
				CALL リソース登录(@"颜绘_裸_愤怒_{角色番号}", GID)
		ENDSELECT
	ENDIF
NEXT

;TEQUIP:角色番号:上半身着衣状况 = 1

RETURN 1

;*******************************************************************************************************
;路人子画像リソース探索用函数
;路人子画像のリソースを探索して配列にリソース名を格纳する
;*******************************************************************************************************
@路人子リソース探索(合成文字列配列, 合成数, 生成文字列, 立绘_Flag)
#DIMS REF 合成文字列配列,0
#DIM REF 合成数
#DIMS 生成文字列
#DIM 立绘_Flag
#DIMS キー
#DIM CN_ENDNO = 18
#DIMS CN_NO_STR
VARSET LOCAL
キー =
CN_NO_STR =
;ゴミ取り
生成文字列 = %REPLACE(生成文字列, "^\"\"", "")%
;DEBUGPRINTFORML %生成文字列%

FOR LOCAL, 0, 路人子素材种别数
	IF DIC_CONTAINSKEY(生成文字列, 路人子素材种别:LOCAL)
		FOR LOCAL:1, CN_ENDNO, -1, -1
			;IF STRFIND(路人子素材种别:LOCAL, "装饰") != -1
				;装饰の场合は复数设定できるのでリソース名検索时は数值部分を削る
			;	キー = "装饰"
			;ELSE
				キー = %路人子素材种别:LOCAL%
			;ENDIF
			FOR LOCAL:2, 0, 路人子素材カテゴリ数
				IF LOCAL:1 < 10
					;0埋め
					CN_NO_STR = CN0{LOCAL:1}
				ELSE
					CN_NO_STR = CN{LOCAL:1}
				ENDIF
				IF SPRITECREATED(@"%CN_NO_STR%_%路人子素材カテゴリ:(LOCAL:2)%_%DIC_GET(生成文字列, キー)%")
					IF (!立绘_Flag)
						合成文字列配列:(合成数++) = %CN_NO_STR%_%路人子素材カテゴリ:(LOCAL:2)%_%DIC_GET(生成文字列, キー)%
						DEBUGPRINTFORML %CN_NO_STR%_%路人子素材カテゴリ:(LOCAL:2)%_%DIC_GET(生成文字列, キー)%
					ELSE
						合成文字列配列:(合成数++) = %CN_NO_STR%_%路人子素材カテゴリ:(LOCAL:2)%_%DIC_GET(生成文字列, キー)%_颜
						DEBUGPRINTFORML %CN_NO_STR%_%路人子素材カテゴリ:(LOCAL:2)%_%DIC_GET(生成文字列, キー)%_颜
					ENDIF
				ENDIF
			NEXT
		NEXT
	ENDIF
NEXT

RETURN 1

;*******************************************************************************************************
;路人子画像解放用函数
;指定した角色番号の路人子のグラフィックを全て破弃する
;*******************************************************************************************************
@路人子画像リセット(角色番号)
#DIM 角色番号
#DIM リソースカウント
IF CFLAG:角色番号:路人子素材CFLAG开始位置 != 0 && GCREATED(CFLAG:角色番号:路人子素材CFLAG开始位置)
	SPRITEDISPOSE @"立绘_服_通常_{角色番号}"
	SPRITEDISPOSE @"立绘_服_笑颜_{角色番号}"
	SPRITEDISPOSE @"立绘_服_愤怒_{角色番号}"
	SPRITEDISPOSE @"立绘_裸_通常_{角色番号}"
	SPRITEDISPOSE @"立绘_裸_笑颜_{角色番号}"
	SPRITEDISPOSE @"立绘_裸_愤怒_{角色番号}"
	SPRITEDISPOSE @"颜绘_服_通常_{角色番号}"
	SPRITEDISPOSE @"颜绘_服_笑颜_{角色番号}"
	SPRITEDISPOSE @"颜绘_服_愤怒_{角色番号}"
	SPRITEDISPOSE @"颜绘_裸_通常_{角色番号}"
	SPRITEDISPOSE @"颜绘_裸_笑颜_{角色番号}"
	SPRITEDISPOSE @"颜绘_裸_愤怒_{角色番号}"
	FOR リソースカウント, 0, 路人子素材リソース数
		GDISPOSE CFLAG:角色番号:(路人子素材CFLAG开始位置 + リソースカウント)
		CFLAG:角色番号:(路人子素材CFLAG开始位置 + リソースカウント) = 0
	NEXT
ENDIF
RETURN 1
