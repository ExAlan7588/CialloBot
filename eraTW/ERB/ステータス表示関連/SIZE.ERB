;-------------------------------------------------
;身高・体重・3尺寸の表示处理
;-------------------------------------------------
;BASE:20 … 身高
;BASE:21 … 体重
;BASE:22 … 胸围
;BASE:23 … 腰围
;BASE:24 … 臀围
;MAXBASE:20 … 身高系数
;MAXBASE:21 … 体重系数
;MAXBASE:22 … 胸围系数
;MAXBASE:23 … 腰围系数
;MAXBASE:24 … 臀围系数

@MAKE_SIZE, ARG
;身体测定フラグが立っている场合は系数を生成
;身体测定済みかつ未设定の场合も生成
;CSVに记入されていない场合の标准值は1000
VARSET LOCAL
IF CFLAG:ARG:身体测定 == 2 || (CFLAG:ARG:身体测定 == 1 && (MAXBASE:ARG:身高 == 0 || MAXBASE:ARG:体重 == 0 || MAXBASE:ARG:胸围 == 0 || MAXBASE:ARG:腰围 == 0 || MAXBASE:ARG:臀围 == 0))
	FOR LOCAL, 20, 25
		MAXBASE:ARG:LOCAL = CSVBASE(ARG,LOCAL)
		SIF MAXBASE:ARG:LOCAL == 0
			MAXBASE:ARG:LOCAL = 1000
		MAXBASE:ARG:LOCAL = MAXBASE:ARG:LOCAL * RAND(950, 1050) / 1000
	NEXT
ENDIF

;===身高の算出===
;基本值
BASE:ARG:身高 = (TALENT:ARG:性别 == 2) ? 1700 # 1500
BASE:ARG:身高 = BASE:ARG:身高 * MAXBASE:ARG:身高 / 1000

;体型补正
IF TALENT:ARG:体型 == -2
	TIMES BASE:ARG:身高, 0.80
ELSEIF TALENT:ARG:体型 == -1
	TIMES BASE:ARG:身高, 0.90
ELSEIF TALENT:ARG:体型 == 1
	TIMES BASE:ARG:身高, 1.10
ELSEIF TALENT:ARG:体型 == 2
	TIMES BASE:ARG:身高, 1.20
ELSEIF TALENT:ARG:体型 == 3
	TIMES BASE:ARG:身高, 1.50
ENDIF

;===腰围の算出===
;基本值
BASE:ARG:腰围 = BASE:ARG:身高 * 40 / 100
BASE:ARG:腰围 = BASE:ARG:腰围 * MAXBASE:ARG:腰围 / 1000

;体型补正
IF TALENT:ARG:体型 == -2
	TIMES BASE:ARG:腰围, 1.16
ELSEIF TALENT:ARG:体型 == -1
	TIMES BASE:ARG:腰围, 1.08
ENDIF

;其他补正
SIF TALENT:ARG:魅力
	TIMES BASE:ARG:腰围, 0.95

;===胸围の算出===
;基本值
BASE:ARG:胸围 = BASE:ARG:身高 * 51 / 100
BASE:ARG:胸围 = BASE:ARG:胸围 * MAXBASE:ARG:胸围 / 1000

;胸围补正
IF TALENT:ARG:胸围 == -2
	TIMES BASE:ARG:胸围, 0.84
ELSEIF TALENT:ARG:胸围 == -1
	TIMES BASE:ARG:胸围, 0.92
ELSEIF TALENT:ARG:胸围 == 1
	TIMES BASE:ARG:胸围, 1.08
ELSEIF TALENT:ARG:胸围 == 2
	TIMES BASE:ARG:胸围, 1.16
ENDIF

;Ｂ感觉による补正
BASE:ARG:胸围 = BASE:ARG:胸围 * (1000 + MIN(ABL:ARG:Ｂ感觉, 100)) / 1000

;其他补正
SIF TALENT:ARG:魅力
	TIMES BASE:ARG:胸围, 1.05

;アンダー胸围以下なら补正
BASE:ARG:胸围 = MAX(BASE:ARG:身高 * 43 / 100, BASE:ARG:胸围)

;===臀围の算出===
;基本值
BASE:ARG:臀围 = BASE:ARG:身高 * 53 / 100
BASE:ARG:臀围 = BASE:ARG:臀围 * MAXBASE:ARG:臀围 / 1000

;其他补正
SIF TALENT:ARG:魅力
	TIMES BASE:ARG:臀围, 1.05

;===体重の算出===
;基本值
BASE:ARG:体重 = BASE:ARG:身高 * BASE:ARG:身高 / 5000
BASE:ARG:体重 = BASE:ARG:体重 * MAXBASE:ARG:体重 / 1000

;体型补正
IF TALENT:ARG:体型 == -2
	TIMES BASE:ARG:体重, 0.80
ELSEIF TALENT:ARG:体型 == -1
	TIMES BASE:ARG:体重, 0.90
ELSEIF TALENT:ARG:体型 == 1
	TIMES BASE:ARG:体重, 1.10
ELSEIF TALENT:ARG:体型 == 2
	TIMES BASE:ARG:体重, 1.20
ELSEIF TALENT:ARG:体型 == 3
	TIMES BASE:ARG:体重, 1.50
ELSEIF TALENT:ARG:体型 == 5
	TIMES BASE:ARG:体重, 10.00
ENDIF

;其他补正
SIF TALENT:ARG:魅力
	TIMES BASE:ARG:体重, 0.95

@SHOW_SIZE, ARG
#DIM L_PENIS,20 ; P尺寸计算用
;LOCAL:21 … 妊娠时の体重の计算值
;LOCAL:23 … 妊娠时の腰围の计算值
VARSET LOCAL
CALL MAKE_SIZE, ARG
IF CFLAG:ARG:身体测定 == 0
	PRINTL
	PRINTFORML □ 尺　寸 □-------------------------------------------------------------------------
	PRINTL  未测定
ELSE
	PRINTL
	PRINT □ 
	CALL PRINTBUTTON_EX("尺寸",160,TFLAG:160)
	PRINTL  □------------------------------------------------------------------------
	IF !TFLAG:尺寸显示
		;プレイヤーの身高・体重は不明
		IF ARG == NO:0 && TALENT:ARG:性别 == 2
			PRINTFORM  身高:???cm  体重:??kg
		ELSE
			;身高
			CALL ShowScaleInCM(ARG, BASE:ARG:身高, " 身高:")
			;体重
			IF TALENT:ARG:妊娠
				;身高150cmで10kg程度增加
				LOCAL:21 = BASE:ARG:体重 + (BASE:ARG:身高 * BASE:ARG:身高 * CFLAG:ARG:妊娠经过日数 / 2250000)
				PRINTFORM    体重:{LOCAL:21 / 10}.{LOCAL:21 % 10}\@ TALENT:ARG:体型 == -5? g# kg\@
				PRINTFORML  妊娠前({BASE:ARG:体重 / 10}.{BASE:ARG:体重 % 10}\@ TALENT:ARG:体型 == -5? g# kg\@)
			ELSE
				;小人の体重は1/10^3
				PRINTFORML    体重:{BASE:ARG:体重 / 10}.{BASE:ARG:体重 % 10}\@ TALENT:ARG:体型 == -5? g# kg\@
			ENDIF
		ENDIF
		;[男人]持ちは3尺寸は表示しない
		IF TALENT:ARG:性别 != 2
			;胸围
			CALL ShowScaleInCM(ARG, BASE:ARG:胸围, "   Ｂ:")
			PRINTFORM %LOCALS:0%
			CALL PRINT_CUPSIZE, ARG
			;腰围
			IF TALENT:ARG:妊娠
				;身高150cmで30cm程度增加
				LOCAL:23 = BASE:ARG:腰围 + (BASE:ARG:身高 * CFLAG:ARG:妊娠经过日数 / 500)
				CALL ShowScaleInCM(ARG, LOCAL:23, "/ Ｗ:")
				CALL ShowScaleInCM(ARG, BASE:ARG:腰围, " 妊娠前(")
				PRINTPLAIN )/
			ELSE
				CALL ShowScaleInCM(ARG, BASE:ARG:腰围, "/ Ｗ:")
				PRINTFORM /
			ENDIF
			;臀围
			CALL ShowScaleInCM(ARG, BASE:ARG:臀围, " Ｈ:")
			PRINTFORML 
		ENDIF
		IF HAS_PENIS(ARG)
			;Ｐ
			CALL GET_PENIS_SIZE, L_PENIS, ARG
			CALL ShowScaleInCM(ARG, L_PENIS:1, " Ｐ:")
			CALL ShowScaleInCM(ARG, L_PENIS:5, " 直径：")
			CALL ShowScaleInCM(ARG, L_PENIS:8, "  （平常时:")
			CALL ShowScaleInCM(ARG, L_PENIS:7, " 直径：")
			PRINTPLAIN )
			CALL ShowScaleInCM(ARG, L_PENIS:11, " 片玉直径：")
			PRINTFORML 
		ENDIF
	ENDIF
ENDIF
PRINTL



@PRINT_CUPSIZE, ARG
;胸围のカップ尺寸生成
SPLIT "(-) ,(AAA) ,(AA) ,(A) ,(B) ,(C) ,(D) ,(E) ,(F) ,(G) ,(H) ,(I) ,(J) ,(K) ,(L) ,(M) ,(N) ,(O) ,(P) ,(Q) ,(R) ,(S) ,(T) ,(U) ,(V) ,(W) ,(X) ,(Y) ,(Z) ,(Z+) ,(规格外) ",",",LOCALS
;基本值
LOCAL = (BASE:ARG:胸围 - (BASE:ARG:身高 * 43 / 100)) / 25

;小人は1/10
SIF TALENT:ARG:体型 == -5
	TIMES LOCAL, 0.10

;文字列变量が若干不自由なので强行作成
LOCAL = LIMIT(LOCAL, 0, 30)
LOCALS:0 = %LOCALS:(LOCAL)%
PRINTFORM %LOCALS:0%



; 阴茎各种尺寸取得 内容は各代入部分を参照
@GET_PENIS_SIZE(L_PENIS, ARG)
#DIM REF L_PENIS,0
#DIM L_PSCALE

; 长さ用シード值
L_PENIS:0 = BASE:ARG:身高*100 + BASE:ARG:腰围*10 + BASE:ARG:臀围

; NOISE2()は平均付近の值が出やすいので、极端な值はレアな存在になる。そんな感じで数值を考える

; 形状の具体的な尺寸のスケール感がいまいちわかってないので随意书きました
; でもこうすると幼儿体型＋形状=1のとき稍微不自然な感じになる。このTALENT:形状は绝对的な评价なのか见た目の割にはという意味なのかコレガワカラナイ
; とりあえずいろいろできる方（相对评价）でやっといた
IF TALENT:ARG:形状 == 1 ; 未成熟
	L_PSCALE = (NOISE2(L_PENIS:0, 25) + 45)
ELSEIF TALENT:ARG:形状 == 2 ; 租賃
	L_PSCALE = (NOISE2(L_PENIS:0, 30) + 70)
ELSEIF TALENT:ARG:形状 == 6 ; 巨根
	L_PSCALE = (NOISE2(L_PENIS:0, 100) + 175)
ELSEIF TALENT:ARG:形状 == 10 ; 马并み
	L_PSCALE = (NOISE2(L_PENIS:0, 125) + 275)
ELSE ; 通常
	; 150cmの身高に对して10.0cm～17.5cmを限界としてみる
	L_PSCALE = (NOISE2(L_PENIS:0, 75) + 100)
ENDIF
; 长さ。身高に応じてリ尺寸。150cmを境に扩大缩小
L_PENIS:1 = BASE:ARG:身高 * L_PSCALE / 1500

; 萎缩率 とりあえず25%～75%
L_PENIS:2 = NOISE2(L_PENIS:0 * 10 + 100, 50) + 25
; 萎缩后尺寸
L_PENIS:8 = L_PENIS:1 * L_PENIS:2 / 100

; 太さ用シード值
L_PENIS:9 = BASE:ARG:身高*1 + BASE:ARG:腰围*100 + BASE:ARG:臀围*10
; 太さ系数
L_PENIS:3 = (NOISE2(L_PENIS:9, 30) + 85)
; 太さ(周)
L_PENIS:4 = L_PENIS:1 * 80 * L_PENIS:3 / 10000
; 太さ(直径)
L_PENIS:5 = L_PENIS:4 * 1000000 / 3141592
; 平常时太さ(周)
L_PENIS:6 = L_PENIS:1 * 60 * L_PENIS:3 / 10000
; 平常时太さ(直径)
L_PENIS:7 = L_PENIS:6 * 1000000 / 3141592

;玉用シード值
L_PENIS:10 = BASE:ARG:身高*10 + BASE:ARG:腰围*1 + BASE:ARG:臀围*100
; 片玉直径 とりあえずちんちんの大きさに比例
L_PENIS:11 = L_PENIS:1 * 18 * (NOISE2(L_PENIS:10, 40) + 80) / 10000
; 袋横幅
L_PENIS:12 = L_PENIS:11 * 210 / 100

@ShowScaleInCM(C_ID, SIZE, ARGS)
#DIM C_ID
#DIM SIZE

PRINTFORM %ARGS%
IF TALENT:C_ID:体型 == -5
	PRINTFORM {SIZE / 100}.{SIZE % 100 / 10}{SIZE % 10}cm
ELSE
	PRINTFORM {SIZE / 10}.{SIZE % 10}cm
ENDIF