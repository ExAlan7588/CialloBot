;-----------------------------------------------------------
;MOB割当の觉书
; 0～19はゲーム每にリセット/着衣のみ
;20～39は每日リセット/裸画像有り
;90以降はCSV割当の固定框
;0～10	各地域土产物屋
;11		（据点增设时予备）
;12		索道人里侧
;13		铃奈庵（小铃不在时）
;14		人里温泉
;15		守谷くじ
;16		索道守谷侧
;17		红魔赌场
;18		月酒屋
;19		人里酒屋
;20		エンカウント贩卖员
;21～23	エンカウント子分貉
;24		エンカウント野篦坊
;25～26	丁半博打参加者
;27		受付水子作成框

;90		土产物屋路人男
;91～94	路人博徒（山童/河童/鸦天狗/蟒蛇）
;95		赌场代贷

;-----------------------------------------------------------
;路人子作成の改变
@路人店员画像作成(店员番号, ARGS = "")
{
#DIMS 种族, 路人子店员最大框 =  "赤巫女", "尼", "人妻", "女仆妖精", "因幡",
								"妖精", "僵尸妖精", "死库水河童", "白狼天狗", "鬼",
								"玉兔", "", "人妻", "看店", "番台ちゃん", 
								"绿巫女", "河童", "兔女郎妖精", "玉兔", "人妻",
								"贩卖员", "茨狸", "茨狸", "茨狸", "野篦坊"
}
#DIM 角色番号
#DIM GID
#DIMS 生成文字列
#DIMS 合成文字列配列, 50
#DIMS 色合成
#DIMS 指定色
#DIMS 感情 = "通常", "笑颜", "愤怒"
#DIMS 着衣
#DIM 合成数
#DIM リソースカウント
#DIM リソース种类
#DIM 店员番号
リソース种类 = 店员番号 > 19 ? 6 # 3
种族:5 = %TEXTR("妖精/妖怪狸/妖怪猫/妖怪狐/茨狸")%
;３日に一度贩卖员が看店に入る
SIF ((GETTIME()/1000000000) % 100) % 3 == 0
	种族:4 = 贩卖员
SIF ((GETTIME()/1000000000) % 100) % 2
	种族:7 = 山童
SIF ARGS != ""
	种族:店员番号 = %ARGS%
CALL SET_MOB_IMAGE_STR(0, 种族:店员番号, 店员番号)
IF 种族:店员番号 == "野篦坊"
	路人子店员画像文字列:店员番号:3 = %DIC_REMOVE(路人子店员画像文字列:店员番号:3, "目瞳")%
	路人子店员画像文字列:店员番号:3 = %DIC_REMOVE(路人子店员画像文字列:店员番号:3, "表情")%
ENDIF
FOR リソースカウント, 0, リソース种类
	;初期化
	GID = 0
	VARSET LOCAL
	VARSET 合成文字列配列,
	合成数 = 0
	
	;画像合成下地用のダミーを配置する
	CALL 画像合成(GID,"ダミー")
	;别绘路人店员の处理
	IF GROUPMATCH(种族:店员番号, "贩卖员", "茨狸")
		CALL 别绘路人子リソース探索(合成文字列配列, 合成数, 路人子店员画像文字列:店员番号:リソースカウント, 1)
		指定色 = %合成文字列配列:49%
		FOR LOCAL, 0, 合成数
			IF 合成文字列配列:LOCAL != ""
				SIF 指定色 != ""
					色合成 = %\@ STRFIND(合成文字列配列:LOCAL, "发") > -1 ? %指定色% # \@%
				CALL 画像合成(GID, 合成文字列配列:LOCAL, 0, 0, 0, 0, 色合成)
			ENDIF
		NEXT
	ELSE
		;使用リソースの名称を配列に取得する
		CALL 路人子リソース探索(合成文字列配列, 合成数, 路人子店员画像文字列:店员番号:リソースカウント, 1)
		;路人子画像は高い番号のリソースから先に合成していく必要があるので降顺にソートする
		ARRAYSORT 合成文字列配列, BACK, 0, 合成数
		;路人子用画像を顺番に合成していく
		FOR LOCAL, 0, 合成数
			色合成 '= ""
			IF 合成文字列配列:LOCAL != ""
				;色合成确认
				FOR LOCAL:1, 0 ,路人子素材色种别数
					IF STRFIND(合成文字列配列:LOCAL, @"_%路人子素材色种别:(LOCAL:1)%_") != -1 && DIC_CONTAINSKEY(路人子店员画像文字列:店员番号:リソースカウント, @"%路人子素材色种别:(LOCAL:1)%色")
						色合成 = %DIC_GET(路人子店员画像文字列:店员番号:リソースカウント, @"%路人子素材色种别:(LOCAL:1)%色")%
						LOCAL:1 = 路人子素材色种别数
					ENDIF
				NEXT
				CALL 画像合成(GID, 合成文字列配列:LOCAL, 0, 0, 0, 0, 色合成)
			ENDIF
		NEXT
	ENDIF
	;泛用テンプレートで使用できるように名前を统一する
	着衣 = %\@ リソースカウント < 3 ? 服 # 裸 \@%
	LOCAL = リソースカウント % 3
	CALL リソース登录(@"颜绘_%着衣%_%感情:LOCAL%_MOB_{店员番号}", GID)
	路人子店员GID:店员番号:リソースカウント = GID
NEXT

;=========================================================
;别绘路人子用の画像文字列を设定
;CID		角色番号
;ARGS		别绘使用の种族名	
;NUMBER		CID=0で路人店员番号、CID=RANDOM_CHARNUMの场合 =1なら风俗娘
;=========================================================
@别绘路人子STR设定(CID, ARGS, NUMBER = 0)
#DIM CID
#DIM NUMBER
#DIMS HAIR_TYPE
#DIMS HAIR_COLOR
#DIMS FACE_TYPE
#DIMS BODY_TYPE
#DIM CLOTHES
#DIM JOB
#DIMS ACCESSORY_TYPE1
#DIMS ACCESSORY_TYPE2
#DIMS 路人子パーツ
#DIMS 路人子パーツ_裸
#DIMS 感情_通常
#DIMS 感情_笑颜
#DIMS 感情_愤怒
#DIMS LAYER
#DIM WORD_COLOR_TYPE
#DIM BUST
;kaoruセットとの识别子を入れておく
路人子パーツ = [00:%ARGS%别绘描画]
JOB = (CID && NUMBER)
;路人店员の胸围は随机
BUST = (CID == RANDOM_CHARANUM) ? TALENT:CID:胸围 # (RAND:3) - 1
SELECTCASE ARGS
	CASE "贩卖员"
		;发型を设定
		HAIR_TYPE = %TEXTR("长发/セミロング/姫カット/ほわほわ/ショートカット/短发/一つ结び/ポニテ/ツインテ/三つ编み/おさげ发/こけし")%
		;瞳型を设定
		FACE_TYPE = %TEXTR("丸目/垂れ目/钓鱼目")%
		;胸围にあわせて素体决め。因幡は贫乳气味
		BUST = BUST - (RAND:2)
		SELECTCASE BUST
			CASE 0
				BODY_TYPE = 普乳素体
			CASE IS > 0
				BODY_TYPE = 巨乳素体
			CASE IS < 0
				BODY_TYPE = 素体
		ENDSELECT
		;发の毛と服が被るか判定
		CLOTHES = GROUPMATCH(HAIR_TYPE, "长发", "セミロング", "姫カット")
		;たまに眼镜/发卡。他に个性ないので裸でもつけっぱ
		ACCESSORY_TYPE1 = %\@ RAND:8 ? # %TEXTR("赤/黑")%眼镜 \@%
		ACCESSORY_TYPE2 = %\@ RAND:6 ? # 发卡 \@%
		
		;可变画像の设定。
		路人子パーツ += 贩卖员发型(HAIR_TYPE)
		路人子パーツ += 别绘路人子アクセ(ARGS, ACCESSORY_TYPE1, 9, ACCESSORY_TYPE2, 10)
		路人子パーツ_裸 = %路人子パーツ%

		路人子パーツ += 贩卖员素体(BODY_TYPE, 0, JOB, CLOTHES)
		路人子パーツ_裸 += 贩卖员素体(BODY_TYPE, 1, JOB, CLOTHES)
		感情_通常 = %贩卖员表情(FACE_TYPE, "通常")%
		感情_笑颜 = %贩卖员表情(FACE_TYPE, "笑颜")%
		感情_愤怒 = %贩卖员表情(FACE_TYPE, "愤怒")%
		
		LAYER = 08
	CASE "茨狸"
		FACE_TYPE = %TEXTR("イト目/垂れ目/ジト目/まる目/普通目/")%
		HAIR_TYPE = %TEXTR("ロング/ほわほわ/ウェーブ/ショートカット/一つ结び/ポニテ/ツインテ/みつ编みＡ/みつ编みＢ/おさげ发/こけし/固有Ａ/固有Ｂ/固有Ｃ/固有Ｄ")%
		;色变更はコレジャナイ感出さないように发色だけ
		LOCALS = 褐色/日烧け/不健康/恶魔/%\@ STRFIND(HAIR_TYPE, "固有") > -1 ? 栗/ # 橙/ \@%
		SIF RAND:5
			路人子パーツ = [00:%ARGS%别绘描画-色/%TEXTR(LOCALS)%]
		SELECTCASE BUST
			CASE 0
				BODY_TYPE = 普素体
			CASE IS > 0
				BODY_TYPE = 巨素体
			CASE IS < 0
				BODY_TYPE = 贫素体
		ENDSELECT
		;たまに亲分と同じ眼镜
		ACCESSORY_TYPE1 = %\@ RAND:7 ? # 丸めがね \@%
		ACCESSORY_TYPE2 = 
		
		;可变画像の设定。
		路人子パーツ = %路人子パーツ%[11:茨狸_狸猫耳][13:茨狸_叶子]
		路人子パーツ += 茨狸发型(HAIR_TYPE)
		路人子パーツ += 别绘路人子アクセ(ARGS, ACCESSORY_TYPE1, 12, ACCESSORY_TYPE2, 12)
		路人子パーツ_裸 = %路人子パーツ%
		路人子パーツ += 茨狸素体(BODY_TYPE, 0, JOB)
		路人子パーツ_裸 += 茨狸素体(BODY_TYPE, 1, JOB)
		感情_通常 = %茨狸表情(FACE_TYPE, "通常")%
		感情_笑颜 = %茨狸表情(FACE_TYPE, "笑颜")%
		感情_愤怒 = %茨狸表情(FACE_TYPE, "愤怒")%
		
		LAYER = 15
ENDSELECT
;口上色は随机
WORD_COLOR_TYPE = GET_CN_WORD_COLOR(STR:(RAND:13 + 2050))
IF CID
	CSTR:CID:路人子画像文字列_服_通常 = %路人子パーツ%%感情_通常%
	CSTR:CID:路人子画像文字列_服_笑颜 = %路人子パーツ%%感情_笑颜%
	CSTR:CID:路人子画像文字列_服_愤怒 = %路人子パーツ%%感情_愤怒%
	CSTR:CID:路人子画像文字列_裸_通常 = %路人子パーツ_裸%%感情_通常%
	CSTR:CID:路人子画像文字列_裸_笑颜 = %路人子パーツ_裸%%感情_笑颜%[%LAYER%:%ARGS%_装饰_照れ]
	CSTR:CID:路人子画像文字列_裸_愤怒 = %路人子パーツ_裸%%感情_愤怒%
	CFLAG:CID:1000 = WORD_COLOR_TYPE
ELSE
	路人子店员画像文字列:NUMBER:0 = %路人子パーツ%%感情_通常%
	路人子店员画像文字列:NUMBER:1 = %路人子パーツ%%感情_笑颜%
	路人子店员画像文字列:NUMBER:2 = %路人子パーツ%%感情_愤怒%
	路人子店员画像文字列:NUMBER:3 = %路人子パーツ_裸%%感情_通常%
	路人子店员画像文字列:NUMBER:4 = %路人子パーツ_裸%%感情_笑颜%[%LAYER%:%ARGS%_装饰_照れ]
	路人子店员画像文字列:NUMBER:5 = %路人子パーツ_裸%%感情_愤怒%
	路人子店员口上色:NUMBER = WORD_COLOR_TYPE
ENDIF
RETURN RESULT

;=========================================================
;贩卖员发型文字列の取得
;=========================================================
@贩卖员发型(HAIR_TYPE)
#FUNCTIONS
#DIMS HAIR_TYPE

SELECTCASE HAIR_TYPE
	CASE "长发"
		LOCALS = [01:贩卖员_长い][04:贩卖员_もみあげ]
	CASE "セミロング"
		LOCALS = [01:贩卖员_ほわほわ][04:贩卖员_もみあげ]
	CASE "姫カット"
		LOCALS = [01:贩卖员_ショート][04:贩卖员_もみあげ]
	CASE "ほわほわ"
		LOCALS = [01:贩卖员_ほわほわ]
	CASE "ショートカット"
		LOCALS = [01:贩卖员_ショート][04:贩卖员_短]
	CASE "短发"
		LOCALS = [01:贩卖员_结ぶ]
	CASE "一つ结び"
		LOCALS = [01:贩卖员_结ぶ][02:贩卖员_一つ结び][04:贩卖员_中]
	CASE "三つ编み"
		LOCALS = [01:贩卖员_结ぶ][02:贩卖员_三つ编み][04:贩卖员_中]
	;テール系と兔耳は相性恶いけど水增しのため入れる
	CASE "ツインテ"
		LOCALS = [01:贩卖员_结ぶ][02:贩卖员_ツインテ][04:贩卖员_中]
	CASE "ポニテ"
		LOCALS = [01:贩卖员_结ぶ][02:贩卖员_ポニテ]
	CASE "おさげ发"
		LOCALS = [01:贩卖员_结ぶ][02:贩卖员_おさげ]
	CASE "こけし"
		LOCALS = [01:贩卖员_切り揃え][04:贩卖员_中][05:贩卖员_ぱっつん]
ENDSELECT
SIF HAIR_TYPE != "こけし" 
	LOCALS = %LOCALS%[05:贩卖员_%TEXTR("短い/ぱっつん/粗め/真ん中寄せ/真ん中分け/斜め/")%]
RETURNF LOCALS

;=========================================================
;	茨狸发型文字列の取得
;=========================================================
@茨狸发型(HAIR_TYPE)
#FUNCTIONS
#DIMS HAIR_TYPE
#DIMS MATCH_TYPE
MATCH_TYPE = [03:茨狸_头%\@ STRFIND(HAIR_TYPE, "固有") > -1 ? %HAIR_TYPE% # 泛用 \@%]
SELECTCASE HAIR_TYPE
	CASE "ロング"
		LOCALS = [02:茨狸_发_ロング]
	CASE "ほわほわ"
		LOCALS = [02:茨狸_发_ほわほわ]
	CASE "ウェーブ"
		LOCALS = [02:茨狸_发_ウェーブ]
	CASE "ショートカット"
		LOCALS = [02:茨狸_发_ショート]
	CASE "一つ结び"
		LOCALS = [02:茨狸_发_结ぶ][01:茨狸_发_一つ结び]
	;テール系とけも耳は相性恶いけど水增しのため入れる
	CASE "ポニテ"
		LOCALS = [02:茨狸_发_结ぶ][01:茨狸_发_ポニテ]
	CASE "ツインテ"
		LOCALS = [02:茨狸_发_结ぶ][01:茨狸_发_ツインテ]
	CASE "みつ编みＡ"
		LOCALS = [02:茨狸_发_结ぶ][01:茨狸_发_みつ编みＡ]
	CASE "みつ编みＢ"
		LOCALS = [01:茨狸_发_みつ编みＢ][02:茨狸_发_结ぶ]
	CASE "おさげ发"
		LOCALS = [02:茨狸_发_结ぶ][01:茨狸_发_おさげ]
	CASE "こけし"
		LOCALS = [02:茨狸_发_切り揃え][07:茨狸_发_ぱっつん]
		GOTO SKIP
	CASE "固有Ａ", "固有Ｂ", "固有Ｃ", "固有Ｄ"
		LOCALS = [02:茨狸_发_%HAIR_TYPE%]
		GOTO SKIP
	CASEELSE
		LOCALS = [02:茨狸_发_ロング]
ENDSELECT
LOCALS = %LOCALS%[07:茨狸_发_%TEXTR("流す/ぱっつん/でこ出し/真ん中寄せ/真ん中分け/")%]
SIF RAND:3
	LOCALS = %LOCALS%[06:茨狸_发_もみあげ%TEXTR("Ａ/Ｂ/")%]
$SKIP
MATCH_TYPE = %MATCH_TYPE%%LOCALS%
RETURNF MATCH_TYPE

;=========================================================
;	贩卖员表情文字列の取得
;;=========================================================
@贩卖员表情(FACE_TYPE, moodStr)
#FUNCTIONS
#DIMS FACE_TYPE
#DIMS moodStr

SELECTCASE FACE_TYPE
	CASE "丸目"
		SELECTCASE moodStr
			CASE "通常"
				RETURNF "[06:贩卖员_丸通常]"
			CASE "笑颜"
				RETURNF "[06:贩卖员_丸笑い]"
			CASE "愤怒"
				RETURNF "[06:贩卖员_丸愤怒]"
		ENDSELECT
	CASE "钓鱼目"
		SELECTCASE moodStr
			CASE "通常"
				RETURNF "[06:贩卖员_钓通常]"
			CASE "笑颜"
				RETURNF "[06:贩卖员_钓笑い]"
			CASE "愤怒"
				RETURNF "[06:贩卖员_钓愤怒]"
		ENDSELECT
	CASE "垂れ目"
		SELECTCASE moodStr
			CASE "通常"
				RETURNF "[06:贩卖员_垂通常]"
			CASE "笑颜"
				RETURNF "[06:贩卖员_垂笑い]"
			CASE "愤怒"
				RETURNF "[06:贩卖员_垂愤怒]"
		ENDSELECT
ENDSELECT
RETURNF LOCALS

;=========================================================
;	茨狸表情文字列の取得
;	口を随机にしてパターン水增し
;;=========================================================
@茨狸表情(FACE_TYPE, moodStr)
#FUNCTIONS
#DIMS FACE_TYPE
#DIMS moodStr

SELECTCASE moodStr
		CASE "通常"
			LOCALS = [09:茨狸_%FACE_TYPE%][08:茨狸_普通口%TEXTR("Ａ/Ｂ/Ｃ/")%][10:茨狸_まろ眉普]
		CASE "愤怒"
			LOCALS = [09:茨狸_%FACE_TYPE%怒][08:茨狸_愤怒口%TEXTR("Ａ/Ｂ/Ｃ/")%][10:茨狸_まろ眉怒]
		CASE "笑颜"
			SELECTCASE FACE_TYPE
				CASE "イト目"
					LOCALS =  [09:茨狸_イト目]
				CASE "垂れ目"
					LOCALS =  [09:茨狸_笑い目垂]
				CASE "まる目"
					LOCALS =  [09:茨狸_笑い目吊]
				CASEELSE
					LOCALS =  [09:茨狸_笑い目普]
			ENDSELECT
			LOCALS = %LOCALS%[08:茨狸_笑い口%TEXTR("Ａ/Ｂ/Ｃ/")%][10:茨狸_まろ眉笑]
ENDSELECT
RETURNF LOCALS

;=========================================================
;	贩卖员服装文字列の取得
;	NUDE	=1 裸
;	JOB		=1 风俗娘
;	CLOTHES	=1 透过处理衣服を着せる 
;=========================================================
@贩卖员素体(BODY_TYPE, NUDE, JOB, CLOTHES)
#FUNCTIONS
#DIM NUDE
#DIM JOB
#DIM CLOTHES
#DIMS BODY_TYPE
#DIMS SETBODY
IF NUDE
	SETBODY = [03:贩卖员_%BODY_TYPE%]
ELSEIF JOB
	SETBODY = [03:贩卖员_%BODY_TYPE%][07:贩卖员_%BODY_TYPE%%\@ CLOTHES ? もみあげ # \@%浴衣]
ELSE
	SETBODY = [03:贩卖员_%\@ CLOTHES ? もみあげ # \@%服素体]
ENDIF
RETURNF SETBODY

;=========================================================
;	茨狸服装文字列の取得
;	NUDE	=1 裸
;	JOB		=1 风俗娘
;=========================================================
@茨狸素体(BODY_TYPE, NUDE, JOB)
#FUNCTIONS
#DIM NUDE
#DIM JOB
#DIMS BODY_TYPE
#DIMS SETBODY
IF NUDE
	SETBODY = [04:茨狸_%BODY_TYPE%]
ELSEIF JOB
	SETBODY = [04:茨狸_%BODY_TYPE%][05:茨狸_%BODY_TYPE%浴巾]
ELSE
	SETBODY = [04:茨狸_%\@ STRFIND(BODY_TYPE, "巨") > -1 ? 巨 # \@%法被]
ENDIF
RETURNF SETBODY

;=========================================================
;	装饰品文字列の取得
;	ここだけ共通化
;=========================================================
@别绘路人子アクセ(TYPE, ACC_TYPE1, ARG:1, ACC_TYPE2, ARG:2)
#FUNCTIONS
#DIMS TYPE
#DIMS ACC_TYPE1
#DIMS ACC_TYPE2
#DIMS SET_ACC
SET_ACC = 
SIF ACC_TYPE1 != ""
	SET_ACC = [%TOSTR(ARG:1, "D2")%:%TYPE%_%ACC_TYPE1%]
SIF ACC_TYPE2 != ""
	SET_ACC = %SET_ACC%[%TOSTR(ARG:2, "D2")%:%TYPE%_%ACC_TYPE2%]
RETURNF SET_ACC

;*******************************************************************************************************
;别绘路人子画像生成用函数
;*******************************************************************************************************
;既存路人子と规格が违うので流用して简略化した(してない)専用函数で生成
;*******************************************************************************************************
@别绘路人子画像生成(角色番号, 立绘_Flag)
#DIM 角色番号
#DIM 立绘_Flag
#DIM GID
#DIMS 着衣
#DIMS 感情 = "通常", "笑颜", "愤怒"
#DIMS 种类
#DIMS 生成文字列
#DIMS 合成文字列配列, 50
#DIMS 指定色
#DIMS 色合成
#DIM 合成数
#DIM リソースカウント
;路人子素材CSTR开始位置 = 30	CSTR:路人子画像文字列_服_通常のCSTR番号
;路人子素材CFLAG开始位置 = 923	路人子画像GraphicsIDが入る场所。1枚の画像につき1个のIDらしい。
;路人子素材リソース数 = 6		CSTR:RANDOM_CHARANUM:路人子画像文字列_服_通常～裸_愤怒までの６个

;リソース作成
;=====================================
FOR リソースカウント, 0, 路人子素材リソース数
	;初期化
	GID = 0
	VARSET LOCAL
	VARSET 合成文字列配列,
	合成数 = 0

	;画像合成下地用のダミーを配置する。GID(GraphicsID)にREF函数で登录可能な空き番号が入って戻ってくる
	CALL 画像合成(GID,"ダミー")
	
	;使用リソースの名称を配列に取得する。CSTR:RANDOM_CHARANUM:路人子画像文字列を渡すと
	;REF函数で合成文字列配列にリソース名、合成数にスプライトの数が入って戻ってくる
	CALL 别绘路人子リソース探索(合成文字列配列, 合成数, CSTR:角色番号:(路人子素材CSTR开始位置 + リソースカウント), 立绘_Flag)
	
	;重ねる顺番で配列に入るのでARRAYSORTは不要
	;ARRAYSORT 合成文字列配列, FORWARD, 0, 合成数
	
	;发色指定を取り出す
	 指定色 = %合成文字列配列:49%
	
	;路人子用画像を顺番に合成していく
	FOR LOCAL, 0, 合成数
		SIF 合成文字列配列:LOCAL == ""
			CONTINUE
		;发色指定があって合成用リソース名に"发"が含まれていたらカラーブレンドを指定
		SIF 指定色 != ""
			色合成 = %\@ STRFIND(合成文字列配列:LOCAL, "发") > -1 ? %指定色% # \@%
		CALL 画像合成(GID, 合成文字列配列:LOCAL, 0, 0, 0, 0, 色合成)
	NEXT
	;合成した画像をリソースに登录する
	CFLAG:角色番号:(路人子素材CFLAG开始位置 + リソースカウント) = GID
	;泛用テンプレートで使用できるように名前を统一する
	LOCAL = リソースカウント % 3
	着衣 = %\@ リソースカウント < 3 ? 服 # 裸\@%
	种类 = %\@ 立绘_Flag ? 颜绘 # 立绘 \@%
	CALL リソース登录(@"%种类%_%着衣%_%感情:LOCAL%_{角色番号}", GID)
NEXT
RETURN 1

;*******************************************************************************************************
;别绘路人子画像リソース探索用函数
;チェックサム的なのを无くしてただ寻找だけ
;*******************************************************************************************************
@别绘路人子リソース探索(合成文字列配列, 合成数, 生成文字列, 立绘_Flag)
#DIMS REF 合成文字列配列,0
#DIM REF 合成数
#DIMS 生成文字列
#DIM 立绘_Flag
#DIMS キー
VARSET LOCAL
VARSET LOCALS
;ゴミ取り
生成文字列 = %REPLACE(生成文字列, "^\"\"", "")%

キー = %TOSTR(LOCAL,"D2")%
;[00:别绘描画]に色指定が含まれるかを见る
IF STRFIND(DIC_GET(生成文字列, キー), "色/") > -1
	SPLIT DIC_GET(生成文字列, キー), "/", LOCALS
	合成文字列配列:49 = %LOCALS:1%
ENDIF

;素材のレイヤー数增やしたらカウントを适宜增やす
FOR LOCAL, 1, 16
	キー = %TOSTR(LOCAL,"D2")%
	SIF !DIC_CONTAINSKEY(生成文字列, キー)
		CONTINUE
	合成文字列配列:(合成数++) = %キー%_%DIC_GET(生成文字列, キー)%%\@ 立绘_Flag ? _颜 # \@%
NEXT
RETURN 1


;*******************************************************************************************************
;お出かけイベント用路人子の画像を消す函数
;*******************************************************************************************************
@路人店员画像リセット
#DIM 番号
FOR 番号, 20, 路人子店员最大框
	SIF !SPRITECREATED(@"颜绘_服_通常_MOB_{番号}")
		CONTINUE
	SPRITEDISPOSE @"颜绘_服_通常_MOB_{番号}"
	SPRITEDISPOSE @"颜绘_服_笑颜_MOB_{番号}"
	SPRITEDISPOSE @"颜绘_服_愤怒_MOB_{番号}"
	SPRITEDISPOSE @"颜绘_裸_通常_MOB_{番号}"
	SPRITEDISPOSE @"颜绘_裸_笑颜_MOB_{番号}"
	SPRITEDISPOSE @"颜绘_裸_愤怒_MOB_{番号}"
	FOR COUNT, 0, 6
		GDISPOSE 路人子店员GID:番号:COUNT
		路人子店员GID:番号:COUNT = 0
		路人子店员画像文字列:番号:COUNT = 
	NEXT
NEXT
RETURN
