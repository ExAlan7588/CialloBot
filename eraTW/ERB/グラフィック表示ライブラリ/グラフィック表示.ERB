;********************************************************************************************
;2019/8/14 作成 ロウ@TW梦子口上の人
;
;Licence  :ライセンスフリー(CC0)。
;
;グラフィック表示简易化ライブラリ
;なんかややこしいグラフィック表示关联を简单に表示できるようにまとめたもの。
;HTMLもどきだし、可读性恶いし、冗长なので「もう画像指定したら纵幅とか胜手に揃えろよ」っていうことで作りました。
;********************************************************************************************

;********************************************************************************************
;画像表示用函数
;これを呼べば画像の纵横计算とかややこしいことしないで画像を一枚表示できる
;横位置は左揃えの场合は左端から、右揃えの场合は右端から指定したピクセル数分间隔を空ける
;画像反转フラグは 1:横反转 2：纵反转 3:纵横反转
;********************************************************************************************
@画像表示(画像名, 表示位置, 纵位置, 横位置, 扩大比率 = 100, 自动改行 = 0, ボタン生成 = 0, VALUE = "", TITLE = "", マウスオーバー画像名 = "", 画像反转フラグ = 0)
#DIMS 画像名				;リソース名
#DIMS マウスオーバー画像名	;マウスオーバー时に表示する画像名 省略可
#DIMS 表示位置				;表示位置指定文字列
#DIM  纵位置				;ピクセル指定 内部でフォント尺寸基准のパーセンテージに变换する
#DIM  横位置				;上に同じ
#DIM  自动改行				;自动改行有无フラグ 默认はしない
#DIM  扩大比率				;パーセンテージ指定 指定しなければ100
#DIM  ボタン生成			;ボタンとして使用するか 默认はしない
#DIMS  VALUE				;ボタンのバリュー
#DIMS  TITLE				;マウスオーバー时に表示する文字列
#DIM  画像反转フラグ		;画像反转を行うかのフラグ
#DIMS  表示HTML
IF SPRITECREATED(画像名)
	SIF 描画开始行数 == 0
		描画开始行数 = LINECOUNT

	表示HTML =
	表示HTML += @"%画像表示位置HTML(表示位置)%"

	SIF GROUPMATCH(表示位置, "LEFT", "left", "左")
		表示HTML += @"%横位置HTML(横位置)%"
	SIF ボタン生成
		表示HTML += @"%ボタン生成HTML(VALUE, TITLE)%"

	表示HTML += @"%画像表示单独HTML(画像名, 纵位置, 扩大比率, マウスオーバー画像名, 画像反转フラグ)%"

	SIF ボタン生成
		表示HTML += @"</button>"
	SIF GROUPMATCH(表示位置, "RIGHT", "right", "右")
		表示HTML += @"%横位置HTML(横位置)%"

	DEBUGPRINTFORML %表示HTML%
	HTML_PRINT 表示HTML

	SIF 自动改行
		CALL ピクセル自动改行(纵位置 + 默认角色画像横幅 * 扩大比率 / 100)
ELSE
	RETURN 0
ENDIF
RETURN 1

;********************************************************************************************
;画像セット函数
;并べて表示したい场合に、画像一斉表示函数と合わせて使用する
;横位置は指定したピクセル数分画像间の间隔を空ける
;********************************************************************************************
@画像セット(画像名, 纵位置, 画像间隔, 扩大比率 = 100, レイヤー阶数 = 0, ボタン生成 = 0, VALUE = "", TITLE = "", マウスオーバー画像名 = "", 画像反转フラグ = 0)
#DIMS 画像名				;リソース名
#DIMS マウスオーバー画像名	;マウスオーバー时に表示する画像名 省略可
#DIMS 表示位置				;表示位置指定文字列
#DIM  纵位置				;ピクセル指定 内部でフォント尺寸基准のパーセンテージに变换する
#DIM  画像间隔				;ピクセル指定 内部でフォント尺寸基准のパーセンテージに变换する
#DIM  扩大比率				;パーセンテージ指定 指定しなければ100
#DIM  レイヤー阶数			;重ね表示时の阶数
#DIM  ボタン生成			;ボタンとして使用するか 默认はしない
#DIMS VALUE					;ボタンのバリュー
#DIMS TITLE					;マウスオーバー时に表示する文字列
#DIM  画像反转フラグ		;画像反转を行うかのフラグ 1:横反转 2：纵反转 3:纵横反转

SIF 描画开始行数 == 0
	描画开始行数 = LINECOUNT
;PRINTFORML %画像名%をチェック
IF SPRITECREATED(画像名)
	SIF TEMP_HTML:レイヤー阶数 != "" && 画像间隔 > 0
		TEMP_HTML:レイヤー阶数 += @"%横位置HTML(画像间隔)%"

	SIF ボタン生成
		TEMP_HTML:レイヤー阶数 += @"%ボタン生成HTML(VALUE, TITLE)%"

	TEMP_HTML:レイヤー阶数 += @"%画像表示单独HTML(画像名, 纵位置, 扩大比率, マウスオーバー画像名, 画像反转フラグ, レイヤー阶数)%"

	SIF ボタン生成
		TEMP_HTML:レイヤー阶数 += @"</button>"

	LOCAL = 纵位置 + 默认角色画像横幅 * 扩大比率 / 100
	SIF LOCAL > TEMP_HTML_MAX_HEIGHT
		TEMP_HTML_MAX_HEIGHT = LOCAL

	SIF MAX_LAYER_NUM < レイヤー阶数
		MAX_LAYER_NUM = レイヤー阶数
ELSE
	;PRINTFORML ないよ
	RETURN 0
ENDIF
RETURN 1

;********************************************************************************************
;ダミーセット函数
;并べて表示したい场合に、画像一斉表示函数と合わせて使用する
;横位置は指定したピクセル数分画像间の间隔を空ける
;********************************************************************************************
@ダミーセット(纵位置, 画像间隔, 扩大比率 = 100, レイヤー阶数 = 0)
#DIM  纵位置				;ピクセル指定 内部でフォント尺寸基准のパーセンテージに变换する
#DIM  画像间隔				;ピクセル指定 内部でフォント尺寸基准のパーセンテージに变换する
#DIM  扩大比率				;パーセンテージ指定 指定しなければ100
#DIM  レイヤー阶数			;重ね表示时の阶数
#DIM  画像横幅				;フォント尺寸基准のパーセンテージ

;uEmuera环境でエフェクトがずれる际には以下の二行をアンコメント
;CALL 画像セット("ダミー", 纵位置, 画像间隔, 扩大比率, レイヤー阶数)
;RETURN 1

SIF TEMP_HTML:レイヤー阶数 != "" && 画像间隔 > 0
	TEMP_HTML:レイヤー阶数 += @"%横位置HTML(画像间隔)%"

画像横幅 = (默认角色画像横幅 * 扩大比率) / GETCONFIG("フォントサイズ")
TEMP_HTML:レイヤー阶数 += @"<shape type='space' param='{画像横幅}'>"

LOCAL = 纵位置 + 默认角色画像横幅 * 扩大比率 / 100
SIF LOCAL > TEMP_HTML_MAX_HEIGHT
	TEMP_HTML_MAX_HEIGHT = LOCAL

SIF MAX_LAYER_NUM < レイヤー阶数
	MAX_LAYER_NUM = レイヤー阶数

RETURN 1

;********************************************************************************************
;画像一斉表示函数
;并べて表示したい场合に、画像セット函数と合わせて使用する
;レイヤー阶数-1で全てのレイヤーを一斉表示する
;********************************************************************************************
@画像一斉表示(表示位置, 横位置, 自动改行 = 0, レイヤー阶数 = -1)
#DIMS 表示位置				;表示位置指定文字列
#DIM  横位置				;上に同じ
#DIM DYNAMIC 自动改行		;自动改行有无フラグ 默认はしない
#DIM DYNAMIC レイヤー阶数	;重ね表示时の阶数

IF レイヤー阶数 == -1
	FOR レイヤー阶数, 0, MAX_LAYER_NUM + 1
		CALL 画像一斉表示(表示位置, 横位置, 0, レイヤー阶数)
	NEXT

	SIF 自动改行
		CALL ピクセル自动改行(TEMP_HTML_MAX_HEIGHT)
	CALL 画像描画结束

	RETURN 1
ENDIF

IF TEMP_HTML:レイヤー阶数 != ""
	TEMP_HTML:レイヤー阶数 = %画像表示位置HTML(表示位置)%%TEMP_HTML:レイヤー阶数%

	SIF GROUPMATCH(表示位置, "LEFT", "left", "左")
		TEMP_HTML:レイヤー阶数 = %横位置HTML(横位置)%%TEMP_HTML:レイヤー阶数%
	SIF GROUPMATCH(表示位置, "RIGHT", "right", "右")
		TEMP_HTML:レイヤー阶数 += @"%横位置HTML(横位置)%"

	HTML_PRINT TEMP_HTML:レイヤー阶数

	SIF 自动改行
		CALL ピクセル自动改行(TEMP_HTML_MAX_HEIGHT)

	TEMP_HTML:レイヤー阶数 =
ELSE
	RETURN 0
ENDIF
RETURN 1

;********************************************************************************************
;画像表示单独のHTML取得函数
;诸々の计算は内部でするので指定するのは画像だけにしたい
;扩大缩小したい场合を考えて一応扩大比率指定出来るように
;********************************************************************************************
@画像表示单独HTML(画像名, 纵位置, 扩大比率 = 100, マウスオーバー画像名 = "", 画像反转フラグ = 0, レイヤー阶数 = 0)
#FUNCTIONS
#DIMS 画像名				;リソース名
#DIMS マウスオーバー画像名	;マウスオーバー时に表示する画像名 省略可
#DIM  扩大比率				;パーセンテージ指定 指定しなければ100
#DIM  画像纵幅				;ややこしいがフォント尺寸基准のパーセンテージ
#DIM  画像横幅				;上に同じ
#DIM  纵位置				;ピクセル指定 内部でフォント尺寸基准のパーセンテージに变换する
#DIM  画像反转フラグ		;画像反转を行うかのフラグ 1:横反转 2：纵反转 3:纵横反转
#DIM  レイヤー阶数			;重ね表示时の阶数

IF SPRITECREATED(画像名)
	;画像纵幅 = (默认角色画像横幅 * 扩大比率) / 100 * (GETBIT(画像反转フラグ, 1) ? -1 # 1 )
	;画像横幅 = (默认角色画像横幅 * 扩大比率) / 100 * (GETBIT(画像反转フラグ, 0) ? -1 # 1 )
	画像纵幅 = (默认角色画像横幅 * 扩大比率) / GETCONFIG("フォントサイズ") * (GETBIT(画像反转フラグ, 1) ? -1 # 1 )
	画像横幅 = (默认角色画像横幅 * 扩大比率) / GETCONFIG("フォントサイズ") * (GETBIT(画像反转フラグ, 0) ? -1 # 1 )

	;切舍て保护のため先に挂け算
	纵位置   = (100 * 纵位置 / GETCONFIG("フォントサイズ")) - (100 * (LINECOUNT-描画开始行数+レイヤー阶数) * (GETCONFIG("一行の高さ") / GETCONFIG("フォントサイズ")))

	LOCALS = <img src='%画像名%' \@ マウスオーバー画像名 != "" ? srcb='%マウスオーバー画像名%' # \@ height='{画像纵幅}' width='{画像横幅}' ypos='{纵位置}'>
	RETURNF LOCALS
ELSE
	PRINTFORML 画像リソースが见つかりません
	RETURNF @""
ENDIF

;********************************************************************************************
;位置揃え指定函数
;PタグがただのALIGNMENT指定用タグと化してるので分离
;返回值としてＰタグの文字列を返す
;ARGS = 画像表示位置指定文字列
;********************************************************************************************
@画像表示位置HTML(表示位置)
#FUNCTIONS
#DIMS 表示位置
SELECTCASE 表示位置
	CASE "LEFT", "left", "左"
		LOCALS = <p align='left'>
		RETURNF LOCALS
	CASE "CENTER", "center", "中央"
		LOCALS = <p align='center'>
		RETURNF LOCALS
	CASE "RIGHT", "right", "右"
		LOCALS = <p align='right'>
		RETURNF LOCALS
	CASEELSE
		;认识できなかったら左揃え
		LOCALS = <p align='left'>
		RETURNF LOCALS
ENDSELECT

;********************************************************************************************
;ボタン生成函数
;返回值としてbuttonタグの文字列を返す
;POSはややこしいので使用しない
;********************************************************************************************
@ボタン生成HTML(VALUE, TITLE)
#FUNCTIONS
#DIMS  VALUE		;ボタンのバリュー
#DIMS  TITLE		;マウスオーバー时に表示する文字列
LOCALS = <button
SIF VALUE != ""
	LOCALS += @" value='%VALUE%'"
SIF TITLE != ""
	LOCALS += @" title='%TITLE%'"
LOCALS += @" >"
RETURNF LOCALS

;********************************************************************************************
;横位置指定函数
;いちいち场合分けするのが嫌なので全てshapeで调节する
;返回值としてshapeタグの文字列を返す
;横位置はピクセル单位での指定
;********************************************************************************************
@横位置HTML(横位置)
#FUNCTIONS
#DIM 横位置		;ピクセル指定 内部でフォント尺寸基准のパーセンテージに变换する

;切舍て保护のため先に挂け算
横位置 = 100 * 横位置 / GETCONFIG("フォントサイズ")

LOCALS = <shape type='space' param='{横位置}'>
RETURNF LOCALS

;********************************************************************************************
;画像合わせ改行函数
;画像に合わせて改行したい场合に使用する
;纵幅をピクセル单位で指定すると、それをぎりぎり内包する行数だけ改行する
;********************************************************************************************
@ピクセル自动改行(纵幅)
#DIM 纵幅
#DIM 行数

;端数切り上げ。演算内容は 纵幅 / GETCONFIG("一行の高さ")
行数 = (纵幅 + GETCONFIG("一行の高さ") - 1) / GETCONFIG("一行の高さ")

FOR LOCAL, 0, 行数 - (LINECOUNT - 描画开始行数)
	PRINTFORML
NEXT
描画开始行数 = 0
RETURN 1

;********************************************************************************************
;画像セット等で变更される变量のリセット
;行ずれの无视、レイヤーの消去に使用
;********************************************************************************************
@画像描画结束
VARSET TEMP_HTML
描画开始行数 = 0
MAX_LAYER_NUM = 0
TEMP_HTML_MAX_HEIGHT = 0