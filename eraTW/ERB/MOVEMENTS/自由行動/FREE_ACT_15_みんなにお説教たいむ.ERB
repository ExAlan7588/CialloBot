@EXIST_FreeAct15
#LOCALSIZE 1
#LOCALSSIZE 1

;オブジェクト本体
@FreeAct15(ARG, O_DATA, V_NAME)
#FUNCTION
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS O_DATA
#DIMS V_NAME
#DIM 经过时间
经过时间 = Activity_Duration:TARGET
SELECTCASE O_DATA
;陪同行动指令の名称
CASE "名称"
	CALLF MAKE_STR(V_NAME, "去看看情况")
CASE "CHAIN"
;他角色に连锁しない场合コメントアウトすること
	CALLF MAKE_INT(V_NAME, 1)
CASE "经过时间"
;陪同行动指令での经过时间
;0だと30分か残り时间全部の小さい方
	CALLF MAKE_INT(V_NAME, 0)
ENDSELECT

@自由行动内容_15(CHARA, ARGS, 时间系数, CHAIN)
#DIM CHARA
#DIM CHAIN
#DIM 现在地
#DIM メッセージ表示
#DIM 时间系数
#DIM 经过时间
现在地 = CFLAG:CHARA:当前位置
メッセージ表示 = 现在地 == CFLAG:MASTER:当前位置 ? 1 # 0
SELECTCASE ARGS
	CASE "发生"
		;屋外で发生
		SIF INROOM(现在地)
			RETURN 0
		;仕事予定の邪魔をしない
		SIF !CHARA_HOLIDAY(CHARA)
			RETURN 0
		;映姫样は常に起点
		SIF CHARA == 30
			GOTO START
		;小町/久侘歌以外の神灵と蓬莱人は对象外
		SIF CHECK_CHARA(CHARA, "蓬莱人") || (TALENT:CHARA:神灵 && !GROUPMATCH(CHARA, 76, 129))
			RETURN 0
		;映姫样がすでに说教中なら发生
		SIF Activity_Type:30 != 15 || 现在地 != CFLAG:30:当前位置 || !Activity_Duration:30
			RETURN 0
		$START
		IF メッセージ表示
			PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%
			IF CHARA == 30 
				PRINTFORMW 对%FA15_TARGET(0)%的说教开始了。
			ELSE
				PRINTFORMW 也\@ CHAIN ? 加入进来开始了说教 # 被捕捉到了映姫的说教中 \@…
			ENDIF
		ELSEIF CHARA == 30
			CALLF FA15_TARGET(0)
		ENDIF
		;自由行动にかける时间をRETURNする。小町のみ最低保障2时间
		IF CHARA == 30
			RETURN 60 + RAND:30
		ELSE
			LOCAL = 30 + RAND:60 + ((CHARA == 76) * 90)
			Activity_Duration:30 = MAX(Activity_Duration:30, LOCAL)
			RETURN LOCAL
		ENDIF
	CASE "行动中"
		;行动中の处理、地の文もここで。
		IF メッセージ表示
			IF CHARA == 30
				IF 充填率(CHARA,1) >= 10 || 充填率(CHARA,2) >= 10
					PRINTFORMW 面色绯红的%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%正在对%FA15_TARGET(1)%进行%TEXTR("持续的/无休止的/长篇大论的/")%说教…
				ELSE
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%正在对%FA15_TARGET(1)%进行%TEXTR("持续的/无休止的/长篇大论的/")%说教…
				ENDIF	
			ELSEIF 现在地 != CFLAG:30:当前位置 || Activity_Type:30 != 15
				CALL FA15_RESET(CHARA)
			ELSE
				PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%对映姫的说教
				SELECTCASE CFLAG:CHARA:性格倾向
					;气弱系
					CASE 1
						PRINTFORMW %TEXTR("就快要哭出来了/边听边低下了头")%…
					;强气系
					CASE 2
						PRINTFORMW %TEXTR("暗中吐了舌头/很随意地听着")%…
					;坦率系
					CASE 3
						PRINTFORMW %TEXTR("闷闷不乐地听着/的各个部分点头称是")%…
					;真面目系
					CASE 4
						PRINTFORMW %TEXTR("一边听一边反省着/认真地听取着")%…
					;其他
					CASEELSE
						PRINTFORMW %TEXTR("温顺地接受着/静静地听着")%…
				ENDSELECT
			ENDIF
		ELSEIF CHARA != 30 && 现在地 != CFLAG:30:当前位置
			CALL FA15_RESET(CHARA)
		ENDIF
	CASE "完了"
		;行动时间を使い切ったときの处理、地の文もここで
		IF メッセージ表示
			IF CHARA == 30
				IF 充填率(CHARA,1) >= 10 || 充填率(CHARA,2) >= 10
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%结束说教时吐出甜美的喘息。
				ELSE
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%似乎终于结束了说教。
				ENDIF	
			ELSE
				PRINTDATAW
					DATAFORM 说教终于结束后的%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%感觉\@ CFLAG:CHARA:性格倾向 == 2 ? 对此很反感 # 松了一口气 \@。
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%在说教结束后\@ CFLAG:CHARA:性格倾向 == 1 ? 仍然在哭泣着 # 发出了一声小小的叹息 \@。
					DATAFORM 说教结束后的%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%\@ CFLAG:CHARA:性格倾向 == 4 ? 礼貌地表达了感谢 # 脸色很不好的样子 \@。
				ENDDATA
			ENDIF
		ENDIF
	CASE "中断"
		;中断メッセージは诶嘿嘿突入时等でも表示されてしまうので表示しない
		;说教する侧がいなくなった场合の处理
		IF CHARA == 30
			FOR COUNT, 1, CHARANUM
				SIF Activity_Type:COUNT != 15
					CONTINUE
				CALL FA15_RESET(COUNT)
			NEXT
		ENDIF
	CASE "交手"
		;自由行动に付き合ったとき用メッセージ
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", PLAYER)%听到了莫名其妙的说教。
		IF TFLAG:193
			PRINTFORML 被映姫发现并被说教了…
		ELSE
			LOCALS = %\@ CHARA == 30 ? %FA15_TARGET(1)% # %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)% \@%
			PRINTDATAL
				DATAFORM ……不知道什么时候才会结束。
				DATAFORM ……不是自己真是太好了。
				DATAFORM ……有些同情不幸的%LOCALS%。
			ENDDATA
		ENDIF
	CASE "ソース"
		;交手指令选择时の入手ソース、时间系数は付き合った时间の1/10
		;ソースの基准よくわかんないので适当
		DOWNBASE:MASTER:气力 += 20 * 时间系数
		;15%でいっしょに说教されて入手ソース增/MASTER消费气力激增
		IF !RAND:6
			TFLAG:193 = 1
			SOURCE:30:欢乐 = (100 + ((ABL:30:亲密 + 好感度系数(TARGET)) * 40)) * 时间系数
			SOURCE:30:征服 = (60 + (ABL:30:施虐属性 * 60)) * 时间系数
			IF TARGET != 30
				SOURCE:TARGET:被动 = (30 + ((ABL:顺从 + 好感度系数(TARGET)) * 50)) * 时间系数
				SOURCE:TARGET:屈从 = (30 + ((ABL:顺从 + 好感度系数(TARGET)) * 50)) * 时间系数
				DOWNBASE:TARGET:气力 += 10 * 时间系数
			ENDIF
			DOWNBASE:MASTER:气力 += 150 * 时间系数
		ELSE
			SOURCE:欢乐 = (50 + ((ABL:亲密 + 好感度系数(TARGET)) * 30)) * 时间系数
			SOURCE:征服 = (20 + (ABL:施虐属性 * 50)) * 时间系数
			IF TARGET != 30
				SOURCE:TARGET:被动 = (20 + ((ABL:顺从 + 好感度系数(TARGET)) * 20)) * 时间系数
				SOURCE:TARGET:屈从 = (20 + ((ABL:顺从 + 好感度系数(TARGET)) * 20)) * 时间系数
				DOWNBASE:TARGET:气力 += 10 * 时间系数
			ENDIF
		ENDIF
ENDSELECT
;说教相手を表示する式中函数
@FA15_TARGET(ARG)
#FUNCTIONS
#DIM 累计
#DIMS 路人
#DIMS ネームド
;说教开始は仕样上常に路人角色
IF !ARG
	SELECTCASE GET_MAPID(CFLAG:30:当前位置)
		CASE 0
			路人 = 妖怪
		CASE 1
			路人 = 参拝客
		CASE 2
			路人 = %TEXTR("町娘/村人/")%
		CASE 3
			路人 = %TEXTR("女仆妖精/哥布林/")%
		CASE 4
			路人 = 因幡
		CASE 5
			路人 = 妖怪%TEXTR("猫/狸/狐/")%
		CASE 6
			路人 = %TEXTR("僵尸妖精/死神/")%
		CASE 7
			路人 = 河童
		CASE 8
			路人 = %TEXTR("参拝客/天狗/山童/")%
			SIF INRANGE(CFLAG:30:当前位置, 830, 850)
				路人 = 天人
		CASE 9
			路人 = 鬼
		CASE 10
			路人 = %TEXTR("玉兔/月人/")%
	ENDSELECT
	LOCALS = %TEXTR("路过的/刚好目光相对的/在附近的/不幸没有逃脱掉的")%%路人%
	RETURNF LOCALS
;映姫样と同じ场所にいて自由行动=15且つ行动时间中の角色が说教され中
ELSE
	ネームド =
	累计 = 0
	FOR COUNT, 1, CHARANUM
		SIF COUNT == 30
			CONTINUE
		SIF CFLAG:30:当前位置 != CFLAG:COUNT:当前位置
			CONTINUE
		SIF Activity_Type:COUNT != 15 || !Activity_Duration:COUNT
			CONTINUE
		ネームド += @"%\@ 累计 ? 和 # \@%%NAME_OUTPUT_TRANSLATION("CALLNAME", COUNT)%"
		累计 ++
	NEXT
	SIF 累计
		RETURNF ネームド
	RETURNF 路人
ENDIF
;集合等での强制中断处理
@FA15_RESET(ARG)
Activity_Type:ARG = 0
Activity_Duration:ARG = 0
RETURN
