;FileName_SLEEP.ERB ----------------------------- Rev1.00
;角色睡眠处理
;CALL		USER
;ARG		0:角色NO, 1:MESSAGE出力要否
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_SLEEP(CHARA, メッセージ表示)
#DIM CHARA
#DIM メッセージ表示
#DIM おやすみ口上
#DIM おやすみ口上２
#DIM 回家口上
#DIM 表示済み
#DIM 人数
SIF TALENT:CHARA:妄想的产物
	RETURN 0
SIF TCVAR:CHARA:烂醉
	RETURN 0

おやすみ口上 = 0
おやすみ口上２ = 0
表示済み = 0
IF CFLAG:CHARA:当前位置 == CFLAG:MASTER:当前位置 && !CFLAG:CHARA:勃然大怒 && メッセージ表示
	;寝る场所にいる
	IF (IN_HOME(CFLAG:MASTER:当前位置) && CFLAG:CHARA:当前位置 == CFLAG:CHARA:初始位置) || CFLAG:MASTER:当前位置 == OMANEKIBEYA()
		;寝ている
		IF CFLAG:CHARA:睡眠
			IF TALENT:CHARA:恋慕 && CFLAG:CHARA:陪睡中
				PRINTDATA
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%幸福的微笑着
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%小声的念着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的名字
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%香甜的睡着
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%无意识地抱住了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%在睡梦中甜甜一笑
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%轻声梦呓
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%在睡梦中翻了个身
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%在%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%胸膛里蹭了蹭脸颊
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%枕着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的臂膀
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%呼出柔和的鼻息
				ENDDATA
				PRINTW
			ELSE
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%在睡着
			ENDIF
			;おやすみ口上,3,睡眠中
			おやすみ口上 = 3
		;陪睡中
		ELSEIF TALENT:CHARA:恋慕 && CFLAG:CHARA:陪睡中
			IF CFLAG:CHARA:延迟
				PRINTDATA
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%幸福的微笑着
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%打了个小哈欠
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%红着脸看着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%悄悄地依偎到%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%身边
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%揉了揉困倦的眼睛
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%稍显疲倦的眨着眼
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%嘟着小嘴
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%睡意阑珊
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%微笑着重复%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的名字
					DATAFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%睡眼惺忪的搂着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
				ENDDATA
				PRINTW
				;おやすみ口上,2,眠そうにしている
				おやすみ口上 = 2
			ELSE
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%败给了睡魔、进入了梦乡…
				;おやすみ口上,9,寝室に居る状态でその场で寝る
				おやすみ口上 = 9
			ENDIF
		;寝るところ
		ELSEIF CFLAG:CHARA:当前位置 == CFLAG:CHARA:前一次的位置
			IF CFLAG:CHARA:延迟
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%正在犯困的样子
				;おやすみ口上,2,眠そうにしている
				おやすみ口上 = 2
			ELSE
				IF CFLAG:CHARA:衰弱
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%说累了、便躺下来了
					;おやすみ口上,5,衰弱して寝る
					おやすみ口上 = 5
				ELSE
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%败给了睡魔、进入了梦乡…
					;おやすみ口上,9,寝室に居る状态でその场で寝る
					おやすみ口上 = 9
				ENDIF
			ENDIF
			SIF !寝室许可(CHARA)
				CALL 被从寝室赶了出来(CHARA)
		ELSE
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%正在犯困的样子
			CALL 被从寝室赶了出来(CHARA)
			;おやすみ口上,2,眠そうにしている
			おやすみ口上 = 2
		ENDIF
		;隐密中セット
		SIF CFLAG:MASTER:当前位置 != CFLAG:MASTER:前一次的位置
			CALL SET_SNEAK(1, CHARA, 1)
	;これから寝る
	ELSEIF 睡眠时间(CHARA) && CFLAG:MASTER:当前位置 == CFLAG:CHARA:当前位置 && CFLAG:CHARA:当前位置 == CFLAG:CHARA:前一次的位置 && !TCVAR:CHARA:休憩中
		IF CFLAG:CHARA:神社在住
			CALL SLEEP_RESIDENT(CHARA)
		ELSE
			CALL SLEEP_VISITOR(CHARA)
		ENDIF
		表示済み = 1
	;相手が自室に归っていく
	ELSE
		;おやすみ口上,1-0,自室に归って寝る
		おやすみ口上 = 1
	ENDIF
	;おやすみ口上or回家口上
	IF (CFLAG:MASTER:当前位置 == CFLAG:CHARA:当前位置 || (睡眠时间(CHARA) && CFLAG:CHARA:当前位置 == CFLAG:CHARA:神社在住)) && !表示済み
		IF CFLAG:CHARA:神社在住 || CFLAG:MASTER:当前位置 == OMANEKIBEYA() || TCVAR:CHARA:休憩中
			CALL KOJO_MESSAGE_SEND("EVENT", 3, CHARA, おやすみ口上)
		ELSE
			回家口上 = 1
			SIF CHK_DATENOW(CFLAG:约会中)
				回家口上 = 2
			CALL KOJO_MESSAGE_SEND("EVENT", 20, CHARA, 回家口上)
		ENDIF
	ENDIF
	;修正睡奸穿衣bug额外代码【&& TCVAR:CHARA:睡眠深度 == 0】
	IF BASE:MASTER:潜伏率 > 0 && TCVAR:CHARA:睡眠深度 == 0
		SIF CFLAG:CHARA:诶嘿嘿
			CALL ENDUFUFU_ALL
	ELSEIF !CFLAG:CHARA:睡眠
			CALL ENDUFUFU_ALL
	ENDIF
ENDIF

;宴会中に寝た角色は宴会フラグが外れる
IF GROUPMATCH(CFLAG:CHARA:职业种类, 48, 49, 50, 51)
	CFLAG:CHARA:宴会参加 = 0
	CFLAG:CHARA:职业种类 = 0
	CFLAG:CHARA:行动 = 0
	BASE:CHARA:工作量 = 0
ENDIF
SIF BASE:CHARA:工作量 <= 0 && CFLAG:CHARA:行动 == 5
	CALL CHARA_JOBEND(CHARA)
;仕事中にぶっ倒れたらお工作结束
IF CFLAG:CHARA:衰弱 && CFLAG:CHARA:行动 == 5
	CFLAG:CHARA:行动 = 0
	BASE:CHARA:工作量 = 0
ENDIF

;自室で寝るべく戻ってくる
IF CFLAG:CHARA:当前位置 != CFLAG:CHARA:初始位置 && !CFLAG:CHARA:睡眠 && !CFLAG:CHARA:出禁
	IF CFLAG:MASTER:当前位置 == CFLAG:CHARA:初始位置
		CFLAG:CHARA:当前位置 = CFLAG:CHARA:初始位置
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%回到了房间
		IF 寝室许可(CHARA) || FLAG:催眠强度 <= 50
			;おやすみ口上,7-0,そのまま寝た
			おやすみ口上２ = 0
			IF CFLAG:MASTER:诶嘿嘿
				LOCAL = FINDCHARA(CFLAG:诶嘿嘿, CFLAG:MASTER:诶嘿嘿, 1)
				IF BASE:MASTER:潜伏率 > 0
					SIF CFLAG:CHARA:诶嘿嘿
						CALL ENDUFUFU_ALL
				ELSE
					CALL ENDUFUFU_ALL
				ENDIF
				人数 = RESULT
				PRINTFORML 面对嘟起嘴的%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%、%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%不情愿地和%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%\@ 人数 > 1 ? 她们 #  \@分开了
				;おやすみ口上,7-2,他角色とのＨを止めさせて寝た
				おやすみ口上２ = 2
			ENDIF
			PRINTL 
			CALL 寝起き更衣(CHARA)
			CALL KOJO_MESSAGE_SEND("EVENT", 3, CHARA, 7, おやすみ口上２)
		ELSE
			CALL 被从寝室赶了出来(CHARA)
		ENDIF
	ENDIF

	;寝间着にお更衣
	CALL CTRL_CLOTHES_SET(CHARA, "现在衣装の变更_睡衣")
	CALL 内衣确认リセット(CHARA)
	CALL CLOTHES_SETTING_TRAIN(CHARA)
ENDIF

;自室でねる
IF !TCVAR:CHARA:休憩中
	CFLAG:CHARA:当前位置 = CFLAG:CHARA:初始位置
	DEBUGPRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%は{TIME}に{CFLAG:CHARA:初始位置}で寝たよ
ENDIF

;施锭
SIF GET_MAPID(CFLAG:CHARA:初始位置) == MAIN_MAP && CFLAG:MASTER:初始位置 != CFLAG:CHARA:初始位置
	LOCK:(CFLAG:CHARA:初始位置) = 1
CFLAG:CHARA:同行 = 0
CALL SET_SLEEP(1,CHARA,0)
SIF CFLAG:CHARA:睡眠
	CALL SET_DERAY(99,CHARA)

;FileName_SLEEP.ERB ----------------------------- Rev1.00
;角色起床处理
;CALL		USER
;ARG		0:角色NO, 1:施锭解除有无, 2:睡眠奸からの起床かどうか
;起床场所	数值1なら同栖、数值0なら非同栖
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_AWAKE, ARG,ARG:1,ARG:2
#DIM 起床场所
起床场所 = 0
IF CFLAG:ARG:睡眠 && CFLAG:ARG:当前位置 == CFLAG:MASTER:当前位置 && CFLAG:ARG:起床时间 <= TIME
	SIF ARG > 0 && !CFLAG:MASTER:睡眠 && !ARG:2
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%醒来了
	TCVAR:ARG:睡眠深度 = 0
	;睡眠奸からの起床ならスルー
	IF !ARG:2
		IF !(IS_LOVER(ARG) && FLAG:催眠强度 <= 50 || (SLEEPTOGETHER && TALENT:LOCAL:爱欲))
			PRINTFORML 害羞的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%在换衣服时把%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%暂时赶出去了
			;おはよう口上
			IF !CFLAG:ARG:认识
				CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,2,0)
			ELSEIF !ARG:1 && !CFLAG:MASTER:睡眠
				CALL KOJO_MESSAGE_SEND("EVENT",2,ARG,4,0)
			ENDIF
		ELSE
			SIF CFLAG:MASTER:初始位置 == CFLAG:ARG:初始位置
				起床场所 = 1
			;おはよう口上
			IF !CFLAG:ARG:认识
				CALL KOJO_MESSAGE_SEND("ENCOUNTER",0,ARG,2,0)
			ELSEIF !ARG:1 && !CFLAG:MASTER:睡眠
				CALL KOJO_MESSAGE_SEND("EVENT",2,ARG,4,起床场所)
			ENDIF
			IF GETBIT(CFLAG:ARG:更衣实行, 1) && !CFLAG:MASTER:睡眠
				CALL 寝起き更衣(ARG)
			ENDIF
		ENDIF
		;修正前。
		;CALL CTRL_CLOTHES_SET(ARG, "现在衣装の变更_便装")
		;CALL 内衣确认リセット(ARG)
		;CALL CLOTHES_SETTING_TRAIN(ARG)
	ENDIF
ENDIF
;memo 开发者向けメモ
;上の处理は、同时刻でプレイヤーと角色が一绪にいたときの对处のため
;上记の书き方だと、もともと寝ていたけど起床したパターンでの服の更衣（具体的にはバニー）は对处できなかったものと思われる
;见た限り、起床时に更衣处理は他で似たような处理はやっていないように见受けられたため、IF文の外に出して对处。
;ただ、更衣の处理は他でもやっているため、コレで良いかは稍微判断がつかないです。
;とりあえず、バグらそうなことだけは确认しています。
IF SPECIAL_OUTFIT(ARG)
	CALL CTRL_CLOTHES_SET(ARG, "现在衣装の变更_衣装セット", SPECIAL_OUTFIT(ARG))
	[IF_DEBUG]
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%是つもと违う服に更衣ました
	[ENDIF]
	TCVAR:ARG:另一种便服 = 1
ELSE
	CALL CTRL_CLOTHES_SET(ARG, "现在衣装の变更_便装")
ENDIF
CALL 内衣确认リセット(ARG)
CALL CLOTHES_SETTING_TRAIN(ARG)

;延迟SET
IF CFLAG:ARG:睡眠
	CALL SET_DERAY(2,ARG)
ENDIF

CALL SET_SLEEP(0,ARG,0)

;各フラグOFF
CALL SET_PRANK(0,ARG,2)
CALL SET_SNEAK(0,ARG,2)
;CALL SET_TOGETHER(0,ARG,0)
;施锭
SIF ARG:1 == 0 && GET_MAPID(CFLAG:ARG:初始位置) == MAIN_MAP
	LOCK:(CFLAG:ARG:初始位置) = 0


;-----------------------------------------
;据点角色が寝る处理
;-----------------------------------------
@SLEEP_RESIDENT(ARG)
IF CHK_DATENOW(CFLAG:ARG:约会中)
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%因为困了回家去了
	CALL SET_DATE(99, ARG)
	CALL 回家の时间经过
	CFLAG:ARG:当前位置 = CFLAG:ARG:神社在住
	TCVAR:ARG:休憩中 = 1
	IF TALENT:ARG:恋慕 && MAP_CAN_MOVE(CFLAG:ARG:神社在住, MASTER)
		CFLAG:MASTER:当前位置 = CFLAG:ARG:神社在住
	ELSE
		CFLAG:MASTER:当前位置 = CFLAG:MASTER:初始位置
	ENDIF
	;多人约会 - 目标1困了回家,猪脚和目标2继续约会
	FOR LOCAL,1,CHARANUM
		SIF LOCAL == ARG
			CONTINUE
		IF CHK_DATENOW(CFLAG:(LOCAL):约会中)
			FLAG:约会的对象 = LOCAL
			TFLAG:约会前好感度 = CFLAG:LOCAL:好感度
			CFLAG:MASTER:约会中 = CFLAG:LOCAL:约会中
			CFLAG:MASTER:当前位置 = CFLAG:LOCAL:当前位置
			PRINTFORML 目送%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%远去,%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%决定继续和%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%约会...
			CFLAG:MASTER:同行 = 180
			CFLAG:LOCAL:同行 = 180
		ENDIF
	NEXT
	;おやすみ口上,1-1,约会中に归って寝る
	CALL KOJO_MESSAGE_SEND("EVENT", 3, ARG, 1, 1)
ELSEIF AT_HOME(MASTER) && CFLAG:ARG:衰弱 && CFLAG:MASTER:当前位置 == CFLAG:ARG:当前位置
	PRINTFORM 把%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%从
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:当前位置)
	PRINT 运到了
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:初始位置)
	PRINTW 
	CFLAG:ARG:当前位置 = CFLAG:ARG:神社在住
	IF 寝室许可(ARG) && MAP_CAN_MOVE(CFLAG:ARG:神社在住, MASTER)
		CFLAG:MASTER:当前位置 = CFLAG:ARG:神社在住
	ELSE
		CFLAG:MASTER:当前位置 = CFLAG:MASTER:初始位置
	ENDIF
	TCVAR:ARG:休憩中 = 1
	;おやすみ口上,5,衰弱して寝る
	CALL KOJO_MESSAGE_SEND("EVENT", 3, ARG, 5)
ELSEIF CFLAG:ARG:当前位置 != CFLAG:ARG:初始位置
	PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%从
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:当前位置)
	PRINT 返回了
	PRINTFORMS NAME_FROM_PLACE(CFLAG:ARG:初始位置)
	PRINTW 
	CFLAG:ARG:当前位置 = CFLAG:ARG:神社在住
	TCVAR:ARG:休憩中 = 1
	;おやすみ口上,1-0,自室に归って寝る
	CALL KOJO_MESSAGE_SEND("EVENT", 3, ARG, 1, 0)
ENDIF

;-----------------------------------------
;来访角色が寝る处理
;-----------------------------------------
@SLEEP_VISITOR(ARG)
IF CHK_DATENOW(CFLAG:ARG:约会中) && CFLAG:MASTER:当前位置 != OMANEKIBEYA()
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%说\@ CFLAG:ARG:衰弱 ? 累了 # 困了\@然后就回去了
	CFLAG:MASTER:同行 = 0
	CFLAG:ARG:同行 = 0
	FLAG:约会的对象 = 0
	TFLAG:约会前好感度 = 0
	;日常ON
	TFLAG:102 = 1
	;插入解除
	TFLAG:MASTERのＰ插入中 = 0
	;回家口上,2,约会中回家
	CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 2)
ELSEIF TALENT:ARG:恋慕 && AT_HOME(MASTER) && CFLAG:MASTER:当前位置 == CFLAG:ARG:当前位置
	;眠い
	IF 睡眠时间(ARG) && !CFLAG:ARG:衰弱
		;回家口上,1,通常回家
		CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 1)
	;疲惫
	ELSE
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%精疲力尽了…
		CALL ASK_YN(@"送到%NAME_FROM_PLACE(GET_ENTRANCE(MAIN_MAP))%为止吧","稍微歇会再走吧")
		IF RESULT == 0
			CFLAG:MASTER:当前位置 = GET_ENTRANCE(MAIN_MAP)
			CALL 据点から返回(ARG)
		ELSE
			PRINTFORM 把%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%从
			PRINTFORMW 往%NAME_FROM_PLACE(默认初始位置)%送去
			CFLAG:ARG:当前位置 = 默认初始位置
			TCVAR:ARG:休憩中 = 1
			CFLAG:MASTER:当前位置 = 默认初始位置
			SIF 默认初始位置 == P54小人的虫笼 && TALENT:MASTER:体型 > -5
				CFLAG:ARG:当前位置 = P10走廊
			;おやすみ口上,8,稍微休んでいきなよ
			CALL KOJO_MESSAGE_SEND("EVENT", 3, ARG, 8)
		ENDIF
	ENDIF
ELSEIF CFLAG:MASTER:当前位置 == OMANEKIBEYA()
	IF CFLAG:MASTER:恶作剧 == 2
		PRINTFORMW 好像对%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%做得太过分了、在醒来之前撤退吧
	ELSE
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%说累了、便躺下来了
	ENDIF
	TCVAR:ARG:休憩中 = 1
	;おやすみ口上,5,疲惫と言って、横成为了
	CALL KOJO_MESSAGE_SEND("EVENT", 3, ARG, 5)
ELSE
	CALL 据点から返回(ARG)
ENDIF

;-----------------------------------------
;来访角色が据点から归って寝る处理
;-----------------------------------------
@据点から返回(ARG)
IF IN_HOME(CFLAG:ARG:当前位置)
	PRINTFORMW 疲惫的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%回去了
	;回家口上,10,疲れて回家
	CALL KOJO_MESSAGE_SEND("EVENT", 20, ARG, 10)
	CFLAG:ARG:据点来访 = 0
ELSE
	PRINTFORMW 昏昏欲睡的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%回到了自己的房间
	;おやすみ口上,10,据点外の角色が自宅前で归って寝る
	CALL KOJO_MESSAGE_SEND("EVENT", 3, ARG, 10)
ENDIF
CFLAG:ARG:当前位置 = CFLAG:ARG:初始位置

;-----------------------------------------
;寝室に入れるかどうか
;追い出されない = 追い出されないかどうか
;-----------------------------------------
@寝室许可(ARG, 追い出されない)
#DIM 追い出されない
#FUNCTION
;催眠mod
SIF FLAG:催眠强度 > 50
	RETURNF 1
;勃然大怒中は许可を贳えない
SIF CFLAG:ARG:勃然大怒
	RETURNF 0
;恋慕or爱欲
SIF IS_LOVER(ARG) || (SLEEPTOGETHER && TALENT:ARG:爱欲)
	RETURNF 1
;开发済み
SIF EXP:ARG:睡眠奸经验 > 300
	RETURNF 1
;复数の角色が寝室を使ってる
SIF 共通的寝室(ARG)
	RETURNF 1
IF 追い出されない
	;缚られてる（强制）
	SIF TEQUIP:ARG:绳
		RETURNF 1
	;诶嘿嘿中
	SIF CFLAG:ARG:诶嘿嘿 == 1
		RETURNF 1
	;恶作剧中
	SIF CFLAG:ARG:恶作剧
		RETURNF 1
	;一时滞在中
	SIF FLAG:一时滞在 == ARG
		RETURNF 1
	;夜袭侵入成功中
	SIF GETBIT(CFLAG:ARG:夜袭, 8)
		RETURNF 1
	;待客室の中
	SIF CFLAG:ARG:当前位置 == OMANEKIBEYA()
		RETURNF 1
ENDIF

;-----------------------------------------
;1つ的房间を复数の角色が寝室に使っている场合
;その中のいずれかの角色が恋慕or爱欲ならば寝室の许可を得られる
;莉莉Ｗ＆Ｂ、秋姊妹、青娥と芳香が该当（旧灵梦的房间の处理）
;-----------------------------------------
@共通的寝室(ARG)
#FUNCTION
SIF CFLAG:ARG:初始位置 == 99
	RETURNF 0
SIF CFLAG:ARG:初始位置 == SUKIMA()
	RETURNF 0

FOR LOCAL, 1, CHARANUM
	SIF LOCAL == ARG
		CONTINUE
	IF CFLAG:LOCAL:初始位置 == CFLAG:ARG:初始位置
		SIF IS_LOVER(LOCAL)
			RETURNF 1
	ENDIF
NEXT
