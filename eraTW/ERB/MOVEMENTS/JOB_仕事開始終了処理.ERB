;FileName_JOB.ERB ----------------------------- Rev1.00
;工作开始处理
;CALL		USER
;ARG		角色NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_JOBSTR(ARG)
#DIM 移动前
#DIM 移动后
#DIMS HOLD_SAMEN


IF EX:ARG:膣内精液 + EX:ARG:肛门内精液 >= 900
	HOLD_SAMEN = 小腹内存储了不可思议的量的精液的
ELSEIF EX:ARG:膣内精液 + EX:ARG:肛门内精液 >= 700
	HOLD_SAMEN = 小腹内存储了满满当当的精液的
ELSEIF EX:ARG:膣内精液 + EX:ARG:肛门内精液 >= 500
	HOLD_SAMEN = 小腹内积攒了满满的精液的
ELSEIF EX:ARG:膣内精液 + EX:ARG:肛门内精液 >= 300
	HOLD_SAMEN = 小腹内积攒了精液的
ELSEIF EX:ARG:膣内精液 + EX:ARG:肛门内精液 >= 100
	HOLD_SAMEN = 小腹内留存着精液的
ELSE
	HOLD_SAMEN =
ENDIF

;FOR LOCAL,0,CHARANUM
;	SIF CFLAG:LOCAL:职场 == 0 && CFLAG:LOCAL:行动 == 5
;		PRINTFORMW ### JOB 326ERROR %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)% ###
;NEXT
VARSET LOCAL
移动前 = 0
移动后 = 0
;泣く泣く仕事へ 真面目だからね
IF CFLAG:ARG:诶嘿嘿 && !GROUPMATCH(CFLAG:ARG:职业种类, 49, 51)
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%必须要\@ CFLAG:ARG:职场 == CFLAG:ARG:当前位置 ? 开始 # 前去 \@工作了…
	CALL ENDUFUFU(ARG)
	CFLAG:MASTER:诶嘿嘿 = 0
	TFLAG:106 = 0
	IF TFLAG:102 == 3 && TFLAG:抱住模式
		TFLAG:抱住模式 = 0
		TFLAG:102 = 1
	ENDIF
ENDIF

SIF CFLAG:ARG:行动 == 5
	CALL JOBMOVE(ARG)

IF CAN_SEE(ARG) && !TFLAG:远距离移动
	IF CFLAG:ARG:当前位置 == TFLAG:宴会场 && TIME_PROGRESS(TIME:1) && (CFLAG:ARG:职业种类 == 49 || CFLAG:ARG:职业种类 == 51)
		CALL 宴会描写(ARG)
	ELSEIF CFLAG:ARG:当前位置 == CFLAG:ARG:职场 && TIME_PROGRESS(TIME:1)
		CALL 仕事情景描写(ARG)
		IF BASE:ARG:工作量
			;仕事がまだ3分の2以上残っている
			IF BASE:ARG:工作量 >= (MAXBASE:ARG:工作量 / 3) * 2
				IF CFLAG:ARG:职业种类 == 48
					PRINTW 　宴会的准备工作还剩很多没完成的样子…
				ELSEIF CFLAG:ARG:职业种类 == 50
					PRINTW 　宴会的善后工作还剩很多没完成的样子…
				ELSEIF CFLAG:ARG:职业种类 == 46
					PRINTW 埋头于玩耍之中…
				ELSEIF STRFIND(GET_JOBNAME(ARG), "午睡") >= 0
					PRINTFORMW 看起来暂时不要打扰对方会比较好的样子…
				ELSE
					PRINTFORMW %GET_JOBNAME(ARG)%还剩很多没完成的样子…
				ENDIF
				LOCAL = 3
			;仕事がまだ3分の1以上残っている
			ELSEIF BASE:ARG:工作量 >= (MAXBASE:ARG:工作量 / 3)
				IF CFLAG:ARG:职业种类 == 48
					PRINTW 　距离宴会的准备工作完成还需要一段时间…
				ELSEIF CFLAG:ARG:职业种类 == 50
					PRINTW 　距离宴会的善后工作完成还需要一段时间…
				ELSEIF CFLAG:ARG:职业种类 == 46
					PRINTW 好像还没玩够的样子…
				ELSE
					PRINTFORMW 距离%GET_JOBNAME(ARG)%完成还需要一段时间…
				ENDIF
				LOCAL = 2
			ELSE
				IF CFLAG:ARG:职业种类 == 48
					PRINTW 　宴会的准备工作还差一点就可以完成了…
				ELSEIF CFLAG:ARG:职业种类 == 50
					PRINTW 　宴会的善后工作还差一点就可以完成了…
				ELSEIF CFLAG:ARG:职业种类 == 46
					PRINTW 玩累了的样子…
				ELSE
					PRINTFORMW %GET_JOBNAME(ARG)%还差一点就可以完成了…
				ENDIF
				LOCAL = 1
			ENDIF
			;通常の工作口上　工作中かつ宴会フラグが立ってなければ表示する
			SIF CFLAG:ARG:宴会参加 == 0
				CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,2,LOCAL)
		ENDIF
		;宴会中の场合のみ、スムーズに次の动作に繋がる
		;酒を饮み终わった时に１ターン何もせずボーっとするのを防止する处理
;		SIF CFLAG:ARG:宴会参加 > 0 && BASE:ARG:工作量 <= 0
;			CALL CHARA_JOBEND(ARG)
	ELSEIF CFLAG:ARG:职业种类 != 51
		IF !AT_HOME(ARG) && GET_MAPID(CFLAG:ARG:当前位置) != MAIN_MAP
			移动前 = 6000 + (CFLAG:ARG:当前位置 / 10)
		ELSE
			移动前 = CFLAG:ARG:当前位置
		ENDIF
		IF IN_HOME(CFLAG:ARG:职场) == 0 && GET_MAPID(CFLAG:ARG:职场) != MAIN_MAP
			移动后 = 6000 + (CFLAG:ARG:职场 / 10)
		ELSE
			移动后 = CFLAG:ARG:职场
		ENDIF
		;时间经过してなかったら非表示
		IF !TIME_PROGRESS(TIME:1)
		;移动前と移动后の位置が同じなら非表示
		ELSEIF NAME_FROM_PLACE(移动前) == NAME_FROM_PLACE(移动后)
		ELSEIF CFLAG:ARG:当前位置 == OMANEKIBEYA()
			;房间に上がってたら出る,すいません面倒なので地の文まとめちゃいます；
			CFLAG:MASTER:当前位置 = CFLAG:ARG:自宅位置
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%好像有事外出了
		ELSEIF CFLAG:ARG:职业种类 == 48
			PRINTFORM %HOLD_SAMEN%%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%为了准备宴会从、
			PRINTFORMS NAME_FROM_PLACE(移动前)
			PRINT 向
			PRINTFORMS NAME_FROM_PLACE(移动后)
			PRINTW 出发了。
		ELSEIF CFLAG:ARG:职业种类 == 49
			PRINTFORM %HOLD_SAMEN%%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%从
			PRINTFORMS NAME_FROM_PLACE(移动前)
			PRINT 向
			PRINTFORMS NAME_FROM_PLACE(移动后)
			PRINTW 、为参加宴会而出发了。
		ELSEIF CFLAG:ARG:职业种类 == 46
			PRINTFORM %HOLD_SAMEN%%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%从
			PRINTFORMS NAME_FROM_PLACE(移动前)
			PRINT 向
			PRINTFORMS NAME_FROM_PLACE(移动后)
			PRINTW 、为玩耍而出发了。
		ELSE
			PRINTFORM %HOLD_SAMEN%%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%为了%GET_JOBNAME(ARG)%、从
			PRINTFORMS NAME_FROM_PLACE(移动前)
			PRINT 向
			PRINTFORMS NAME_FROM_PLACE(移动后)
			PRINTW 出发了。
		ENDIF
		
		;通常の职场に移动口上　宴会フラグが立ってなければ表示する
		SIF CFLAG:ARG:宴会参加 == 0
			CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,1,LOCAL)
	ENDIF
ENDIF
SIF CFLAG:ARG:陪睡中
	CALL SET_TOGETHER(0,ARG,1)
SIF CFLAG:ARG:同行 && CFLAG:ARG:职业种类 != 51
	CFLAG:ARG:同行 = 0
IF FLAG:约会的对象 == ARG
	FLAG:约会的对象 = 0
	TFLAG:约会前好感度 = 0
	CFLAG:ARG:约会中 = MAIN_MAP
ENDIF
;职场へ移动
SIF CFLAG:ARG:职场 && SHIRAHU(ARG)
	CFLAG:ARG:当前位置 = CFLAG:ARG:职场
SIF CFLAG:ARG:职业种类 == 49 && !TCVAR:ARG:烂醉 && (BASE:ARG:酒气 > (MAXBASE:ARG:酒气 / 10) * 8)
	CALL ENKAI_DRUNK(ARG)
CFLAG:ARG:行动 = 5
IF TCVAR:ARG:更衣
	CALL OKIGAE(ARG,TCVAR:ARG:更衣)
ENDIF
;FileName_JOB.ERB ----------------------------- Rev1.00
;工作结束处理
;CALL		USER
;ARG		角色NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@CHARA_JOBEND(ARG)
;仕事后延迟SET
SIF CFLAG:ARG:行动 == 5
	CALL SET_DERAY(3,ARG)

;工作量を0にする
SIF CFLAG:ARG:行动 == 5
	BASE:ARG:工作量 = 0
;このタイミングで疲劳追加
CALL ADD_TIRED(ARG, MAXBASE:ARG:工作量 / 2)

;仕事中フラグOFF
CFLAG:ARG:行动 = 0
IF CFLAG:ARG:当前位置 == CFLAG:ARG:职场 && CFLAG:ARG:职业种类 != 51
	SELECTCASE CFLAG:ARG:职业种类
	CASE 46
		IF CFLAG:ARG:当前位置 == CFLAG:MASTER:当前位置
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%结束了玩耍。
			;お疲れ样口上
			SIF CFLAG:ARG:宴会参加 == 0
				CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,3,0)
		ENDIF
	CASE 48
		IF CFLAG:ARG:当前位置 == CFLAG:MASTER:当前位置
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的宴会准备已经结束了。
			SIF ARG == [[灵梦]]
				PRINTL 　真是辛苦了
		ENDIF
		IF DAY == FLAG:开始日 && TIME >= FLAG:33
			CALL ENKAI_START(ARG)
		ELSEIF 宴会举办中判定()
			CALL ENKAI_START(ARG)
		ENDIF
	CASE 49
		CALL ENKAI_DRUNK(ARG)
	CASE 50
		IF CFLAG:ARG:当前位置 == CFLAG:MASTER:当前位置
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的宴会善后已经结束了。
			SIF ARG == [[灵梦]]
				PRINTL 　真是辛苦了
		ENDIF
		CFLAG:ARG:职业种类 = 0
		CFLAG:ARG:宴会参加 = 3
		CALL CHARA_JOBEND_GO_HOME(ARG)
	CASEELSE
		IF CFLAG:ARG:当前位置 == CFLAG:MASTER:当前位置
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的%GET_JOBNAME(ARG)%结束了。
			;お疲れ样口上
			CALL KOJO_MESSAGE_SEND("EVENT",13,ARG,3,0)
			IF MAXBASE:ARG:工作量 < (TCVAR:ARG:帮忙工作量 * 3) && TCVAR:ARG:礼物
				PRINTFORMW 从%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%得到了%ITEMNAME:(TCVAR:ARG:礼物)%
				ITEM:(TCVAR:ARG:礼物) ++
			ENDIF
		ENDIF
		IF CFLAG:ARG:当前位置 / 100 != MAIN_MAP && !CFLAG:ARG:神社在住 && VISIT(ARG)
			IF !RAND:15
				IF FLAG:交流来访模式 == 1 && CFLAG:ARG:好感度 <= 50
				ELSEIF FLAG:交流来访模式 == 2 && !TALENT:ARG:思慕 && !IS_LOVER(ARG) && !TALENT:ARG:恋人 && !TALENT:ARG:炮友
				ELSEIF FLAG:交流来访模式 == 3
				ELSE
					CALL 主人公物件へ向かう(ARG)
				ENDIF
			ELSEIF CFLAG:ARG:当前位置 == CFLAG:MASTER:当前位置 && CAN_DATE(ARG) && !FLAG:约会的对象
				PRINTFORML 要邀请%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%约会么？
				CALL ASK_YN
				IF !RESULT
					CALL 外出先から诱えるか(ARG)
					IF RESULT
						CFLAG:MASTER:同行 = 60
						CFLAG:ARG:同行 = 60
						CFLAG:ARG:约会中 = CFLAG:MASTER:当前位置 / 100
						FLAG:约会的对象 = ARG
						TFLAG:约会前好感度 = CFLAG:ARG:好感度
						TIME += 1
						PRINTFORMW 决定工作结束后和%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%去约会
						;必要かわからんが一応
						RETURN
					ENDIF
				ENDIF
			ENDIF
		ENDIF
		CALL CHARA_JOBEND_GO_HOME(ARG)
	ENDSELECT
ENDIF


@CHARA_JOBEND_GO_HOME(ARG)
IF CFLAG:ARG:神社在住 && !AT_HOME(ARG)
	SIF CFLAG:ARG:当前位置 == CFLAG:MASTER:当前位置
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%回去%GET_MAPNAME(MAIN_MAP)%了
	CFLAG:ARG:当前位置 = MAXROOM
ENDIF
