;--------------------------------------------------
;地点名变更函数
;--------------------------------------------------
@MAP_PLACENAME_8(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P823空室
			IF CFLAG:MASTER:初始位置 == ARG
				RETURNF CALLNAME:MASTER + "私室"
			ELSE
				RETURNF "空室"
			ENDIF
		CASE SUMIKOMI_ROOM
			;※このMapでは未实装
			IF FLAG:住宿人物
				RETURNF CALLNAME:(FLAG:住宿人物) + "的房间"
			ELSE
				RETURNF "空的房间"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "隙间空间"
		CASE MAXROOM
			RETURNF "山の麓"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "山顶の外"
ENDIF

;--------------------------------------------------
;场所ARGがどのエリアにあるか
;表示するMapの番号と连动（TFLAG:地图切换）
;ARG     场所ID
;--------------------------------------------------
@MAP_AREASELECT_8(ARG)
#FUNCTION
SELECTCASE ARG
	;天界
	CASE P830玄云海, P832有顶天街, P833桃园
		RETURNF 3
	;守矢神社
	CASE P811境内 TO P826走廊
		RETURNF 2
	;比那名居邸
	CASE P831比那名居邸前, P834比那名居邸土间 TO P842比那名居邸库房
		RETURNF 4
	;妖怪之山・山顶
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;内部构造がある场所の内部の判定
;TFLAG:地图切换  内部察觉が必要なMap番号
;                P_ID  表示ボタンの场所ID
;                Q_ID  判定する内部の场所ID,必ずP_IDと一致した数值も含める
;--------------------------------------------------
@GETMAP_BUILDING_8(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:地图切换 == 1
	SELECTCASE P_ID
		;天界
		CASE P830玄云海
			SELECTCASE Q_ID
			CASE P830玄云海 TO P842比那名居邸库房
				RETURNF 1
			ENDSELECT
		;守矢神社
		CASE P811境内
			SELECTCASE Q_ID
			CASE P811境内 TO P826走廊
				RETURNF 1
			ENDSELECT
	ENDSELECT
ELSEIF TFLAG:地图切换 == 3
	SELECTCASE P_ID
		;比那名居邸
		CASE P831比那名居邸前
			SELECTCASE Q_ID
			CASE P831比那名居邸前, P834比那名居邸土间 TO P842比那名居邸库房
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;移动制限设定
;ARG    场所ID
;CHARA  移动する角色ID
;RETURNF 1=移动できる,0=移动できない
;TFLAG:移动不能信息   ↓の@MAP_CANNOT_WORD用の设定
;--------------------------------------------------
@MAP_CAN_MOVE_8(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;谁かが住んでないと入れない
	CASE P823空室
		IF !PRIVATEROOM:(ARG % 100) && CFLAG:MASTER:初始位置 != ARG
			TFLAG:移动不能信息 = 999
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;移动不能时に表示されるメッセージ
;--------------------------------------------------
@MAP_CANNOT_WORD_8(ARG)
RETURN
SELECTCASE TFLAG:移动不能信息
	CASE 1
		PRINTFORMW ※在这里输入消息。执行行数+1的CLEARLINE
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;据点専用の情景描写
;WAITはかけないように
;-----------------------------------------
@MAP_UNIQUEMESSAGE_8
IF WITH_MOB()
	RETURN 1
ELSE
	SELECTCASE CFLAG:MASTER:当前位置
		;索道站
		CASE P803索道站
			IF INRANGE(TIME, 540, 1020)
				PRINTFORML 能看到充满干劲的河童在接待索道的业务
			ELSE
				PRINTFORML 索道站关门了
			ENDIF
	ENDSELECT
ENDIF
RETURN 1

;-----------------------------------------
;据点の场所IDをお出かけMap上の场所IDに置换する函数
;ARG      对象の场所ID（据点Map）
;RETURNF  差し替え场所ID（お出かけMap）
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_8(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P830玄云海 TO P842比那名居邸库房
		RETURNF 天界
	CASE P801山顶入口, P802桥, P803索道站, P806瀑布背面 TO P809果宅
		RETURNF 九天瀑布
	CASE P805顶上, P851久侘歌的小屋 TO P854龙宅
		RETURNF 绝景之丘
	CASE P814湖畔
		RETURNF 山之湖
	CASE P804守矢神社鸟居, P811境内, P813社务所, P815庭
		RETURNF 守矢神社境内
	CASE P855虹龙洞, P856百百世的住所
		RETURNF 虹龙洞
	CASEELSE
		RETURNF 守矢神社本殿
ENDSELECT
RETURNF 0

;--------------------------------------------------
;Mapの介绍文
;--------------------------------------------------
@Map介绍_8
;とりあえず5行
PRINTFORML 以守矢神社为中心、包括天狗的住居和天界在内的地图。
PRINTFORML 虽然离大地图的中心有点远、但是可以通过索道直接到人间之里、所以交通状况其实还不错。
PRINTFORML 守矢神签里虽然可以抽中有用的道具和约会权、但在概率上来讲需要做好大量破财的觉悟。
PRINTFORML 有多个采集点、若有在磨炼采集技能的话、到了对应季节应该能获得多样的素材。
PRINTFORML 这里也有一定数量的钓鱼或由山湖水酿酒方面的工作。也有不知不觉就过了一天的情况
