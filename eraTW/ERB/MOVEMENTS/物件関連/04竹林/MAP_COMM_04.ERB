;--------------------------------------------------
;地点名变更函数
;--------------------------------------------------
@MAP_PLACENAME_4(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P412空的房间, P428二层空的房间, P460空的房间
			IF CFLAG:MASTER:初始位置 == ARG
				RETURNF CALLNAME:MASTER + "私室"
			ELSE
				RETURNF "空的房间"
			ENDIF
		CASE P442空的小屋
			IF CFLAG:MASTER:初始位置 == ARG
				RETURNF CALLNAME:MASTER + "的小屋"
			ELSE
				RETURNF "空的小屋"
			ENDIF
		CASE SUMIKOMI_ROOM
			IF FLAG:住宿人物
				RETURNF CALLNAME:(FLAG:住宿人物) + "的小屋"
			ELSE
				RETURNF "废屋"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "隙间空间"
		CASE MAXROOM
			RETURNF "竹林之外"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "竹林之外"
ENDIF

;--------------------------------------------------
;场所ARGがどのエリアにあるか
;表示するMapの番号と连动（TFLAG:地图切换）
;ARG     场所ID
;--------------------------------------------------
@MAP_AREASELECT_4(ARG)
#FUNCTION
SELECTCASE ARG
	;永远亭
	CASE P403等候室 TO P430地下室
		RETURNF 1
	;梦幻馆
	CASE P450玄关 TO P466庭
		RETURNF 2
	;迷途竹林
	CASEELSE
		RETURNF 3
ENDSELECT

;--------------------------------------------------
;内部构造がある场所の内部の判定
;TFLAG:地图切换  内部察觉が必要なMap番号
;                P_ID  表示ボタンの场所ID
;                Q_ID  判定する内部の场所ID,必ずP_IDと一致した数值も含める
;--------------------------------------------------
@GETMAP_BUILDING_4(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:地图切换 == 3
	SELECTCASE P_ID
		;梦幻馆
		CASE P450玄关
			SELECTCASE Q_ID
			CASE P450玄关 TO P466庭
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;移动制限设定
;ARG    场所ID
;CHARA  移动する角色ID
;RETURNF 1=移动できる,0=移动できない
;TFLAG:移动不能信息   ↓の@MAP_CANNOT_WORD用の设定
;--------------------------------------------------
@MAP_CAN_MOVE_4(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;奥ノ院判定
	CASE P407走廊 TO P430地下室
		;除外
		SIF GROUPMATCH(CHARA, [[铃仙]], [[帝]], [[辉夜]], [[永琳]])
			RETURNF 1
		;月の民も良いよね
		SIF GROUPMATCH(CHARA, [[依姫]], [[丰姫]], [[铃仙二号]], [[探女]])
			RETURNF 1
		;中からは动ける
		SIF INRANGE(CFLAG:CHARA:当前位置,P407走廊, P430地下室)
			RETURNF 1
		;你
		IF CHARA == MASTER
			IF 永远亭侵入许可() || INRANGE(CFLAG:MASTER:初始位置, P401永远亭入口, P430地下室) || FLAG:时间停止
				RETURNF 1
			ELSE
				TFLAG:移动不能信息 = 1
				RETURNF 0
			ENDIF
		;それ以外は入れない
		ELSE
			RETURNF 0
		ENDIF
	;谁かが住んでないと入れない
	CASE P412空的房间, P428二层空的房间, P460空的房间, P442空的小屋
		IF !PRIVATEROOM:(ARG % 100) && CFLAG:MASTER:初始位置 != ARG
			TFLAG:移动不能信息 = 999
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;你が奥ノ院に入れる判定
;--------------------------------------------------
@永远亭侵入许可()
#FUNCTION
;催眠强度50以上
SIF FLAG:催眠强度 > 50
	RETURNF 1
SIF CFLAG:[[辉夜]]:认识
	RETURNF 1
SIF TALENT:[[铃仙]]:恋慕 || TALENT:[[帝]]:恋慕 || TALENT:[[辉夜]]:恋慕 || TALENT:[[永琳]]:恋慕
	RETURNF 1
SIF GROUPMATCH(FLAG:约会的对象, [[铃仙]], [[帝]], [[辉夜]], [[永琳]])
	RETURNF 1
SIF GROUPMATCH(FLAG:扮演, [[铃仙]], [[帝]], [[辉夜]], [[永琳]])
	RETURNF 1
SIF CFLAG:[[铃仙]]:同行 || CFLAG:[[帝]]:同行 || CFLAG:[[辉夜]]:同行 || CFLAG:[[永琳]]:同行
	RETURNF 1
SIF FLAG:宴会举办FLAG == 3 && TIME >= FLAG:开始时间
	RETURNF 1
RETURNF 0

;--------------------------------------------------
;移动不能时に表示されるメッセージ
;--------------------------------------------------
@MAP_CANNOT_WORD_4(ARG)
SELECTCASE TFLAG:移动不能信息
	CASE 1
		PRINTW 没有关系亲密的人的话无法进入永远亭里面…
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;据点専用の情景描写
;WAITはかけないように
;-----------------------------------------
@MAP_UNIQUEMESSAGE_4
IF WITH_MOB()
	CALL MOB_MESSAGE_4
	RETURN 1
ENDIF

;-----------------------------------------
;据点の场所IDをお出かけMap上の场所IDに置换する函数
;ARG      对象の场所ID（据点Map）
;RETURNF  差し替え场所ID（お出かけMap）
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_4(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P401永远亭入口 TO P406来客用厕所
		RETURNF 永远亭
	CASE P407走廊 TO P430地下室
		RETURNF 永远亭的内院
	CASE P431兔的洞穴
		RETURNF 兔的洞穴
	CASE P432影狼的家, P433斜角的竹林, P442空的小屋, P445妹红的家
		RETURNF 斜角的竹林
	CASE P434迷失的小道, P435水井, P440废屋, P441温泉
		RETURNF 迷失的小道
	CASE P436无名之丘, P437梅蒂欣的家
		RETURNF 无名之丘
	CASE P438太阳花田, P439拉尔瓦的家
		RETURNF 太阳花田
	CASE P450玄关 TO P466庭
		RETURNF 梦幻馆
	CASEELSE
		RETURNF 竹林入口
ENDSELECT
RETURNF 0

;--------------------------------------------------
;Mapの介绍文
;--------------------------------------------------
@Map介绍_4
;とりあえず5行
PRINTFORML ※介绍文募集中※
PRINTFORML 除了竹林众之外、幽香和旧作角色生活的梦幻馆也包括在内。
PRINTFORML 想进入永远亭的深处的话、必须和关系者一起同行、或者和关系者有着足够密切的关系。
PRINTFORML 如果在时间停止下擅自侵入的话、一解除就会被赶出来。
PRINTFORML 
