;--------------------------------------------------
;地点名变更函数
;--------------------------------------------------
@MAP_PLACENAME_3(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P304空的小屋
			IF CFLAG:MASTER:初始位置 == ARG
				RETURNF CALLNAME:MASTER + "的小屋"
			ELSE
				RETURNF "空的小屋"
			ENDIF
		CASE SUMIKOMI_ROOM
			IF FLAG:住宿人物
				RETURNF CALLNAME:(FLAG:住宿人物) + "的房间"
			ELSE
				RETURNF "空的房间"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "隙间空间"
		CASE MAXROOM
			RETURNF "雾的另一边"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "湖の外"
ENDIF

;--------------------------------------------------
;场所ARGがどのエリアにあるか
;表示するMapの番号と连动（TFLAG:地图切换）
;ARG     场所ID
;--------------------------------------------------
@MAP_AREASELECT_3(ARG)
#FUNCTION
SELECTCASE ARG
	;红魔馆・一阶＆地下
	CASE P301正门 TO P316一层厕所, P328地下走廊 TO P333大浴场
		RETURNF 2
	;红魔馆・二层＆三层
	CASE P319二层走廊 TO P327小露台
		RETURNF 3
	;废洋馆
	CASE P334废洋馆・入口 TO P342莉莉卡私室
		RETURNF 4
	;雾之湖
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;内部构造がある场所の内部の判定
;TFLAG:地图切换  内部察觉が必要なMap番号
;                P_ID  表示ボタンの场所ID
;                Q_ID  判定する内部の场所ID,必ずP_IDと一致した数值も含める
;--------------------------------------------------
@GETMAP_BUILDING_3(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:地图切换 == 1
	SELECTCASE P_ID
		;红魔馆
		CASE P301正门
			SELECTCASE Q_ID
			CASE P301正门 TO P333大浴场
				RETURNF 1
			ENDSELECT
		;废洋馆
		CASE P334废洋馆・入口
			SELECTCASE Q_ID
			CASE P334废洋馆・入口 TO P342莉莉卡私室
				RETURNF 1
			ENDSELECT
	ENDSELECT
ELSEIF TFLAG:地图切换 == 2
	SELECTCASE P_ID
		;红魔馆・二层＆三层
		CASE P319二层走廊
			SELECTCASE Q_ID
			CASE P319二层走廊 TO P327小露台
				RETURNF 1
			ENDSELECT
	ENDSELECT
ELSEIF TFLAG:地图切换 == 3
	SELECTCASE P_ID
		;红魔馆・一阶＆地下
		CASE P309大厅
			SELECTCASE Q_ID
			CASE P309大厅 TO P316一层厕所, P328地下走廊 TO P333大浴场
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;移动制限设定
;ARG    场所ID
;CHARA  移动する角色ID
;RETURNF 1=移动できる,0=移动できない
;TFLAG:移动不能信息   ↓の@MAP_CANNOT_WORD用の设定
;--------------------------------------------------
@MAP_CAN_MOVE_3(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;湖底
	CASE P348雾之湖・湖底
		;水栖でないと入れない
		IF !TALENT:CHARA:水栖 && !ITEM:避水玉
			TFLAG:移动不能信息 = 1
			RETURNF 0
		ENDIF
	;谁かが住んでないと入れない
	CASE P304空的小屋, P321空的房间
		IF !PRIVATEROOM:(ARG % 100) && CFLAG:MASTER:初始位置 != ARG
			TFLAG:移动不能信息 = 999
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;移动不能时に表示されるメッセージ
;--------------------------------------------------
@MAP_CANNOT_WORD_3(ARG)
SELECTCASE TFLAG:移动不能信息
	CASE 1
		;PRINTFORMW 溺れてしまう…
		PRINTFORMW 会淹死的…
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;据点専用の情景描写
;WAITはかけないように
;-----------------------------------------
@MAP_UNIQUEMESSAGE_3
IF WITH_MOB()
	CALL MOB_MESSAGE_3
	RETURN 1
ELSEIF !FLAG:时间停止
	IF CFLAG:MASTER:当前位置 == P329仓库
		PRINTFORML 猫咪在这里
		RETURN 1
	ENDIF
ENDIF

;-----------------------------------------
;据点の场所IDをお出かけMap上の场所IDに置换する函数
;ARG      对象の场所ID（据点Map）
;RETURNF  差し替え场所ID（お出かけMap）
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_3(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P343湖畔, P346大妖精的小屋 TO P348雾之湖・湖底
		RETURNF 雾之湖
	CASE P344蕾蒂的冰屋, P345琪露诺的冰屋
		RETURNF 冰造的小雪屋
	CASE P334废洋馆・入口 TO P342莉莉卡私室
		RETURNF 废洋馆
	CASE P301正门 TO P308西阳台
		RETURNF 正门
	CASE P313厨房, P314食堂
		RETURNF 食堂
	CASE P310图书馆 TO P312小恶魔私室
		RETURNF 大图书馆
	CASE P328地下走廊 TO P333大浴场
		RETURNF 地下室
	CASEELSE
		RETURNF 大厅
ENDSELECT
RETURNF 0

;--------------------------------------------------
;Mapの介绍文
;--------------------------------------------------
@Map介绍_3
;とりあえず5行
PRINTFORML 以红魔馆・雾湖为中心、包含了普利兹姆利巴三姐妹所住的废弃洋馆的地区。
PRINTFORML 最大的特征是仅此一家的赌场。
PRINTFORML 雾湖除了其湖水在酿酒上能派上用场之外、也能在此钓鱼、算是很便利的据点。
PRINTFORML 就跟从来的吸血鬼洋馆一样、这里的住人都是到了晚上才开始活动。或许要根据需求来调整一下的生活节奏。
PRINTFORML 据点内可帮忙的工作虽然不缺、但因为有人负责清扫的关系、或许很难看到红魔馆内会累积污垢。
