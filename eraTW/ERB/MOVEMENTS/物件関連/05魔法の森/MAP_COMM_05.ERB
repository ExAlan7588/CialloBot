;--------------------------------------------------
;地点名变更函数
;--------------------------------------------------
@MAP_PLACENAME_5(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P531人偶洋馆・空的房间
			IF CFLAG:MASTER:初始位置 == ARG
				RETURNF "人偶洋馆・" + CALLNAME:MASTER + "的房间"
			ELSE
				RETURNF "人偶洋馆・空的房间"
			ENDIF
		CASE SUMIKOMI_ROOM
			;※このMapでは未实装
			IF FLAG:住宿人物
				RETURNF CALLNAME:(FLAG:住宿人物) + "的房间"
			ELSE
				RETURNF "空的房间"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "隙间空间"
		CASE MAXROOM
			RETURNF "森林之外"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "森林之外"
ENDIF

;--------------------------------------------------
;场所ARGがどのエリアにあるか
;表示するMapの番号と连动（TFLAG:地图切换）
;ARG     场所ID
;--------------------------------------------------
@MAP_AREASELECT_5(ARG)
#FUNCTION
SELECTCASE ARG
	;魔理沙宅
	CASE P510雾雨魔法店 TO P515魔理沙宅・仓库
		RETURNF 3
	;人偶的洋馆
	CASE P521人偶洋馆・爱丽丝的房间 TO P531人偶洋馆・空的房间
		RETURNF 2
	;软绵绵艾伦の店
	CASE P534轻飘飘的艾伦魔法商店 TO P537艾伦宅・澡堂
		RETURNF 4
	;魔法之森
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;内部构造がある场所の内部の判定
;TFLAG:地图切换  内部察觉が必要なMap番号
;                P_ID  表示ボタンの场所ID
;                Q_ID  判定する内部の场所ID,必ずP_IDと一致した数值も含める
;--------------------------------------------------
@GETMAP_BUILDING_5(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:地图切换 == 1
	SELECTCASE P_ID
		;魔理沙宅
		CASE P510雾雨魔法店
			SELECTCASE Q_ID
			CASE P510雾雨魔法店 TO P514魔理沙宅・澡堂
				RETURNF 1
			ENDSELECT
		;人偶洋馆
		CASE P525人偶洋馆・客厅
			SELECTCASE Q_ID
			CASE P521人偶洋馆・爱丽丝的房间 TO P531人偶洋馆・空的房间
				RETURNF 1
			ENDSELECT
		;艾伦宅
		CASE P534轻飘飘的艾伦魔法商店
			SELECTCASE Q_ID
			CASE P534轻飘飘的艾伦魔法商店 TO P537艾伦宅・澡堂
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;移动制限设定
;ARG    场所ID
;CHARA  移动する角色ID
;RETURNF 1=移动できる,0=移动できない
;TFLAG:移动不能信息   ↓の@MAP_CANNOT_WORD用の设定
;--------------------------------------------------
@MAP_CAN_MOVE_5(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	;香霖堂,10时～18时
	CASE P502香霖堂
		IF !KOURINDOU_IS_WORKING_HOURS()
			TFLAG:移动不能信息 = 1
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;移动不能时に表示されるメッセージ
;--------------------------------------------------
@MAP_CANNOT_WORD_5(ARG)
SELECTCASE TFLAG:移动不能信息
	CASE 1
		PRINTFORMW 非営业时间
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;据点専用の情景描写
;WAITはかけないように
;-----------------------------------------
@MAP_UNIQUEMESSAGE_5
SELECTCASE CFLAG:MASTER:当前位置
	CASE P502香霖堂
		PRINTFORML 狭窄的地方对着各种杂物
		PRINTFORML 有很多都随便贴着「非卖品」……
	CASE P503岔道
		PRINTFORML 看见了魔理沙家的后门和水井
;	CASE 520
;		PRINTFORML 摆放着写着「慶贺新筑」的大花圈
	CASE P524人偶洋馆・研究室
		PRINTFORML 书架上摆放着药瓶和人偶
	;CASE P543露米亚的栖息处
	;	PRINTFORML 角落里散落着不认识的骨头
ENDSELECT

RETURN 1
;-----------------------------------------
;非自宅时に差し替える场所IDを返す函数
;ARG,对象ID
;RETURNF,差し替え済み对象ID
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_5(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P502香霖堂
		RETURNF 香霖堂
	CASE P540古木的大树 TO P545慧之子的栖息处
		RETURNF 古木的大树
	CASE P510雾雨魔法店 TO P515魔理沙宅・仓库
		RETURNF 雾雨魔法店
	CASE P520人偶洋馆前 TO P531人偶洋馆・空的房间
		RETURNF 人偶的洋馆
	CASE P506无缘冢
		RETURNF 无缘冢
	CASE P509再思之道
		RETURNF 再思之道
	CASEELSE
		RETURNF 森林的入口
ENDSELECT
RETURNF 0

;--------------------------------------------------
;Mapの介绍文
;--------------------------------------------------
@Map介绍_5
;とりあえず5行
PRINTFORML 这里是以魔理沙宅邸、人形馆、软绵绵艾伦的店为中心的地区。
PRINTFORML 这里有着不需要工具也能进行合成的场所、以及多种类的采集地点。
PRINTFORML 而其中【蘑菇群生地】也只有以此为据点时才会出现的采集地点。若想采集蘑菇类的物品时、推荐来这里。
PRINTFORML 房间也相对的多、很适合展现你的扫除技能。但能搭把手帮忙的工作则相对比较少。
PRINTFORML 从无缘冢到这里有互通的道路。
