;-------------------------------------------------
;路人のいるMapを返す
;ARG=MAPID
;-------------------------------------------------
@GET_EXISTMOB(ARG)
#FUNCTION
SIF GROUPMATCH(ARG, 3, 4, 9, 10)
	RETURNF 1

;-------------------------------------------------
;お出かけ时の路人判定
;ARG=场所ID
;-------------------------------------------------
@DATE_HITOGOMI(ARG)
#FUNCTION
SIF !ARG
	ARG = CFLAG:MASTER:当前位置
IF IN_HOME(ARG)
	RETURNF 0
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "路人")
		RETURNF 1
ENDIF
RETURNF 0

;-------------------------------------------------
;MASTERが在宅中かつ路人と同じ位置にいるならMAIN_MAPを返す
;-------------------------------------------------
@WITH_MOB()
#FUNCTION
SIF AT_HOME(MASTER) && ROOMDATA:(CFLAG:MASTER:当前位置 % 100) & 场所_路人
	RETURNF MAIN_MAP

;-------------------------------------------------
;路人の与える污れ值（扫除值）
;ARG=MAPID
;-------------------------------------------------
@MOB_YOGORE(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 3
		RETURNF -3
	CASE 4
		SIF !GROUPMATCH(TIME:2,1,6,7)
			RETURNF 1
	CASE 9
		SIF !GROUPMATCH(TIME:2,1,6,7)
			RETURNF 2
	CASE 10
		;玉兔はプラス减少ゼロ
ENDSELECT

;-------------------------------------------------
;路人の基本名
;ARG=MAPID
;-------------------------------------------------
@MOB_NAME(ARG)
#FUNCTIONS
SIF !ARG
	ARG = MAIN_MAP
SELECTCASE ARG
CASE 3
	LOCALS = 哥布林
CASE 4
	LOCALS = 因幡
CASE 9
	LOCALS = %ANIMALNAME()%
CASE 10
	LOCALS = 玉兔
ENDSELECT
RETURNF LOCALS

;-------------------------------------------------
;お出かけ中の路人名
;ARG=场所ID
;-------------------------------------------------
@DATE_HITOGOMI_SYUBETU(ARG)
#FUNCTIONS
VARSET LOCALS
SIF !ARG
	ARG = CFLAG:PLAYER:当前位置
SELECTCASE GET_MAPID(ARG)
	;路人の种类
	;月
	CASE 10
		IF ARG == 月之都
			LOCALS += "月人"
		ELSE
			LOCALS += "玉兔"
		ENDIF
	;地底（街道と温泉では鬼も出てくる）
	CASE 9
		IF ARG != 地灵殿 && !RAND:3
			LOCALS += "鬼"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;妖怪之山山顶（守矢神社境内のみ希に人类）
	CASE 8
		IF ARG == 守矢神社境内 && !RAND:5
			LOCALS += "人类"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;妖怪之山山麓
	CASE 7
	;三途之川～冥界（死神、妖怪、希に人类）
	CASE 6
		IF !RAND:5
			LOCALS += "人类"
		ELSEIF !RAND:3
			LOCALS += "妖怪"
		ELSE
			LOCALS += "死神"
		ENDIF
	;魔法之森
	CASE 5
	;迷途竹林（妖怪ウサギ、希に人类(患者)）
	CASE 4
		IF RAND:5
			LOCALS += "妖怪兔"
		ELSE
			LOCALS += "人类"
		ENDIF
	;红魔馆（お出かけ中の路人は全员少女なのでホフさんはお休み）
	CASE 3
		LOCALS += "妖精女仆"
	;人间之里（基本人类、希に妖怪）
	CASE 2
		IF RAND:5
			LOCALS += "人类"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;命莲寺（人类と妖怪）
	CASE 1
		IF !RAND:2
			LOCALS += "人类"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;博丽神社
	CASE 0
ENDSELECT
LOCALS += "少女"
RETURNF LOCALS

;-------------------------------------------------
;まかないイベント处理
;-------------------------------------------------
@MAKANAI
#DIM 气力恢复量
#DIM 体力恢复量
VARSET LOCAL
IF DISH_NAME == "" && !TFLAG:まかない && TIME:2 == 3
	;吃饭できない人がいるかをチェック
	FOR LOCAL, 0, CHARANUM
		SIF TCVAR:LOCAL:空腹时刻 - TIME:1 > 0 && CFLAG:LOCAL:当前位置 == CFLAG:MASTER:当前位置
			LOCAL:1 ++
	NEXT
	IF !LOCAL:1 && !RAND:5
		TFLAG:まかない = 1
		PRINTFORML 被推荐了由%MOB_NAME()%制作的料理
		PRINTFORML 要吃么？
		CALL ASK_YN
		IF RESULT
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%断然拒绝了
		ELSE
			SELECTCASE RAND:5
				CASE 0, 1
					PRINTFORML 味道马马虎虎
					体力恢复量 = 100
					气力恢复量 = 300
				CASE 2, 3
					PRINTFORML 还不错
					体力恢复量 = 150
					气力恢复量 = 400
				CASE 4
					PRINTFORML 这个真好吃！
					体力恢复量 = 300
					气力恢复量 = 500
			ENDSELECT
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%\@GET_TARGETNUM() ? 们# \@吃饱了
			TIME += 15
			FOR LOCAL, 0, CHARANUM
				IF CFLAG:LOCAL:当前位置 == CFLAG:MASTER:当前位置
					CALL RECOVER(LOCAL, 体力恢复量, "体力")
					CALL RECOVER(LOCAL, 气力恢复量, "气力")
					CALL 满腹度上升(LOCAL, "轻食")
				ENDIF
			NEXT
		ENDIF
	ENDIF
ENDIF

;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;红魔馆の路人设定（哥布林）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;-------------------------------------------------
;路人のいる场所
;-------------------------------------------------
@MOB_PLACE_3
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~场所_路人
	SELECTCASE TIME:2
		CASE 2
			SIF GROUPMATCH(LOCAL, P302庭, P306后院, P307东阳台, P308西阳台)
				ROOMDATA:ROOM |= 场所_路人
		CASE 3
			SIF GROUPMATCH(LOCAL, P313厨房, P314食堂, P315会客厅)
				ROOMDATA:ROOM |= 场所_路人
		CASE 4
			SIF GROUPMATCH(LOCAL, P309大厅, P319二层走廊)
				ROOMDATA:ROOM |= 场所_路人
		CASE 5
			SIF GROUPMATCH(LOCAL, P324大露台, P327小露台)
				ROOMDATA:ROOM |= 场所_路人
		CASE 6
			SIF GROUPMATCH(LOCAL, P306后院, P328地下走廊, P329仓库, P330佣人间)
				ROOMDATA:ROOM |= 场所_路人
	ENDSELECT
NEXT
;-------------------------------------------------
;路人のメッセージ
;-------------------------------------------------
@MOB_MESSAGE_3
IF FLAG:时间停止
	PRINTFORML 哥布林们停下了动作…
ELSEIF CFLAG:MASTER:当前位置 == P329仓库
	PRINTFORML 哥布林们照看着猫…
ELSE
	PRINTFORML 哥布林们正在干活…
ENDIF
;-------------------------------------------------
;路人のイベント
;-------------------------------------------------
@MOB_TURNENDEVENT_3
IF !FLAG:时间停止
	IF CFLAG:MASTER:诶嘿嘿
		PRINTFORML 被哥布林提醒说这里是公共场所请不要做这种行为
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%觉得有点尴尬
		BASE:TARGET:情绪 = 0
		CALL ENDUFUFU_ALL
	ELSEIF CFLAG:MASTER:当前位置 == P313厨房
		CALL MAKANAI
	ENDIF
ENDIF

;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;永远亭の路人设定（因幡）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;-------------------------------------------------
;路人のいる场所
;-------------------------------------------------
@MOB_PLACE_4
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~场所_路人
	SELECTCASE TIME:2
		CASE 1 TO 7
			SIF GROUPMATCH(LOCAL, P407走廊, P408中走廊, P409走廊, P421庭, P422后院, P431兔的洞穴)
				ROOMDATA:ROOM |= 场所_路人
	ENDSELECT
NEXT
;-------------------------------------------------
;路人のメッセージ
;-------------------------------------------------
@MOB_MESSAGE_4
IF FLAG:时间停止
	PRINTFORML 因幡们停下了动作
ELSEIF GROUPMATCH(TIME:2, 1, 6, 7)
	PRINTFORML 因幡们睡着觉…
ELSEIF !DAY:3 % 10
	IF !TALENT:MASTER:性别 & 2
		PRINTFORML 发情中的因幡们陷入了苦恼……
	ELSE
		PRINTFORML 感到了从发情中的因幡们投来的强烈视线……
	ENDIF
ELSE
	SELECTCASE RAND:5
		CASE 0
			PRINTFORML 因幡们正在闲逛
		CASE 1
			PRINTFORML 因幡们在玩
		CASE 2
			PRINTFORML 因幡们在聊天
		CASE 3
			PRINTFORML 因幡和因幡肩并着肩
		CASE 4
			PRINTFORML 因幡们堆在一起
	ENDSELECT
ENDIF
;-------------------------------------------------
;路人のイベント
;-------------------------------------------------
@MOB_TURNENDEVENT_4
IF !FLAG:时间停止
	IF CFLAG:MASTER:诶嘿嘿
		PRINTFORML 四周看到%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%们的因幡们乱哄哄的吵闹了起来、并且听到动静之后开始聚集了过来
		PRINTFORML 感觉气雰有些不妙……
		BASE:TARGET:情绪 = 0
		CALL ENDUFUFU_ALL
	ENDIF
ENDIF
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;地灵殿の路人设定（ペット＆怨灵＆鬼）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;-------------------------------------------------
;路人のいる场所
;-------------------------------------------------
@MOB_PLACE_9
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~场所_路人
	SIF GROUPMATCH(LOCAL, P904大会堂, P905一层走廊, P913食堂, P914二层走廊, P915露台, P923后院, P926中庭)
		ROOMDATA:ROOM |= 场所_路人
	SIF GROUPMATCH(LOCAL, P931大街, P932后街, P933酒场, P938鬼的聚集场所)
		ROOMDATA:ROOM |= 场所_路人
	SIF GROUPMATCH(LOCAL, P928灼热地狱遗迹, P929地狱的人工太阳, P930旧血池地狱)
		ROOMDATA:ROOM |= 场所_路人
NEXT
;-------------------------------------------------
;路人のメッセージ
;-------------------------------------------------
@MOB_MESSAGE_9
IF INRANGE(CFLAG:MASTER:当前位置, P928灼热地狱遗迹, P930旧血池地狱)
	IF FLAG:时间停止
		PRINTL 怨灵们停下了动作
	ELSE
		IF CFLAG:诶嘿嘿
			PRINTL 怨灵看起来不自在的样子…
		ELSEIF !RAND:2
			PRINTL 怨灵在彷徨
		ELSE
			PRINTL 怨灵飘荡着
		ENDIF
	ENDIF
ELSEIF INRANGE(CFLAG:MASTER:当前位置, P931大街, P939小胡同)
	IF FLAG:时间停止
		PRINTL 鬼们停下了动作
	ELSEIF GROUPMATCH(TIME:2, 1, 6, 7)
		PRINTL 鬼们在睡觉…
	ELSE
		SELECTCASE RAND:6
			CASE 0
				PRINTL 鬼们举行着酒宴
			CASE 1
				PRINTL 鬼们聚集在一起
			CASE 2
				PRINTL 鬼们在打着架
			CASE 3
				PRINTL 鬼们徘徊着
			CASE 4
				PRINTL 鬼们吵闹着
			CASE 5
				PRINTL 鬼躺在地上…真碍事
		ENDSELECT
	ENDIF
ELSE
	IF FLAG:时间停止
		PRINTFORML %ANIMALNAME()%停下了动作
	ELSEIF GROUPMATCH(TIME:2, 1, 6, 7)
		PRINTFORML %ANIMALNAME()%在睡觉…
	ELSEIF CFLAG:MASTER:当前位置 == P923后院
		IF !RAND:2
			PRINTFORML %ANIMALNAME()%在洗澡
		ELSE
			PRINTFORML %ANIMALNAME()%在游泳
		ENDIF
	ELSE
		IF !RAND:2
			PRINTFORML %ANIMALNAME()%徘徊着
		ELSE
			PRINTFORML %ANIMALNAME()%躺在地上…真碍事
		ENDIF
	ENDIF
ENDIF
;-------------------------------------------------
;ペットの名前
;-------------------------------------------------
@ANIMALNAME()
#FUNCTIONS
SELECTCASE TIME:1 % 25
	;小型哺乳类
	CASE 0
		LOCALS = 狗
	CASE 1
		LOCALS = 猫
	CASE 2
		LOCALS = 考拉
	CASE 3
		LOCALS = 大猩猩
	CASE 4
		LOCALS = 泽西恶魔
	CASE 5
		LOCALS = 鎧鼠
	CASE 6
		LOCALS = 小野猪
	CASE 7
		LOCALS = 水豚
	;大型哺乳类
	CASE 8
		LOCALS = 食蟻兽
	CASE 9
		LOCALS = 马
	CASE 10
		LOCALS = 牛
	CASE 11
		LOCALS = 羊
	CASE 12
		LOCALS = 羊駝
	CASE 13
		LOCALS = 海豹
	;超大型哺乳类
	CASE 14
		LOCALS = 大象
	CASE 15
		LOCALS = 长颈鹿
	;鸟类
	CASE 16
		LOCALS = 雏鸡
	CASE 17
		LOCALS = 企鵝
	CASE 18
		LOCALS = 鯨头鸛
	CASE 19
	;LOCALS = 僵尸妖精
		LOCALS = 孔雀
	CASE 20
		LOCALS = 鴕鸟
	;爬虫类
	CASE 21
		LOCALS = 鰐鱼
	CASE 22
		LOCALS = 科莫多龙
	CASE 23
		LOCALS = 缅甸蟒
	CASE 24
		LOCALS = 象龟
ENDSELECT
RETURNF LOCALS
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
;月都の路人设定（玉兔）
;＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝
@MOB_PLACE_10
#DIM ROOM
FOR LOCAL, MINROOM(), MAXROOM
	ROOM = LOCAL % 100
	ROOMDATA:ROOM &= ~场所_路人
	SELECTCASE TIME:2
		CASE 2
			SIF GROUPMATCH(LOCAL, P1003玉兔街, P1004广场, P1014桃林, P1020绵月亭・门, P1023门卫值班室, P1025玉兔值班室)
				ROOMDATA:ROOM |= 场所_路人
		CASE 3
			SIF GROUPMATCH(LOCAL, P1003玉兔街, P1004广场, P1020绵月亭・门, P1024玉兔食堂)
				ROOMDATA:ROOM |= 场所_路人
		CASE 4
			SIF GROUPMATCH(LOCAL, P1003玉兔街, P1004广场, P1014桃林, P1020绵月亭・门, P1023门卫值班室, P1025玉兔值班室)
				ROOMDATA:ROOM |= 场所_路人
		CASE 5
			SIF GROUPMATCH(LOCAL, P1003玉兔街, P1004广场, P1020绵月亭・门, P1024玉兔食堂)
				ROOMDATA:ROOM |= 场所_路人
		CASE 6
			SIF GROUPMATCH(LOCAL, P1003玉兔街, P1004广场, P1020绵月亭・门, P1023门卫值班室, P1024玉兔食堂, P1025玉兔值班室)
				ROOMDATA:ROOM |= 场所_路人
		CASEELSE
			SIF GROUPMATCH(LOCAL, P1020绵月亭・门)
				ROOMDATA:ROOM |= 场所_路人
	ENDSELECT
NEXT
;-------------------------------------------------
;路人のメッセージ
;-------------------------------------------------
@MOB_MESSAGE_10
IF FLAG:时间停止
	IF GROUPMATCH(CFLAG:MASTER:当前位置, P1020绵月亭・门, P1023门卫值班室)
		PRINTFORML 门卫们停下了动作
	ELSE
		PRINTFORML 玉兔们停下了动作
	ENDIF
ELSE
	SELECTCASE CFLAG:MASTER:当前位置
	CASE P1003玉兔街, P1004广场
		PRINTFORML 玉兔们忙忙碌碌的
	CASE P1014桃林
		PRINTFORML 玉兔们在训练
	CASE P1020绵月亭・门
		PRINTFORML 门卫在看守着
	CASE P1023门卫值班室
		PRINTFORML 门卫们在休息
	CASE P1024玉兔食堂
		PRINTFORML 玉兔们在用餐
	CASE P1025玉兔值班室
		PRINTFORML 玉兔们在休息
	ENDSELECT
ENDIF
;-------------------------------------------------
;路人のイベント
;-------------------------------------------------
@MOB_TURNENDEVENT_10
IF !FLAG:时间停止
	IF CFLAG:MASTER:诶嘿嘿
		IF GROUPMATCH(CFLAG:MASTER:当前位置, P1020绵月亭・门, P1023门卫值班室)
			PRINTFORML 门卫因为什么事情而嘈嚷地跑了过来
			PRINTFORML 感觉气雰有些不妙……
		ELSE
			PRINTFORML 四周看到%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%们的玉兔们乱哄哄的吵闹了起来、并且听到动静之后开始聚集了过来
			PRINTFORML 感觉气雰有些不妙……
		ENDIF
		BASE:TARGET:情绪 = 0
		CALL ENDUFUFU_ALL
	ENDIF
ENDIF