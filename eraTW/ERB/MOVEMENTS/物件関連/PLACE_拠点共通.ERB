;-----------------------------------------------------------
;房间に居る人数取得
;ARG            房间NO
;RETURNF        人数
;-----------------------------------------------------------
@IN_ROOM_NUMBER(ARG)
#FUNCTION
RETURNF CMATCH(CFLAG:当前位置, ARG)

;-----------------------------------------------------------
;房间最小数参照
;RETURN        最小房间番号
;-----------------------------------------------------------
@MINROOM()
#FUNCTION
RETURNF MAIN_MAP * 100 + 1

;-----------------------------------------------------------
;どこにもいない角色が送られる场所
;RETURNF        隙间空间
;-----------------------------------------------------------
@SUKIMA()
#FUNCTION
RETURNF 99 + MAIN_MAP * 100

;-----------------------------------------------------------
;待客室
;RETURNF        待客室
;-----------------------------------------------------------
@OMANEKIBEYA()
#FUNCTION
RETURNF 98 + MAIN_MAP * 100

;-----------------------------------------------------------
;约会道中
;ARG            MAPID
;RETURNF        约会道中
;-----------------------------------------------------------
@ROAD_TO(ARG)
#FUNCTION
RETURNF 97 + ARG * 100

;-----------------------------------------------------------
;室内判定
;RETURNF 1=室内（和室）,2=室内（洋室）,0=室外
;严密な使い分けはされていないが、屋外だけど屋根のある场所はINROOMにもOUTROOFにも引っかからない
;现状、靴を脱掉かどうかのためだけに洋室判定がある
;-----------------------------------------------------------
@INROOM(ARG)
#FUNCTION
IF IN_HOME(ARG)
	IF ROOMDATA:(ARG % 100) & 场所_洋室
		RETURNF 2
	ELSEIF ROOMDATA:(ARG % 100) & 场所_屋内
		RETURNF 1
	ENDIF
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 1
ELSE
	IF ODEKAKEMAP_SETTING(ARG, "洋室")
		RETURNF 2
	ELSEIF ODEKAKEMAP_SETTING(ARG, "屋内")
		RETURNF 1
	ENDIF
ENDIF
RETURNF 0

;-----------------------------------------------------------
;屋外判定
;RETURNF 1=屋外（屋根がない）,0=屋根がある
;-----------------------------------------------------------
@OUTROOF(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_屋内
		RETURNF 0
	SIF ROOMDATA:(ARG % 100) & 场所_屋根
		RETURNF 0
	RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSEIF ARG == ROAD_TO(GET_MAPID(ARG))
	;地底はダメ
	SIF GET_MAPID(ARG) == 9
		RETURNF 0
	RETURNF 1
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "屋内")
		RETURNF 0
	SIF ODEKAKEMAP_SETTING(ARG, "屋根")
		RETURNF 0
	RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;开阔处の良い场所
;RETURNF	1=广阔室内,2=广阔野外,0=广くない
;-----------------------------------------------------------
@OPENPLACE(ARG)
#FUNCTION
IF IN_HOME(ARG)
	IF ROOMDATA:(ARG % 100) & 场所_广阔
		IF INROOM(ARG)
			RETURNF 1
		ELSE
			RETURNF 2
		ENDIF
	ENDIF
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSEIF ARG == ROAD_TO(GET_MAPID(ARG))
	RETURNF 1
ELSE
	IF ODEKAKEMAP_SETTING(ARG, "广阔")
		IF INROOM(ARG)
			RETURNF 1
		ELSE
			RETURNF 2
		ENDIF
	ENDIF
ENDIF
RETURNF 0

;-----------------------------------------------------------
;赏月ポイント
;RETURNF	1=月が见える,0=月が见えない
;实质空が见えるかどうかの判定
;-----------------------------------------------------------
@MOON_WATCH_POINT(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_赏月
		RETURNF 1
	SIF OUTROOF(ARG)
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA() || ARG == ROAD_TO(GET_MAPID(ARG))
	;地底と月はダメ
	SIF GROUPMATCH(GET_MAPID(ARG), 9, 10)
		RETURNF 0
	RETURNF 1
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "赏月")
		RETURNF 1
	SIF OUTROOF(ARG)
		RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;被窝がある房间
;ARGが无くても机能する
;RETURNF	1=被窝がある,0=被窝がない
;-----------------------------------------------------------
@BEDROOM(ARG)
#FUNCTION
SIF !ARG
	ARG = CFLAG:MASTER:当前位置
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_被窝
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 1
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "被窝")
		RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;休憩できる场所（指令の休憩）
;RETURNF	1=休憩できる,0=休憩できない
;-----------------------------------------------------------
@休憩可(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_休憩
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 1
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "休憩")
		RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;澡堂がある场所
;RETURNF	1=澡堂,0=澡堂じゃない
;-----------------------------------------------------------
@BATHCHECK(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_澡堂
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA() && TFLAG:泡澡堂
	RETURNF 1
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "澡堂")
		RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;なぜか２つあるお澡堂函数
;参数ARGが无くても机能する,というかそれでしか使ってない样子
;こっちは据点内的澡堂だけ
;-----------------------------------------------------------
@BATHROOM(ARG)
#FUNCTION
SIF !ARG
	ARG = CFLAG:MASTER:当前位置
SIF !IN_HOME(ARG)
	RETURNF 0
RETURNF BATHCHECK(ARG)

;-----------------------------------------------------------
;厕所がある房间
;RETURNF	1=厕所,0=厕所じゃない
;-----------------------------------------------------------
@IN_TOILET(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_厕所
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSE
	RETURNF 0
ENDIF
RETURNF 0

;-----------------------------------------------------------
;料理ができる场所
;RETURNF	1=料理できる,0=料理できない
;-----------------------------------------------------------
@KITCHEN(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_厨房
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 1
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "厨房")
		RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;战斗训练できる场所
;弹幕胜负等も兼ねている
;RETURNF	1=训练できる,0=训练できない
;-----------------------------------------------------------
@战斗训练可(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_训练
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "训练")
		RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;勉强できる场所
;RETURNF	1=勉强できる,0=勉强できない
;-----------------------------------------------------------
@勉强可(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_学习
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 1
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "勉强")
		RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;天之声が闻こえる场所
;RETURNF	1=天之声が闻こえる,0=天之声が闻こえない
;-----------------------------------------------------------
@天之声可(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_天之声
		RETURNF 1
	SIF ITEM:祭坛 && ARG == CFLAG:MASTER:初始位置
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "天之声")
		RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;家庭菜园ができる场所
;RETURNF	1=家庭菜园ができる,0=家庭菜园ができない
;-----------------------------------------------------------
@家庭菜园可(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_菜园
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSE
	RETURNF 0
ENDIF
RETURNF 0

;-----------------------------------------------------------
;身体测定できる场所判定
;RETURNF	1=できる,0=できない
;-----------------------------------------------------------
@MEASUREMENTCHECK(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_身体测定
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "身体测定")
		RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;打水ができる场所
;RETURNF	1=打水できる,0=打水できない
;-----------------------------------------------------------
@打水可(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_打水
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSE
	SIF ODEKAKEMAP_SETTING(ARG, "打水")
		RETURNF 1
ENDIF
RETURNF 0

;-----------------------------------------------------------
;物干し竿
;RETURNF	1=物干しできる,0=物干しできない
;物干し描写のためだけにある
;-----------------------------------------------------------
@物干し可(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_物干し
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSE
	RETURNF 0
ENDIF
RETURNF 0

;-----------------------------------------------------------
;憩いスペース
;RETURNF	1=饮食できる,0=饮食できない
;自由行动の吃饭や晩酌等で使用
;起居室・客厅・走廊・休憩所等
;-----------------------------------------------------------
@憩いスペース(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_饮食
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSE
	RETURNF 0
ENDIF
RETURNF 0

;-----------------------------------------------------------
;つまみ食いスポット
;RETURNF	1=つまみ食いできる,0=つまみ食いできない
;自由行动のつまみ食いのためだけにある
;厨房・食料库・旱田等
;-----------------------------------------------------------
@つまみ食いスポット(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_つまみ食い
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSE
	RETURNF 0
ENDIF
RETURNF 0

;-----------------------------------------------------------
;炬燵のある场所
;RETURNF	1=炬燵がある,0=炬燵がない
;自由行动で使用
;憩いスペースからさらに起居室・客厅に限定したもの
;-----------------------------------------------------------
@炬燵のある场所(ARG)
#FUNCTION
IF IN_HOME(ARG)
	SIF ROOMDATA:(ARG % 100) & 场所_炬燵
		RETURNF 1
ELSEIF ARG == OMANEKIBEYA()
	RETURNF 0
ELSE
	RETURNF 0
ENDIF
RETURNF 0

;-----------------------------------------------------------
;澡堂から追い出された时の逐出先
;-----------------------------------------------------------
@KICKOUT(ARG)
#FUNCTION
SIF !ARG
	ARG = CFLAG:MASTER:当前位置
FOR LOCAL, MINROOM(), MAXROOM
	SIF CAN_MOVE(ARG, LOCAL) & 1
		RETURNF LOCAL
NEXT
RETURNF CFLAG:MASTER:前一次的位置

;-----------------------------------------------------------
;角色ARGの当前位置の名称を返す函数
;ARG  角色ID
;-----------------------------------------------------------
@PLACE_NAME(ARG)
#FUNCTIONS
IF !IN_HOME(CFLAG:ARG:当前位置) && CFLAG:ARG:当前位置 != OMANEKIBEYA()
	LOCALS = %DATENAME_SPOT()%
ELSEIF CHK_DATENOW(CFLAG:MASTER:约会中) && CFLAG:MASTER:当前位置 != OMANEKIBEYA()
	LOCALS = %DATENAME_PLACE()%の%DATENAME_SPOT()%
ELSE
	LOCALS = %NAME_FROM_PLACE(CFLAG:ARG:当前位置)%
ENDIF
RETURNF LOCALS

;-----------------------------------------------------------
;据点の住人かをチェックする函数
;ARG    MAPID
;CHARA  判定する角色
;-----------------------------------------------------------
@MAP_住人(ARG, CHARA)
#FUNCTION
#DIM CHARA
;CSVの初始位置を取得
LOCAL = CSVCFLAG(CHARA, GETNUM(CFLAG, "初始位置"))
SIF LOCAL == 99 || !LOCAL
	RETURNF 0
SIF LOCAL / 100 == ARG
	RETURNF 1

;-----------------------------------------------------------
;角色ARGが你から见える位置にいるか
;ARG  角色ID
;-----------------------------------------------------------
@CAN_SEE(ARG)
#FUNCTION
SIF CFLAG:MASTER:当前位置 == CFLAG:ARG:当前位置
	RETURNF 1
SIF GETBIT(CAN_MOVE(CFLAG:MASTER:当前位置, CFLAG:ARG:当前位置), 1)
	RETURNF 1
RETURNF 0
