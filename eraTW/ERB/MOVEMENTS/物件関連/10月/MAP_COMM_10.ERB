;--------------------------------------------------
;地点名变更函数
;--------------------------------------------------
@MAP_PLACENAME_10(ARG)
#FUNCTIONS
IF GET_MAPID(ARG) == MAIN_MAP
	SELECTCASE ARG
		CASE P1033绵月亭・空的房间, P1063纯狐邸・空的房间
			IF CFLAG:MASTER:初始位置 == ARG
				RETURNF CALLNAME:(FLAG:住宿人物) + "私室"
			ELSE
				RETURNF "空的房间"
			ENDIF
		CASE P1007闲置的房子
			IF CFLAG:MASTER:初始位置 == ARG
				RETURNF "你的家"
			ELSE
				RETURNF "闲置的房子"
			ENDIF
		CASE SUMIKOMI_ROOM
			;※このMapでは未实装
			IF FLAG:住宿人物
				RETURNF CALLNAME:(FLAG:住宿人物) + "的房间"
			ELSE
				RETURNF "空的房间"
			ENDIF
		CASE MINROOM() TO (MAXROOM - 1)
			RETURNF STR:(ARG + 8000)
		CASE OMANEKIBEYA()
			RETURNF OMANEKI_PLACE()
		CASE SUKIMA()
			RETURNF "隙间空间"
		CASE MAXROOM
			RETURNF "梦的彼侧"
		CASEELSE
			RETURNF "不明"
	ENDSELECT
ELSE
	RETURNF "月の外"
ENDIF

;--------------------------------------------------
;场所ARGがどのエリアにあるか
;表示するMapの番号と连动（TFLAG:地图切换）
;ARG     场所ID
;--------------------------------------------------
@MAP_AREASELECT_10(ARG)
#FUNCTION
SELECTCASE ARG
	;绵月亭
	CASE P1020绵月亭・门 TO 1049
		RETURNF 2
	;哆来咪宅
	CASE P1050哆来咪宅・起居室 TO 1059
		RETURNF 3
	;纯狐邸
	CASE P1060纯狐邸・玄关 TO P1072赫卡提亚私室
		RETURNF 4
	;月の世界
	CASEELSE
		RETURNF 1
ENDSELECT

;--------------------------------------------------
;内部构造がある场所の内部の判定
;TFLAG:地图切换  内部察觉が必要なMap番号
;                P_ID  表示ボタンの场所ID
;                Q_ID  判定する内部の场所ID,必ずP_IDと一致した数值も含める
;--------------------------------------------------
@GETMAP_BUILDING_10(P_ID, Q_ID)
#FUNCTION
#DIM P_ID
#DIM Q_ID
IF TFLAG:地图切换 == 1
	SELECTCASE P_ID
		;绵月亭
		CASE P1020绵月亭・门
			SELECTCASE Q_ID
			CASE P1020绵月亭・门 TO 1049
				RETURNF 1
			ENDSELECT
		;哆来咪宅
		CASE P1050哆来咪宅・起居室
			SELECTCASE Q_ID
			CASE P1050哆来咪宅・起居室 TO 1059
				RETURNF 1
			ENDSELECT
		;纯狐邸
		CASE P1060纯狐邸・玄关
			SELECTCASE Q_ID
			CASE P1060纯狐邸・玄关 TO P1072赫卡提亚私室
				RETURNF 1
			ENDSELECT
	ENDSELECT
ENDIF
RETURNF 0

;--------------------------------------------------
;移动制限设定
;ARG    场所ID
;CHARA  移动する角色ID
;RETURNF 1=移动できる,0=移动できない
;TFLAG:移动不能信息   ↓の@MAP_CANNOT_WORD用の设定
;--------------------------------------------------
@MAP_CAN_MOVE_10(ARG, CHARA)
#FUNCTION
#DIM CHARA
SELECTCASE ARG
	CASE P1005探女宅・起居室, P1006探女宅・寝室
		;除外
		SIF GROUPMATCH(CHARA, [[依姫]], [[丰姫]], [[探女]], [[永琳]])
			RETURNF 1
		;中からは动ける
		SIF INRANGE(CFLAG:CHARA:当前位置, P1005探女宅・起居室, P1006探女宅・寝室)
			RETURNF 1
		;你
		IF CHARA == MASTER
			;同行か恋慕なら入れる（家主睡眠中も可
			IF CFLAG:[[探女]]:同行 || TALENT:[[探女]]:恋慕
				RETURNF 1
			;家主がいて起きてる
			ELSEIF INRANGE(CFLAG:[[探女]]:当前位置, P1005探女宅・起居室, P1006探女宅・寝室) && SHIRAHU([[探女]])
				RETURNF 1
			;家主以外の谁かがいる,この人物は寝てたりしても良い
			ELSEIF IN_ROOM_NUMBER( P1005探女宅・起居室) || IN_ROOM_NUMBER(P1006探女宅・寝室)
				RETURNF 1
			ELSE
				TFLAG:移动不能信息 = 999
				RETURNF 0
			ENDIF
		;部下
		ELSEIF GROUPMATCH(CHARA, [[铃仙二号]], [[清兰]], [[铃瑚]], [[铃仙]])
			;谁かがいる,この人物は寝てたりしても良い
			IF IN_ROOM_NUMBER( P1005探女宅・起居室) || IN_ROOM_NUMBER(P1006探女宅・寝室)
				RETURNF 1
			ELSE
				RETURNF 0
			ENDIF
		;それ以外は入れない
		ELSE
			RETURNF 0
		ENDIF
	;入り口から近いせいであまりにも人が来るので入室制限をする
	CASE P1050哆来咪宅・起居室 TO 1059
		;除外
		SIF GROUPMATCH(CHARA, [[哆来咪]], [[菫子]], [[探女]])
			RETURNF 1
		;中からは动ける
		SIF INRANGE(CFLAG:CHARA:当前位置, P1050哆来咪宅・起居室, 1059)
			RETURNF 1
		;你は关系无
		SIF CHARA == MASTER
			RETURNF 1
		;家主がいて起きてる
		IF INRANGE(CFLAG:[[哆来咪]]:当前位置, P1050哆来咪宅・起居室, 1059) && SHIRAHU([[哆来咪]])
			RETURNF 1
		ENDIF
		;それ以外は入れない
		RETURNF 0
	CASE P1031宝物库
		;除外
		SIF GROUPMATCH(CHARA, [[依姫]], [[丰姫]], [[探女]], [[永琳]], [[青娥]])
			RETURNF 1
		;你
		IF CHARA == MASTER
			;信赖度1000以上
			IF CFLAG:[[依姫]]:信赖度 >= 1000 || CFLAG:[[丰姫]]:信赖度 >= 1000
				RETURNF 1
			ELSE
				TFLAG:移动不能信息 = 999
				RETURNF 0
			ENDIF
		;それ以外は入れない
		ELSE
			RETURNF 0
		ENDIF
	;谁かが住んでないと入れない
	CASE P1033绵月亭・空的房间, P1063纯狐邸・空的房间, P1007闲置的房子
		IF !PRIVATEROOM:(ARG % 100) && CFLAG:MASTER:初始位置 != ARG
			TFLAG:移动不能信息 = 999
			RETURNF 0
		ENDIF
ENDSELECT
RETURNF 1

;--------------------------------------------------
;移动不能时に表示されるメッセージ
;--------------------------------------------------
@MAP_CANNOT_WORD_10(ARG)
RETURN
SELECTCASE TFLAG:移动不能信息
	CASE 1
		PRINTFORMW ※在这里输入消息。执行行数+1的CLEARLINE
		CLEARLINE 2
ENDSELECT
REUSELASTLINE

;-----------------------------------------
;据点専用の情景描写
;WAITはかけないように
;-----------------------------------------
@MAP_UNIQUEMESSAGE_10
IF WITH_MOB()
	CALL MOB_MESSAGE_10
ENDIF
RETURN 1

;-----------------------------------------
;据点の场所IDをお出かけMap上の场所IDに置换する函数
;ARG      对象の场所ID（据点Map）
;RETURNF  差し替え场所ID（お出かけMap）
;-----------------------------------------
@MAP_SEARCH_REPLACEMENT_10(ARG)
#FUNCTION
SELECTCASE ARG
	CASE P1001槐安通路, P1050哆来咪宅・起居室 TO 1059
		RETURNF 槐安通路
	CASE P1002月之都, P1005探女宅・起居室 TO P1007闲置的房子
		RETURNF 月之都
	CASE P1020绵月亭・门 TO 1049
		RETURNF 绵月亭
	CASE P1014桃林, P1015沙滩
		RETURNF 桃林～沙滩
	CASE P1016入海口, P1060纯狐邸・玄关 TO P1072赫卡提亚私室
		RETURNF 宁静海
	CASEELSE
		RETURNF 玉兔之街
ENDSELECT
RETURNF 0

;--------------------------------------------------
;Mapの介绍文
;--------------------------------------------------
@Map介绍_10
;とりあえず5行
PRINTFORML 由槐安通路～月之都、绵月亭、哆来咪宅、纯狐邸这四张地图组成的区域
PRINTFORML 月都内有住宅街和商家街、也可以在月之都郊外～月面进行菜园和训练等户外作业
PRINTFORML 有三栋房屋的地图是哆来咪的家、然后纯狐邸、绵月亭的规模依次增大、扫除难度也随着地图规模增大
PRINTFORML 生活必要的设备到处都有、可以随意选择自己喜欢的住所。然而所有的私室候补都要求高信赖度
PRINTFORML 顺便说一下、哆来咪宅的设计参考了貘的头
