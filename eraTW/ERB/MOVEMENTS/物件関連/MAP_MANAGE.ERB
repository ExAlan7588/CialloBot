;------------------------------------------
;	引っ越しの表示と处理
;------------------------------------------
@SET_MAINHOME
#DIM CHK_TRAINING
#DIM CHK_GARDENING
#DIM 设定前MAIN_MAP
#DIM 设定前酒虫
#DIM 设定前初始位置
#DIM 设定前大家
#DIM ANIMATION_SEED
#DIM PREV_REDRAW
#DIM lastTime
#DIM flushTime


ANIMATION_SEED = 0
PREV_REDRAW = CURRENTREDRAW()

SIF AllowMove
	lastTime = GETTIME()

IF AllowMove
	flushTime = 80
ELSE
	flushTime = ANIMATERECOLOREDMAPS
ENDIF

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
;update map image
IF ANIMATERECOLOREDMAPS && !FLAG:70
	IF AllowMove
		IF GETTIME() - lastTime > ANIMATERECOLOREDMAPS
			ANIMATION_SEED ++
			lastTime = GETTIME()
		ENDIF
	ELSE
		ANIMATION_SEED ++
	ENDIF
ENDIF
;
;
;メンバーの起床时间再设定
;
IF !引っ越し
	IF ゲーム开始处理中 || ゲーム继承处理中
		MAIN_MAP = 0
		TRYCALLFORM ROOMSETTING_{MAIN_MAP}
	ENDIF
	设定前MAIN_MAP = MAIN_MAP
	设定前酒虫 = 据点_酒虫
	设定前初始位置 = 默认初始位置
	设定前大家 = 据点_家主
	引っ越し = 1
ENDIF
IF FLAG:一时滞在
	PRINTFORMW 因为有来这里过夜的角色、所以无法搬家
	RETURN
ENDIF
DRAWLINE
;Color map
IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED, "HIKKOSHI")
ELSE
	CALL DRAW_MAP("HIKKOSHI")
ENDIF
DRAWLINE
PRINTFORML ■%GET_MAPNAME(MAIN_MAP)%
CALL 物件データ(MAIN_MAP)
PRINTFORM 采集可能地点：
CALL ShowGatheringSpots(MAIN_MAP)
PRINTFORML
FONTITALIC
CALLFORM Map介绍_{MAIN_MAP}
FONTREGULAR
PRINTL
DRAWLINEFORM =
PRINT 可以对自宅进行设定。要住在哪里呢？
PRINTBUTTON @"[预览图切换({TFLAG:地图切换})]", 1997
PRINTL
FOR LOCAL, 0, 11
	SIF MAIN_MAP == LOCAL
		SETCOLOR C_AQUA
	SIF GET_EXISTMAP(LOCAL)
		PRINTFORM [{LOCAL + 2000}] %GET_MAPNAME(LOCAL), 16, LEFT%
	SIF GROUPMATCH(LOCAL, 4, 9, 10)
		PRINTL
	RESETCOLOR
NEXT
PRINT [1999] 决定　　　　　　
SIF !ゲーム开始处理中 && !ゲーム继承处理中
	PRINT [1998] 中止
PRINTL
;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	REDRAW PREV_REDRAW
	TINPUT flushTime,99999,0,""
ELSE
	INPUT
ENDIF
;据点切り替え
IF RESULT == 99999
	PREV_REDRAW = CURRENTREDRAW()
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
ELSEIF RESULT >= 2000 && GET_EXISTMAP(RESULT - 2000)
	MAIN_MAP = RESULT - 2000
	CALLFORM ROOMSETTING_{MAIN_MAP}
	CALLFORM DEFAULT_OWNER_{MAIN_MAP}
	TFLAG:地图切换 = 1
	RESTART
;プレビュー切り替え
ELSEIF RESULT == 1997
	TFLAG:地图切换 += 1
	SIF TFLAG:地图切换 > MAP_PAGES
		TFLAG:地图切换 = 1
	RESTART
;中止
ELSEIF RESULT == 1998 && !ゲーム开始处理中 && !ゲーム继承处理中
	引っ越し = 0
	MAIN_MAP = 设定前MAIN_MAP
	据点_酒虫 = 设定前酒虫
	默认初始位置 = 设定前初始位置
	据点_家主 = 设定前大家
	TFLAG:地图切换 = 0
	CALLFORM ROOMSETTING_{MAIN_MAP}
	RETURN
;决定
ELSEIF RESULT == 1999
	SIF 设定前MAIN_MAP == MAIN_MAP
	CALLFORM DEFAULT_OWNER_{MAIN_MAP}
ELSE
	RESTART
ENDIF
;决定后
FLAG:神签契约 = 0
TFLAG:地图切换 = 0
CALL SET_ENKAI_LOCATIONDISPLAY
CALL MAP_DEFAULTRESIDENT(MAIN_MAP)
;施锭
VARSET LOCK

FOR LOCAL,1,CHARANUM
	CALL CHARA_SLEEP, LOCAL, 0
NEXT
CHK_TRAINING = 0
CHK_GARDENING = 0
FOR LOCAL,1,100
	SIF ROOMDATA:LOCAL & 场所_训练
		CHK_TRAINING = 1
	SIF ROOMDATA:LOCAL & 场所_菜园
		CHK_GARDENING = 1
NEXT
SIF !CHK_TRAINING
	CALL COLORMESSAGE(@"这个地图无法进行[战斗训练]",C_YELLOW,2)
SIF !CHK_GARDENING
	CALL COLORMESSAGE(@"这个地图无法进行[家庭菜园]",C_YELLOW,2)
SIF !SUMIKOMI_ROOM
	CALL COLORMESSAGE(@"这个地图不会发生住进事件。",C_YELLOW,2)
SIF FLAG:扮演 && CFLAG:(FLAG:扮演):出禁 && MAIN_MAP == GET_MAPID(CSVCFLAG(FLAG:扮演,311))
	CFLAG:MASTER:初始位置 = CSVCFLAG(FLAG:扮演,311)
;陷阱ポイントの张替え
CALL TRAP_MOVING

@HAS_DUPLICATE_KEY, ARG
#FUNCTION
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:311 == ARG
		SIF TALENT:LOCAL:恋慕
			RETURNF 1
	ENDIF
NEXT

@MAP_DEFAULTRESIDENT(ARG, UPDATE)
#DIM UPDATE
;ARG=Map番号
SIF !UPDATE
	FLAG:住宿人物 = 0
MAIN_MAP = ARG
FOR LOCAL, 1, CHARANUM
	IF MAP_住人(MAIN_MAP, LOCAL)
		CFLAG:LOCAL:初始位置 = CSVCFLAG (LOCAL, GETNUM(CFLAG, "初始位置"))
		CFLAG:LOCAL:神社在住 = CFLAG:LOCAL:初始位置
		SIF !UPDATE
			DEBUGPRINTFORML 在%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%住下来了
	ELSE
		PRIVATEROOM:(CFLAG:LOCAL:初始位置 % 100) = 0
		CFLAG:LOCAL:初始位置 = SUKIMA()
		CFLAG:LOCAL:神社在住 = 0
	ENDIF
	CFLAG:LOCAL:自宅位置 = CSVCFLAG (LOCAL, GETNUM(CFLAG, "自宅位置"))
	CFLAG:LOCAL:当前位置 = CFLAG:LOCAL:初始位置
	CFLAG:LOCAL:约会中 = MAIN_MAP
NEXT
SIF !UPDATE
	PRINTFORMW %GET_MAPNAME(ARG)%设定完毕
CALL ROOMSETTING
IF !UPDATE
	CFLAG:MASTER:初始位置 = 默认初始位置
	CFLAG:MASTER:当前位置 = CFLAG:MASTER:初始位置
	引っ越し = 0
ENDIF

@ROOMSETTING
#DIM R_ID
#DIM 私室候补
#DIM CHARA
私室候补 = 0
SUMIKOMI_ROOM = 0
;据点住人は初始位置をPRIVATEROOMに设定,施锭の都合で先に处理
VARSET PRIVATEROOM
FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:神社在住 && !CFLAG:LOCAL:出禁
		;PRIVATEROOM变量に、そこ的家主の番号を保存
		PRIVATEROOM:(CFLAG:LOCAL:初始位置 % 100) = LOCAL
	 	SIF CFLAG:LOCAL:房东候补
			私室候补 ++
	ENDIF
NEXT
TRYCCALLFORM ROOMSETTING_{MAIN_MAP}
	PRINTFORMW %GET_MAPNAME(MAIN_MAP)%的房间情报设定完毕
	;住宿人物用のワンルーム
	IF FLAG:住宿人物 && SUMIKOMI_ROOM
		R_ID = SUMIKOMI_ROOM % 100
		ROOMDATA:R_ID |= 场所_ワンルーム
	ENDIF
	FOR LOCAL, 1, 100
		;补完设定
		;MASTERの初始位置は最低限の机能を备える,饮食はできなくなる
		IF CFLAG:MASTER:初始位置 % 100 == LOCAL
			ROOMDATA:LOCAL |= 场所_被窝
			ROOMDATA:LOCAL |= 场所_学习
			ROOMDATA:LOCAL |= 场所_プライベート
			ROOMDATA:LOCAL &= ~场所_饮食
		ENDIF
		;ワンルーム
		IF ROOMDATA:LOCAL & 场所_ワンルーム
			ROOMDATA:LOCAL |= 场所_被窝
			ROOMDATA:LOCAL |= 场所_屋内
			ROOMDATA:LOCAL |= 场所_厨房
			ROOMDATA:LOCAL |= 场所_学习
			ROOMDATA:LOCAL |= 场所_休憩
			ROOMDATA:LOCAL |= 场所_饮食
			ROOMDATA:LOCAL |= 场所_プライベート
			ROOMDATA:LOCAL |= 场所_炬燵
			ROOMDATA:LOCAL &= ~场所_过疎
		ENDIF
		;被窝
		IF ROOMDATA:LOCAL & 场所_被窝
			ROOMDATA:LOCAL |= 场所_休憩
		ENDIF
		;洋室
		IF ROOMDATA:LOCAL & 场所_洋室
			ROOMDATA:LOCAL |= 场所_屋内
		ENDIF
		;厨房
		IF ROOMDATA:LOCAL & 场所_厨房
			ROOMDATA:LOCAL |= 场所_つまみ食い
		ENDIF
		;施锭チェック,初始位置が变わった场合、施锭を解锭する
		R_ID = LOCAL + MAIN_MAP * 100
		SIF LOCK:R_ID && !PRIVATEROOM:LOCAL
			LOCK:R_ID = 0
	NEXT
CATCH
	PRINTFORMW 房间情报的设定失败了
ENDCATCH
IF (ゲーム开始处理中 || ゲーム继承处理中) || !默认初始位置 || 引っ越し
	IF 私室候补
		CALL 引っ越し初始位置设定
	ELSE
		CALL 默认引っ越し
	ENDIF
ENDIF

@默认引っ越し
TRYCCALLFORM DEFAULT_OWNER_{MAIN_MAP}
CATCH
	PRINTFORMW 房间情报的设定失败了
ENDCATCH
PRINTFORMW 将%NAME_FROM_PLACE(默认初始位置)%设定为了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的私室

@引っ越し初始位置设定
#DIM CHARA
#DIM ANIMATION_SEED
#DIM PREV_REDRAW
ANIMATION_SEED = 0
PREV_REDRAW = CURRENTREDRAW()

;Map Animation Code
$MAPANIMATION
LINESTART = LINECOUNT
SIF ANIMATERECOLOREDMAPS && !FLAG:70
	ANIMATION_SEED ++
DRAWLINE
IF RECOLOREDMAPS
	CALL DRAW_COLOREDMAP(ANIMATION_SEED)
ELSE
	CALL DRAW_MAP("HIKKOSHI")
ENDIF
DRAWLINE
PRINTFORML %PRINT_COLOR("这个地图有多个", C_WHITE) + PRINT_COLOR("私室候补", C_ORANGE) + PRINT_COLOR("。", C_WHITE)%
SIF DAY <= 1
	PRINTFORML 第一天限定选项：无视必要信赖选择住的地点。
PRINTBUTTON @"[预览图切换({TFLAG:地图切换})]", 1997
PRINTL
FOR CHARA, 1, CHARANUM
	IF CFLAG:CHARA:神社在住 && CFLAG:CHARA:房东候补
		SIF CFLAG:CHARA:居住必要信赖度 > CFLAG:CHARA:信赖度 && DAY > 1
			SETCOLOR C_L_GRAY
		PRINTBUTTON @"[{CFLAG:CHARA:房东候补 % 100, 3}] - %NAME_FROM_PLACE(CFLAG:CHARA:房东候补), 20, LEFT%　家主:%NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA), 15, LEFT%　必要信赖度:{CFLAG:CHARA:居住必要信赖度}", CHARA
		PRINTL
		RESETCOLOR
	ENDIF
NEXT
PRINTFORML [  0] - ({默认初始位置 % 100, 2})%NAME_FROM_PLACE(默认初始位置)%（默认设定）

;魔改功能
IF 默认初始位置 % 100 == 53
	PRINTFORM [ 58] - (58)大别墅的主卧　
FONTSTYLE 5
SETCOLOR C_GRAY
PRINTFORML (魔改功能)
FONTSTYLE 0
RESETCOLOR
ENDIF

;Map Animation Code
IF ANIMATERECOLOREDMAPS && !FLAG:70
	REDRAW PREV_REDRAW
	TINPUT ANIMATERECOLOREDMAPS,1234,0,""
ELSE
	INPUT
ENDIF

IF RESULT == 0
	CALL 默认引っ越し
ELSEIF RESULT == 1234
	PREV_REDRAW = CURRENTREDRAW()
	REDRAW 0
	CLEARLINE LINECOUNT - LINESTART
	GOTO MAPANIMATION
ELSEIF RESULT < CHARANUM && CFLAG:RESULT:神社在住 && CFLAG:RESULT:房东候补
	CHARA = RESULT
	IF CHARA != 0 && CFLAG:CHARA:居住必要信赖度 > CFLAG:CHARA:信赖度 && DAY > 1
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%还没有信赖%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%到这种程度…
		RESTART
	ELSE
		据点_家主 = CHARA
		默认初始位置 = CFLAG:CHARA:房东候补
		ROOMDATA:(默认初始位置 % 100) |= 场所_被窝
		ROOMDATA:(默认初始位置 % 100) |= 场所_プライベート
		PRINTFORMW 将%NAME_FROM_PLACE(CFLAG:CHARA:房东候补)%设定为了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的私室
	ENDIF
;プレビュー切り替え
ELSEIF RESULT == 1997
	TFLAG:地图切换 += 1
	SIF TFLAG:地图切换 > MAP_PAGES
		TFLAG:地图切换 = 1
	RESTART
ELSEIF RESULT == 58
	据点_家主 = 1
	默认初始位置 = 58
	PRINTFORMW 将大别墅主卧设定为了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的私室
ELSE
	RESTART
ENDIF

;-------------------------------------------------
;引っ越し下见时にROOMDATAを参照して施设の有无を表示する函数
;ARG	MAP_ID
;-------------------------------------------------
@物件データ(ARG)
#DIM MAPDATA
VARSET MAPDATA
VARSET LOCAL
PRINT 施设：
;Mapデータの参照
FOR LOCAL, 1, 100
	SIF (ROOMDATA:LOCAL & 场所_澡堂) != 0
		MAPDATA |= 场所_澡堂
	SIF (ROOMDATA:LOCAL & 场所_厨房) != 0
		MAPDATA |= 场所_厨房
	SIF (ROOMDATA:LOCAL & 场所_训练) != 0
		MAPDATA |= 场所_训练
	SIF (ROOMDATA:LOCAL & 场所_学习) != 0
		MAPDATA |= 场所_学习
	SIF (ROOMDATA:LOCAL & 场所_菜园) != 0
		MAPDATA |= 场所_菜园
	SIF (ROOMDATA:LOCAL & 场所_天之声) != 0
		MAPDATA |= 场所_天之声
NEXT
FONTBOLD
CALL COLORMESSAGE(@"【澡堂】", (MAPDATA & 场所_澡堂) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【厨房】", (MAPDATA & 场所_厨房) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【训练】", (MAPDATA & 场所_训练) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【学习】", (MAPDATA & 场所_学习) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【菜园】", (MAPDATA & 场所_菜园) ? C_WHITE # C_GRAY, 0)
CALL COLORMESSAGE(@"【天之声】", (MAPDATA & 场所_天之声) ? C_WHITE # C_GRAY, 0)
FONTREGULAR

;地点の参照
FOR LOCAL, MINROOM(), MAXROOM
	SIF FISHING_SPOT(LOCAL)
		LOCAL:1 = 1
	SIF IS_LIBRARY(LOCAL, 1)
		LOCAL:2 = 1
	SIF GATHERING_SPOT(LOCAL)
		LOCAL:3 = 1
	SIF PHARMACY_SPOT(LOCAL) == 2
		LOCAL:4 = 1
	SIF STALL_SPOT(LOCAL)
		LOCAL:5 = 1
	SIF PLACE_SENTO(LOCAL)
		LOCAL:6 = 1
	SIF OMIKUJI_SPOT(LOCAL)
		LOCAL:7 = 1
	SIF MORIYA_OMIKUJI(LOCAL)
		LOCAL:8 = 1
	SIF KAIDASHI_SHOP(LOCAL)
		LOCAL:9 = 1
	SIF FLOWER_SHOP(LOCAL)
		LOCAL:10 = 1
	SIF SAKE_SHOP(LOCAL)
		LOCAL:11 = 1
	SIF BOOK_RENTAL(LOCAL)
		LOCAL:12 = 1
	SIF YASAI_SHOP(LOCAL)
		LOCAL:13 = 1
	SIF CASINO_SPOT(LOCAL)
		LOCAL:14 = 1
	SIF HUNTING_SPOT(LOCAL)
		LOCAL:15 = 1
	SIF SUMIKOMI_ROOM == LOCAL
		LOCAL:16 = 1
NEXT
FOR LOCAL, 1, 20
	IF LOCAL:LOCAL
		SELECTCASE LOCAL
		CASE 1
			PRINTFORM 【钓鱼】
		CASE 2
			PRINTFORM 【读书】
		CASE 3
			PRINTFORM 【采集】
		CASE 4
			PRINTFORM 【调合】
		CASE 5
			PRINTFORM 【露店】
		CASE 6
			PRINTFORM 【钱汤】
		CASE 7
			PRINTFORM 【神签】
		CASE 8
			PRINTFORM 【守矢神签】
		CASE 9
			PRINTFORM 【食料品店】
		CASE 10
			PRINTFORM 【花屋】
		CASE 11
			PRINTFORM 【酒屋】
		CASE 12
			PRINTFORM 【借书屋】
		CASE 13
			PRINTFORM 【蔬菜店】
		CASE 14
			PRINTFORM 【红魔CASINO】
		CASE 15
			PRINTFORM 【诱捕】
		CASE 16
			PRINTFORM 【住处】
		ENDSELECT
	ENDIF
NEXT
PRINTL
PRINT 住人：
;角色表示
FOR LOCAL, 1, CHARANUM
	IF MAP_住人(MAIN_MAP, LOCAL)
		SIF LOCAL:21
			PRINTFORM 、
		RESULT = 0
		TRYCALLFORM M_KOJO_K{LOCAL}
		PRINTFORM \@ LOCAL:20 ? 、 # \@\@ RESULT ? * # \@%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%
		LOCAL:21 ++
	ENDIF
NEXT
PRINTL

;Map内の采集可能地点を表示する函数
@ShowGatheringSpots(ARG)
#DIM cStart
#DIM cEnd
#DIM SpotID
#DIM Num
#DIM Wordcount
#DIM pCount

cStart = 1 + ARG * 100
cEnd = 99 + ARG * 100
Num = 0
pCount = 0
FOR LOCAL, cStart, cEND
	SpotID = GATHERING_SPOT(LOCAL, 1)
	IF SpotID
		pCount ++
		STRLENFORM %ForagePlaceName(SpotID)%
		Wordcount = RESULT:0
		Num += Wordcount
		IF Num > 110
			PRINTL
			PRINTFORM 　　　　　　　
			Num = Wordcount
		ENDIF
		PRINTFORM %ForagePlaceName(SpotID)%
		SIF Num <= 106
			PRINTFORM 　　
	ENDIF
NEXT
SIF !pCount
	PRINT 无
PRINTFORML

@KOURINDOU_IS_WORKING_HOURS()
#FUNCTION
;香霖堂,10时～18时
SIF INRANGE(TIME, 600, 1080)
	RETURNF 1
RETURNF 0