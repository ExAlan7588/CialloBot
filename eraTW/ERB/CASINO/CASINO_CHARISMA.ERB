

;C/Yは筹码・￥
@CASINO_TRADE
#LOCALSIZE 1
#LOCALSSIZE 1
#DIM LCOUNT
#DIM C_RATE
#DIM C_MAX
#DIM C_PRICE
#DIM Y_RATE
#DIM Y_MAX
#DIM Y_PRICE
C_RATE = CHARISMA_NOW()
;Y_RATEはC_RATEの1/100だが精度の关系で不使用
;Y_RATE = FUNC_CASINO("TRADE") / 100
;C购入取引に使用筹码入力の上限
C_MAX = MIN(CM_LIMIT - MONEY:2, MONEY / C_RATE)

PRINTFORML 妖精女仆
PRINTFORML 　欢迎光临
PRINTFORML 　这里可以用￥来交换筹码
IF C_RATE < 100
	PRINTFORML 　非常抱歉
	PRINTFORML 　现在无法购买筹码。
ENDIF
PRINTFORML 你の保有￥　　　　　　%金额表示(MONEY), 13, RIGHT%
PRINTFORML 你拥有的筹码　　　%三桁区切(MONEY:2), 13, RIGHT% / %三桁区切(CM_LIMIT)%
PRINTFORMW 现在的￥/筹码兑换率 　%金额表示(C_RATE), 13, RIGHT%
DRAWLINE
CALL ASK_M(@"购入筹码",C_RATE > 100 && C_MAX > 0, @"卖出筹码",FLAG:会员ランク,"放弃",1)
SELECTCASE RESULT
	CASE 0
		IF CM_LIMIT <= MONEY:2
			PRINTFORML 对不起、客人你的筹码已经达到上限了、请使用后再购买
			PRINTFORML 或提高会员等级、欢迎再次光临
			RETURN
		ENDIF
		PRINTFORML 妖精女仆
		PRINTFORML 　请输入要买的筹码数量（上限%三桁区切(C_MAX)%）
		FOR LCOUNT, 0, LOG10(C_MAX) + 1
			LOCAL = POWER(10, LCOUNT)
			PRINTFORML [{LOCAL, LOG10(C_MAX) + 1}] - 购入{LOCAL}筹码
		NEXT
		SIF C_MAX >= 4
			PRINTFORML [{C_MAX / 2, LOG10(C_MAX) + 1}] - 购入最大值的一半
		PRINTFORML [{C_MAX, LOG10(C_MAX) + 1}] - 购入最大值
		PRINTFORML [{0, LOG10(C_MAX) + 1}] - 放弃
		INPUT
		IF INRANGE(RESULT, 1, C_MAX)
			C_PRICE = RESULT
			Y_PRICE = C_PRICE * C_RATE
			CALL CASINO_MONEY("￥", -Y_PRICE)
			CALL CASINO_MONEY("筹码", C_PRICE)
			PRINTFORML 妖精女仆
			PRINTFORML 　感谢您的使用
			PRINTFORMW 　购买%三桁区切(C_PRICE)%个筹码花费了%金额表示(Y_PRICE)%
		ENDIF
	CASE 1
		CALL CM_BALANCE
	CASE 2
		RETURN
ENDSELECT

;为替相场の变动
@MOENY_HENDOU
#LOCALSIZE 1
#LOCALSSIZE 1
IF FLAG:筹码变动累积 >= 10
	FLAG:筹码变动状况 = RAND(1, 10)
	SELECTCASE FLAG:筹码变动状况
		CASE 9
			CALL SHOW_REMILLIA_INTERVIEW
		CASE 1
			CALL SHOW_CHARISMA_GUARD
	ENDSELECT
	FLAG:筹码变动累积 = 0
ENDIF

;极端な状况の补正
IF FLAG:筹码人气 >= 300
	SIF RAND:(FLAG:筹码人气 / 50)
		FLAG:筹码变动状况 = MAX(1, FLAG:筹码变动状况 - 2)
ELSEIF FLAG:筹码人气 <= 50 && INRANGE(FLAG:筹码变动状况, 1, 4)
	SIF RAND:(6 - FLAG:筹码变动状况)
		FLAG:筹码变动状况 += 2
ENDIF

SELECTCASE FLAG:筹码变动状况
CASE 1
	LOCAL = 60 + RAND:10
CASE 2
	LOCAL = 70 + RAND:10
CASE 3
	LOCAL = 80 + RAND:10
CASE 4
	LOCAL = 95 + RAND:5
CASE 5
	IF FLAG:筹码人气 <= 85
		FLAG:筹码人气 += RAND(1, 10)
	ELSEIF FLAG:筹码人气 >= 150
		FLAG:筹码人气 -= RAND(1, 10)
	ELSEIF RAND:2
		FLAG:筹码人气 += RAND(1, 10)
	ELSE
		FLAG:筹码人气 -= RAND(1, 10)
	ENDIF
CASE 6
	LOCAL = 105 + RAND:5
CASE 7
	LOCAL = 125 + RAND:10
CASE 8
	LOCAL = 150 + RAND:50
CASE 9
	LOCAL = 200 + RAND:100
ENDSELECT

IF FLAG:筹码变动状况 == 5
	FLAG:筹码变动累积 += 5
ELSE
	FLAG:筹码人气 = FLAG:筹码人气 * LOCAL / 100
	FLAG:筹码变动累积 += ABS(5 - FLAG:筹码变动状况) + RAND:2
ENDIF

FLAG:筹码人气 = MAX(10, FLAG:筹码人气)
TIMES FLAG:筹码流通, 0.98



;￥、やってることは筹码と同样
IF FLAG:￥变动累积 >= 10
	FLAG:￥变动状况 = RAND(1, 10)
	FLAG:￥变动累积 = 0
ENDIF

;极端な状况の补正
IF FLAG:￥人气 >= 300
	SIF RAND:(FLAG:￥人气 / 50)
		FLAG:￥变动状况 = MAX(1, FLAG:￥变动状况 - 2)
ELSEIF FLAG:￥人气 <= 30 && INRANGE(FLAG:￥变动状况, 1, 4)
	SIF RAND:(6 - FLAG:￥变动状况)
		FLAG:￥变动状况 += 2
ENDIF

SELECTCASE FLAG:￥变动状况
CASE 1
	LOCAL = 60 + RAND:10
CASE 2
	LOCAL = 70 + RAND:10
CASE 3
	LOCAL = 80 + RAND:10
CASE 4
	LOCAL = 95 + RAND:5
CASE 5
	IF FLAG:￥人气 <= 85
		FLAG:￥人气 += RAND(1, 10)
	ELSEIF FLAG:￥人气 >= 150
		FLAG:￥人气 -= RAND(1, 10)
	ELSEIF RAND:2
		FLAG:￥人气 += RAND(1, 10)
	ELSE
		FLAG:￥人气 -= RAND(1, 10)
	ENDIF
CASE 6
	LOCAL = 105 + RAND:5
CASE 7
	LOCAL = 125 + RAND:10
CASE 8
	LOCAL = 150 + RAND:50
CASE 9
	LOCAL = 200 + RAND:100
ENDSELECT

IF FLAG:￥变动状况 == 5
	FLAG:￥变动累积 += 5
ELSE
	FLAG:￥人气 = FLAG:￥人气 * LOCAL / 100
	FLAG:￥变动累积 += ABS(5 - FLAG:￥变动状况) + RAND:2
ENDIF

FLAG:￥人气 = MAX(10, FLAG:￥人气)
TIMES FLAG:￥流通, 0.98


@TODAYS_CHARISMA
#DIM 年月

GETTIME
DEBUGPRINTFORML {RESULT / 10000000000000}年 {(RESULT / 100000000000) % 100}月 {(RESULT / 1000000000) % 100}日 {(RESULT / 10000000) % 100}时だよ

年月 = RESULT / 100000000000

IF 年月 != FLAG:前回筹码设定月
	DUMPRAND
	PRINTFORMW 设定{RESULT / 10000000000000}年 {(RESULT / 100000000000) % 100}月的筹码行情
	FLAG:前回筹码设定月 = 年月
	RANDOMIZE 年月
	CALL SET_THISMONTHCHARISMA
	INITRAND
ENDIF
CALL SHOW_CHARISMA

[SKIPSTART]

#LOCALSIZE 1
#LOCALSSIZE 1
PRINTFORML 
PRINTFORML ～今天的筹码～
PRINTFORML 　1筹码＝%金额表示(FUNC_CASINO("TRADE"))%
PRINTFORML 　＜筹码＞
PRINTFORML 　　倾向{FLAG:筹码变动状况}　累积{FLAG:筹码变动累积, 2}　人气{FLAG:筹码人气}％
PRINTFORML 　＜￥＞
PRINTFORML 　　倾向{FLAG:￥变动状况}　累积{FLAG:￥变动累积, 2}　人气{FLAG:￥人气}％
PRINTFORMW 

[SKIPEND]


;红魔赌场用初期设定
@SET_CHARISMA(TYPE)
#LOCALSIZE 1
#LOCALSSIZE 1
#DIMS TYPE
#DIM LCOUNT
SELECTCASE TYPE
;まだ筹码を设定していないデータ用
CASE "NEW"
	FLAG:筹码人气 = 100
	FLAG:筹码变动状况 = 5
	FLAG:￥人气 = 100
	FLAG:￥变动状况 = 5
;すでに筹码を设定している周回用
;要するに红魔赌场の初期化指令だがカモられ指数は初期化しない
CASE "RESET"
	FLAG:筹码流通 = 0
	FLAG:筹码人气 = 100
	FLAG:筹码变动状况 = 5
	FLAG:筹码变动累积 = 0

	FLAG:￥流通 = 0
	FLAG:￥人气 = 100
	FLAG:￥变动状况 = 5
	FLAG:￥变动累积 = 0
ENDSELECT

@SET_THISMONTHCHARISMA
#DIM 筹码变动累积
#DIM 变动幅

筹码变动累积 = 0

;初日は150~250
CHARISMA_RATE:0 = 150 + RAND:100
CHARISMA_TENDENCY:0 = 5
FOR LOCAL,1,32
	IF 筹码变动累积 >= 10
		;适正レートだと安定しやすい
		IF INRANGE(CHARISMA_TENDENCY:(LOCAL - 1),150,250) && !RAND:2
			CHARISMA_TENDENCY:LOCAL = 5
		ELSEIF CHARISMA_TENDENCY:(LOCAL - 1) == 5
			;安定からは极端な变动をしない
			CHARISMA_TENDENCY:LOCAL = RAND(3,8)
		ELSEIF CHARISMA_RATE:(LOCAL -1) > 250
			CHARISMA_TENDENCY:LOCAL = RAND(2, 8)
		ELSEIF CHARISMA_RATE:(LOCAL -1) < 150
			CHARISMA_TENDENCY:LOCAL = RAND(3, 9)
		ELSE
			CHARISMA_TENDENCY:LOCAL = RAND(2, 9)
		ENDIF
		筹码变动累积 = 0
	ELSE
		;变动しないなら前期と同じ
		CHARISMA_TENDENCY:LOCAL = CHARISMA_TENDENCY:(LOCAL -1)
	ENDIF
	;前期筹码价格に応じて补正
	SELECTCASE CHARISMA_RATE:(LOCAL -1)
		CASE IS < 50
			IF !RAND:4
				CHARISMA_TENDENCY:LOCAL = 9
				筹码变动累积 = 0
			ENDIF
		CASE 50 TO 100
			IF !RAND:5
				CHARISMA_TENDENCY:LOCAL = 8
				筹码变动累积 = 0
			ENDIF
		CASE 250 TO 300
			IF !RAND:5
				CHARISMA_TENDENCY:LOCAL = 2
				筹码变动累积 = 0
			ENDIF
		CASE IS > 300
			IF !RAND:4
				CHARISMA_TENDENCY:LOCAL = 1
				筹码变动累积 = 0
			ENDIF
	ENDSELECT

	SELECTCASE CHARISMA_TENDENCY:LOCAL
		CASE 1
			变动幅 = - RAND:30 - 100
			筹码变动累积 += 3 + RAND:2 + RAND:3
		CASE 2
			变动幅 = RAND:20 - 50
			筹码变动累积 += 2 + RAND:2 + RAND:2
		CASE 3
			变动幅 = RAND:10 - 30
			筹码变动累积 += 1 + RAND:3 + RAND:2
		CASE 4
			变动幅 = -RAND:5 - 5
			筹码变动累积 += 1 + RAND:3 + RAND:2
		CASE 5
			变动幅 = RAND:10 - 5
			筹码变动累积 += 1 + RAND:3
		CASE 6
			变动幅 = RAND:5 + 5
			筹码变动累积 += 1 + RAND:3 + RAND:2
		CASE 7
			变动幅 = 30 - RAND:10
			筹码变动累积 += 1 + RAND:3 + RAND:3
		CASE 8
			变动幅 = RAND:20 + 50
			筹码变动累积 += 2 + RAND:2 + RAND:2
		CASE 9
			变动幅 = RAND:30 + 100
			筹码变动累积 += 3 + RAND:2 + RAND:3
	ENDSELECT

	CHARISMA_RATE:LOCAL = CHARISMA_RATE:(LOCAL -1) + 变动幅
	CHARISMA_RATE:LOCAL = LIMIT(CHARISMA_RATE:LOCAL,1,400)
	SIF CHARISMA_RATE:LOCAL == 1 || CHARISMA_RATE:LOCAL == 400
		筹码变动累积 = 10
NEXT

@SHOW_CHARISMA
#DIM 前期筹码倾向
IF NOW_PERIOD()
	前期筹码倾向 = CHARISMA_TENDENCY:(NOW_PERIOD() -1)
	LOCAL = CHARISMA_TENDENCY:(NOW_PERIOD()-1)
	IF CHARISMA_TENDENCY:NOW_PERIOD() != 前期筹码倾向 && NOW_PERIOD() > 1
	SETCOLOR C_YELLOW
		SELECTCASE CHARISMA_TENDENCY:NOW_PERIOD()
			CASE 9
				CALL SHOW_REMILLIA_INTERVIEW
			CASE 5
				SIF GROUPMATCH(前期筹码倾向,1,2,8,9)
					PRINTFORML %SHOW_TENDENCY(前期筹码倾向)%的筹码价格没什么波动的样子。
			CASE 1
				CALL SHOW_CHARISMA_GUARD
		ENDSELECT
	RESETCOLOR
	PRINTFORMW 
	ENDIF
ENDIF
PRINTL
PRINTFORML ◇本月{NOW_PERIOD()}号的筹码行情：１筹码＝%金额表示(CHARISMA_NOW())%
PRINTFORMW 价格的变动为：%SHOW_TENDENCY(CHARISMA_TENDENCY:NOW_PERIOD())%

IF FLAG:新闻订阅
	PRINTFORML 
	PRINTFORML 最近的筹码波动为
	FOR LOCAL,1,40
		SIF !INRANGE(LOCAL,NOW_PERIOD()-9,NOW_PERIOD())
			CONTINUE
		CALL PRINT_COLORBAR, CHARISMA_RATE:LOCAL, 400, 40, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("水色"),
		PRINTFORML 　{LOCAL}期%金额表示(CHARISMA_RATE:LOCAL)%　%SHOW_TENDENCY(CHARISMA_TENDENCY:LOCAL)%
	NEXT
ENDIF

@SHOW_TENDENCY(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		LOCALS = 暴落
	CASE 2
		LOCALS = 下落
	CASE 3
		LOCALS = 下降
	CASE 4
		LOCALS = 微减
	CASE 5
		LOCALS = 安定
	CASE 6
		LOCALS = 坚调
	CASE 7
		LOCALS = 上升
	CASE 8
		LOCALS = 高腾
	CASE 9
		LOCALS = 暴腾
ENDSELECT
RETURNF LOCALS

@NOW_PERIOD()
#FUNCTION
GETTIME
;筹码变动は一日一度に变更
LOCAL = (RESULT / 1000000000) % 100
;LOCAL = ((RESULT / 1000000000) % 100 - 1) * 6 + ((RESULT / 10000000) % 100) / 4
RETURNF LOCAL

@NOW_MONTH()
#FUNCTION
GETTIME
LOCAL = RESULT / 100000000000
RETURNF LOCAL

@CHARISMA_NOW()
#FUNCTION
RETURNF CHARISMA_RATE:NOW_PERIOD()

@CM_BALANCE(ARG)
#DIM 卖出筹码
#DIM 卖出额
#DIM 手数料
#DIM 精算额
#DIM レート

レート = CHARISMA_NOW()

IF FLAG:新闻订阅
	PRINTFORML 
	PRINTFORML 最近的筹码波动为
	FOR LOCAL,1,40
		SIF !INRANGE(LOCAL,NOW_PERIOD()-9,NOW_PERIOD())
			CONTINUE
		CALL PRINT_COLORBAR, CHARISMA_RATE:LOCAL, 400, 40, UNICODE(0x2585), UNICODE(0x2585), BARCOLORSET("水色"),
		PRINTFORML 　{LOCAL}期%金额表示(CHARISMA_RATE:LOCAL)%　%SHOW_TENDENCY(CHARISMA_TENDENCY:LOCAL)%
	NEXT
ENDIF
PRINTFORML 请输入要出售多少筹码
PRINTFORML 手续费是{CM_COMMISSION}％
PRINTFORML [{MONEY:2}] - 全部卖出
PRINTFORML [0] - 中止
INPUT
IF !INRANGE(RESULT,0,MONEY:2)
	PRINTFORMW 无效的数值
	RETURN
ELSEIF !RESULT
	RETURN
ENDIF
卖出筹码 = RESULT
卖出额 = 卖出筹码 * レート
手数料 = 卖出额 * CM_COMMISSION / 100

精算额 = 卖出额 - 手数料
PRINTFORML 卖出汇率：%金额表示(CHARISMA_NOW())% × 卖出额：{卖出筹码} ＝ %金额表示(卖出额)%
PRINTFORML 手续费：%金额表示(手数料), 13%
PRINTFORML 精算额　%金额表示(精算额), 13%

PRINTFORML 可以吗？
CALL ASK_YN
SIF RESULT
	RETURN

MONEY += 精算额
MONEY:2 -= 卖出筹码

@SHOW_CHARISMA_GUARD
IF FLAG:新闻订阅
	RESETCOLOR
	PRINTFORML ────────────────────────
	FONTBOLD
	PRINTDATAL
		DATAFORM 　　　　红魔馆馆主　以那种姿态接近中！
		DATAFORM 　　　这个简直就是铁壁般的防御啊！
		DATAFORM 　　　　　筹码的价格突然下降！
	ENDDATA
	FONTREGULAR
	PRINTFORML ────────────────────────
	SETFONT "ＭＳ Ｐゴシック"
	PRINTL 　　　　　　　　　　　　　 　 　,. -‐'''''"￣￣｀"''ヽ. 　 〉､
	PRINTL 　　　　　　　 ,.-ｧ　　　　r'/´　　　　　　　　　　　｀Y/ヽヽ.
	PRINTL 　　　　　　／::〈　　　r‐､iY　　　　　　　　　　　　　 ', 　 ｀ヽ.
	PRINTL 　　　　　//ヽ:::::' , 　 i:::|:::ヽ>,r'^ｰ､､,___________,r'ヽ,イ　　　　',
	PRINTL 　　　　 //　　｀ヽ:ヽ〈::〈::::::::7 　 r'´､r-､__,. ->i 　iカ.　　　　 !
	PRINTL 　　　　,','　　　　　｀'ｰ|_!._::::,' 　 ﾊ_!_,.ｨ　ﾊ　i､ ﾊ　|_「　　__　　|
	PRINTL 　　 　 |'　　　　　 　　　レ7　　|./--'､ﾚ'　V,ﾆ.! 　|/ ./´　｀ヽ|
	PRINTL 　　 　 |　　　　　　　　〈,rﾍﾊﾍr|ﾊ"　　____ 　 ,ﾊﾝヽ./　っ
	PRINTL 　　 　 |　　,.-‐､／´￣｀ヽ!｀￣ > !ヽr´--'＜'ﾊ〉　　っ　
	PRINTL 　　　　ﾚ'´　　　　 　 く￣ﾍ´　イ＞''"´￣｀'ヽr-ｧ
	PRINTL 　　　　　　　　　　　　 ヽ:::::ヽr ´ ／　　/　　　ヽ!
	PRINTL 　　　　　　　　　　　　 く:::::＞!イ　　　/　 i　　　ﾊ
	PRINTL 　　　　　　　　　　　　　｀7:::く /__r､__ﾄ､　i__r! _,rﾍ>
	PRINTL 　　　　　　　　　　　　 　 レ'｀ﾄ!.二Y　 ￣Y'ｰ'ｲ￣
	SETFONT
	PRINTFORMW ────────────────────────
	SETCOLOR C_YELLOW
	
ENDIF
PRINTFORML 文文。新闻刊登了蕾米莉亚抱头蹲防姿势的照片！
PRINTFORML 因此筹码价格大幅度下降！
RESETCOLOR

@SHOW_REMILLIA_INTERVIEW
IF FLAG:新闻订阅
	RESETCOLOR
	PRINTFORML ────────────────────────
	FONTBOLD
	PRINTDATAL
		;DATAFORM 　　　　红魔馆当主　その魅力に迫る！
		;DATAFORM 　『筹码』新たな通货　その秘密とは！？
		;DATAFORM 　　ついに自机化！？噂の真相を本人に直击！
		DATAFORM 　　　　红魔馆馆主　魅力尽显！
		DATAFORM 　新的货币『筹码』　它的秘密是！？
		DATAFORM 　　终于自机化了！？向本人直击传闻的真相！
	ENDDATA
	FONTREGULAR
	PRINTFORML ────────────────────────
	SETFONT "ＭＳ Ｐゴシック"
	PRINTL 　　　　　　　　　　　　　　　　　　＿＿＿＿__
	PRINTL 　　　　　　 　 　 　 　 　 ,..　　´　　　　　 　 　 ｀　　､
	PRINTL 　　　 　 　 　 　 　 　 ／　　　　　　　　　　　　　　　 ＼
	PRINTL 　　　　　 　 　 　 　 ,:　　　　　　　　　　　　　　　　　 　 ヽ
	PRINTL 　 　 　 　 　 　  　 /　　　　　　　　　　　 　 　 　 　 　 　 :.
	PRINTL 　 　 　 ,.イ｢￣:::＼{_,.---- ､/￣7ー〈´￣ ｀ヽ／⌒､　　　}
	PRINTL 　　　 /:::::{ L:::::::／∧　　　 {＿∧＿,ﾉ､＿__/ ヽ　　}＿,＜
	PRINTL 　　　 ＼::::､ |:::/　 ': :＼＿ノ__/ | : : : ,.ｨ､-l―l L＿/ ⌒ 〉｀ヽ､
	PRINTL 　　　r:::∧::} }:_＼ |: : :|: : :|: :/｀ {: : l: : :| }/从:|: :|: : : |:＜＿,.-､}
	PRINTL 　　　{:::::､{::::∧_ノ {: : :{: : :{:ｨ=≠ミ、: ､:|ｲ芯笊ｱ': : :/{: : :､＿_ノ
	PRINTL 　　　 ＼:＼乂_r-八:､乂: 从弋ﾋｿ ＼:}'　ゞ-ﾟ'/ : : ｲ: |: : :ﾊ/ニヽ
	PRINTL 　　　 　 ＼::/　 /＿}､}: ＼}∧　　 　 '　　 　 ﾑイ:./: ,': :/: :}ニニﾑ
	PRINTL 　　　　　　 ｀　　 　 /: /: : : :人　　　ｰ　'　　 ｨ/: ,: :/:イｨミ､//＼ ﾑ
	PRINTL 　　　　　　 　 　 　 {: ｲ: :{ : : : : ＞,....　__ 　 ´｛: /イ::｢_ﾌ⌒ヽ､/ ∧ }
	PRINTL 　　　 　 　 　 　 　 从{､:｣乂≧=｢/￣ ∧　　/从{' {::l |_ﾌ　 　∨/ } |
	PRINTL 　　　　　　　　 　 　 　　　/ 　 〈/:|　 {　'　 /　／　|::| !ﾌ　　　 {//| |
	PRINTL 　 　　 　 　 　 　 　 　 　 j 　 〈/|::|　 ＼ 　 ／　　/:/｢ｨ　　　　|// '
	PRINTL 　　　　　 　 　 　 　 　 ／ 　 〈il |::|　/::∧/＼:.､ /://ｲ　　 　　|/ｲ
	PRINTL 　　　　　　　 　 ｨ=v^＞===-＜{|::∨イ{⌒Y､ ＼://　　　　　　 ヽ
	PRINTL 　 　 　 　 　 　 ∧ 〉,.=======/　 ／::乂_ﾉ:: 　 ∨{　　 　 　 /　 }
	PRINTL 　　　　　　　　 {ﾆつﾑ　　　　 ,' /::::::::::/::::|::::|　 　 乂-r､＿,/＿ノ
	PRINTL 　　　　　　　　 {￣__) ゝ.＿_,ノ_,'::::::::::/::::/:::::|＿＿_ |ー{＿∧＿〉､
	PRINTL ￣￣￣￣￣¨ゝ--'＿ノ ¨´￣￣￣￣￣￣　　　　｀ヽ　　/―― ｀――――
	PRINTL 　　　　　　　　 {　　　 ﾉ､__,..-- ､　__　　　　 　 　 　 　 　 /
	PRINTL 　　　　　　　　 乂_,.イ　　　　　　 　 ｀ ¨　　―　　　＿_.ノ
	PRINTL 　　　　 　 　 　 　 ,..------ ､
	PRINTL 　　　　　　 　　　（ (￣￣￣) ）
	PRINTL 　　　　　　　　　　｀ ━━━ ´
	SETFONT
	PRINTFORMW ────────────────────────
	SETCOLOR C_YELLOW
ENDIF
;PRINTFORML 蕾米莉亚へのインタビュー记事が文文新闻の一面を饰りました！
;PRINTFORMW その内容が反响を呼び筹码价格が暴腾しています！
PRINTFORML 对蕾米莉亚的采访报道登上了文文。新闻的头版！
PRINTFORMW 其内容引起了反响，筹码价格暴涨！
RESETCOLOR
