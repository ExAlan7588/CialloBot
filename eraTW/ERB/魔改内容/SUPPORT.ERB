;数据更新
@updateVersion()
	;#DIM index
	;本体更新
	; SIF LASTLOAD_VERSION != GAMEBASE_VERSION
		CALL gameUpdateVersion
	;kojo更新
	;FOR index, 0, CHARANUM
	;	CALL KOJO_MESSAGE_SEND("KOJO_VERSION", 0, index)
	;NEXT

;本体更新
@gameUpdateVersion()
	;这个函数用来代替UPDATE,玩家不用一更新就点UPDATE
	;如果你的更新不需要数据处理你就无视了吧
	;一下是ym的,你当作例子康康
	; ;eratohoYM AE M 4282 → eratohoYM AE M 4284の存档变换处理
	; SIF LASTLOAD_VERSION < 4284
	; 	CALL SAVE_SUPPORT_VERSION_4284

	; ;eratohoYM AE M 4284 → eratohoYM AE M 4285の存档变换处理
	; SIF LASTLOAD_VERSION < 4285
	; 	CALL SAVE_SUPPORT_VERSION_4285
	CALL UPDATE

@UPDATE_OF_KOJO_ORIGIN

;口上の存在判定 and RESULTSに文字列代入
FOR LOCAL, 1, CHARANUM
	VARSET RESULTS
	TARGET = LOCAL
	IF CFLAG:LOCAL:口上セレクタ
		TRYCALLFORM M_KOJO_K{LOCAL}_{CFLAG:LOCAL:口上セレクタ}
	ELSE
		TRYCALLFORM M_KOJO_K{LOCAL}
	ENDIF
	PRINTFORML 正在确认%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%%RESULTS:1%的口上是否存在UPDATE
	TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{LOCAL}
	WAIT
NEXT

@UPDATE_AND_SELECTION_OF_KOJO_MODDING(WORK_MODE)
#DIM DYNAMIC MAXPAGE_MODE_0
#DIM DYNAMIC MAXPAGE_MODE_1
#DIM DYNAMIC MAXPAGE
#DIM DYNAMIC PAGE
#DIM DYNAMIC MARK_KOJO_AVAILABLE, 人物数量上限
#DIM DYNAMIC NUM_OF_CHARA_WITH_KOJO
#DIM DYNAMIC LIST_OF_CHARA_WITH_KOJO, 人物数量上限
#DIM DYNAMIC LIST_OF_CHARA_ALTERED_BY_TOUHOU, 人物数量上限
#DIM DYNAMIC C_ID
#DIM DYNAMIC LINE_RECORD
#DIM DYNAMIC CHOOSE_COMMAND
#DIMS DYNAMIC HTML
#DIMS DYNAMIC COLOR
#DIM DYNAMIC REDRAW_RECORD
#DIM OPERATE_MODE = 1
#DIMS DYNAMIC WORK_MODE
#DIM DYNAMIC NUM_OF_KOJO
CURRENTREDRAW
REDRAW_RECORD = RESULT
REDRAW 0
;确认角色口上存在情况
VARSET RESULT
FOR LOCAL, 0, CHARANUM
	IF CFLAG:LOCAL:口上セレクタ
		TRYCCALLFORM M_KOJO_K{LOCAL}_{CFLAG:LOCAL:口上セレクタ}
		CATCH
			VARSET RESULT
		ENDCATCH
	ELSE
		TRYCCALLFORM M_KOJO_K{LOCAL}
		CATCH
			VARSET RESULT
		ENDCATCH
	ENDIF
	;有口上角色标记
	IF RESULT
		MARK_KOJO_AVAILABLE:LOCAL = RESULT
		NUM_OF_CHARA_WITH_KOJO ++ 
	ENDIF
NEXT
;不过滤无口上角色时的最多页数
MAXPAGE_MODE_0 = (CHARANUM - 1) / 20 + 1
;过滤无口上角色时的最多页数
MAXPAGE_MODE_1 = (NUM_OF_CHARA_WITH_KOJO - 1) / 20 + 1
;制作以原作顺序排列的人物列表
VARSET RESULT
IF FLAG:排序
	CALL CHARASORT_EX
	LOCAL:1 = 0
	FOR LOCAL, 0, EX_CHARANUM
		SIF RESULT:LOCAL < 0 
			CONTINUE
		LIST_OF_CHARA_ALTERED_BY_TOUHOU:(LOCAL:1) = RESULT:LOCAL
		LOCAL:1 ++
	NEXT
ENDIF
;制作过滤无口上角色时的角色列表
LOCAL:2 = 0
FOR LOCAL, 0, CHARANUM
	SIF !MARK_KOJO_AVAILABLE:LOCAL
		CONTINUE
	IF FLAG:排序
		LIST_OF_CHARA_WITH_KOJO:(FINDELEMENT(LIST_OF_CHARA_ALTERED_BY_TOUHOU, LOCAL, 1, CHARANUM)) = LOCAL
	ELSE
		LIST_OF_CHARA_WITH_KOJO:(LOCAL:2) = LOCAL
	ENDIF
	LOCAL:2 ++
NEXT

IF FLAG:排序
	FOR LOCAL, 0, NUM_OF_CHARA_WITH_KOJO
		IF !LIST_OF_CHARA_WITH_KOJO:LOCAL
			ARRAYSHIFT LIST_OF_CHARA_WITH_KOJO, -1, 0, LOCAL
			LOCAL --
		ENDIF
	NEXT
ENDIF

LINE_RECORD = LINECOUNT

WHILE 1
	DRAWLINE
	DRAWLINE
	IF WORK_MODE == "SELECTION"
		RESETCOLOR
		PRINTFORML %UNICODE(0x3000) * 4%角色口上选择界面%UNICODE(0x3000) * 4%
		PRINTFORML ＜　ＰＡＧＥ　．　%TOFULL(TOSTR(PAGE + 1))%　／　ＡＬＬ．ＰＡＧＥ．%TOFULL(TOSTR(MAXPAGE))%＞
	ELSE
		HTML_PRINT @"<i><b><u>　　　　角色口上ＵＰＤＡＴＥ选择界面　　　　</u></b></i>"
		HTML_PRINT @"<i><font color = '#0x00FF00'>＜　ＰＡＧＥ　．　%TOFULL(TOSTR(PAGE + 1))%　／　ＡＬＬ．ＰＡＧＥ．%TOFULL(TOSTR(MAXPAGE))%＞</font></i>"
	ENDIF
	IF OPERATE_MODE == 1
		HTML_PRINT @"<i><nonbutton title = '当前显示角色均为有口上角色'><font color='#0x90EE90'>　　过滤无口上角色功能开启　　</font></nonbutton></i>"
	ELSE
		HTML_PRINT @"<i><nonbutton title = '已显示全部角色，包括无口上角色'><font color='#0x404040'>　　过滤无口上角色功能关闭　　</font></nonbutton></i>"
	ENDIF
	IF OPERATE_MODE == 1
		MAXPAGE = MAXPAGE_MODE_1
	ELSE
		MAXPAGE = MAXPAGE_MODE_0
	ENDIF
	IF PAGE >= MAXPAGE
		PAGE = MAXPAGE - 1
	ELSEIF PAGE < 0 
		PAGE = 0
	ENDIF
	FOR LOCAL, PAGE * 20, (PAGE + 1) * 20
		VARSET HTML
		VARSET RESULT
		VARSET RESULTS
		VARSET C_ID
		IF OPERATE_MODE == 1
			SIF LOCAL > NUM_OF_CHARA_WITH_KOJO - 1
				BREAK
			SIF LIST_OF_CHARA_WITH_KOJO:LOCAL
				C_ID = LIST_OF_CHARA_WITH_KOJO:LOCAL
		ELSE 
			SIF LOCAL > CHARANUM - 1
				BREAK
			IF FLAG:排序
				C_ID = LIST_OF_CHARA_ALTERED_BY_TOUHOU:LOCAL
			ELSE
				C_ID = LOCAL
			ENDIF
		ENDIF
		IF WORK_MODE == "SELECTION"
			RESETCOLOR
			PRINTPLAINFORM [{LOCAL, 4, LEFT}] - %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID), 25, LEFT%
			IF FINDELEMENT(LIST_OF_CHARA_WITH_KOJO, C_ID) > 0
				VARSET RESULT
				VARSET RESULTS
				IF CFLAG:C_ID:口上セレクタ
					TRYCALLFORM M_KOJO_K{C_ID}_{CFLAG:C_ID:口上セレクタ}
				ELSE
					TRYCALLFORM M_KOJO_K{C_ID}
				ENDIF
				SIF !STRLENS(RESULTS:1) && RESULT == 1 && CFLAG:C_ID:口上セレクタ == 0
					RESULTS:1 = 初期
				SIF STRLENS(RESULTS:1)
					PRINTFORM %UNICODE(0x3000) * 4%%RESULTS:1%口上
			ENDIF
			PRINTL
			VARSET NUM_OF_KOJO
			FOR LOCAL:3, 0, 10
				VARSET RESULT
				VARSET RESULTS
				IF LOCAL:3 > 0
					TRYCALLFORM M_KOJO_K{C_ID}_{LOCAL:3}
				ELSE
					TRYCALLFORM M_KOJO_K{C_ID}
				ENDIF
				SIF RESULT == 1
					NUM_OF_KOJO ++
				SIF !STRLENS(RESULTS:1) && RESULT == 1 && CFLAG:C_ID:口上セレクタ == 0
					RESULTS:1 = 初期
				IF RESULT == 1 && CFLAG:C_ID:口上セレクタ == LOCAL:3
					SETCOLOR C_GREEN
					PRINTPLAINFORM %UNICODE(0x3000) * 3%└ %RESULTS:1%口上
					RESETCOLOR
					PRINTL
				ELSEIF RESULT == 1
					SETCOLOR C_ORANGE
					PRINTBUTTON @"%UNICODE(0x3000) * 3%└ %RESULTS:1%口上", 2000 + C_ID * 10 + LOCAL:3
					RESETCOLOR
					PRINTL
				ENDIF
			NEXT
			SIF NUM_OF_KOJO == 1
				CLEARLINE 1
		ELSE
			VARSET RESULT
			IF CFLAG:C_ID:口上セレクタ
				TRYCALLFORM M_KOJO_K{C_ID}_{CFLAG:C_ID:口上セレクタ}
			ELSE
				TRYCALLFORM M_KOJO_K{C_ID}
			ENDIF
			SIF !STRLENS(RESULTS:1) && RESULT == 1 && CFLAG:C_ID:口上セレクタ == 0
				RESULTS:1 = 初期
			RESETCOLOR
			VARSET COLOR
			TRYCALLFORM M_KOJO%RESULTS%_COLOR_K{C_ID}
			COLOR '= "0x" + hex_to_str(GETCOLOR())
			HTML += @"<button value='{C_ID}'><font color='#%COLOR%'>[{LOCAL, 4, LEFT}"
			HTML += @"]  -  %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID), 16, LEFT%    %CSVCSTR(C_ID, 10), 100, LEFT%"
			IF FINDELEMENT(LIST_OF_CHARA_WITH_KOJO, C_ID) > 0 
				HTML += @"----------------%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%%RESULTS:1%口上</font></button>"
			ELSE
				HTML += @"----------------无口上</font></button>"
			ENDIF
			HTML_PRINT HTML
			RESETCOLOR
		ENDIF
	NEXT
	IF WORK_MODE == "SELECTION"
		IF PAGE == 0
			PRINTPLAINFORM %UNICODE(0x3000) * 2%［跳转首页］%UNICODE(0x3000) * 2%
			PRINTPLAINFORM %UNICODE(0x3000) * 2%［上一页］%UNICODE(0x3000) * 2%
			PRINTBUTTON @"%UNICODE(0x3000) * 2%［下一页］%UNICODE(0x3000) * 2%", 1002
			PRINTBUTTON @"%UNICODE(0x3000) * 2%［跳转尾页］%UNICODE(0x3000) * 2%", 1003
		ELSEIF PAGE == MAXPAGE - 1
			PRINTBUTTON @"%UNICODE(0x3000) * 2%［跳转首页］", 1000
			PRINTBUTTON	@"%UNICODE(0x3000) * 2%［上一页］%UNICODE(0x3000) * 2%", 1001
			PRINTPLAINFORM %UNICODE(0x3000) * 2%［下一页］%UNICODE(0x3000) * 2%
			PRINTPLAINFORM %UNICODE(0x3000) * 2%［跳转尾页］%UNICODE(0x3000) * 2%
		ELSEIF INRANGE(PAGE, 0, MAXPAGE - 2)
			PRINTBUTTON @"%UNICODE(0x3000) * 2%［跳转首页］%UNICODE(0x3000) * 2%", 1000
			PRINTBUTTON	@"%UNICODE(0x3000) * 2%［上一页］%UNICODE(0x3000) * 2%", 1001
			PRINTBUTTON @"%UNICODE(0x3000) * 2%［下一页］%UNICODE(0x3000) * 2%", 1002
			PRINTBUTTON @"%UNICODE(0x3000) * 2%［跳转尾页］%UNICODE(0x3000) * 2%", 1003
		ENDIF
	ELSE
		IF PAGE == 0
			{
			HTML_PRINT @"<nonbutton title = '跳转至首页'>　　［　＜＜　］　　</nonbutton>
			<nonbutton title = '上一页'>　　［　＜　］　　</nonbutton>
			<button value = '1002' title = '下一页'>　　［　＞　］　　</button>
			<button value = '1003' title = '跳转至尾页'>　　［　＞＞　］　　</button>"
			}
		ELSEIF PAGE == MAXPAGE - 1
			{
			HTML_PRINT @"<button value = '1000' title = '跳转至首页'>　　［　＜＜　］　　</button>
			<button value = '1001' title = '上一页'>　　［　＜　］　　</button>	
			<nonbutton title = '下一页'>　　［　＞　］　　</nonbutton>
			<nonbutton title = '跳转至尾页'>　　［　＞＞　］　　</nonbutton>"
			}
		ELSEIF INRANGE(PAGE, 0, MAXPAGE - 2)
			{
			HTML_PRINT @"<button value = '1000' title = '跳转至首页'>　　［　＜＜　］　　</button>
			<button value = '1001' title = '上一页'>　　［　＜　］　　</button>
			<button value = '1002' title = '下一页'>　　［　＞　］　　</button>
			<button value = '1003' title = '跳转至尾页'>　　［　＞＞　］　　</button>"
			}
		ENDIF
	ENDIF
	IF OPERATE_MODE == 1
		HTML_PRINT @"<button value = '1004' title = '当前显示角色均为有口上角色'>　　［过滤功能：开启］　　</button>"
	ELSE
		HTML_PRINT @"<button value = '1004' title = '已显示全部角色，包括无口上角色'>　　［过滤功能：关闭］　　</button>"
	ENDIF
	SIF !STRLENS(WORK_MODE)
		HTML_PRINT @"<button value = '1005' title = '经典模式，一次性全过一遍'>　　［ＵＰＤＡＴＥ全体进行］　　</button>"
	HTML_PRINT @"<button value = '1100'>　　［退出］　　</button>"
	DRAWLINE
	DRAWLINE
	VARSET RESULT
	INPUT
	CHOOSE_COMMAND = RESULT
	SELECTCASE CHOOSE_COMMAND
		CASE 1000
			PAGE = 0
		CASE 1001
			PAGE -= 1
		CASE 1002
			PAGE += 1
		CASE 1003
			PAGE = MAXPAGE - 1
		CASE 1004
			OPERATE_MODE = !OPERATE_MODE
		CASE 1 TO 500
			VARSET RESULTS
			TARGET = CHOOSE_COMMAND
			IF CFLAG:CHOOSE_COMMAND:口上セレクタ
				TRYCALLFORM M_KOJO_K{CHOOSE_COMMAND}_{CFLAG:CHOOSE_COMMAND:口上セレクタ}
			ELSE
				TRYCALLFORM M_KOJO_K{CHOOSE_COMMAND}
			ENDIF
			PRINTFORML 正在确认%NAME_OUTPUT_TRANSLATION("CALLNAME", CHOOSE_COMMAND)%%RESULTS:1%的口上是否存在UPDATE
			TRYCALLFORM M_KOJO%RESULTS%_UPDATE_K{CHOOSE_COMMAND}
			WAIT
			PRINTL
		CASE 1005
			CALL UPDATE_OF_KOJO_ORIGIN
		CASE 1100
			CLEARLINE LINECOUNT - LINE_RECORD
			BREAK
		CASE 2000 TO 5000
			C_ID = (CHOOSE_COMMAND - 2000) / 10
			CFLAG:C_ID:口上セレクタ = (CHOOSE_COMMAND - 2000) % 10
	ENDSELECT
	CLEARLINE LINECOUNT - LINE_RECORD
	CONTINUE
WEND

REDRAW REDRAW_RECORD

@PRINT_TOUCH_INFO
CALL INFO_CHARA_TOUCH(MASTER)
SIF TARGET > 0
	CALL INFO_CHARA_TOUCH(TARGET)
SIF ASSI > 0
	CALL INFO_CHARA_TOUCH(ASSI)
WAIT

@INFO_CHARA_TOUCH(C_ID)
#DIM        C_ID
#DIM        BODY_PARTS
#DIMS CONST PARTS_NAME = "阴茎", "阴蒂", "指", "口", "胸", "膣", "肛门", "足"
#DIMS CONST POSITION_NAME = "正常位", "后背位", "骑乘位" , "对面位", "背面位", "小人飞机杯"
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的接触：
FOR BODY_PARTS, 0, 8
	SIF PARTS_NAME:BODY_PARTS == "阴茎" && !GETBIT(TALENT:C_ID:性别, 1)
		CONTINUE
	PRINTFORM %PARTS_NAME:BODY_PARTS, 4, LEFT%　－　
	IF TEQUIP:C_ID:(101 + BODY_PARTS) == 0
		PRINTFORM 无
	ELSE 
		PRINTFORM %PARTS_NAME:(TEQUIP:C_ID:(101 + BODY_PARTS) - 1), 4, LEFT%
	ENDIF
	PRINTL
NEXT
IF CFLAG:C_ID:诶嘿嘿 > 0
	PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的体位：
	IF TEQUIP:C_ID:体位 > 0
		PRINTFORM %POSITION_NAME:(TEQUIP:C_ID:体位 - 1)%
	ELSE
		PRINTFORM 无
	ENDIF
	PRINTL
ENDIF

@TSP_GRADE_MODIFICATION
#FUNCTION
SELECTCASE MAXBASE:MASTER:TSP
	CASE 0 TO 199
		RETURNF 1
	CASE 200 TO 399
		RETURNF 2
	CASE 400 TO 799
		RETURNF 3
	CASE 800 TO 1599
		RETURNF 4
	CASE 1600 TO 3199
		RETURNF 5
	CASE 3200 TO 6665
		RETURNF 6
	CASE IS >= 6666
		RETURNF 7
	CASEELSE
		THROW ⚠️WARNING⚠️ ILLEGAL TSP VALUE
ENDSELECT
RETURNF 0

@Xray_modding_setting
WHILE 1
    LOCAL = LINECOUNT
    DRAWLINE
    FONTSTYLE 1
    PRINTL 透视图优化补丁设定界面
    FONTREGULAR
    DRAWLINE
    PRINTFORML [·]全局设定
    HTML_PRINT @"<button value='1'>　\@GETBIT(Xray_display_mode, 1)? %UNICODE(0x2611)% # %UNICODE(0x2610)% \@日常时不显示透视图</button>"
    HTML_PRINT @"<\@ GETBIT(Xray_display_mode, 1) ? button value='2' # nonbutton \@>\@ GETBIT(Xray_display_mode, 1) ? # <font color='#0x696969'> \@　　\@GETBIT(Xray_display_mode, 2)? %UNICODE(0x2611)% # %UNICODE(0x2610)% \@日常时若阴道/肛内润滑则显示透视图\@ GETBIT(Xray_display_mode, 1) ? # </font> \@<\@ GETBIT(Xray_display_mode, 1) ? /button # /nonbutton \@>"
    HTML_PRINT @"<\@ GETBIT(Xray_display_mode, 1) ? button value='3' # nonbutton \@>\@ GETBIT(Xray_display_mode, 1) ? # <font color='#0x696969'> \@　　\@GETBIT(Xray_display_mode, 3)? %UNICODE(0x2611)% # %UNICODE(0x2610)% \@日常时若阴道/肛内存在精液则显示透视图\@ GETBIT(Xray_display_mode, 1) ? # </font> \@<\@ GETBIT(Xray_display_mode, 1) ? /button # /nonbutton \@>"
	HTML_PRINT @"<button value='0'>　[0]退出设定</button>"
    INPUT
    SELECTCASE RESULT
        CASE 0
            CLEARLINE LINECOUNT - LOCAL
            BREAK
        CASE 1
            INVERTBIT Xray_display_mode, 1
			IF !GETBIT(Xray_display_mode, 1)
				CLEARBIT Xray_display_mode, 2
				CLEARBIT Xray_display_mode, 3
			ENDIF
        CASE 2
            INVERTBIT Xray_display_mode, 2
        CASE 3
            INVERTBIT Xray_display_mode, 3
    ENDSELECT
    DRAWLINE
    CLEARLINE LINECOUNT - LOCAL
WEND

@LOCKPICK(LOCK_DIFFICULTY)
#DIM DYNAMIC LOCK_DIFFICULTY
#DIM DYNAMIC MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE
#DIM DYNAMIC HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE
#DIM DYNAMIC HALF_OF_SURROUNDING_RANGE, 5
#DIM DYNAMIC ANGLE_OF_LOCKPICK
#DIM DYNAMIC SAVE_ANGLE_OF_LOCKPICK
#DIM DYNAMIC UNLOCK_PROGRESSION
#DIM DYNAMIC ANIMATED_UNLOCK_PROGRSSION
#DIM DYNAMIC LINE_COUNT_FOR_LOCKPICK
#DIM PREV_REDRAW
#DIM DYNAMIC UNLOCK_BLOCKED
SIF LOCKPICK_SYSTEM <= 0
	RETURN 0
SIF UNLOCK_BLOCKED == 1
	ANGLE_OF_LOCKPICK = 90
PREV_REDRAW = CURRENTREDRAW()
REDRAW 0
LINE_COUNT_FOR_LOCKPICK = LINECOUNT
CALL LOCK_DIFFICULTY_SET(LOCK_DIFFICULTY, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE, HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE, HALF_OF_SURROUNDING_RANGE)
DO
	SAVE_ANGLE_OF_LOCKPICK = ANGLE_OF_LOCKPICK
	SIF ANIMATED_UNLOCK_PROGRSSION > UNLOCK_PROGRESSION
		ANIMATED_UNLOCK_PROGRSSION = UNLOCK_PROGRESSION
	CUSTOMDRAWLINE ━
	HTML_PRINT @"<p align='center'>开锁难度： <font color='#%LOCK_DIFFICULTY_COLOR(LOCK_DIFFICULTY)%'>%LOCK_DIFFICULTY_GRADE(LOCK_DIFFICULTY)%</font></p>"
	CUSTOMDRAWLINE ━

	HTML_PRINT @"<p align='center'><nonbutton title='调整开锁器角度尝试开锁'>开锁器角度：　　{ANGLE_OF_LOCKPICK}°</nonbutton></p>"
	PRINTL
	IF !UNLOCK_BLOCKED
		IF ANIMATED_UNLOCK_PROGRSSION < UNLOCK_PROGRESSION
			ANIMATED_UNLOCK_PROGRSSION ++
			HTML_PRINT @"<p align='center'>%HTML_COLORBAR(ANIMATED_UNLOCK_PROGRSSION, 42, 42, UNICODE(0x2588), UNICODE(0x2588), BARCOLORSET("青绿"), 0x202020)%</p>"
		ELSE
			HTML_PRINT @"<p align='center'>%HTML_COLORBAR(UNLOCK_PROGRESSION, 42, 42, UNICODE(0x2588), UNICODE(0x2588), BARCOLORSET("青绿"), 0x202020)%</p>"
		ENDIF
	ELSE
		HTML_PRINT @"<p align='center'>%HTML_COLORBAR(UNLOCK_PROGRESSION, 42, 42, UNICODE(0x2588), UNICODE(0x2588), 0xFF0000, 0x202020)%</p>"
	ENDIF
	PRINTL
	HTML_PRINT @"<p align='center'><\@ LOCKPICK_SYSTEM == 2 ? nonbutton # button \@ title='尝试开锁' \@ LOCKPICK_SYSTEM == 2 ? # value='10'\@>［转动锁\@ LOCKPICK_SYSTEM == 2 ? (A) #\@］</\@ LOCKPICK_SYSTEM == 2 ? nonbutton # button \@></p>"
	IF LOCKPICK_SYSTEM == 1
		{
		HTML_PRINT @"<br><p align='center'><nonbutton title='调整开锁器角度'>转动开锁器：</nonbutton>
		<button title='角度减小12°' value='0'>　%"[----]", 6%　</button>
		<button title='角度减小4°' value='1'>　%"[---]", 6%　</button>
		<button title='角度减小2°' value='2'>　%"[--]", 6%　</button>
		<button title='角度减小1°' value='3'>　%"[-]", 6%　</button>
		<button title='角度调整回90°' value='8'>　%"[重置角度]", 6%　</button>
		<button title='角度增大1°' value='4'>　%"[+]", 6%　</button>
		<button title='角度增大2°' value='5'>　%"[++]", 6%　</button>
		<button title='角度增大4°' value='6'>　%"[+++]", 6%　</button>
		<button title='角度增大12°' value='7'>　%"[++++]", 6%　</button></p>
		<nonbutton title='调整开锁器角度'>　　　　　　</nonbutton>"
		}
	ENDIF
	HTML_PRINT @"\@ LOCKPICK_SYSTEM == 2 ? # <br>\@<p align='center'><\@ LOCKPICK_SYSTEM == 2 ? nonbutton # button \@ title='停止开锁' \@ LOCKPICK_SYSTEM == 2 ? # value='9'\@>　[EXIT\@ LOCKPICK_SYSTEM == 2 ? (Esc) #\@]　</\@ LOCKPICK_SYSTEM == 2 ? nonbutton # button \@></p>"
	CUSTOMDRAWLINE ━
	CUSTOMDRAWLINE ━
	IF UNLOCK_BLOCKED == 1
		TWAIT 100, 0
		UNLOCK_BLOCKED = 0
		CLEARLINE LINECOUNT - LINE_COUNT_FOR_LOCKPICK
		CONTINUE
	ELSEIF ANIMATED_UNLOCK_PROGRSSION < UNLOCK_PROGRESSION
		INPUTMOUSEKEY 20
		SIF RESULT:0 == 1 && (RESULT:1 == 0x100000 || RESULT:1 == 0x200000)
			ANIMATED_UNLOCK_PROGRSSION = UNLOCK_PROGRESSION
		CLEARLINE LINECOUNT - LINE_COUNT_FOR_LOCKPICK
		CONTINUE
	ENDIF
	IF LOCKPICK_SYSTEM == 1
		INPUT
		SELECTCASE RESULT
			CASE 10
				IF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE)
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 42)
				ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:0, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:0)
					SIF UNLOCK_PROGRESSION + 6 > 36
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 36)
				ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:1, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:1)
					SIF UNLOCK_PROGRESSION + 6 > 30
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 30)
				ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:2, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:2)
					SIF UNLOCK_PROGRESSION + 6 > 24
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 24)
				ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:3, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:3)
					SIF UNLOCK_PROGRESSION + 6 > 18
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 18)
				ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:4, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:4)
					SIF UNLOCK_PROGRESSION + 6 > 12
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 12)
				ELSE
					SIF UNLOCK_PROGRESSION + 6 > 6
						UNLOCK_BLOCKED = 1
					UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 6)
				ENDIF
				SIF ANIMATED_UNLOCK_PROGRSSION == UNLOCK_PROGRESSION && UNLOCK_PROGRESSION == 42
					BREAK
			CASE 0
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK - 12, 0, 180)
			CASE 1
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK - 4, 0, 180)
			CASE 2
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK - 2, 0, 180)
			CASE 3
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK - 1, 0, 180)
			CASE 4
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK + 1, 0, 180)
			CASE 5
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK + 2, 0, 180)
			CASE 6
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK + 4, 0, 180)
			CASE 7
				ANGLE_OF_LOCKPICK = LIMIT(ANGLE_OF_LOCKPICK + 12, 0, 180)
			CASE 8
				ANGLE_OF_LOCKPICK = 90
			CASE 9
				BREAK
		ENDSELECT
	ELSEIF LOCKPICK_SYSTEM == 2
		INPUTMOUSEKEY 20
		SELECTCASE RESULT
			CASE 3
				IF RESULT:1 == 65
					IF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE)
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 42)
					ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:0, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:0)
						SIF UNLOCK_PROGRESSION + 6 > 36
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 36)
					ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:1, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:1)
						SIF UNLOCK_PROGRESSION + 6 > 30
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 30)
					ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:2, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:2)
						SIF UNLOCK_PROGRESSION + 6 > 24
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 24)
					ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:3, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:3)
						SIF UNLOCK_PROGRESSION + 6 > 18
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 18)
					ELSEIF INRANGE(ANGLE_OF_LOCKPICK, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - HALF_OF_SURROUNDING_RANGE:4, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE - 1 + HALF_OF_SURROUNDING_RANGE:4)
						SIF UNLOCK_PROGRESSION + 6 > 12
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 12)
					ELSE
						SIF UNLOCK_PROGRESSION + 6 > 6
							UNLOCK_BLOCKED = 1
						UNLOCK_PROGRESSION = MIN(UNLOCK_PROGRESSION + 6, 6)
					ENDIF
					SIF ANIMATED_UNLOCK_PROGRSSION == UNLOCK_PROGRESSION && UNLOCK_PROGRESSION == 42
						BREAK
				ELSEIF RESULT:1 == 27
					BREAK
				ENDIF
		ENDSELECT
		IF MOUSEX() <= 0
			ANGLE_OF_LOCKPICK = 0
		ELSEIF MOUSEX() > 0
			ANGLE_OF_LOCKPICK = MOUSEX() * 181 / GETCONFIG("ウィンドウ幅")
			SIF ANGLE_OF_LOCKPICK > 180
				ANGLE_OF_LOCKPICK = 180
		ENDIF
	ENDIF
	SIF SAVE_ANGLE_OF_LOCKPICK != ANGLE_OF_LOCKPICK
		UNLOCK_PROGRESSION = 0
	CLEARLINE LINECOUNT - LINE_COUNT_FOR_LOCKPICK
LOOP 1
REDRAW PREV_REDRAW
CLEARLINE LINECOUNT - LINE_COUNT_FOR_LOCKPICK
SIF ANIMATED_UNLOCK_PROGRSSION == UNLOCK_PROGRESSION && UNLOCK_PROGRESSION == 42
	RETURN 1
RETURN 0


@LOCK_DIFFICULTY_SET(LOCK_DIFFICULTY, MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE, HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE, HALF_OF_SURROUNDING_RANGE)
#DIM REF LOCK_DIFFICULTY
#DIM REF MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE
#DIM REF HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE
#DIM REF HALF_OF_SURROUNDING_RANGE

SELECTCASE LOCK_DIFFICULTY
	;easy
	CASE 0
		HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE = 5
		HALF_OF_SURROUNDING_RANGE:0 = 6
		HALF_OF_SURROUNDING_RANGE:1 = 7
		HALF_OF_SURROUNDING_RANGE:2 = 8
		HALF_OF_SURROUNDING_RANGE:3 = 10
		HALF_OF_SURROUNDING_RANGE:4 = 12
	;normal
	CASE 1
		HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE = 4
		HALF_OF_SURROUNDING_RANGE:0 = 5
		HALF_OF_SURROUNDING_RANGE:1 = 6
		HALF_OF_SURROUNDING_RANGE:2 = 7
		HALF_OF_SURROUNDING_RANGE:3 = 8
		HALF_OF_SURROUNDING_RANGE:4 = 9
	;hard
	CASE 2
		HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE = 3
		HALF_OF_SURROUNDING_RANGE:0 = 4
		HALF_OF_SURROUNDING_RANGE:1 = 5
		HALF_OF_SURROUNDING_RANGE:2 = 5
		HALF_OF_SURROUNDING_RANGE:3 = 6
		HALF_OF_SURROUNDING_RANGE:4 = 6
	;extremely hard
	CASE 3
		HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE = 2
		HALF_OF_SURROUNDING_RANGE:0 = 3
		HALF_OF_SURROUNDING_RANGE:1 = 3
		HALF_OF_SURROUNDING_RANGE:2 = 4
		HALF_OF_SURROUNDING_RANGE:3 = 4
		HALF_OF_SURROUNDING_RANGE:4 = 4
ENDSELECT

MEDIAN_OF_RIGHT_UNLOCK_ANGLE_RANGE = RAND(HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE, 181 - HALF_OF_RIGHT_UNLOCK_ANGLE_RANGE)

@LOCK_DIFFICULTY_GRADE(LOCK_DIFFICULTY)
#FUNCTIONS
#DIM REF LOCK_DIFFICULTY

SELECTCASE LOCK_DIFFICULTY
	;easy
	CASE 0
		RETURNF "简单"
	;normal
	CASE 1
		RETURNF "中等"
	;hard
	CASE 2
		RETURNF "困难"
	;extremely hard 
	CASE 3
		RETURNF "非常困难"
ENDSELECT

@LOCK_DIFFICULTY_COLOR(LOCK_DIFFICULTY)
#FUNCTIONS
#DIM REF LOCK_DIFFICULTY

SELECTCASE LOCK_DIFFICULTY
	;easy
	CASE 0
		RETURNF "0x00FF00"
	;normal
	CASE 1
		RETURNF "0x0078B4"
	;hard
	CASE 2
		RETURNF "0xCC99FF"
	;extremely hard 
	CASE 3
		RETURNF "0xFF0000"
ENDSELECT

@TREASURE_CHEST
#DIM DYNAMIC CHEST_GRADE = -1
#DIM DYNAMIC CHEST_RANDOM_VALUE
VARSET CHEST_LOOT
VARSET AMOUNT_OF_CHEST_LOOT
SELECTCASE TALENT:MASTER:采集Lv
	CASE 1
		CHEST_GRADE = 0
	CASEELSE
		CHEST_RANDOM_VALUE = RAND:100
		IF CHEST_RANDOM_VALUE < 2 * (TALENT:MASTER:采集Lv - 1)
			CHEST_GRADE = 3
		ELSEIF CHEST_RANDOM_VALUE < 4 * (TALENT:MASTER:采集Lv - 1)
			CHEST_GRADE = 2
		ELSEIF CHEST_RANDOM_VALUE < 6 * (TALENT:MASTER:采集Lv - 1)
			CHEST_GRADE = 1
		ELSE
			CHEST_GRADE = 0
		ENDIF
ENDSELECT

SIF CHEST_GRADE == -1
	RETURN 0

VARSET RESULT
HTML_PRINT @"发现了一个<font color='#%LOCK_DIFFICULTY_COLOR(CHEST_GRADE)%'>上锁的宝箱</font>！<br>要尝试打开吗？"
CALL ASK_YN

SIF RESULT == 1
	RETURN 0

VARSET RESULT
CALL LOCKPICK(CHEST_GRADE)
IF RESULT == 1
	PRINTFORMW 开锁成功
	SELECTCASE CHEST_GRADE
		CASE 0
			DO
				LOCAL:0 = RAND(600, 711)
				SIF ITEMNAME:(LOCAL:0) == ""
					CONTINUE
				SIF ITEMPRICE:(LOCAL:0) >= 500
					CONTINUE
				SIF ITEM:(LOCAL:0) >= 999
					CONTINUE
				IF CHEST_LOOT:0 == 0
					CHEST_LOOT:0 = LOCAL:0
					SIF RAND:2
						CONTINUE
				ENDIF
				SIF CHEST_LOOT:1 == 0
					CHEST_LOOT:1 = LOCAL:0
				SIF CHEST_LOOT:0 > 0 || CHEST_LOOT:1 > 0
					BREAK
			LOOP 1
		CASE 1
			DO
				IF RAND:2
					LOCAL:0 = RAND(600, 711)
				ELSE
					LOCAL:0 = RAND(151, 384)
				ENDIF
				SIF ITEMNAME:(LOCAL:0) == ""
					CONTINUE
				SIF ITEMPRICE:(LOCAL:0) >= 5000
					CONTINUE
				SIF ITEM:(LOCAL:0) >= 999
					CONTINUE
				IF CHEST_LOOT:0 == 0
					CHEST_LOOT:0 = LOCAL:0
					SIF RAND:2
						CONTINUE
				ENDIF
				SIF CHEST_LOOT:1 == 0
					CHEST_LOOT:1 = LOCAL:0
				SIF CHEST_LOOT:0 > 0 || CHEST_LOOT:1 > 0
					BREAK
			LOOP 1
		CASE 2
			DO
				IF RAND:2
					LOCAL:0 = RAND(600, 711)
				ELSE
					LOCAL:0 = RAND(151, 384)
				ENDIF
				SIF ITEMNAME:(LOCAL:0) == ""
					CONTINUE
				SIF INRANGE(LOCAL:0, 600, 706) && INRANGE(ITEMPRICE:(LOCAL:0), 5000, 50000)
					CONTINUE
				SIF ITEM:(LOCAL:0) >= 999
					CONTINUE
				IF CHEST_LOOT:0 == 0
					CHEST_LOOT:0 = LOCAL:0
					SIF RAND:4
						CONTINUE
				ENDIF
				SIF CHEST_LOOT:1 == 0
					CHEST_LOOT:1 = LOCAL:0
				SIF CHEST_LOOT:0 > 0 || CHEST_LOOT:1 > 0
					BREAK
			LOOP 1
		CASE 3
			DO
				IF ITEM:避水玉 == 0 && !RAND:10
					LOCAL:0 = 404
				ELSEIF !RAND:10
					LOCAL:0 = 531
				ELSEIF RAND:2
					LOCAL:0 = RAND(502, 519)
				ELSE
					IF RAND:2
						LOCAL:0 = RAND(560, 587)
					ELSE
						LOCAL:0 = RAND(850, 858)
					ENDIF
				ENDIF
				SIF ITEMNAME:(LOCAL:0) == ""
					CONTINUE
				SIF ITEM:(LOCAL:0) >= 999
					CONTINUE
				SIF CHEST_LOOT:0 == 404
					CONTINUE
				IF CHEST_LOOT:0 == 0
					CHEST_LOOT:0 = LOCAL:0
					CONTINUE
				ENDIF
				SIF CHEST_LOOT:1 == 0
					CHEST_LOOT:1 = LOCAL:0
				SIF CHEST_LOOT:0 > 0 || CHEST_LOOT:1 > 0
					BREAK
			LOOP 1
	ENDSELECT
ELSE
	RETURN 0
ENDIF
IF CHEST_LOOT:0 > 0
	LOCALS = %ITEMNAME:(CHEST_LOOT:0)%
	IF CHEST_LOOT:0 != 404
		AMOUNT_OF_CHEST_LOOT:0 = RAND(4 - CHEST_GRADE, 8 - CHEST_GRADE)
	ELSE
		AMOUNT_OF_CHEST_LOOT:0 = 1
	ENDIF
	CALL GET_ITEM(LOCALS, AMOUNT_OF_CHEST_LOOT:0, 2)
ENDIF
IF CHEST_LOOT:1 > 0
	LOCALS = %ITEMNAME:(CHEST_LOOT:1)%
	IF CHEST_LOOT:1 != 404
		AMOUNT_OF_CHEST_LOOT:1 = RAND(4 - CHEST_GRADE, 8 - CHEST_GRADE)
	ELSE
		AMOUNT_OF_CHEST_LOOT:1 = 1
	ENDIF
	CALL GET_ITEM(LOCALS, AMOUNT_OF_CHEST_LOOT:1, 2)
ENDIF
RETURN 1

@NAME_OUTPUT_TRANSLATION(ARGS, ARG)
#FUNCTIONS
SELECTCASE ARGS
	CASE "NAME"
		SELECTCASE ARG
			CASE MASTER TO [[小兔姫]]
				RETURNF @"%NAME:ARG%"
			CASE [[伊丽丝]]
				RETURNF @"伊莉丝"
			CASE [[萨里爱尔]]
				RETURNF @"萨丽艾尔"
			CASE [[萨拉]]
				RETURNF @"萨拉"
			CASE [[奥莲姬]]
				RETURNF @"奥莲姬"
			CASE [[矜羯罗]]
				RETURNF @"矜羯罗"	
			CASE [[魔眼]]
				RETURNF @"幽玄魔眼"
			CASE [[菊理]]
				RETURNF @"菊理"
			CASE [[美天]]
				RETURNF @"孙美天"
			CASE [[慧之子]]
				RETURNF @"三头·慧之子"
			CASE [[血枪]]
				RETURNF @"天火人·血枪"
			CASE [[日狭美]]
				RETURNF @"豫母都·日狭美"
			CASE [[残无]]
				RETURNF @"日白·残无"
			CASE [[瑞灵]]
				RETURNF @"宫出口·瑞灵"
			CASE IS <= [[路人子]]
				RETURNF @"%NAME:ARG%"
			CASEELSE
				RETURNF "NULL"
		ENDSELECT
	CASE "CALLNAME"
		SELECTCASE ARG
			CASE MASTER TO [[小兔姫]]
				RETURNF @"%CALLNAME:ARG%"
			CASE [[伊丽丝]]
				RETURNF @"伊莉丝"
			CASE [[萨里爱尔]]
				RETURNF @"萨丽艾尔"
			CASE [[萨拉]]
				RETURNF @"萨拉"
			CASE [[奥莲姬]]
				RETURNF @"奥莲姬"
			CASE [[矜羯罗]]
				RETURNF @"矜羯罗"	
			CASE [[魔眼]]
				RETURNF @"幽玄魔眼"
			CASE [[菊理]]
				RETURNF @"菊理"
			CASE [[美天]]
				RETURNF @"美天"
			CASE [[慧之子]]
				RETURNF @"慧之子"
			CASE [[血枪]]
				RETURNF @"血枪"
			CASE [[日狭美]]
				RETURNF @"日狭美"
			CASE [[残无]]
				RETURNF @"残无"
			CASE [[瑞灵]]
				RETURNF @"瑞灵"
			CASE IS <= [[路人子]]
				RETURNF @"%CALLNAME:ARG%"
			CASEELSE
				RETURNF "NULL"
		ENDSELECT
	CASEELSE
		RETURNF "NULL"
ENDSELECT

@SELECT_CHILD_INTERACTABLE_HERE
#DIM DYNAMIC _LINECOUNT
#DIM CHILDNUM
#DIM DYNAMIC _COUNT = 1
_LINECOUNT = LINECOUNT
PRINTFORML 想找哪个孩子呢？
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:儿童人数 <= 0
		CONTINUE
	FOR LOCAL:1, 1, MAXCHILD + 1
		IF CHILD_INTERACTABLE_HERE:LOCAL:(LOCAL:1) > 0
			IF CHILD_INTERACTABLE_HERE:LOCAL:(LOCAL:1) < 1000 
				IF STRLENS(CHILD_ACT:(CHILD_INTERACTABLE_HERE:LOCAL:(LOCAL:1)):(LOCAL:1)) > 0
					SETCOLOR C_GREEN
					PRINTBUTTON @"[{_COUNT, 2}]-%CHILDNAME:LOCAL:(LOCAL:1), 8%　\@ CHILD_SEX:LOCAL:(LOCAL:1) == 1 ? ♀ # ♂\@　　未成年　　母亲：%NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%", CHILD_INTERACTABLE_HERE:LOCAL:(LOCAL:1)
				ELSE
					SETCOLOR 0x669966
					PRINTPLAINFORM [{_COUNT, 2}]-%CHILDNAME:LOCAL:(LOCAL:1), 8%　\@ CHILD_SEX:LOCAL:(LOCAL:1) == 1 ? ♀ # ♂\@　　未成年　　母亲：%NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%
				ENDIF
			ELSE
				PRINTBUTTON @"[{_COUNT, 2}]-%CHILDNAME:LOCAL:(LOCAL:1), 8%　\@ CHILD_SEX:LOCAL:(LOCAL:1) == 1 ? ♀ # ♂\@　　成年　　母亲：%NAME_OUTPUT_TRANSLATION("NAME", LOCAL)%", CHILD_INTERACTABLE_HERE:LOCAL:(LOCAL:1)
			ENDIF
			RESETCOLOR
			PRINTL
			_COUNT ++
		ELSE
			CONTINUE
		ENDIF
	NEXT
NEXT
PRINTBUTTON @"[0]还是算了", -1
INPUT
CLEARLINE LINECOUNT - _LINECOUNT
SIF RESULT == -1
	PRINTFORML 还是先不打扰孩子们了
RETURN RESULT
[SKIPSTART]
@照片拍摄(ARG, 拍摄察觉)
#FUNCTIONS
#DIMS DYNAMIC 时间描述
#DIMS DYNAMIC 天气描述
#DIMS DYNAMIC 地点描述
#DIMS DYNAMIC 服装描述
#DIMS DYNAMIC 行动描述
#DIMS DYNAMIC 人物描述
#DIMS DYNAMIC 完整胶片
#DIM _COUNT
#DIM DYNAMIC 拍摄察觉
IF CFLAG:ARG:诶嘿嘿 == 0
	;时间
	SELECTCASE TIME:2
		CASE 1
			时间描述 = %TEXTR("破晓/拂晓/凌晨/黎明/薄晓")%
		CASE 2
			时间描述 = %TEXTR("早朝/早晨/上午/日间")%
		CASE 3
			时间描述 = %TEXTR("中午/午间/白昼/昼")%
		CASE 4
			时间描述 = %TEXTR("午后/下午/下昼/下晌")%
		CASE 5
			时间描述 = %TEXTR("落日/暮色/暮间/傍晚/薄暮")%
		CASE 6
			时间描述 = %TEXTR("夜晚/夜幕/晚间/夜色")%
		CASE 7
			时间描述 = %TEXTR("午夜/深夜/半夜/夜阑")%
	ENDSELECT
	时间描述 += 时
	;天气，天界和地底不存在天气
	IF OUTROOF(CFLAG:ARG:当前位置) && ((MAIN_MAP != 8 && CFLAG:ARG:当前位置 != 天界) || (MAIN_MAP == 8 && MAP_SEARCH_REPLACEMENT_8(CFLAG:ARG:当前位置) != 天界))
		IF INRANGE(FLAG:异常气象, 0, 8)
			SELECTCASE TIME:5
				CASE 0, 1, 2, 19
					天气描述 = %TEXTR("晴日/晴空/晴天")%
				CASE 3
					天气描述 = %TEXTR("阴天/多云/云海/稠云")%
				CASE 4, 20
					天气描述 = %TEXTR("雨天/细雨/轻雨/小雨")%
				CASE 5
					天气描述 = %TEXTR("大雨/落雨")%
				CASE 6
					天气描述 = %TEXTR("雾雨/薄雨")%
				CASE 7, 18
					天气描述 = %TEXTR("青雾/雾/薄雾/雾色朦胧")%
				CASE 8
					天气描述 = %TEXTR("雪/落雪/飘雪/飞雪")%
				CASE 9
					天气描述 = %TEXTR("大雪/纷飞大学/鹅毛大雪/漫天飞雪")%
				CASE 10
					天气描述 = %TEXTR("冰雾/细雪")%
				CASE 11
					天气描述 = 雾雪
				CASE 12
					天气描述 = %TEXTR("雨夹雪/交加雪雨")%
				CASE 13
					天气描述 = %TEXTR("下落冰雹/下落冰粒")%
				CASE 14
					天气描述 = %TEXTR("暴雨/倾盆大雨/滂沱大雨")%
				CASE 15
					天气描述 = 落下大块冰雹
				CASE 16
					天气描述 = 交加风雨
				CASE 17
					天气描述 = 晴岚
			ENDSELECT
		ELSE
			SELECTCASE FLAG:异常气象
				CASE 9
					天气描述 = %TEXTR("台风/狂风暴雨/疾风骤雨/狂风骤雨/暴风骤雨")%
				CASE 10
					天气描述 = %TEXTR("猛吹雪/暴雪/雪虐风饕")%
				CASE 11
					天气描述 = 钻石尘
				CASE 12
					天气描述 = %TEXTR("沙尘暴/漫天黄沙/肆虐黄风")%
				CASE 13
					天气描述 = %TEXTR("烈日/毒日")%
				CASE 14
					天气描述 = 极光
			ENDSELECT
		ENDIF
	ENDIF

	;地底没有明确的时间观念
	SIF GET_MAPID(CFLAG:ARG:当前位置) == 9
		VARSET 时间描述

	IF GET_MAPID(CFLAG:ARG:当前位置) == MAIN_MAP
		地点描述 = %STR:(8000 + CFLAG:ARG:当前位置)%
	ELSE
		地点描述 = %STR:(6000 + CFLAG:ARG:当前位置 / 10)%
	ENDIF

	SIF CURRENT_CLOTHES_SET(ARG) != CSVCFLAG(NO:ARG, GETNUM(CFLAG, "基本服装SET")) && (INRANGE(CURRENT_CLOTHES_SET(ARG), 1, 30) || INRANGE(CURRENT_CLOTHES_SET(ARG), 50, 99))
		服装描述 = GET_STR(ARG, "衣装セット", CURRENT_CLOTHES_SET(ARG), "名前")
	;自由行动、工作（正常工作或宴会）或无特殊行动
	IF Activity_Duration:ARG > 0
		IF !FreeAct_Chain(ARG)
			行动描述 = %FreeAct_Name(ARG)%
		ELSE
			FOR _COUNT, 2, GET_TARGETNUM() + 1
				IF RELATION:ARG:(TARGET:_COUNT) > 100 && Activity_Duration:ARG == Activity_Duration:(TARGET:_COUNT)
					行动描述 += @"和%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET:_COUNT)%"
					BREAK
				ENDIF
			NEXT
			行动描述 += FreeAct_Name(ARG)
		ENDIF
	ELSEIF CFLAG:ARG:行动 == 5
		IF INRANGE(CFLAG:ARG:宴会参加, 1, 2) || CFLAG:ARG:职业种类 == JOB_醉いつぶれる
			SELECTCASE CFLAG:ARG:职业种类
				CASE JOB_宴会准备
					行动描述 += @"进行宴会的准备工作"
				CASE JOB_宴会参加
					行动描述 += @"参加%EnkaiTitle%"
				CASE JOB_宴会の片づけ
					行动描述 += @"进行宴会的善后工作"
				CASE JOB_醉いつぶれる
					行动描述 += @"烂醉如泥"
			ENDSELECT
		ELSE
			行动描述 += @"进行%GET_JOBNAME(ARG)%"
		ENDIF
	ELSE
		; 种族 开朗／阴郁 冷漠 喜欢引人注目 心情
		IF TALENT:ARG:思慕

		ELSEIF TALENT:ARG:恋慕

		ELSEIF TALENT:ARG:炮友 || TALENT:ARG:爱欲 || TALENT:ARG:淫乱

		ELSE
			IF (OUTROOF(CFLAG:ARG:当前位置) && ((MAIN_MAP != 8 && CFLAG:ARG:当前位置 != 天界) || (MAIN_MAP == 8 && MAP_SEARCH_REPLACEMENT_8(CFLAG:ARG:当前位置) != 天界))) && !(GROUPMATCH(TIME:5, 0, 1, 2, 3, 7, 17, 18) || INRANGE(FLAG:异常气象, 0, 8))
				IF 
					SELECTCASE 


				IF TALENT:ARG:人类 > 0
					SELECTCASE TALENT:ARG:人类
					ENDSELECT
				ELSEIF TALENT:ARG:妖怪 > 0
					SELECTCASE TALENT:ARG:妖怪
					ENDSELECT
				ELSEIF TALENT:ARG:妖精 > 0
					SELECTCASE TALENT:ARG:妖精
					ENDSELECT
				ELSEIF TALENT:ARG:神灵 > 0
					SELECTCASE TALENT:ARG:神灵
					ENDSELECT
				ELSEIF TALENT:ARG:幽灵 > 0
					SELECTCASE TALENT:ARG:幽灵
					ENDSELECT
				ELSEIF TALENT:ARG:付丧神 > 0
					SELECTCASE TALENT:ARG:付丧神
					ENDSELECT
				ELSEIF TALENT:ARG:人形 > 0
					SELECTCASE TALENT:ARG:人形
					ENDSELECT
				ELSEIF TALENT:ARG:追加种族 > 0
					SELECTCASE TALENT:ARG:追加种族
					ENDSELECT
				ENDIF

			
			
		ENDIF

	ENDIF
	完整胶片 = %时间描述%在%天气描述%下的%地点描述%\@STRLENS(服装描述) > 0 ? ，穿着%服装描述% # \@%行动描述%的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%
ELSE
ENDIF
RETURNF 完整胶片

行动描述 = 

被雨淋湿了
寻找可以挡雨的地方
伸出手去接雨滴

衣服因为湿透而变得半透明
;小孩吗
用力踩下地上的水坑而导致积水四溅
抬头看天而导致雨水滴入眼睛
扯了扯因为湿透而沾在身上的衣服
出神地看着不断落下的雨水
;动物吗
抖动着甩掉身上沾着的雨水

[SKIPEND]