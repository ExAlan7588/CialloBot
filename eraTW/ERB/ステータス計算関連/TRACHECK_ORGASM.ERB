;────────────────────────────────────
;绝顶系の处理など
;────────────────────────────────────
;-------------------------------------------------
;绝顶处理
;-------------------------------------------------
@ORGASM_ADD(ARG)
#DIM 合计绝顶数
#DIM 挑逗中
#DIM MultipleEc
#DIM Multiplier

VARSET LOCAL
挑逗中 = 0
LOCAL = ((CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ) >= PALAMLV:4) + (CUP:ARG:快Ｖ + PALAM:ARG:快Ｖ >= PALAMLV:4) + (CUP:ARG:快Ａ + PALAM:ARG:快Ａ >= PALAMLV:4) + (CUP:ARG:快Ｂ + PALAM:ARG:快Ｂ >= PALAMLV:4) + (CUP:ARG:快Ｍ + PALAM:ARG:快Ｍ >= PALAMLV:4)
SELECTCASE LOCAL
	CASE 5
		LOCAL:1 = 5000
	CASE 4
		LOCAL:1 = 3000
	CASE 3
		LOCAL:1 = 2000
	CASE 2
		LOCAL:1 = 1500
	CASE 1
		LOCAL:1 = 1000
ENDSELECT

;视认性の问题で残しておく
C = 0
V = 0
A = 0
B = 0
M = 0
;绝顶Ｃ
;うっかり绝顶
CALL 绝顶强度判定(ARG,0,LOCAL:1)
C = RESULT
;绝顶Ｖ
CALL 绝顶强度判定(ARG,1,LOCAL:1)
V = RESULT

;绝顶Ａ
CALL 绝顶强度判定(ARG,2,LOCAL:1)
A = RESULT

;绝顶Ｂ
CALL 绝顶强度判定(ARG,3,LOCAL:1)
B = RESULT
	
;绝顶Ｍ
CALL 绝顶强度判定(ARG,4,LOCAL:1)
M = RESULT
IF TEQUIP:ARG:挑逗模式
	IF 挑逗发生(ARG,C + V + A + B + M)
		EX:ARG:Ｃ寸止 += C
		EX:ARG:Ｖ寸止 += V
		EX:ARG:Ａ寸止 += A
		EX:ARG:Ｂ寸止 += B
		EX:ARG:Ｍ寸止 += M
		TCVAR:ARG:挑逗回数 ++
		挑逗中 = 1
	ENDIF
ENDIF
SIF 挑逗中
	GOTO JIRASI
IF CUP:ARG:快Ｃ
	C += EX:ARG:Ｃ寸止
	EX:ARG:Ｃ寸止 = 0
ENDIF
IF CUP:ARG:快Ｖ
	V += EX:ARG:Ｖ寸止
	EX:ARG:Ｖ寸止 = 0
ENDIF
IF CUP:ARG:快Ａ
	A += EX:ARG:Ａ寸止
	EX:ARG:Ａ寸止 = 0
ENDIF
IF CUP:ARG:快Ｂ
	B += EX:ARG:Ｂ寸止
	EX:ARG:Ｂ寸止 = 0
ENDIF
IF CUP:ARG:快Ｍ
	M += EX:ARG:Ｍ寸止
	EX:ARG:Ｍ寸止 = 0
ENDIF
;-------------------------------------------------
;绝顶时の追加处理
;-------------------------------------------------
SIF FLAG:时间停止
	EX:ARG:时间停止TotalEX_TRACKER = EX:ARG:时间停止TotalEX_TRACKER + C + V + A + B + M 
NOWEX:ARG:TotalEX = C + V + A + B + M 
MultipleEc = MIN(C, 1) + MIN(V, 1) + MIN(A, 1) + MIN(B, 1) + MIN(M, 1) - 1
POWER Multiplier, 2, MultipleEc
;PRINTFORML {C + V + A + B + M} ec {MultipleEc} {Multiplier}
IF C && V && A && B && M
	NOWEX:ARG:五重绝顶 = 1
ELSEIF C && V && A && B
	NOWEX:ARG:四重绝顶 = 1
ELSEIF C && V && A && M
	NOWEX:ARG:四重绝顶 = 2
ELSEIF C && V && B && M
	NOWEX:ARG:四重绝顶 = 3
ELSEIF C && A && B && M
	NOWEX:ARG:四重绝顶 = 4
ELSEIF V && A && B && M
	NOWEX:ARG:四重绝顶 = 5
ELSEIF C && V && A
	NOWEX:ARG:三重绝顶 = 1
ELSEIF B && V && A
	NOWEX:ARG:三重绝顶 = 2
ELSEIF C && B && A
	NOWEX:ARG:三重绝顶 = 3
ELSEIF C && V && B
	NOWEX:ARG:三重绝顶 = 4
ELSEIF C && V && M
	NOWEX:ARG:三重绝顶 = 5
ELSEIF B && V && M
	NOWEX:ARG:三重绝顶 = 6
ELSEIF A && V && M
	NOWEX:ARG:三重绝顶 = 7
ELSEIF B && C && M
	NOWEX:ARG:三重绝顶 = 8
ELSEIF A && B && M
	NOWEX:ARG:三重绝顶 = 9
ELSEIF C && A && M
	NOWEX:ARG:三重绝顶 = 10
ELSEIF C && V
	NOWEX:ARG:二重绝顶 = 1
ELSEIF C && A
	NOWEX:ARG:二重绝顶 = 2
ELSEIF V && A
	NOWEX:ARG:二重绝顶 = 3
ELSEIF C && B
	NOWEX:ARG:二重绝顶 = 4
ELSEIF V && B
	NOWEX:ARG:二重绝顶 = 5
ELSEIF A && B
	NOWEX:ARG:二重绝顶 = 6
ELSEIF A && M
	NOWEX:ARG:二重绝顶 = 7
ELSEIF V && M
	NOWEX:ARG:二重绝顶 = 8
ELSEIF C && M
	NOWEX:ARG:二重绝顶 = 9
ELSEIF B && M
	NOWEX:ARG:二重绝顶 = 10
ENDIF
C *= Multiplier
V *= Multiplier
A *= Multiplier
B *= Multiplier
M *= Multiplier

IF (C + V + A + B + M) > 0
	TCVAR:ARG:挑逗回数 = 0
	TEQUIP:ARG:挑逗模式 = 0
ENDIF
合计绝顶数 = C + V + A + B + M
;时奸刻印
IF FLAG:70 && ARG
	IF  TALENT:ARG:路人 == 1
		IF 合计绝顶数 >= 4 && MARK:ARG:时奸刻印 < 3
			TCVAR:ARG:时奸刻印取得用 = 3
		ELSEIF 合计绝顶数 >= 2 && MARK:ARG:时奸刻印 < 2
			TCVAR:ARG:时奸刻印取得用 = 2
		ELSEIF  合计绝顶数 >= 1 && MARK:ARG:时奸刻印 < 1
			TCVAR:ARG:时奸刻印取得用 = 1
		ENDIF
	ELSE
		IF 合计绝顶数 >= 16 && MARK:ARG:时奸刻印 < 3
			TCVAR:ARG:时奸刻印取得用 = 3
		ELSEIF 合计绝顶数 >= 8 && MARK:ARG:时奸刻印 < 2
			TCVAR:ARG:时奸刻印取得用 = 2
		ELSEIF  合计绝顶数 >= 1 && MARK:ARG:时奸刻印 < 1
			TCVAR:ARG:时奸刻印取得用 = 1
		ENDIF
	ENDIF
ELSE
;快乐刻印の处理
;绝顶の部位と强度の合计が一定以上で刻印を取得する
;合计绝顶数が8以上で快乐刻印３に相当
	IF  TALENT:ARG:路人 == 1
		IF 合计绝顶数 >= 4 && MARK:ARG:快乐刻印 <= 3
			TCVAR:ARG:快乐强度 = 3
		;合计绝顶数が5以上で快乐刻印２に相当
		ELSEIF 合计绝顶数 >= 2 && MARK:ARG:快乐刻印 < 2
			TCVAR:ARG:快乐强度 = 2
		;合计绝顶数が2以上で快乐刻印１に相当
		ELSEIF 合计绝顶数 >= 1 && MARK:ARG:快乐刻印 < 1
			TCVAR:ARG:快乐强度 = 1
		ELSE
			TCVAR:ARG:快乐强度 = 0
		ENDIF
	ELSEIF 合计绝顶数 >= 24 && MARK:ARG:快乐刻印 <= 3
		TCVAR:ARG:快乐强度 = 3
	;合计绝顶数が12以上で快乐刻印２に相当
	ELSEIF 合计绝顶数 >= 12 && MARK:ARG:快乐刻印 < 2
		TCVAR:ARG:快乐强度 = 2
	;合计绝顶数が2以上で快乐刻印１に相当
	ELSEIF 合计绝顶数 >= 2 && MARK:ARG:快乐刻印 < 1
		TCVAR:ARG:快乐强度 = 1
	ELSE
		TCVAR:ARG:快乐强度 = 0
	ENDIF
ENDIF
IF FLAG:70 == 1
ELSE
	IF C
		SOURCE:ARG:露出 += 500 * C
		SOURCE:ARG:屈从 += 200 * C * (SYNCED_ORGASM(ARG) ? 120 # 100) / 100
		SIF SOURCE:ARG:魔力印记 == 0
			SOURCE:ARG:反感 += 40 * C * (CFLAG:ARG:诶嘿嘿 ? 1 # 3)
		DOWNBASE:ARG:体力 += 20
		DOWNBASE:ARG:气力 += 10
	ENDIF
	IF V
		SOURCE:ARG:恭顺 += 500 * V * (SYNCED_ORGASM(ARG) ? 120 # 100) / 100
		SOURCE:ARG:露出 += 700 * V
		SOURCE:ARG:屈从 += 400 * V * (SYNCED_ORGASM(ARG) ? 120 # 100) / 100
		IF SOURCE:ARG:魔力印记 == 0
			SOURCE:ARG:反感 += 80 * V * (CFLAG:ARG:诶嘿嘿 ? 1 # 3)
		ENDIF
		SOURCE:ARG:情欲 += 800 * V
		DOWNBASE:ARG:体力 += 30
		DOWNBASE:ARG:气力 += 20
	ENDIF
	IF A
		SOURCE:ARG:恭顺 += 700 * A * (SYNCED_ORGASM(ARG) ? 120 # 100) / 100
		SOURCE:ARG:露出 += 900 * A
		SOURCE:ARG:屈从 += 500 * A * (SYNCED_ORGASM(ARG) ? 120 # 100) / 100
		IF SOURCE:ARG:魔力印记 == 0
			SOURCE:ARG:反感 += 120 * A * (CFLAG:ARG:诶嘿嘿 ? 1 # 3)
		ENDIF
		SOURCE:ARG:情欲 += 500 * A
		DOWNBASE:ARG:体力 += 40
		DOWNBASE:ARG:气力 += 20
	ENDIF
	IF B
		SOURCE:ARG:露出 += 500 * B
		SOURCE:ARG:屈从 += 200 * B * (SYNCED_ORGASM(ARG) ? 120 # 100) / 100
		SIF SOURCE:ARG:魔力印记 == 0
			SOURCE:ARG:反感 += 50 * B * (CFLAG:ARG:诶嘿嘿 ? 1 # 3)
		DOWNBASE:ARG:体力 += 20
		DOWNBASE:ARG:气力 += 10
	ENDIF
	IF M
		SOURCE:ARG:情爱 += 1500 * M * (SYNCED_ORGASM(ARG) ? 120 # 100) / 100
		SOURCE:ARG:恭顺 += 1000 * M * (SYNCED_ORGASM(ARG) ? 120 # 100) / 100
		DOWNBASE:ARG:体力 += 10
		DOWNBASE:ARG:气力 += 5
	ENDIF
	IF MultipleEc == 5
		DOWNBASE:ARG:体力 -= 80
		DOWNBASE:ARG:气力 -= 70
	ELSEIF MultipleEc == 4
		DOWNBASE:ARG:体力 -= 40
		DOWNBASE:ARG:气力 -= 30
	ELSEIF MultipleEc == 3
		DOWNBASE:ARG:体力 -= 20
		DOWNBASE:ARG:气力 -= 10
	ENDIF
ENDIF
;NOWEXにデータを入れる（绝顶时口上に使用）
NOWEX:ARG:Ｃ绝顶 = C
NOWEX:ARG:Ｖ绝顶 = V
NOWEX:ARG:Ａ绝顶 = A
NOWEX:ARG:Ｂ绝顶 = B
NOWEX:ARG:Ｍ绝顶 = M
;绝顶回数を增やす
EX:ARG:Ｃ绝顶 += C
EX:ARG:Ｖ绝顶 += V
EX:ARG:Ａ绝顶 += A
EX:ARG:Ｂ绝顶 += B
EX:ARG:Ｍ绝顶 += M

;时间停止中もしくは睡眠中なら、无自觉绝顶回数を增やす（ただし、你を除く）
IF FLAG:70 > 0 || CFLAG:ARG:睡眠 && ARG
	EX:ARG:无自觉Ｃ绝顶 += C
	EX:ARG:无自觉Ｖ绝顶 += V
	EX:ARG:无自觉Ａ绝顶 += A
	EX:ARG:无自觉Ｂ绝顶 += B
	EX:ARG:无自觉Ｍ绝顶 += M
ENDIF

;绝顶经验を增やす
EXP:ARG:绝顶经验 += 合计绝顶数

IF TALENT:ARG:路人 == 1 && 合计绝顶数
	IF ABL:ARG:Ｃ感觉 < 3 && C
		CALL ChangeABL(ARG, "Ｃ感觉", 1)
	ENDIF
	IF ABL:ARG:Ｂ感觉 < 3 && B
		CALL ChangeABL(ARG, "Ｂ感觉", 1)
	ENDIF
	IF ABL:ARG:Ｖ感觉 < 3 && V
		CALL ChangeABL(ARG, "Ｖ感觉", 1)
	ENDIF
	IF ABL:ARG:Ａ感觉 < 3 && A
		CALL ChangeABL(ARG, "Ａ感觉", 1)
	ENDIF
	IF ABL:ARG:Ｍ感觉 < 3 && M
		CALL ChangeABL(ARG, "Ｍ感觉", 1)
	ENDIF
	IF MAX(GETPALAMLV(PALAM:情欲,5), 合计绝顶数) > ABL:ARG:欲望 + 1
		CALL ChangeABL(ARG, "欲望", 1)
	ENDIF
ENDIF

;体位别绝顶经验
IF TEQUIP:ARG:50 > -1
	LOCAL:3 = TEQUIP:ARG:体位
ELSEIF TEQUIP:ARG:51 > -1
	LOCAL:3 = TEQUIP:ARG:体位 + 5
ELSE
	LOCAL:3 = 0
ENDIF
SIF LOCAL:3
	EXP:ARG:(LOCAL:3 + 100) += 1

;绝顶经验の追加、无仅在有意识时你に加算されない
SIF !SHIRAHU(ARG) && ARG
	EXP:ARG:无自觉绝顶经验 += C + V + A + B + M
EXP:ARG:Ｃ绝顶经验 += C
EXP:ARG:Ｖ绝顶经验 += V
EXP:ARG:Ａ绝顶经验 += A
EXP:ARG:Ｂ绝顶经验 += B
EXP:ARG:Ｍ绝顶经验 += M

;ビデオ
IF TEQUIP:ARG:28
	IF NOWEX:ARG:8
		LOCALS = O2/O3/O4/
		TSTR:1 += LOCALS
	ENDIF
	IF NOWEX:ARG:7
		LOCALS = O2/O3/
		TSTR:1 += LOCALS
	ENDIF
	IF NOWEX:ARG:6
		LOCALS = O2/
		TSTR:1 += LOCALS
	ENDIF
	IF NOWEX:ARG:0 == 2
		LOCALS = C/C/
		TSTR:1 += LOCALS
	ELSEIF NOWEX:ARG:0 == 1
		LOCALS = C/
		TSTR:1 += LOCALS
	ENDIF
	IF NOWEX:ARG:1 == 2
		LOCALS = V/V/
		TSTR:1 += LOCALS
	ELSEIF NOWEX:ARG:1 == 1
		LOCALS = V/
		TSTR:1 += LOCALS
	ENDIF
	IF NOWEX:ARG:2 == 2
		LOCALS = A/A/
		TSTR:1 += LOCALS
	ELSEIF NOWEX:ARG:2 == 1
		LOCALS = A/
		TSTR:1 += LOCALS
	ENDIF
	IF NOWEX:ARG:3 == 2
		LOCALS = B/B/
		TSTR:1 += LOCALS
	ELSEIF NOWEX:ARG:3 == 1
		LOCALS = B/
		TSTR:1 += LOCALS
	ENDIF
ENDIF

;绝顶时、利尿剂or漏尿癖で漏らす
IF FLAG:癖好有效 > 0 && MultipleEc > 0 && ( TCVAR:利尿剂 > 0 || TALENT:漏尿癖 > 0 )
	NOWEX:ARG:放尿 = MultipleEc
	EX:ARG:放尿 += MultipleEc
	EXP:ARG:放尿经验 += Multiplier
	;获得经验值*5 だけ利尿剂の持续时间を短缩
	TCVAR:利尿剂 = MAX( TCVAR:利尿剂 - (Multiplier * 5) , 0)
ENDIF

$JIRASI
;IF 挑逗中

RETURN 1

@MESSAGE_PALAMCNG_B2
IF NOWEX:五重绝顶
	IF !GETBIT(CFLAG:3,2)
		EXP:异常经验 ++
		SETBIT CFLAG:3,2
	ENDIF
	PRINTL 五 重 绝 顶
	PRINTL (分别得到了16倍的珠)
ELSEIF NOWEX:四重绝顶
	PRINTL 四 重 绝 顶
	PRINTL (分别得到了8倍的珠)
ELSEIF NOWEX:三重绝顶
	PRINTL (分别得到了4倍的珠)
ELSEIF NOWEX:二重绝顶
	PRINTL (分别得到了2倍的珠)
ELSEIF NOWEX:Ｂ绝顶
	IF NOWEX:Ｂ绝顶 == 3
		PRINTL 超强绝顶Ｂ
	ELSEIF NOWEX:Ｂ绝顶 == 2
		PRINTL 强绝顶Ｂ
	ELSE
		PRINTL 绝顶Ｂ
	ENDIF
ELSEIF NOWEX:Ａ绝顶
	IF NOWEX:Ａ绝顶 == 3
		PRINTL 超强绝顶Ａ
	ELSEIF NOWEX:Ａ绝顶 == 2
		PRINTL 强绝顶Ａ
	ELSE
		PRINTL 绝顶Ａ
	ENDIF
ELSEIF NOWEX:Ｖ绝顶
	IF NOWEX:Ｖ绝顶 == 3
		PRINTL 超强绝顶Ｖ
	ELSEIF NOWEX:Ｖ绝顶 == 2
		PRINTL 强绝顶Ｖ
	ELSE
		PRINTL 绝顶Ｖ
	ENDIF
ELSEIF NOWEX:Ｃ绝顶
	IF NOWEX:Ｃ绝顶 == 3
		PRINTL 超强绝顶Ｃ
	ELSEIF NOWEX:Ｃ绝顶 == 2
		PRINTL 强绝顶Ｃ
	ELSE
		PRINTL 绝顶Ｃ
	ENDIF
ELSEIF NOWEX:Ｍ绝顶
	IF NOWEX:Ｍ绝顶 == 3
		PRINTL 超强绝顶Ｍ
	ELSEIF NOWEX:Ｍ绝顶 == 2
		PRINTL 强绝顶Ｍ
	ELSE
		PRINTL 绝顶Ｍ
	ENDIF
ENDIF

;绝顶による欲望ＬＶUP
LOCAL = 0
SIF NOWEX:Ｃ绝顶 >= 2 || NOWEX:Ｂ绝顶 >= 2 || NOWEX:Ｖ绝顶 || NOWEX:Ａ绝顶
	LOCAL = 1
SIF NOWEX:Ｖ绝顶 >= 2 || NOWEX:Ａ绝顶 >= 2 || NOWEX:Ｍ绝顶 >= 1
	LOCAL = 3
SIF (NOWEX:Ｃ绝顶 >= 2 || NOWEX:Ｂ绝顶 >= 2) && NOWEX:二重绝顶
	LOCAL = 6
SIF NOWEX:三重绝顶
	LOCAL = 9
SIF NOWEX:四重绝顶
	LOCAL = 12
SIF NOWEX:五重绝顶
	LOCAL = 15
;自制心があると１下がる
SIF TALENT:自制心
	LOCAL -= 1
IF ABL:欲望 < LOCAL
	IF SOURCE:魔力印记 == 1
		PRINTFORML 虽然高潮的不行，但由于%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%收到魔力印记的影响，抑制住了欲望......
	ELSE
		ABL:欲望 = LOCAL
		PRINTFORML 并且欲望变成了LV{LOCAL}
		;欲望の上升による[圧抑]の消灭をチェック
		CALL YOKUBO_UP_CHECK
	ENDIF
ENDIF
PRINTL 
;-------------------------------------------------
;调教对象の喷乳チェック
;-------------------------------------------------
@TARGET_MILK_CHECK(ARG)
SIF !TALENT:ARG:母乳体质
	RETURN 0

LOCAL = CUP:ARG:快Ｃ / 10 + CUP:ARG:快Ｖ / 10 + CUP:ARG:快Ａ / 10 + CUP:ARG:快Ｂ * 3
SIF LOCAL == 0
	RETURN 0

;自制心
SIF TALENT:ARG:自制心
	LOCAL /= 2

;接受快感
SIF TALENT:ARG:快感应答 > 0
	TIMES LOCAL , 1.20

;否定快感（淫乱、娼妇で无效）
SIF TALENT:ARG:快感应答 < 0 && !TALENT:ARG:淫乱
	TIMES LOCAL , 0.80

;B敏感
SIF TALENT:ARG:Ｂ感度 > 0
	TIMES LOCAL , 1.20

;媚药
SIF TCVAR:ARG:媚药 || TCVAR:ARG:发情
	LOCAL *= 2

;仙香玉兔・毒气花园
SIF (IsInsenceValid(ARG) || IsGardenValid(ARG))
	TIMES LOCAL , 1.20

;利尿剂
SIF TCVAR:ARG:利尿剂
	LOCAL /= 2

;睡眠药
SIF TCVAR:ARG:睡眠药强度
	LOCAL /= 2

;调教者が幼儿退行
SIF TALENT:PLAYER:幼儿／幼儿退行
	TIMES LOCAL , 1.20

;调教者が幼稚
SIF TALENT:PLAYER:幼稚
	TIMES LOCAL , 1.20

;贫乳
SIF TALENT:ARG:胸围 < 0
	TIMES LOCAL , 0.50

LOCAL = 100 + LOCAL / 2
SIF TEQUIP:ARG:挑逗模式
	LOCAL = LOCAL * 8 / 10
BASE:ARG:母乳 += LOCAL
IF TEQUIP:ARG:挑逗模式 && BASE:ARG:母乳 > MAXBASE:ARG:母乳 && ARG
	TCVAR:ARG:挑逗回数 ++
	RETURN 0
ELSEIF  BASE:ARG:母乳 > MAXBASE:ARG:母乳 * 2
	LOCAL = 1
ELSEIF BASE:ARG:母乳 > MAXBASE:ARG:母乳
	LOCAL = 0
ELSE
	RETURN 0
ENDIF
LOCAL ++
DOWNBASE:ARG:体力 += 20 * LOCAL
DOWNBASE:ARG:气力 += 40 * LOCAL
IF EXP:ARG:喷乳经验 < EXPLV:1
	SOURCE:ARG:露出 += 5000 * LOCAL
	SOURCE:ARG:屈从 += 2500 * LOCAL
	SOURCE:ARG:不洁 += 1000 * LOCAL
ELSEIF EXP:ARG:喷乳经验 < EXPLV:2
	SOURCE:ARG:露出 += 2500 * LOCAL
	SOURCE:ARG:屈从 += 1500 * LOCAL
	SOURCE:ARG:不洁 += 750 * LOCAL
ELSEIF EXP:ARG:喷乳经验 < EXPLV:3
	SOURCE:ARG:露出 += 1500 * LOCAL
	SOURCE:ARG:屈从 += 750 * LOCAL
	SOURCE:ARG:不洁 += 500 * LOCAL
ELSEIF EXP:ARG:喷乳经验 < EXPLV:4
	SOURCE:ARG:露出 += 750 * LOCAL
	SOURCE:ARG:屈从 += 400 * LOCAL
	SOURCE:ARG:不洁 += 350 * LOCAL
ELSEIF EXP:ARG:喷乳经验 < EXPLV:5
	SOURCE:ARG:露出 += 500 * LOCAL
	SOURCE:ARG:屈从 += 250 * LOCAL
	SOURCE:ARG:不洁 += 200 * LOCAL
ELSE
	SOURCE:ARG:露出 += 250 * LOCAL
	SOURCE:ARG:屈从 += 150 * LOCAL
ENDIF

IF EXP:ARG:喷乳经验 == 0
	EXP:ARG:异常经验 ++
ENDIF
EXP:ARG:喷乳经验 += LOCAL
;Ｂに母乳污れ
STAIN:ARG:5 |= 16
	BASE:ARG:母乳 -= MAXBASE:ARG:母乳 * LOCAL
SIF BASE:ARG:母乳 >= MAXBASE:ARG:母乳
	BASE:ARG:母乳 = MAXBASE:ARG:母乳 - 1
NOWEX:ARG:喷乳 += LOCAL
EX:ARG:喷乳 += LOCAL

@TARGET_MILK_MESSAGE(ARG)
CALL COLORMESSAGE(@"《喷乳》（%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%）",C_PINK,1,1)
PRINTFORML 从%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的乳头喷出了\@ NOWEX:ARG:喷乳 > 1 ? 大量的 # \@母乳
IF TEQUIP:ARG:吸乳头
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%将充满嘴里的醇厚甘甜的奶水咕噜作响地喝下去了
	CALL RECOVER(MASTER,NOWEX:ARG:喷乳 * 10,"体力")
	CALL RECOVER(MASTER,NOWEX:ARG:喷乳 * 20,"气力")
	CALL RECOVER(MASTER,NOWEX:ARG:喷乳 * 10,"精力")
	CALL COLORMESSAGE(@"体力＋{NOWEX:ARG:喷乳 * 10}",C_AQUA,1)
	CALL COLORMESSAGE(@"气力＋{NOWEX:ARG:喷乳 * 20}",C_AQUA,1)
	CALL COLORMESSAGE(@"精力＋{NOWEX:ARG:喷乳 * 10}",C_AQUA,1)
ENDIF


@绝顶强度判定(ARG,绝顶部位,ARG:2)
#DIM 绝顶部位
#DIM 欲求不满低下系数
#DIM 上升值
; ARG:2=绝顶补正
VARSET LOCAL
欲求不满低下系数 = MAX(60 - ABL:ARG:欲望 * 2, 30)


SIF FLAG:70
	欲求不满低下系数 /= 2
SIF IsHeat(ARG)
	欲求不满低下系数 /= 2
LOCAL = (CUP:ARG:绝顶部位 > 0) * ARG:2
; IF CUP:ARG:绝顶部位 + PALAM:ARG:绝顶部位 + LOCAL >= PALAMLV:4 * 3
; 	LOCAL:1 = 3
; 	CDOWN:ARG:绝顶部位 = PALAMLV:4 * 3 - LOCAL
; ELSEIF CUP:ARG:绝顶部位 + PALAM:ARG:绝顶部位 + LOCAL >= PALAMLV:4 * 2
; 	LOCAL:1 = 2
; 	CDOWN:ARG:绝顶部位 = PALAMLV:4 * 2 - LOCAL
; ELSEIF CUP:ARG:绝顶部位 + PALAM:ARG:绝顶部位 + LOCAL >= PALAMLV:4
; 	LOCAL:1 = 1
; 	CDOWN:ARG:绝顶部位 = PALAMLV:4 - LOCAL
; ENDIF
; 计算绝顶强度，最大为 3
LOCAL:1 = MIN((CUP:ARG:绝顶部位 + PALAM:ARG:绝顶部位 + LOCAL) / PALAMLV:4, 3)
CDOWN:ARG:绝顶部位 = PALAMLV:4 * LOCAL:1 - LOCAL
;DOWN:0で下げても绝顶以上なら
;その值-1になるように调整（10000で绝顶なら9999）
SIF CUP:ARG:绝顶部位 + PALAM:ARG:绝顶部位 - CDOWN:ARG:绝顶部位 >= PALAMLV:4
	CDOWN:ARG:绝顶部位 = CUP:ARG:绝顶部位 + PALAM:ARG:绝顶部位 - PALAMLV:4 + 1
CFLAG:ARG:积攒度 -= LOCAL:1 * 欲求不满低下系数
CFLAG:ARG:积攒度 = MAX(CFLAG:ARG:积攒度, 0)
IF ARG == MASTER && TARGET >= 1 && (TALENT:ARG:性别 & 1) && !FLAG:70
	CFLAG:ARG:积攒度 -= LOCAL:1 * 20
	CFLAG:ARG:积攒度 = MAX(CFLAG:ARG:积攒度, 0)
ENDIF
上升值 = LOCAL:1
IF !TALENT:ARG:妄想的产物
	SIF MAXBASE:ARG:体力 > CSVBASE(ARG,0) * 2 || MAXBASE:ARG:气力 > CSVBASE(ARG,1) * 2
		上升值 -= 1
	SIF MAXBASE:ARG:体力 > CSVBASE(ARG,0) * 3 || MAXBASE:ARG:气力 > CSVBASE(ARG,1) * 3
		上升值 -= 1
	上升值 = MAX(上升值,0)
	IF ARG
		SIF MAXBASE:ARG:体力 < 5000 || !RAND:2
			MAXBASE:ARG:体力 += 上升值
		SIF MAXBASE:ARG:气力 < 5000 || !RAND:2
			MAXBASE:ARG:气力 += 上升值
	ENDIF
ENDIF
RETURN LOCAL:1

@挑逗发生(ARG,ARG:1)
#FUNCTION
SIF TCVAR:MASTER:要去了
	RETURNF 0
SIF (ARG:1 + EX:ARG:Ｃ寸止 + EX:ARG:Ｖ寸止 + EX:ARG:Ａ寸止 + EX:ARG:Ｂ寸止 + EX:ARG:Ｍ寸止) >= (5 - TCVAR:ARG:挑逗回数) * 2
	RETURNF 0
RETURNF 1

;同时绝顶したか
@SYNCED_ORGASM(ARG)
#FUNCTION
SIF ARG == MASTER
	RETURNF 0
SIF !NOWEX:ARG:Ｃ绝顶 && !NOWEX:ARG:Ｂ绝顶 && !NOWEX:ARG:Ｖ绝顶 && !NOWEX:ARG:Ａ绝顶 && !NOWEX:ARG:Ｍ绝顶 && !NOWEX:ARG:射精
	RETURNF 0
SIF !NOWEX:MASTER:Ｃ绝顶 && !NOWEX:MASTER:Ｂ绝顶 && !NOWEX:MASTER:Ｖ绝顶 && !NOWEX:MASTER:Ａ绝顶 && !NOWEX:MASTER:Ｍ绝顶 && !NOWEX:MASTER:射精
	RETURNF 0

RETURNF 1