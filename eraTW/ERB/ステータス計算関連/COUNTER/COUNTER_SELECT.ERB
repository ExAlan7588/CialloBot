@Counter呼び出し处理
#DIM NUM_TARGET
NUM_TARGET = GET_TARGETNUM()
;--------------------------------------------------------------------
;TCVAR:相手:20　指令番号　内容の大筋 
;TCVAR:相手:21　派生　内容の详细
;--------------------------------------------------------------------
IF ( TFLAG:102 == 3 || TFLAG:102 == 2 ) && !CFLAG:TARGET:陪睡中
;IF !CFLAG:TARGET:陪睡中
	;Counter履历作成
	ARRAYCOPY "SELECTCCOM", "PREVCCOM"
	VARSET SELECTCCOM
	;Counter
	FOR LOCAL, 1, NUM_TARGET + 1
		SIF TARGET:LOCAL <= 0
		CONTINUE
		;给TCVAR:MASTER:21和TCVAR:MASTER:20赋值
		CALL EVENT_COUNTER(TARGET:LOCAL,LOCAL)
		;CALL EVENT_COUNTER_COMBINATION
		SIF (TCVAR:(TARGET:LOCAL):20 || TCVAR:MASTER:20) && SELECTCOM != 350
			CALL KOJO_MESSAGE_SEND_COUNTER(1,TARGET:LOCAL,0,0)
	NEXT
	; FOR LOCAL, 1, NUM_TARGET + 1
	; 	SIF TARGET:LOCAL <= 0
	; 	CONTINUE
	; 	IF TCVAR:(TARGET:LOCAL):20 || TCVAR:MASTER:20
	; 	;根据TCVAR:MASTER:21和TCVAR:MASTER:20的赋值选择相应文本
	; 		CALL KOJO_MESSAGE_SEND_COUNTER(1,TARGET:LOCAL,0,0)
	; 	ENDIF
	; NEXT
ENDIF

@EVENT_COUNTER(ARG,ARG:1)
#DIM 候补, CCOM_MAXNUM
#DIM 基本值
VARSET LOCAL
VARSET 候补
;----------------------------------------------------
;中止
;----------------------------------------------------
;ARG 对象角色
;ARG:1 行动顺
SIF GROUPMATCH(SELECTCOM, 350, 355, 400, 402, 404, 405, 420, 604, 699)
	RETURN 0
SIF ARG <= 0
	RETURN 0
SIF !SHIRAHU(ARG)
	RETURN 0
SIF FLAG:70 == 1 && CFLAG:ARG:344 == 0
	RETURN 0
SIF CFLAG:ARG:行动 == 5
	RETURN 0
SIF TCVAR:ARG:亲热 < 1
	RETURN 0
SIF TEQUIP:ARG:六九式 == 1
	CALL EVENT_COUNTER_POSE_69(ARG,0)
TCVAR:ARG:30 ++
IF TCVAR:ARG:30 > 1
	TCVAR:MASTER:20 = MASTER_COUNTER_RECORD:ARG:0
	TCVAR:MASTER:21 = MASTER_COUNTER_RECORD:ARG:1
	RETURN 0
ENDIF

;----------------------------------------------------
;特殊な处理
;----------------------------------------------------
;诶嘿嘿中

IF CFLAG:ARG:诶嘿嘿 == 1
	DEBUGPRINT Counterリアクション
	CALL EVENT_COUNTER_REACTION(ARG,ARG:1)
	CALL EVENT_COUNTER_SOURCE(ARG,ARG:1)
	RETURN 0
ENDIF
;把柄
;要调整 ENDUREとRANDで茶を浊す
;2023/01/10 オプション选择制に移行,结束处理をENDUFUFU_ALLに一任
IF ARG:1 == 1 && DECIDE_PUNISHMENT(ARG)
	CALL EVENT_COUNTER_PUNISHMENT(ARG,ARG:1)
	CALL ENDUFUFU_ALL
	TFLAG:102 = 1
	RETURN 0
ENDIF

;----------------------------------------------------
;通常
;----------------------------------------------------
; 低いほど角色能力が顕着に反映される。高くすればするほどただの均等随机に近付く。デフォは10
基本值 = 10

;------------------------------------------
;2019/08/09 新规判定实装
;2023/01/10 简易フィルタ实装
;各番号带に于ける处理を、现行の指令处理に沿うようにCounter指令として分离。
;详细については各指令(CCOM)ファイル内に集约。
;------------------------------------------
FOR LOCAL, 1, CCOM_MAXNUM
[IF_DEBUG]
	SIF CCOM_FILTER:LOCAL
		CONTINUE
[ENDIF]
	;各指令の实行可否を确认
	TRYCALLFORM CCOM_ABLE{LOCAL}(ARG)
	IF RESULT
		;各指令の实行判定值を算出
		TRYCALLFORM CCOM_ORDER{LOCAL}(LOCAL, ARG, 基本值)
		LOCAL:10 = RESULT
		;addition custom code - from anon
		CALL KOJO_CHECK(ARG, "CCOM_ORDER", ,LOCAL, LOCAL:10)
		SIF RESULT
			LOCAL:10 = MAX(RESULT, 0)

		IF LOCAL:10
			候补:LOCAL = LOCAL:10
			DEBUGPRINTFORML Counter{LOCAL}が候补に（判定值{LOCAL:10}）
		ENDIF
	ENDIF
	RESULT = 0
NEXT
;------------------------------------------
;实行判定・登录
;------------------------------------------
LOCAL:4 = ARRAY_HIT(候补, CCOM_MAXNUM)
SIF TCVAR:ARG:次回Counter && 候补:(TCVAR:ARG:次回Counter) > 0
	LOCAL:4 = TCVAR:ARG:次回Counter
TCVAR:ARG:次回Counter = 0
; 决定
IF LOCAL:4 > 0
	TCVAR:ARG:Counter行动 = LOCAL:4
	;实行中指令として登录
	FOR LOCAL:2, 0, 最大被逆推人数 - 1
		IF SELECTCCOM:(LOCAL:2):0 == 0
			SELECTCCOM:(LOCAL:2):0 = ARG
			SELECTCCOM:(LOCAL:2):1 = (TCVAR:ARG:Counter行动)
			BREAK
		ENDIF
	NEXT
	;指令を实行
	DEBUGPRINTFORML Counter：{ARG}-{TCVAR:ARG:Counter行动}
	TRYCALLFORM CCOMF{TCVAR:ARG:Counter行动}(ARG)
	SIF !RESULT
		DEBUGPRINTFORML CCOMF{TCVAR:ARG:Counter行动}({ARG})の呼び出しに失败しました。
ENDIF

IF CFLAG:ARG:诶嘿嘿 == 2
	CALL EVENT_COUNTER_REACTION(ARG,ARG:1)
	;诶嘿嘿=2，COUNTER_SOURCE在SOURCE内处理
	;处理个der，还得有这个
	CALL EVENT_COUNTER_SOURCE(ARG,ARG:1)
ENDIF
