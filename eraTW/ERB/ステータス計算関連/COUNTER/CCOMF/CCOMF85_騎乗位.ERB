;*********************************************************************************************
;Counter指令：骑乘位Ｖ
;ARG = 对象の角色番号
;*********************************************************************************************
@CCOMF85(ARG)

	;体势の变更
	;=============================
	SIF MASTER_POSE(0,SET_V) == ARG || MASTER_POSE(0,SET_A) == ARG
		CALL TOUCH_RESET_T(ARG)
	CALL TOUCH_SET(SET_FROM_V,1,ARG)
	TFLAG:MASTERのＰ插入中 = 1
	TFLAG:MASTERのＣ使用中 ++
	
	;ソース计算および内部处理系统
	;=============================
	SIF TEQUIP:PLAYER:0
		CALL DATUI_BOTTOM(ARG,0)
	IF BEDROOM(CFLAG:MASTER:当前位置)
		SIF TEQUIP:ARG:0
			CALL DATUI_BOTTOM_T(ARG,0)
	ELSE
		SIF TEQUIP:ARG:0 & 14
			CALL DATUI_BOTTOM_T(ARG,1)
	ENDIF
	TCVAR:PLAYER:童贞丧失 = 1
	TCVAR:ARG:避孕 = 1
	DOWNBASE:ARG:体力 += 60
	DOWNBASE:ARG:气力 += 120
	SOURCE:ARG:征服 += 700
	SOURCE:ARG:快Ｖ = 300
	SOURCE:ARG:情爱 = 150
	SOURCE:ARG:性行动 = 500
	SOURCE:ARG:达成 = 300
	SOURCE:ARG:苦痛 = 250
	SOURCE:ARG:露出 = 60
	SOURCE:ARG:施虐 += 200
	TCVAR:116 = PLAYER
	CALL PAIN_CHECK_V(ARG)
	;射精部位（1=膣内 2=肛门 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=肛门侍奉 10=道具 11=触手 12=Ａ调教 13=Ｖ调教）
	CALL 射精对象登录(PLAYER, ARG, 1)
	TEQUIP:ARG:体位 = 3
	;强度
	TCVAR:PLAYER:4 = 1000
	CALL STAIN_膣内Ｐ插入
	EXP:ARG:Ｖ性交经验 ++
	EXP:PLAYER:插入经验 ++
	CALL LOST_VIRGIN(ARG)
	EXP:ARG:爱情经验 += TALENT:ARG:恋慕
	TEQUIP:ARG:50 = MASTER
	TCVAR:ARG:嗜虐フラグ = 1

	;マ斯塔Counter制御
	;(ビット立ちで不可 0=摸屁股 1=拥抱 2=接吻 3=胸爱抚 4=肛门爱抚 5=阴蒂爱抚 6=指插入れ 7=掀裙子)
	;=============================
	;CALL Counter插入され继续中处理(ARG)
	SETBIT TCVAR:ARG:26 ,6
	SETBIT TCVAR:ARG:26 ,7

RETURN 1

;*********************************************************************************************
;Counter实行可否出力函数
;ARG = 对象の角色番号
;*********************************************************************************************
@CCOM_ABLE85(ARG)

	;毒人形だと性器がついてないのでダメ
	SIF TALENT:ARG:196 == 99
		RETURN 0
	;体格差がある场合は経験がないとダメ
	SIF 插入不可(ARG) == 1 && EXP:ARG:Ｖ经验 < 50 && !TALENT:ARG:禁断的知识
		RETURN 0
	;さらに体格差があると禁断的知识が必要
	SIF 插入不可(ARG) == 2 && (EXP:ARG:Ｖ经验 < 50 || !TALENT:ARG:禁断的知识)
		RETURN 0
	SIF 插入不可(ARG) == 3
		RETURN 0
	SIF !(TALENT:ARG:2 & 1)
		RETURN 0
	SIF TALENT:ARG:妊娠 && (CFLAG:TARGET:903 <= 5 || CFLAG:TARGET:903 >= 25)
		RETURN 0
	SIF CFLAG:ARG:育儿人数 >= 1 && CFLAG:ARG:育儿人数 <= 30
		RETURN 0
	SIF !(TALENT:PLAYER:2 & 2)
		RETURN 0
	SIF BASE:MASTER:勃起 < 1000
		RETURN 0
	SIF TFLAG:63
		RETURN 0
	SIF TEQUIP:50 >= 0 || TEQUIP:51 >= 0
		RETURN 0
	SIF TEQUIP:ARG:体位
		RETURN 0
	SIF TEQUIP:MASTER:50 >= 0 || TEQUIP:MASTER:51 >= 0
		RETURN 0
	SIF PALAM:ARG:润滑 < PALAMLV:3
		RETURN 0
	SIF TEQUIP:ARG:振动棒
		RETURN 0
	;贞操带、触手胖次、振动棒胖次、二穴振动棒胖次を是てるとダメ
	SIF EQUIP:下半身内衣２ == 27 || EQUIP:下半身内衣２ == 28 || EQUIP:下半身内衣２ == 29 || EQUIP:下半身内衣２ == 30
		RETURN 0

RETURN 1

;*********************************************************************************************
;Counter实行判定值算出函数
;ARG = 对象の角色番号
;*********************************************************************************************
@CCOM_ORDER85(指令番号, ARG, 基本值)
#DIM 指令番号
#DIM 基本值
#DIMS Counterタイプ

	;Counterタイプ设定
	;=============================
	Counterタイプ = 性交

	;实行判定值算出式
	;=============================
	LOCAL = 基本值 + ABL:ARG:Ｖ感觉 * 3 + EX:ARG:Ｖ寸止 * 10

	;Counterタイプ别计算处理 实质实行判定处理を含む
	;=============================
	LOCAL = MAX(LOCAL + COUNTER_SIKOU:ARG:指令番号, 1)
	LOCAL *= COUNTER_TYPE(Counterタイプ, ARG)

RETURN LOCAL
