@被逆推
#DIM 人の气息
#DIM 被逆推判定
#DIM 抱きつき值
#DIM 被逆推值
#DIM 抱きつき优先
#DIM C_ID

IF FLAG:推倒
	被逆推判定 = 0
	被逆推判定 += ABL:TARGET:战斗能力 - ABL:MASTER:战斗能力
	被逆推判定 += TALENT:TARGET:体型 - TALENT:MASTER:体型
	IF CFLAG:TARGET:害羞 > 0
		被逆推判定 += 1
	ELSEIF CFLAG:TARGET:害羞 < 0
		被逆推判定 -= 1
	ENDIF
	IF CFLAG:TARGET:诱惑 > 0
		被逆推判定 += 1
	ELSEIF CFLAG:TARGET:诱惑 < 0
		被逆推判定 -= 1
	ENDIF
	IF CFLAG:TARGET:积极性 > 0
		被逆推判定 += 1
	ELSEIF CFLAG:TARGET:积极性 < 0
		被逆推判定 -= 1
	ENDIF
	IF CFLAG:TARGET:攻 > 0
		被逆推判定 += 1
	ELSEIF CFLAG:TARGET:攻 < 0
		被逆推判定 -= 1
	ENDIF
	IF GROUPMATCH(CFLAG:TARGET:性格倾向,1,3)
		被逆推判定 += 1
	ELSEIF GROUPMATCH(CFLAG:TARGET:性格倾向,2,4)
		被逆推判定 -= 1
	ENDIF
	;被逆推判定 += ABL:TARGET:施虐属性 + ABL:MASTER:受虐属性
	;被逆推判定 -= TALENT:MASTER:胆量 + TALENT:MASTER:态度 + TALENT:MASTER:回应 + TALENT:MASTER:自尊心 + TALENT:MASTER:难以越过的底线 + TALENT:MASTER:抵抗 + TALENT:MASTER:贞操
	;被逆推判定 += (CFLAG:TARGET:掌握把柄 * 2) + (TALENT:TARGET:施虐狂 * 3) + (TALENT:MASTER:受虐狂 * 3) + (TCVAR:TARGET:发情 * 3)
	IF CFLAG:TARGET:积攒度 < 600
		被逆推判定 = 0
	ELSEIF CFLAG:TARGET:积攒度 < 800
		被逆推判定 += 1
	ELSEIF CFLAG:TARGET:积攒度 < 1000
		被逆推判定 += 2
	ELSE
		被逆推判定 += 3
	ENDIF
	SIF FLAG:催眠强度 > 50
		被逆推判定 -= 1
ENDIF

IF CFLAG:诶嘿嘿 == 1
	TFLAG:102 = 2
ELSEIF CFLAG:诶嘿嘿 == 2
	TFLAG:102 = 3
ELSEIF TFLAG:102 == 3 && CFLAG:MASTER:诶嘿嘿 == 2
	;既に处理済みなのでスキップ
ELSE
	TFLAG:102 = 1
ENDIF

IF !(TFLAG:102 == 3 && CFLAG:MASTER:诶嘿嘿 == 2)
	TFLAG:抱住模式 = 0
ENDIF

;抱きつきor被逆推の处理
;约会中は发生しない
IF !CHK_DATENOW(CFLAG:TARGET:约会中)
	;抱きつきと被逆推、どちらが优先して实行されるかを判定。抱きつきは女同士限定
	IF (TALENT:MASTER:性别 & 1) && (TALENT:性别 & 1)
		抱きつき值 = 抱きつき判定(TARGET)
	ELSE
		抱きつき值 = 0
	ENDIF
	被逆推值 = ENDURE(TARGET)

	抱きつき优先 = (抱きつき值 >= 1 && 抱きつき值 > 被逆推值 + 40 && !(被逆推值 >= 600 && BASE:理性 <= 300))

	;抱きつきが优先の场合
	IF 抱きつき优先
		IF !CFLAG:诶嘿嘿 && TFLAG:102 != 3 && 抱きつき值 >= RAND:250 + 250 && TCVAR:抱きつき禁止时间 <= 0
			;室外では抱きつかれない
			IF (!HITOKE() && INROOM(CFLAG:MASTER:当前位置) && AT_HOME(MASTER)) || CFLAG:MASTER:当前位置 == OMANEKIBEYA()
				;同室にいるのが1人のみの场合(抱きつきは复数人不可)
				IF GET_TARGETNUM() == 1
					PRINTL
					PRINTFORMW %CALLNAME%抱住了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%！
					CALL KOJO_MESSAGE_SEND("EVENT",10,TARGET,1,1)
					TFLAG:抱きつき解放ポイント = 2 + RAND:(MIN(ABL:欲望, 10) / 2 + 2) + MIN(CFLAG:积攒度, 1000) / 200
					CFLAG:诶嘿嘿 = 2
					CFLAG:MASTER:诶嘿嘿 = 2
					TFLAG:102 = 3
					TFLAG:抱住模式 = 1
					IF !TCVAR:发情 && !TCVAR:媚药 && !GETBIT(CFLAG:既成事实 , 1) && !TALENT:合意误认
						SETBIT CFLAG:既成事实 , 1
						CALL COLORMESSAGE(@"获得了和%CALLNAME%的合意",C_YELLOW,2)
					ELSE
						CFLAG:ヤラせちゃった = 1
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	;被逆推が优先の场合
	ELSE
		IF ((!CFLAG:诶嘿嘿 && TFLAG:102 != 3) || (TFLAG:102 == 3 && TFLAG:抱住模式)) && 被逆推值 >= RAND:300 + 300
			;室外では被逆推ない
			IF (!HITOKE() && INROOM(CFLAG:MASTER:当前位置) && AT_HOME(MASTER)) || CFLAG:MASTER:当前位置 == OMANEKIBEYA()
				;同室にいるのが1人のみの场合または复数人に被逆推たとき
				IF GET_TARGETNUM() == 1 || 复数被逆推() == 1
					;被逆推ない效果
					IF FLAG:推倒 && 被逆推判定 <= 0
						PRINTL
						PRINTFORML 突然被%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%抱了过来！
						PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%
						IF PALAM:TARGET:情欲 >= PALAMLV:10
							PRINT 闪闪发光的
						ELSE
							PRINT 水润的
						ENDIF
						PRINTFORML 眼睛注视过来
						WAIT
						$INPUT_LOOP
						PRINTFORML 顺势被逆推吗？
						PRINTL [ 0 ] - 拒绝
						PRINTL [ 1 ] - 躺下
						INPUT
						IF RESULT == 0 || RESULT == 1
							IF RESULT == 0
								PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%将%CALLNAME%轻轻推开了
							ELSEIF RESULT == 1
								GOTO CHOOSE_TO_BE_PUSHED
							ENDIF
						ELSE
							GOTO INPUT_LOOP
						ENDIF
					;延迟中は袭い挂かってこない
					ELSEIF CFLAG:TARGET:延迟 > 0

					;甲斐性无しモードの时、まれに押し倒させてくる
					ELSEIF FLAG:甲斐性无 && REVERSE_MIRADA(TARGET)
						PRINTL
						PRINTFORML 突然被%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%抱住了！
						PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%从怀里拿出一块石头，向%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%炫耀。
						PRINTL
						CALL COLORMESSAGE(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%握紧石头，石头开始神秘地闪耀起来…", C_PINK, 2, 1)
						PRINTL 　・
						PRINTL 　・
						PRINTL 　・
						PRINTFORMW 醒来时，发现%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%被%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%推倒了！
						IF !UFUFU_START_TIME
							UFUFU_START_TIME = TIME
							UFUFU_START_DAY = DAY
						ENDIF
						;本体に取り込まれて、このタイミングで口上を差し込みたい人が现れるまで封印
						;CALL KOJO_MESSAGE_SEND("EVENT",10,TARGET,5,0)
						FOR LOCAL, 1, GET_TARGETNUM() + 1
							C_ID = TARGET:LOCAL
							CFLAG:C_ID:诶嘿嘿 = 1
							IF !TCVAR:C_ID:发情 && !TCVAR:C_ID:媚药 && !GETBIT (CFLAG:C_ID:既成事实 , 1)
								SETBIT CFLAG:C_ID:既成事实 , 1
								CALL COLORMESSAGE(@"获得了和%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的合意",C_YELLOW,2)
							ELSE
								CFLAG:C_ID:ヤラせちゃった = 1
							ENDIF
						NEXT
						CFLAG:MASTER:诶嘿嘿 = 1
						TFLAG:102 = 2
						TFLAG:抱住模式 = 0
					ELSE
						$CHOOSE_TO_BE_PUSHED
						PRINTL
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%被推倒了！
						IF !UFUFU_START_TIME
							UFUFU_START_TIME = TIME
							UFUFU_START_DAY = DAY
						ENDIF
						;CALL KOJO_EVENT(12, TARGET)
						CALL KOJO_MESSAGE_SEND("EVENT",10,TARGET,1,0)
						FOR LOCAL, 1, GET_TARGETNUM() + 1
							C_ID = TARGET:LOCAL
							CFLAG:C_ID:诶嘿嘿 = 2
							IF !TCVAR:C_ID:发情 && !TCVAR:C_ID:媚药 && !GETBIT (CFLAG:C_ID:既成事实 , 1) && !TALENT:C_ID:合意误认
								SETBIT CFLAG:C_ID:既成事实 , 1
								CALL COLORMESSAGE(@"获得了和%NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%的合意",C_YELLOW,2)
							ELSE
								CFLAG:C_ID:ヤラせちゃった = 1
							ENDIF
						NEXT
						CFLAG:MASTER:诶嘿嘿 = 2
						TFLAG:102 = 3
						TFLAG:抱住模式 = 0
						TFLAG:被逆推开始时间 = TIME + DAY * 1440
					ENDIF
				ENDIF
				IF INROOM(CFLAG:MASTER:当前位置) == 2
					CALL DATUI_SHOES(MASTER,0)
					CALL DATUI_SHOES(TARGET,0)
				ENDIF
			;室外(情侣酒店と情人旅馆除く)で被逆推条件を满たした场合
			ELSEIF CFLAG:MASTER:当前位置 != 260 && CFLAG:MASTER:当前位置 != 950
				PRINTL
				SELECTCASE CFLAG:TARGET:积攒度
					CASE IS < 600
						PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%扯了扯袖子
						PRINTFORMW 似乎想去没人的地方的样子
					CASE IS < 800
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%依偎在%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%身上、抬头凝视着你…
					CASE IS < 1000
						PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%依偎在%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%身上、\@ BATHROOM(CFLAG:当前位置) ? 伸了进去 # 隔着衣服 \@轻轻的抚摸着股间…
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%用眼神诉说着情欲
						IF TCVAR:TARGET:诱惑时刻 != TIME
							PALAM:TARGET:润滑 += 500
							TCVAR:TARGET:诱惑时刻 = TIME
						ENDIF
					CASEELSE
						PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%满脸通红的挂在%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%身上、把%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的手拉到了自己那湿透的私处…
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%已经忍耐不住想要立刻和%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%做爱的样子
						IF TCVAR:TARGET:诱惑时刻 != TIME
							PALAM:TARGET:润滑 += 2000
							TCVAR:TARGET:诱惑时刻 = TIME
						ENDIF
				ENDSELECT
				CALL KOJO_MESSAGE_SEND("EVENT",10,TARGET,4, CFLAG:TARGET:积攒度)
			ENDIF
		ENDIF
	ENDIF
ENDIF


;复数被逆推の判定
@复数被逆推()
#FUNCTION
#DIM 判定值
#DIM 角色别判定值
#DIM 相性补正
#DIM NUM_TARGET
#DIM C_ID

判定值 = 0
NUM_TARGET = GET_TARGETNUM()
SIF NUM_TARGET <= 1
	RETURNF 0
FOR LOCAL, 1, NUM_TARGET + 1
	C_ID = TARGET:LOCAL
	SIF CFLAG:C_ID:同室 != 1
		CONTINUE
	SIF ENDURE(C_ID) < 450
		RETURNF 0
	;判定を通ったら角色别判定值に入れる
	角色别判定值 = ENDURE(C_ID)
	DEBUGPRINTFORML 补正前：({C_ID}) {ENDURE(C_ID)}
	;ここから角色别判定值を补正していく
	;百合属性での补正(3未满にはペナルティ)
	SIF ABL:(C_ID):百合属性 == 0
		TIMES 角色别判定值, 0
	SIF ABL:(C_ID):百合属性 == 1
		TIMES 角色别判定值, 0.50
	SIF ABL:(C_ID):百合属性 == 2
		TIMES 角色别判定值, 0.80
	;3以上では(1+0.1*(百合属性 - 3))倍
	SIF ABL:(C_ID):百合属性 >= 3
		角色别判定值 = 角色别判定值 * (100 + 10 * (ABL:(C_ID):百合属性 - 3)) / 100
	DEBUGPRINTFORML 百合属性补正后：({C_ID}) {角色别判定值}
	;百合中毒での补正 (1+0.1*百合中毒)倍
	角色别判定值 = 角色别判定值 * (100 + 10 * ABL:(C_ID):百合中毒) / 100
	DEBUGPRINTFORML 百合中毒补正后：({C_ID}) {角色别判定值}
	
	;相性での补正
	;仲のいい角色がいると一绪にMASTERを推倒やすく(完全に趣味)
	相性补正 = 0
	;同室の角色を探り各角色に对して相性值を见ていく
	FOR LOCAL:1, 1, NUM_TARGET + 1
		SIF CFLAG:(TARGET:(LOCAL:1)):同室 != 1
			CONTINUE
		SIF LOCAL == LOCAL:1
			CONTINUE
		;相性补正 += RELATION:LOCAL:(LOCAL:2)
		;ほんとは上の一文だけで済ませたいんだけど......
		;_Replace.csvのRELATIONの初期值が働かない(泣)
		;なので苦肉の策として0は100とみなす(暴论)
		相性补正 += RELATION:(C_ID):(TARGET:(LOCAL:1))
		SIF RELATION:(C_ID):(TARGET:(LOCAL:1)) == 0
			相性补正 += 100
	NEXT
	;平均值をとる(小数点以下切舍て) 当人を除いた(GET_TARGETNUM() - 1)人で割る
	;0除算注意(无いと思うけど)
	相性补正 /= NUM_TARGET - 1
	角色别判定值 = 角色别判定值 * 相性补正 / 100
	DEBUGPRINTFORML 相性补正后：({(C_ID)}) {角色别判定值}
	;补正后一位角色别判定值を全体の判定值に入れる
	判定值 += 角色别判定值
NEXT
;平均值をとる(小数点以下切舍て)
;0除算がこわいぃ
判定值 /= NUM_TARGET
DEBUGPRINTFORML 判定值：{判定值}
;判定值が最大だとして、概率は「(400+200*同室の人数)分の150」に设定(人数增えると难易度上升)
SIF RAND:(400 + 200 * NUM_TARGET) + 450 <= 判定值
	RETURNF 1
RETURNF 0


;==============================================================
;FileName_ATTITUDE.ERB ------------------------------- Rev1.00
;推倒率判定
;CALL		USER
;ARG		角色NO
;RETURN		0:推倒不成立, 1:推倒成立
;COMMENT	茶浊し系函数。出来が恶い。


;-----------------------------------------------------------
@ENDURE(ARG)
#FUNCTION
;素質により判定变化
LOCAL = CFLAG:ARG:合意判定
LOCAL += (EX:ARG:Ｃ寸止 + EX:ARG:Ｖ寸止 + EX:ARG:Ａ寸止 + EX:ARG:Ｂ寸止 + EX:ARG:Ｍ寸止) * 20
;抱き着くと被逆推率上升
IF SELECTCOM == 311
	LOCAL += 30
	;小心者の勇气には応えてくれやすい
	SIF FLAG:甲斐性无
		LOCAL += 100
ENDIF
LOCAL += TALENT:ARG:性的兴趣 * 20
SIF TALENT:ARG:处女
	LOCAL -= 50
SIF TALENT:ARG:恋慕
	LOCAL = LOCAL + 50
SIF TALENT:ARG:自尊心 > 0
	LOCAL -= 20
SIF CFLAG:ARG:允许无套 == 2
	LOCAL = LOCAL + 50
SIF CFLAG:ARG:强行内射
	LOCAL -= 100
SIF TALENT:ARG:胆量 < 0 || TALENT:ARG:回应 < 0 || TALENT:ARG:冷漠
	LOCAL = LOCAL - 30
SIF TALENT:ARG:羞耻心 > 0
	LOCAL -= 30
SIF TALENT:ARG:羞耻心 < 0
	LOCAL = LOCAL + 20
SIF TALENT:ARG:自制心 
	LOCAL = LOCAL - 20
SIF TALENT:ARG:贞操 < 0 || TALENT:ARG:小恶魔
	LOCAL = LOCAL + 20
SIF TALENT:ARG:贞操 > 0 || TALENT:ARG:难以越过的底线
	LOCAL -= 20
IF !GETBIT(CFLAG:ARG:既成事实, 1) && TALENT:ARG:贞操 > 0
	LOCAL -= 30
	SIF !TALENT:ARG:恋慕 && !TALENT:ARG:炮友
		LOCAL -= 50
ENDIF
SIF OTOKOGIRAI(ARG) && !(IsHeat(ARG) || IsInsenceValid(ARG))
	LOCAL = LOCAL / 2
SIF TALENT:ARG:难以越过的底线 && TALENT:ARG:恋慕
	LOCAL = LOCAL * 12 / 5
SIF MARK:ARG:反抗刻印 == 3
	LOCAL = 0
SIF TFLAG:120 && TFLAG:120 != ARG
	LOCAL = 0
SIF !SHIRAHU(ARG)
	LOCAL = 0
SIF CFLAG:ARG:行动 == 5
	LOCAL = 0
SIF CFLAG:ARG:延迟
	LOCAL = 0
SIF CFLAG:ARG:陪睡中
	LOCAL = 0
SELECTCASE CFLAG:ARG:积攒度
	CASE IS >= 1000
		LOCAL *= 3
	CASE IS >= 800
		LOCAL = LOCAL * 3 / 2
	CASE IS >= 600
		LOCAL = LOCAL * 5 / 4
	CASE IS >= 500
	CASE IS >= 300
		LOCAL /= 2
	CASE IS >= 200
		LOCAL /= 4
	CASE IS >= 100
		LOCAL /= 10
	CASEELSE
		LOCAL = 0
ENDSELECT

;体力に依存して推倒率减少

SELECTCASE BASE:ARG:体力
	CASE IS < 750
		LOCAL = 0
	CASE IS < 1000
		LOCAL /= 2
	CASE IS < 1500
		LOCAL = LOCAL * 3 / 4
	CASEELSE
ENDSELECT

SELECTCASE BASE:ARG:气力
	CASE 0
		LOCAL = 0
	CASE IS < 500
		LOCAL /= 2
	CASEELSE
ENDSELECT

;理性に依存して推倒率减少
IF BASE:ARG:理性 > 800
	LOCAL = LOCAL / 5
ELSEIF BASE:ARG:理性 > 600
	LOCAL = LOCAL / 2
ELSE
	;Do Notting
ENDIF
SELECTCASE BASE:ARG:情绪
	CASE IS < 200
		LOCAL = 0
	CASE 201 TO 400
		LOCAL /= 4
	CASE 401 TO 600
		LOCAL /= 2
	CASE 601 TO 800
	CASEELSE
		LOCAL = LOCAL * 6 / 5
ENDSELECT
SIF FLAG:甲斐性无
	LOCAL = LOCAL * 2
SIF CFLAG:ARG:勃然大怒
	LOCAL = 0
SIF CFLAG:ARG:推倒禁止
	LOCAL = 0
SIF TFLAG:パール期限
	LOCAL = 0
RETURNF LOCAL

;==============================================================
;抱きつき判定
;==============================================================
@抱きつき判定(ARG)
#FUNCTION
;素質により判定变化
LOCAL = CFLAG:ARG:合意判定
SIF TALENT:ARG:恋慕
	LOCAL = LOCAL + 50
SIF CFLAG:ARG:强行内射
	LOCAL -= 100
;抱き着くと抱き着き判定上升
IF SELECTCOM == 311
	LOCAL += 30
	;小心者の勇气には応えてくれやすい
	SIF FLAG:甲斐性无
		LOCAL += 100
ENDIF
SIF TALENT:ARG:胆量 < 0 || TALENT:ARG:回应 < 0 || TALENT:ARG:冷漠 || TALENT:ARG:羞耻心 > 0
	LOCAL = LOCAL - 30
SIF TALENT:ARG:性的兴趣 > 0 || TALENT:ARG:羞耻心 < 0
	LOCAL = LOCAL + 20
SIF TALENT:ARG:自制心 || TALENT:ARG:性的兴趣 < 0
	LOCAL = LOCAL - 20
SIF TALENT:ARG:贞操 < 0 || TALENT:ARG:小恶魔
	LOCAL = LOCAL + 20
SIF TALENT:ARG:贞操 > 0 || TALENT:ARG:难以越过的底线
	LOCAL = LOCAL / 2
SIF OTOKOGIRAI(ARG)
	LOCAL = LOCAL / 2
SIF TALENT:ARG:性别嗜好 == 3 || MARK:ARG:反抗刻印 == 3
	LOCAL = 0
SIF TFLAG:120 && TFLAG:120 != ARG
	LOCAL = 0
SIF !SHIRAHU(ARG)
	LOCAL = 0
SIF CFLAG:ARG:行动 == 5
	LOCAL = 0
SIF CFLAG:ARG:勃然大怒
	LOCAL = 0
SIF CFLAG:ARG:延迟
	LOCAL = 0
SIF CFLAG:ARG:陪睡中
	LOCAL = 0
SELECTCASE CFLAG:ARG:积攒度
	CASE IS >= 1000
		LOCAL *= 2
	CASE IS >= 800
		LOCAL = LOCAL * 7 / 5
	CASE IS >= 600
		LOCAL = LOCAL * 6 / 5
	CASE IS >= 400
	CASE IS >= 300
		LOCAL /= 2
	CASE IS >= 150
		LOCAL /= 4
	CASEELSE
		LOCAL = 0
ENDSELECT

;体力に依存して推倒率减少
SELECTCASE BASE:ARG:体力
	CASE IS < 750
		LOCAL = 0
	CASE IS < 1000
		LOCAL /= 2
	CASE IS < 1500
		LOCAL = LOCAL * 3 / 4
	CASEELSE
ENDSELECT

SELECTCASE BASE:ARG:情绪
	CASE IS < 200
		LOCAL = 0
	CASE 201 TO 500
		LOCAL = LOCAL * 2 / 3
	CASE 501 TO 800
	CASEELSE
		LOCAL = LOCAL * 3 / 2
ENDSELECT
SIF FLAG:甲斐性无
	LOCAL = LOCAL * 2
RETURNF LOCAL

;==============================================================
;ミラダ逆使用判定
;この前にも推倒判定があるため、素質などはそこまで詳しく见ない
;RANDを含むため实行するたびに结果が变わる
;C_ID	キャラNO
;RETURN	0=使用しない, 1=使用する
;==============================================================
@REVERSE_MIRADA(C_ID)
#FUNCTION
#DIM C_ID

;使用率の基准值
;普段の度胸の无さへの信赖が高いほど、石に赖りたくなる
LOCAL:0 = SQRT(LIMIT(CFLAG:C_ID:信赖度, 0, 20000) * 2)
;欲望BOUNS1
LOCAL:0 += 5 * MIN(ABL:C_ID:欲望, 10)
;陷落素質BOUNS
IF TALENT:C_ID:恋慕 && ABL:C_ID:欲望 > 10 && GROUPMATCH(TALENT:C_ID:处女, 1, -1)
	LOCAL:0 *= 2
ELSEIF TALENT:C_ID:爱欲
	LOCAL:0 += 200
ELSEIF TALENT:C_ID:炮友 == 2
	LOCAL:0 += 100
ELSEIF TALENT:C_ID:炮友
	LOCAL:0 += 50
ELSEIF TALENT:C_ID:思慕
	LOCAL:0 /= 3
ENDIF

;DEBUGPRINTFORM ミラダ逆使用率:{LOCAL:0 + 1}/1000
SIF RAND(1000) <= LOCAL:0
	RETURNF 1
RETURNF 0

@HITOKE(同室チェック)
;MASTERの位置から见て他的房间にだれかいるかどうか
;同室チェックの场合、同室に复数人いる场合も真
#FUNCTION
#DIM 同室チェック
SIF 同室チェック && FINDELEMENT(TARGET, 0) - 1 > 1
	RETURNF 1
;公共の场
SIF ROOMDATA:(CFLAG:MASTER:当前位置 % 100) & 场所_公共の场
	RETURNF 1
SIF TFLAG:描写中の儿童
	RETURNF 1
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:当前位置 == CFLAG:MASTER:当前位置
		CONTINUE
	SIF CAN_SEE(LOCAL)
		RETURNF 1
NEXT
RETURNF 0
