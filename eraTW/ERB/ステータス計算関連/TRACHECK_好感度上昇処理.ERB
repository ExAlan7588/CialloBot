;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;好感度上升计算处理
;CALL		USER
;ARG		0:角色NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@FAVOR_CALC(ARG)
#DIM 愤怒上升值
#DIM 情绪上升值
#DIM 信赖度キャップ补正
#DIM 理性减少值
SIF FLAG:70 == 1 && CFLAG:TARGET:344 == 0
	RETURN

;COMABLE范围外抑止
SIF !TFLAG:102
	RETURN 0
;对象がMASTER以外の时启动
SIF !ARG
	RETURN 0
;主人(你)と对象の能力を计算
;ABL:顺从をみる
TFLAG:97 += (ABL:ARG:顺从 + ABL:ARG:亲密) / 2

;主人と对象が同性
IF HETEROSEX(TARGET,PLAYER)
	;男なら断袖属性、女なら百合属性を见る
	IF ABL:ARG:(HETEROSEX(TARGET,PLAYER) + 16) == 0
		TFLAG:97 -= 1
	ELSEIF ABL:ARG:(HETEROSEX(TARGET,PLAYER) + 16) < 5
		TFLAG:97 += 0
	ELSEIF ABL:ARG:(HETEROSEX(TARGET,PLAYER) + 16) < 9
		TFLAG:97 += 1
	ELSE
		TFLAG:97 += 2
	ENDIF
	;男ならＢＬ中毒、女なら百合中毒を见る
	IF ABL:ARG:(HETEROSEX(TARGET,PLAYER) + 31) > 9
		TFLAG:97 += 2
	ELSEIF ABL:ARG:(HETEROSEX(TARGET,PLAYER) + 31) > 4
		TFLAG:97 += 1
	ENDIF
	;两面通吃
	SIF TALENT:ARG:性别嗜好 == -1
		TFLAG:97 += 1
ENDIF
;素質による增减
;叛逆
SIF TALENT:ARG:态度 > 0
	TFLAG:97 -= 2
;自尊心高
SIF TALENT:ARG:自尊心 > 0
	TFLAG:97 -= 1
;坦率
SIF TALENT:ARG:态度 < 0 && TFLAG:97 > 0
	TFLAG:97 += 1
;自制心
SIF TALENT:ARG:自制心
	TFLAG:97 -= 1
;感情缺乏
SIF TALENT:ARG:感情缺乏
	TFLAG:97 -= 1
;抵抗
SIF TALENT:ARG:抵抗
	TFLAG:97 -= 1
;献身的
SIF TALENT:ARG:献身的 && TFLAG:97 > 0
	TFLAG:97 += 1
;开朗同士
SIF TALENT:ARG:开朗／阴郁 > 0 && TALENT:MASTER:开朗／阴郁 > 0
	TFLAG:97 += 1

;阴郁同士
SIF TALENT:ARG:开朗／阴郁 < 0 && TALENT:MASTER:开朗／阴郁 < 0
	TFLAG:97 += 1
;接受快感
IF TALENT:ARG:快感应答 > 0
	TFLAG:97 += 1
;否定快感
ELSEIF TALENT:ARG:快感应答 < 0
	TFLAG:97 -= 1
ENDIF
;心情
TFLAG:97 += TALENT:ARG:15
;讨厌男人
IF OTOKOGIRAI(ARG)
	TFLAG:97 -= 3
	IF GETBIT (CFLAG:ARG:继承, 1)
		TFLAG:97 += 2
	ELSEIF GETBIT (CFLAG:ARG:继承, 0)
		TFLAG:97 += 1
	ENDIF
ENDIF
;恋慕
IF TALENT:ARG:恋慕
	TFLAG:97 += 2
;催情药
ELSEIF TCVAR:ARG:催情药
	TFLAG:97 ++
	SIF CFLAG:ARG:2 < 1000
		TFLAG:97 ++
ENDIF
;仙香玉兔
IF IsInsenceValid(ARG)
	TFLAG:97 ++
	SIF CFLAG:ARG:2 < 1000
		TFLAG:97 ++
ENDIF
;毒气花园
SIF IsGardenValid(ARG)
	TFLAG:97 ++

SIF TALENT:ARG:动物耳 && TALENT:ARG:动物耳 == TFLAG:耳补正
	TFLAG:97 += 3
;思慕
SIF TALENT:ARG:思慕
	TFLAG:97 += 1
;主人の容姿
;TFLAG:97 += TALENT:MASTER:容姿
;主人の魅力
SIF TALENT:MASTER:魅力
	TFLAG:97 += 1
;主人の魅惑
SIF TALENT:MASTER:魅惑
	TFLAG:97 += 1
;主人の谜之魅力
SIF TALENT:MASTER:谜之魅力
	TFLAG:97 += 1
;主人の人气
SIF TALENT:MASTER:人气
	TFLAG:97 += 1

IF GETBIT (CFLAG:ARG:继承, 1)
	TFLAG:97 += 2
ELSEIF GETBIT (CFLAG:ARG:继承, 0)
	TFLAG:97 += 1
ENDIF

LOCAL = 0
LOCAL:1 = 0
;各种快感
;50上限、30000と+300 この数值が大きければSOURCE:ARGの增加に对して钝感になる
LOCAL += (100 - 30000 / (SOURCE:ARG:快Ｃ + SOURCE:ARG:快Ｖ + SOURCE:ARG:快Ａ + SOURCE:ARG:快Ｂ + 300)) * (1 + TALENT:ARG:快感应答)
;情爱
LOCAL += (100 - 100000 / (SOURCE:ARG:情爱 + 1000))
LOCAL:1 += (100 - 100000 / (SOURCE:ARG:情爱 + 1000))
;情欲
LOCAL += (20 - 20000 / (SOURCE:ARG:情欲 + 1000))
LOCAL:1 += (100 - 100000 / (SOURCE:ARG:情欲 + 1000))
;达成感
LOCAL += (30 - 30000 / (SOURCE:ARG:达成 + 1000))
;恭顺
LOCAL += (20 - 20000 / (SOURCE:ARG:恭顺 + 1000))
;屈服
LOCAL += (20 - 20000 / (SOURCE:ARG:屈从 + 1000))
;露出
LOCAL += (30 - 30000 / (SOURCE:ARG:露出 + 1000)) * (ABL:ARG:露出癖 - 3) / 3

;苦痛
LOCAL += (50 - 20000 / (SOURCE:ARG:苦痛 + 400)) * (ABL:ARG:受虐属性 - 3) / 3
;欢乐
LOCAL += (50 - 10000 / (SOURCE:ARG:欢乐 + 2000))
;征服
LOCAL += (30 - 90000 / (SOURCE:ARG:征服 + 3000))
;被动
LOCAL += (30 - 90000 / (SOURCE:ARG:被动 + 3000))
;恐怖
LOCAL -= (50 - 20000 / (SOURCE:ARG:恐怖 + 400)) * (ABL:ARG:顺从 - 3) / 3
;不洁
LOCAL -= (50 - 25000 / (SOURCE:ARG:不洁 + 500)) * (2 - TALENT:ARG:污臭耐性) / 2
;郁闷
LOCAL -= (50 - 15000 / (SOURCE:ARG:郁闷 + 300))
;偏离正轨
LOCAL -= (50 - 25000 / (SOURCE:ARG:偏离正轨 + 500))
;反抗
LOCAL -= (50 - 10000 / (SOURCE:ARG:反感 + 200))

;好感度上升分に计算值追加
TFLAG:97 += LOCAL / 10

;日常时ON
IF TFLAG:102 != 2
	;情绪に好感度上升分追加
	情绪上升值 = MIN(ABL:ARG:亲密 + 1,10) * LOCAL / 3
	SIF FLAG:约会的对象 == ARG
		TFLAG:情绪BOUNS *= 2
	情绪上升值 = GET_REVISION3(情绪上升值, 50 + TFLAG:情绪BOUNS, 100 + TFLAG:情绪BOUNS, 200 + TFLAG:情绪BOUNS)

	;抑制フラグが立ってる
	IF TFLAG:情绪上升抑制
		情绪上升值 /= 5
		SIF BASE:ARG:情绪 > MAXBASE:ARG:情绪 / 2
		情绪上升值 = 0
	ENDIF
	;路人がいると上升量半减
	SIF WITH_MOB()
		情绪上升值 /= 2
		IF WEATHERDLC
			IF TIME:5 == 17
				情绪上升值 = 情绪上升值 * 12 / 10
			ELSEIF TIME:5 == 20
				情绪上升值 = 情绪上升值 * 8 /10
			ENDIF
		ENDIF
		SIF CFLAG:ARG:睡眠 ;修正BUG，防止睡着了还会涨
			情绪上升值 = 0

	BASE:ARG:情绪 += MIN(情绪上升值,200)
	BASE:ARG:情绪 = MIN(BASE:ARG:情绪 , MAXBASE:ARG:情绪)
	;同室にパルパルがいる状态で他の娘と情绪を上げると愤怒が溜まる
	IF ARG != 60 && CFLAG:ARG:当前位置 == CFLAG:60:当前位置
		IF TALENT:60:恋慕
			BASE:60:愤怒 += MIN(ABL:60:亲密 + 1,10) * LOCAL / 3
		ELSEIF TALENT:60:思慕
			BASE:60:愤怒 += MIN(ABL:60:亲密 + 1,10) * LOCAL / 5
		ELSEIF CFLAG:60:2 > 500
			BASE:60:愤怒 += MIN(ABL:60:亲密 + 1,10) * LOCAL / 8
		ENDIF
	ENDIF
	;理性を快感度分减少
	理性减少值 = LOCAL:1 * (GETPALAMLV(PALAM:ARG:情欲,10) + 1) / 10 * (CFLAG:ARG:积攒度 + 1000) / 2000
	IF TFLAG:理性削り
		理性减少值 *= 4
		SIF CFLAG:ARG:以身体为目的
			理性减少值 *= 3
	ENDIF
	BASE:ARG:理性 = MAX(BASE:ARG:理性 - 理性减少值, 0 )
	愤怒上升值 = 0
	;日常ONかつ反感ソース有りで愤怒值上升
	SIF SOURCE:ARG:反感 > 0 && TFLAG:102 == 1
		愤怒上升值 = SOURCE:ARG:反感 * 3 * (2000 - BASE:ARG:情绪) * (500 + BASE:ARG:理性) / ((2000 + BASE:ARG:情绪) * (500 + MAXBASE:ARG:理性))
	;振动棒胖次や触手胖次を是てる时は愤怒值が上升しない 逆ギレ防止
	SIF SOURCE:ARG:反感 > 0 && TFLAG:102 == 1 && (EQUIP:ARG:下半身内衣２ == 28 || EQUIP:ARG:下半身内衣２ == 29 || EQUIP:ARG:下半身内衣２ == 30)
		愤怒上升值 = 0
	;贞操带を是てる时は愤怒上升值が增加
	SIF SOURCE:ARG:反感 > 0 && TFLAG:102 == 1 && EQUIP:ARG:下半身内衣２ == 27
		愤怒上升值 = 愤怒上升值 * 4 / 3
	;心情で愤怒上升值が变化
	IF TALENT:ARG:15 == 1
		愤怒上升值 = 愤怒上升值 * 7 / 10
	ELSEIF TALENT:ARG:15 == -1
		愤怒上升值 = 愤怒上升值 * 3 / 2
	ENDIF
	SIF TCVAR:ARG:睡眠药强度 == 1 && CFLAG:ARG:恶作剧
		愤怒上升值 = 愤怒上升值 / 2
	SIF TCVAR:ARG:睡眠药强度 == 2 && CFLAG:ARG:恶作剧
		愤怒上升值 = 愤怒上升值 / 5
	SIF TCVAR:ARG:睡眠药强度 == 3 && CFLAG:ARG:恶作剧
		愤怒上升值 = 0
	;BASE:ARG:愤怒 += 愤怒上升值
	;反抗抑制
	SIF TALENT:ARG:反抗抑制
		愤怒上升值 = 愤怒上升值 / 3
	SIF FLAG:催眠强度
		愤怒上升值 = 愤怒上升值 * (101 - FLAG:催眠强度) / 100
	BASE:ARG:愤怒 += 愤怒上升值
	;愤怒が有顶天で愤怒フラグON
	SIF BASE:ARG:愤怒 > MAXBASE:ARG:愤怒
		CALL GET_ANGRY(ARG)
	BASE:ARG:愤怒 = MIN(BASE:ARG:愤怒 , MAXBASE:ARG:愤怒 )
	;心情不快（反抗刻印取得时）は常に愤怒
	IF CFLAG:ARG:心情不快
		BASE:ARG:愤怒 = MAXBASE:ARG:愤怒
		CFLAG:ARG:勃然大怒 = 1
	ENDIF
	;怒ると目觉める
	IF CFLAG:ARG:勃然大怒 && CFLAG:ARG:睡眠
		CALL CHARA_AWAKE(ARG,1)
		IF CFLAG:ARG:当前位置 == CFLAG:MASTER:当前位置 && CFLAG:ARG:当前位置 == CFLAG:ARG:311
			IF IS_LOVER(ARG)
				IF CFLAG:ARG:衰弱
					PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%确认来访者是%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%之后就疲惫的睡着了…
				ELSE
					PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%确认来访者是%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%之后露出了安心的表情…
				ENDIF
			ELSE
				IF CFLAG:ARG:衰弱
					PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%疲惫的睡着了…
				ELSE
					PRINTFORML 被%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%赶出来了…
				ENDIF
				CALL GETOUT(MASTER)
			ENDIF
		ENDIF
	ENDIF
	;警告牌发行チェック
	SIF (CFLAG:ARG:勃然大怒 == 1) && RAND:((ABL:ARG:亲密 + 1) * 3) == 0 && !CFLAG:ARG:睡眠 && FLAG:18
		CALL CARD_GOT_CHECK(ARG)
ENDIF

;反抗刻印补正
TFLAG:97 -= (FLAG:4 + 1) * MARK:反抗刻印

;结果が一定数未满の场合
IF MARK:反抗刻印 >= 3 && TFLAG:97 < -3
	TFLAG:97 = -3
ELSEIF MARK:反抗刻印 == 2 && TFLAG:97 < -2
	TFLAG:97 = -2
ELSEIF MARK:反抗刻印 == 1 && TFLAG:97 < -1
	TFLAG:97 = -1
ELSEIF TFLAG:97 < 0
	TFLAG:97 = 0
ENDIF

;冷漠の场合、恋慕がないと好感度が上がりにくい
IF TALENT:ARG:冷漠 && !TALENT:ARG:恋慕
	IF TFLAG:97 > 1
		TFLAG:97 /= 2
	ELSEIF TFLAG:97 > 0
		TFLAG:97 = 1
	ENDIF
	SIF ABL:ARG:施虐属性 >= 4
		TFLAG:97 += 1
ENDIF
;风骚だと亲密５から上げにくくなる
IF TALENT:ARG:风骚
	TFLAG:97 += 2
	SIF ABL:ARG:亲密 >= 5
		TFLAG:97 /= 2
ENDIF
[SKIPSTART]
;不诚实度によるペナルティ
SELECTCASE TCVAR:ARG:不诚实度
	CASE IS <= 10
		 TFLAG:97 ++
	CASE 11 TO 20
	CASE 21 TO 50
		TFLAG:97 -= 1
	CASE 51 TO 100
		TFLAG:97 -= 3
	CASE 101 TO 300
		TFLAG:97 -= 5
	CASEELSE
		TFLAG:97 -= 10
ENDSELECT
[SKIPEND]
;助手调教中
SIF ASSIPLAY == 1
	TFLAG:97 = 0
;强行
SIF TCVAR:ARG:强行
	TFLAG:97 = MIN(0,TFLAG:97 * -3)
;对象が眠っていたら上升值0
SIF !SHIRAHU(ARG)
	TFLAG:97 = 0
;好感度が信赖度の4倍を超えると上升值が低下　ただし约会中は例外
;好感上升界限 被移除
;IF CFLAG:ARG:2 > 500 && CFLAG:ARG:好感度 > CFLAG:ARG:信赖度 * 4
;	信赖度キャップ补正 = CFLAG:ARG:好感度
;	SIF FLAG:约会的对象 == ARG
;		信赖度キャップ补正 /= 2
;	IF TALENT:ARG:恋慕
;		TFLAG:97 = GET_REVISION(CFLAG:ARG:信赖度 * 40, TFLAG:97,信赖度キャップ补正)
;	ELSEIF TALENT:ARG:思慕
;		TFLAG:97 = GET_REVISION(CFLAG:ARG:信赖度 * 30, TFLAG:97,信赖度キャップ补正)
;	ELSE
;		TFLAG:97 = GET_REVISION(CFLAG:ARG:信赖度 * 20, TFLAG:97,信赖度キャップ补正)
;	ENDIF
;ENDIF
;同时に绝顶
SIF SYNCED_ORGASM(ARG)
	TFLAG:97 = TFLAG:97 * 3 / 2
;对方主导诶嘿嘿
SIF CFLAG:诶嘿嘿 == 2
	TFLAG:97 = TFLAG:97 * 6 / 5
;路人
SIF TALENT:ARG:路人 == 1
	CALL YUKIZURI_BONUS(ARG)
;好感度减少
SIF TFLAG:好感度减少 && TFLAG:97 > 0
	TFLAG:97 = TFLAG:97 * -1 *TFLAG:好感度减少
SIF TFLAG:好感度BOUNS && TFLAG:97 > 0
	;TFLAG:97 = TFLAG:97 * TFLAG:好感度BOUNS
	TFLAG:97 = TFLAG:97 * (100 + TFLAG:好感度BOUNS) / 100

;好感度上升が低い场合、据点住人であり路人が同じ场所にいればBOUNS
SIF TFLAG:97 < 3 && WITH_MOB() && MAP_住人(MAIN_MAP,ARG)
	TFLAG:97 ++
;指令失败した场合好感度は上がらない
SIF TFLAG:98 < 0 || TFLAG:好感度没有取得
	TFLAG:97 = MIN(0,TFLAG:97)

SIF TFLAG:3 == 300 && TFLAG:193 < 0
	TFLAG:97 = 0

;烂醉奸バレ时は问答无用で好感度大幅低下
SIF TCVAR:ARG:烂醉奸バレ
	TFLAG:97 -= MAX(30 - ABL:ARG:亲密, 0) * 3

SIF FLAG:好感度上升率 > 0
	TFLAG:97 = TFLAG:97 * (FLAG:好感度上升率 + TALENT:94 * 2)

;好感度に上升值を加算
CFLAG:ARG:2 += TFLAG:97

;好感度は0以下にも100万以上にもならない
CFLAG:ARG:2 = LIMIT(CFLAG:ARG:2, 0, 1000000)
LOCAL:1 = 0
FOR LOCAL,0,100
	SIF SOURCE:ARG:LOCAL
		LOCAL:1 ++
NEXT
SIF !LOCAL:1
	TFLAG:97 = 0

;好感度表示
SIF TFLAG:97
	PRINTFORML \@(TFLAG:97 >= 0) ? 好感度＋{TFLAG:97} # 好感度－{-TFLAG:97}\@
;好感度上升值合算
FLAG:12 += TFLAG:97

@YUKIZURI_BONUS(ARG)
TFLAG:97 *= 10
SIF CFLAG:ARG:以身体为目的
	TFLAG:97 *= 2
LOCAL = GETPALAMLV(PALAM:ARG:好意,10) - 1
CALL YUKIZURI_BONUS_1(ARG, "亲密", LOCAL)

LOCAL = GETPALAMLV(PALAM:ARG:习得,3)
CALL YUKIZURI_BONUS_1(ARG, "技巧", LOCAL)

LOCAL = MAX(GETPALAMLV(PALAM:ARG:恭顺,3), GETPALAMLV(PALAM:ARG:屈服,3))
CALL YUKIZURI_BONUS_1(ARG, "顺从", LOCAL)

LOCAL = GETPALAMLV(PALAM:ARG:恭顺,3) - 1
CALL YUKIZURI_BONUS_1(ARG, "侍奉精神", LOCAL)


@YUKIZURI_BONUS_1(C_ID, CHK_ABL, ARG)
#DIM C_ID
#DIMS CHK_ABL

SIF !ARG
	RETURN
SIF ABL:C_ID:CHK_ABL >= ARG
	RETURN
LOCAL = FINDELEMENT(ABLNAME, CHK_ABL)
CALL COLORMESSAGE(@"%ABLNAME:LOCAL%上升到{ARG}",C_YELLOW,2,1)
ABL:C_ID:CHK_ABL = ARG