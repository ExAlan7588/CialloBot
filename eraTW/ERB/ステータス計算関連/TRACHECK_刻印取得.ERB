
;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;刻印取得のチェック
;CALL		USER
;ARG		0:角色NO
;RETURN		VOID
;COMMENT	21/08/27 反抗刻印取得仕样を一部变更
;-----------------------------------------------------------
@MARK_GOT_CHECK(ARG)
#DIM 刻印取得值
;-------------------------------------------------
;苦痛刻印
;-------------------------------------------------
IF CUP:ARG:苦痛 >= 2000 && CUP:ARG:苦痛 < 4000 && MARK:ARG:苦痛刻印 <= 0
	MARK:ARG:苦痛刻印 = 1
	TFLAG:苦痛刻印取得 = 1
ELSEIF CUP:ARG:苦痛 >= 4000 && CUP:ARG:苦痛 < 8000 && MARK:ARG:苦痛刻印 <= 1
	MARK:ARG:苦痛刻印 = 2
	TFLAG:苦痛刻印取得 = 2
	;坚强・感情缺乏の场合は苦痛刻印による顺从UPは无
	IF !ABL:ARG:顺从 && TALENT:ARG:胆量 > 0 && !TALENT:ARG:感情缺乏
		ABL:ARG:顺从 = 1
		TFLAG:刻印顺从变化 = 1
	ENDIF
ELSEIF CUP:ARG:苦痛 >= 8000 && MARK:ARG:苦痛刻印 <= 2
	MARK:ARG:苦痛刻印 = 3
	TFLAG:苦痛刻印取得 = 3
	;坚强・感情缺乏の场合は苦痛刻印による顺从UPは无
	IF !ABL:ARG:顺从 && TALENT:ARG:胆量 > 0 && !TALENT:ARG:感情缺乏
		ABL:ARG:顺从 = 1
		TFLAG:刻印顺从变化 = 1
	ENDIF
	IF TALENT:PLAYER:施虐狂
		EXP:ARG:异常经验 += 2
	ELSEIF TALENT:PLAYER:擅长用针 && SELECTCOM == 103
		EXP:ARG:异常经验 += 2
	ELSE
		EXP:ARG:异常经验 += 1
	ENDIF
ENDIF
;-------------------------------------------------
;时奸刻印
;-------------------------------------------------
IF TCVAR:ARG:时奸刻印取得用 == 1 && MARK:ARG:时奸刻印 <= 0
	MARK:ARG:时奸刻印 = 1
	TFLAG:时奸刻印取得 = 1
ELSEIF TCVAR:ARG:时奸刻印取得用 == 2 && MARK:ARG:时奸刻印 <= 1
	MARK:ARG:时奸刻印 = 2
	TFLAG:时奸刻印取得 = 2
ELSEIF TCVAR:ARG:时奸刻印取得用 == 3 && MARK:ARG:时奸刻印 <= 2
	MARK:ARG:时奸刻印 = 3
	TFLAG:时奸刻印取得 = 3
ENDIF
;-------------------------------------------------
;快乐刻印
;-------------------------------------------------
IF TCVAR:ARG:快乐强度 == 1 && MARK:ARG:快乐刻印 <= 0
	MARK:ARG:快乐刻印 = 1
	TFLAG:23 = 1
ELSEIF TCVAR:快乐强度 == 2 && MARK:ARG:快乐刻印 <= 1
	MARK:ARG:快乐刻印 = 2
	TFLAG:23 = 2
ELSEIF TCVAR:快乐强度 == 3 && MARK:ARG:快乐刻印 <= 2
	MARK:ARG:快乐刻印 = 3
	TFLAG:23 = 3
	;自制心・感情缺乏の场合は快乐刻印による顺从UPは无
	IF !ABL:ARG:顺从 && !TALENT:ARG:自制心 && !TALENT:ARG:感情缺乏
		ABL:ARG:顺从 = 1
		TFLAG:刻印顺从变化 = 1
	ENDIF
;ELSEIF  TCVAR:快乐强度 == 3 && TALENT:幼稚 && !TALENT:初潮
;		TALENT:初潮 = 1
;		TFLAG:23 = 4
ENDIF
;-------------------------------------------------
;屈服刻印
;-------------------------------------------------
IF FLAG:70 == 1 || CFLAG:ARG:恶作剧
ELSE
[SKIPSTART]
	A = CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ｂ + CUP:ARG:快Ａ

	;快乐によるLvUPは10番+取得した屈服刻印のレベルのTFLAGを与える
	IF A >= 3000 && A < 5000 && MARK:ARG:快乐刻印 > MARK:ARG:屈服刻印 && MARK:ARG:屈服刻印 <= 0
		MARK:ARG:屈服刻印 = 1
		TFLAG:24 = 11
	ELSEIF A >= 5000 && A < 9000 && MARK:ARG:快乐刻印 > MARK:ARG:屈服刻印 && MARK:ARG:屈服刻印 <= 1
		MARK:ARG:屈服刻印 = 2
		TFLAG:24 = 12
		;感情缺乏の场合は屈服刻印による顺从UPは无
		IF !ABL:ARG:顺从 && !TALENT:ARG:感情缺乏
			ABL:ARG:顺从 = 1
			TFLAG:刻印顺从变化 = 1
		ENDIF
	ELSEIF A >= 9000 && MARK:ARG:快乐刻印 > MARK:ARG:屈服刻印 && MARK:ARG:屈服刻印 <= 2
		MARK:ARG:屈服刻印 = 3
		TFLAG:24 = 13
		;感情缺乏の场合は屈服刻印による顺从UPは无
		IF ABL:ARG:顺从 <= 1 && !TALENT:ARG:感情缺乏
			ABL:ARG:顺从 = 2
			TFLAG:刻印顺从变化 = 1
		ENDIF
	ENDIF
	A = CUP:ARG:苦痛 + CUP:ARG:恐怖

	;苦痛と恐怖によるLvUPは20番+取得した屈服刻印のレベルのTFLAGを与える
	IF A >= 1500 && A < 2400 && MARK:ARG:屈服刻印 <= 0
		MARK:ARG:屈服刻印 = 1
		TFLAG:24 = 21
	ELSEIF A >= 2400 && A < 6000 && MARK:ARG:屈服刻印 <= 1
		MARK:ARG:屈服刻印 = 2
		TFLAG:24 = 22
		;感情缺乏の场合は屈服刻印による顺从UPは无
		IF !ABL:ARG:顺从 && !TALENT:ARG:感情缺乏
			ABL:ARG:顺从 = 1
			TFLAG:刻印顺从变化 = 1
		ENDIF
	ELSEIF A >= 6000 && MARK:ARG:屈服刻印 <= 2
		MARK:ARG:屈服刻印 = 3
		TFLAG:24 = 23
		;感情缺乏の场合は屈服刻印による顺从UPは无
		IF ABL:ARG:顺从 <= 1 && !TALENT:ARG:感情缺乏
			ABL:ARG:顺从 = 2
			TFLAG:刻印顺从变化 = 1
		ENDIF
	ENDIF
[SKIPEND]

A = CUP:ARG:恭顺 + CUP:ARG:屈服

;恭顺と屈服によるLvUPは30番+取得した屈服刻印のレベルのTFLAGを与える
IF A >= 4500 && A < 7000 && MARK:ARG:屈服刻印 <= 0
	MARK:ARG:屈服刻印 = 1
	TFLAG:24 = 31
ELSEIF A >= 7000 && A < 10000 && MARK:ARG:屈服刻印 <= 1
	MARK:ARG:屈服刻印 = 2
	TFLAG:24 = 32
	;感情缺乏の场合は屈服刻印による顺从UPは无
	IF !ABL:ARG:顺从 && !TALENT:ARG:感情缺乏
		ABL:ARG:顺从 = 1
		TFLAG:刻印顺从变化 = 1
	ENDIF
ELSEIF A >= 10000 && MARK:ARG:屈服刻印 <= 2
	MARK:ARG:屈服刻印 = 3
	TFLAG:24 = 33
	;感情缺乏の场合は屈服刻印による顺从UPは无
	IF ABL:ARG:顺从 <= 1 && !TALENT:ARG:感情缺乏
		ABL:ARG:顺从 = 2
		TFLAG:刻印顺从变化 = 1
	ENDIF
ENDIF

;-------------------------------------------------
;反抗刻印
;------------------------------------------------- 
;MASTERは除外
SIF TALENT:ARG:反抗抑制
	RETURN 0
SIF CFLAG:ARG:恶作剧
	RETURN 0
SIF ARG == MASTER
	RETURN 0
SIF TFLAG:150
	RETURN 0
刻印取得值 = CUP:ARG:反感+CUP:ARG:不快
;反抗履历残している场合、少しだけ反抗刻印を付きづらくする
;同レベル以下の刻印が付かないだと强すぎるので、刻印レベルを１段阶引き下げに
SIF MARK:ARG:反抗取得履历
		刻印取得值 = INRANGE(刻印取得值, 500, 700) ? 0 # 刻印取得值
SIF MARK:ARG:反抗取得履历 > 1
		刻印取得值 = INRANGE(刻印取得值, 1001, 1300) ? 1000 # 刻印取得值
SIF MARK:ARG:反抗取得履历 > 2
		刻印取得值 = INRANGE(刻印取得值, 2001, 2500) ? 2000 # 刻印取得值
SELECTCASE 刻印取得值
	CASE 500 TO 1000
		IF MARK:ARG:反抗刻印 < 1
			MARK:ARG:反抗刻印 = 1
			MARK:ARG:反抗取得履历 = MAX(MARK:ARG:反抗取得履历,1)
			TFLAG:反抗刻印取得 = 1
			CFLAG:ARG:心情不快 = 1
			CFLAG:ARG:勃然大怒 = 1
			TALENT:ARG:心情 = -1
		ENDIF
	CASE 1001 TO 2000
		IF MARK:ARG:反抗刻印 < 2
			MARK:ARG:反抗刻印 = 2
			TFLAG:反抗刻印取得 = 2
			MARK:ARG:反抗取得履历 = MAX(MARK:ARG:反抗取得履历,2)
			CFLAG:ARG:心情不快 = 2
			CFLAG:ARG:勃然大怒 = 1
			TALENT:ARG:心情 = -1
			IF MARK:ARG:反抗取得履历 < 2 || 刻印取得值 >= 1200
				;感情缺乏の场合は反抗刻印による顺从ダウンは无
				IF INRANGE(ABL:ARG:顺从,1,5) && !TALENT:ARG:感情缺乏
					ABL:顺从 = MAX(ABL:顺从 - 1, 0)
					ABL:亲密 = MAX(ABL:亲密 - 1, 0)
					TFLAG:刻印顺从变化 = 1
				ELSEIF ABL:ARG:顺从 > 5 && !TALENT:ARG:感情缺乏
					ABL:顺从 = MAX(ABL:顺从 - 1, 0)
					TFLAG:刻印顺从变化 = 2
				ENDIF
			ENDIF
		ENDIF
	CASE IS > 2000
		IF MARK:ARG:反抗刻印 < 3
			MARK:ARG:反抗刻印 = 3
			TFLAG:反抗刻印取得 = 3
			MARK:ARG:反抗取得履历 = MAX(MARK:ARG:反抗取得履历,3)
			CFLAG:ARG:心情不快 = 3
			CFLAG:ARG:勃然大怒 = 1
			TALENT:ARG:心情 = -1
			IF MARK:ARG:反抗取得履历 < 3 || 刻印取得值 >= 2400
				;感情缺乏の场合は反抗刻印による顺从ダウンは无
				IF INRANGE(ABL:ARG:顺从,1,5) && !TALENT:ARG:感情缺乏
					ABL:顺从 = MAX(ABL:顺从 - 2, 0)
					ABL:亲密 = MAX(ABL:亲密 - 2, 0)
					TFLAG:刻印顺从变化 = 1
				ELSEIF ABL:ARG:顺从 > 5 && !TALENT:ARG:感情缺乏
					ABL:顺从 = MAX(ABL:顺从 - 1, 0)
					ABL:亲密 = MAX(ABL:亲密 - 1, 0)
					TFLAG:刻印顺从变化 = 2
				ENDIF
			ENDIF
		ENDIF
ENDSELECT
ENDIF
;警告牌发行チェック
SIF !TALENT:ARG:恋慕 || RAND:(ABL:ARG:亲密 + 1) == 0 || MARK:ARG:反抗刻印 == 3 && FLAG:18
	CALL CARD_GOT_CHECK(ARG)
TFLAG:150 = 0