;FileName_TRACHECK.ERB ----------------------------- Rev1.00
;处女丧失时の处理
;CALL		USER
;ARG		0:角色NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@LOST_VIRGIN(ARG)
#DIM DYNAMIC 丧失者No
#DIMS DYNAMIC 丧失者名
#DIM DYNAMIC 取得者No
#DIMS DYNAMIC 取得者名
#DIMS 修饰语
;-------------------------------------------------
;处女丧失のチェック
;-------------------------------------------------
IF TALENT:ARG:处女 == 1 && EXP_UP(1,ARG) > 0 && !TFLAG:2 && GETBIT(TALENT:ARG:性别,0) ;まずＶがないと！みんな有るけど…念の为。

	修饰语 = ""
	IF ARG == MASTER
		CFLAG:ARG:处女丧失互动对象 = TARGET
		丧失者No = MASTER
		丧失者名 = %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
		取得者No = MASTER_POSE(SET_P,SET_V)
		;Ｐ以外で丧失した场合、MASTER_POSE(SET_P,SET_V)は0となり、结果的に取得者名まで你になってしまう
		;よって取得者No=0ならTARGETを代入
		SIF !取得者No
			取得者No = TARGET
		取得者名 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 取得者No)%
	ELSE
		CFLAG:ARG:处女丧失互动对象 = PLAYER
		丧失者名 = %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%
		丧失者No = ARG
		取得者名 = %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
		取得者No = MASTER
	ENDIF
	IF ARG != MASTER
		;贞操重视
		IF TALENT:ARG:贞操 > 0
			IF !TALENT:ARG:恋慕
				TIMES SOURCE:ARG:情爱 , 0.60
				TIMES SOURCE:ARG:反感 , 10.00
			ENDIF
		;不在乎贞操
		ELSEIF TALENT:ARG:贞操 < 0
			TIMES SOURCE:ARG:情爱 , 0.60
			TIMES SOURCE:ARG:反感 , 0.50
		ELSEIF TALENT:ARG:无知
			TIMES SOURCE:ARG:情爱 , 0.30
			TIMES SOURCE:ARG:反感 , 0.75
			SOURCE:ARG:恐怖 += 3000
		ENDIF
		IF CFLAG:ARG:诶嘿嘿 == 2
			TIMES SOURCE:ARG:情爱 , 1.50
			TIMES SOURCE:ARG:反感 , 0.25
		ENDIF
	ENDIF
	SETCOLOR 247,171,166
	PRINTFORMW 【处女丧失】
	IF TEQUIP:ARG:50 == MASTER && GROUPMATCH(TALENT:MASTER:形状,5,6)
		PRINTFORMW %取得者名%那凶恶的肉棒、毫不留情地撕裂了%丧失者名%的纯洁……
		TIMES SOURCE:ARG:苦痛, 3.00
		TIMES SOURCE:ARG:恐怖, 3.00
	ENDIF
	IF FLAG:70
		IF ARG == MASTER
			PRINTFORMW %取得者名%在对方无意识的情况下、夺走了\@ TALENT:ARG:路人 == 1 ? 刚遇到的# \@%丧失者名%的处女……
		ELSE
			PRINTFORMW %丧失者名%在无意识的情况下、被\@ TALENT:ARG:路人 == 1 ? 刚遇到的# \@%取得者名%夺走了处女……
		ENDIF
	ELSE
		;陷落素質による处理
		;MASTER丧失
		IF ARG == MASTER
			IF TCVAR:TARGET:烂醉
				PRINTFORMW 意识模糊的%取得者名%、把%丧失者名%的处女夺走了……
			;睡眠奸、お前がパパになるんだよ！
			ELSEIF CFLAG:TARGET:睡眠
				PRINTFORMW 熟睡中的%取得者名%、被迫夺走了%丧失者名%的处女
			;无知ふたろり（ショタ）で处女丧失とか最高……いや、再考
			ELSEIF TALENT:TARGET:无知
				;IF GROUPMATCH(TALENT:MASTER:形状,5,6)
				;无知な相手のちんちんが入ってる场合
				IF TEQUIP:MASTER:50 == 取得者No
					PRINTFORMW 缺乏性知识的%取得者名%不知道自己被做了什么、对\@ TALENT:取得者No:2 & 2 ?强烈的快感和 # \@%丧失者名%流出的鲜血而感到害怕……
				;ちんちん以外
				ELSE
					PRINTFORMW 看到破瓜血的%取得者名%、以为自己弄伤了%丧失者名%而慌了手脚
				ENDIF
				PRINTFORMW %丧失者名%一边强忍着破瓜的疼痛、一边让%取得者名%冷静下来
			;素質恋人
			ELSEIF TALENT:TARGET:恋人  || TALENT:TARGET:恋慕 > 1
				PRINTFORMW %取得者名%入迷地看着从%丧失者名%秘处流出的鲜血……
				TFLAG:150 = 2
				TIMES SOURCE:TARGET:情爱 , 3.00
				TIMES SOURCE:TARGET:反感 , 0.10
			;恋慕
			ELSEIF TALENT:TARGET:恋慕
				PRINTFORMW %取得者名%露出了开心的笑容、凝视着献出处女的%丧失者名%……
				TFLAG:150 = 2
				TIMES SOURCE:TARGET:情爱 , 2.00
				TIMES SOURCE:TARGET:反感 , 0.30
				EXP:TARGET:爱情经验 += 30
				CFLAG:ARG:处女丧失目标对象 |= 丧失_恋慕
			ELSEIF TALENT:TARGET:思慕
				IF TCVAR:TARGET:强行 == 1
					PRINTFORMW 在%取得者名%对快感不知所措的时候、%丧失者名%含着泪看了过来……
					TIMES SOURCE:TARGET:情爱 , 0.80
					TIMES SOURCE:TARGET:反感 , 1.50
					CFLAG:ARG:处女丧失目标对象 |= 丧失_强行
				ELSEIF TALENT:TARGET:贞操 >= 0
					PRINTFORMW %取得者名%虽然很困惑、但总觉得很开心似的看着%丧失者名%……
					TIMES SOURCE:TARGET:情爱 , 1.30
					TIMES SOURCE:TARGET:反感 , 0.50
				ENDIF
				CFLAG:ARG:处女丧失目标对象 |= 丧失_思慕
			ELSE
				IF TCVAR:TARGET:强行 == 1
					TIMES SOURCE:TARGET:情爱 , 0.10
					TIMES SOURCE:TARGET:反感 , 3.00
					PRINTFORMW %取得者名%用混杂了愤怒和困惑的表情、瞪着%丧失者名%……
					CFLAG:ARG:处女丧失目标对象 |= 丧失_强行
				ELSE
					PRINTFORMW %取得者名%露出想说什么的表情、盯着和%丧失者名%的结合处……
				ENDIF
			ENDIF
		;MASTERがTARGETの膣に插入している
		ELSEIF TEQUIP:ARG:50 == MASTER
			IF TCVAR:ARG:烂醉
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%在意识混沌的时候被夺走了处女……
			;睡眠奸　良い子は沉浸在梦乡中间にママにしちゃおうね～
			ELSEIF CFLAG:ARG:睡眠
				PRINTFORMW %丧失者名%的第一次在睡着的时候被夺走了……
			ELSEIF TALENT:ARG:无知
				PRINTFORMW 性知识匮乏的%丧失者名%不明白\@ TCVAR:ARG:误爆插入フラグ ? 发生了什么 # 被做了什么\@，在处女失去的痛苦中感到困惑和害怕...
				PRINTFORMW %取得者名%让%丧失者名%平静了下来
			;亲爱かつ相手が主人(现在亲爱がないので、恋人で代用
			ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕 > 1
				SIF TCVAR:ARG:误爆插入フラグ
					PRINT 事故虽然是导火索，但是
				PRINTFORMW %丧失者名%因为能让%取得者名%成为第一次的对象而感到无上的幸福感的样子……
				TFLAG:150 = 2
				TIMES SOURCE:ARG:情爱 , 3.00
				TIMES SOURCE:ARG:反感 , 0.10
				 CFLAG:ARG:处女丧失目标对象 |= 丧失_恋慕
			;恋慕かつ相手が主人
			ELSEIF TALENT:ARG:恋慕
				SIF TCVAR:ARG:误爆插入フラグ
					PRINT 事故虽然是导火索，但是
				PRINTFORMW %丧失者名%因为把处女献给了%取得者名%而感到幸福的样子……
				TFLAG:150 = 2
				TIMES SOURCE:ARG:情爱 , 2.00
				TIMES SOURCE:ARG:反感 , 0.30
				EXP:ARG:爱情经验 += 30
				CFLAG:ARG:处女丧失目标对象 |= 丧失_恋慕
			ELSEIF TALENT:ARG:思慕
				IF TCVAR:ARG:强行 == 1
					PRINTFORMW 被怀有好感的对象硬上了的%丧失者名%、眼泪如雨点般开始洒落下来……
					TIMES SOURCE:ARG:情爱 , 0.80
					TIMES SOURCE:ARG:反感 , 1.50
					CFLAG:ARG:处女丧失目标对象 |= 丧失_强行
				ELSEIF TCVAR:ARG:误爆插入フラグ
					PRINTFORMW 虽然怀有好意，但%丧失者名%稍微后悔在事故中将贞操奉献给了%取得者名%......
					TIMES SOURCE:ARG:情爱 , 1.30
					TIMES SOURCE:ARG:反感 , 0.80
				ELSEIF TALENT:ARG:贞操 >= 0
					PRINTFORMW 尽管怀有好感、%丧失者名%还是对被%取得者名%夺去贞操感到不知如何是好……
					TIMES SOURCE:ARG:情爱 , 1.30
					TIMES SOURCE:ARG:反感 , 0.80
				ENDIF
				CFLAG:ARG:处女丧失目标对象 |= 丧失_思慕
			ELSE
				IF TCVAR:ARG:强行 == 1
					TIMES SOURCE:ARG:情爱 , 0.10
					TIMES SOURCE:ARG:反感 , 3.00
					IF CFLAG:ARG:败犬
						PRINTFORMW %丧失者名%作为败北的代价、被%取得者名%夺走了处女……
					ELSE
						PRINTFORMW %丧失者名%的反抗徒劳无功、被%取得者名%夺走了处女……
					ENDIF
					CFLAG:ARG:处女丧失目标对象 |= 丧失_强行
					IF TALENT:ARG:路人 == 1
						CSTR:ARG:路人子PROFILE２ = %REPLACE(@"%CSTR:ARG:路人子PROFILE２%","现在已经老实了","、处女被夺走了")%
						CSTR:ARG:路人子PROFILE２ = %REPLACE(@"%CSTR:ARG:路人子PROFILE２%","肛门处女被夺走了","处女被夺走了")%
					ENDIF
				ELSEIF TALENT:ARG:路人 == 1
					PRINTFORMW 看到秘处流下的鲜血、%丧失者名%意识到自己把处女献给了刚认识不久的%取得者名%、有些后悔……
				ELSEIF TCVAR:ARG:误爆插入フラグ
					PRINTFORMW %丧失者名%不小心将处女之身献给了%取得者名%......
				ELSE
					PRINTFORMW %丧失者名%半推半就地被%取得者名%夺走了处女……
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	IF CSTR:ARG:处女丧失履历 == ""
		IF GROUPMATCH(TCVAR:丧失者No:Counter行动, 80, 83, 85)
			CALL SET_HISTORY_LOST_V(丧失者No, 取得者No, "阴茎")
		ELSEIF TCVAR:取得者No:Counter行动 == 57
			CALL SET_HISTORY_LOST_V(丧失者No, 取得者No, "指")
		ELSEIF SELECTCOM == 44
			CALL SET_HISTORY_LOST_V(丧失者No, 取得者No, "振动棒")
		ELSE
			CALL SET_HISTORY_LOST_V(丧失者No, 取得者No, "阴茎")
		ENDIF
	ENDIF
ENDIF
RESETCOLOR
;-------------------------------------------------
;再生处女丧失のチェック
;-------------------------------------------------
IF TALENT:ARG:处女 == 2 && EXP_UP(1,ARG)
	TCVAR:ARG:破瓜 = 2
	;视频拍摄
	IF TEQUIP:28
		;性交
		IF TEQUIP:ARG:50
			LOCALS = LV1/
		ELSEIF TEQUIP:ARG:13
			LOCALS = LV3/
		;其他
		ELSE
			LOCALS = LV4/
		ENDIF
		TSTR:1 += LOCALS
	ENDIF
	PRINTW 再生处女丧失
ENDIF
;-------------------------------------------------
;无自觉处女丧失のチェック
;-------------------------------------------------
IF TALENT:ARG:处女 == -1 && EXP_UP(1,ARG) && CFLAG:ARG:292 == 1 && !FLAG:70 && !TCVAR:ARG:烂醉 && !CFLAG:MASTER:恶作剧
	TCVAR:ARG:破瓜 = -1
	CFLAG:ARG:292 = 0
	PRINTW 处女丧失？
	SETCOLOR 247,171,166
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%对自己没有感受到痛苦而感到困惑…
	RESETCOLOR
ENDIF

@童贞无接吻经验丧失(ARG)
#DIM シチュエーション
;シチュエーション	 0:通常
;					 1:约会归り
;					 2:自慰暴露
;					 4:阴茎に
;					 8:自发的に
;					16:无自觉
;					32:买春
シチュエーション = 0
VARSET LOCALS
FONTBOLD
SETCOLOR 255,100,255
IF !TALENT:ARG:非童贞 && TALENT:ARG:性别 & 2
	IF TCVAR:ARG:童贞丧失 == 1
		PRINTFORMW 童贞丧失(%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%)
		TALENT:ARG:非童贞 = 1
	ENDIF
ENDIF

;无接吻经验丧失
IF ARG == MASTER && TALENT:ARG:无接吻经验 && GROUPMATCH(SELECTCOM,2,20,312)
	シチュエーション |= 8
	IF GROUPMATCH(SELECTCOM,20,312) && (TFLAG:193 != 99 && PREVCOM != 309)
		CALL SET_FIRSTKISS(MASTER, ARG, シチュエーション)
	ELSEIF GROUPMATCH(SELECTCOM,2) && TALENT:TARGET:性别 & 2
		シチュエーション |= 4
		CALL SET_FIRSTKISS(ARG, TARGET, シチュエーション)
	ENDIF
ELSEIF TALENT:MASTER:无接吻经验 && GROUPMATCH(TCVAR:ARG:Counter行动,30,39,60,71,91)
	IF GROUPMATCH(TCVAR:ARG:Counter行动,30,39,60)
		CALL SET_FIRSTKISS(MASTER, ARG, シチュエーション)
	ELSEIF GROUPMATCH(TCVAR:ARG:Counter行动,71,91) && TALENT:ARG:性别 & 2
		シチュエーション |= 4
		CALL SET_FIRSTKISS(MASTER, ARG, シチュエーション)
	ENDIF
ENDIF
IF TALENT:ARG:无接吻经验 && (TCVAR:ARG:ＦＫ丧失 || GROUPMATCH(TCVAR:ARG:Counter行动,30,39,60,51))
	;无自觉
	SIF CFLAG:ARG:睡眠 || FLAG:70
		シチュエーション |= 16
	;自发的に
	SIF GROUPMATCH(TCVAR:ARG:Counter行动,30,39,60,51)
		シチュエーション |= 8
	;阴茎に
	SIF TCVAR:ARG:ＦＫ丧失 == 2 || GROUPMATCH(TCVAR:ARG:Counter行动,51)
		シチュエーション |= 4

	IF GROUPMATCH(SELECTCOM,20,312) && (TFLAG:193 != 99 && PREVCOM != 309)
		;デコチューを除外する
		CALL SET_FIRSTKISS(ARG, MASTER, シチュエーション)
	ELSEIF GROUPMATCH(SELECTCOM,81,88,140,337) || GROUPMATCH(TCVAR:ARG:Counter行动,30,60,51)
		CALL SET_FIRSTKISS(ARG, MASTER, シチュエーション)
	ELSEIF TCVAR:ARG:Counter行动 == 39
		CALL SET_FIRSTKISS(ARG, MASTER, シチュエーション, "糖と一绪に")
	ENDIF
ENDIF

;肛门处女丧失
IF CFLAG:ARG:肛门处女丧失目标对象 & 丧失_时间停止 || !CFLAG:ARG:肛门处女丧失纪念日
	IF TCVAR:ARG:Ａ丧失发生 
		;时间停止中に経験済み
		IF !FLAG:70 && ARG && CFLAG:ARG:肛门处女丧失目标对象 & 丧失_时间停止
			SIF FIRSTTIME("Ａ破瓜",0)
				PRINTFORMW 肛门处女丧失？(%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%)
			TCVAR:ARG:Ａ破瓜 = -1
		;完全に初めて
		ELSEIF !CFLAG:ARG:肛门处女丧失纪念日
			PRINTFORMW 肛门处女丧失(%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%)
			IF TEQUIP:ARG:51 == MASTER && GROUPMATCH(TALENT:MASTER:形状,5,6,10,11)
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%那凶恶的肉棒、毫不留情地开通了%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%紧闭的后庭……
				TIMES SOURCE:ARG:苦痛, 3.00
				TIMES SOURCE:ARG:恐怖, 3.00
				CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_凶恶
			ENDIF
			IF ARG != MASTER && !GROUPMATCH(SELECTCOM,45,46)
				IF CFLAG:ARG:睡眠
					PRINTFORMW 趁%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%睡着正香的时候，%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%夺走了%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的肛门处女……
					CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_睡眠
				ELSEIF FLAG:时间停止
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%在无意识的情况下，被\@ TALENT:ARG:路人 == 1 ? 刚遇到的# \@%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%夺走了肛门处女……
					CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_时间停止
				ELSEIF TALENT:ARG:无知
					PRINTFORMW 性知识匱乏的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%完全不知道自己在被做什么、只是对排泄的后穴被插入的事感到了困惑与慌张……
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%安慰起了%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%。
					CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_无知
				ELSEIF TALENT:ARG:思慕
					IF TCVAR:ARG:强行 == 1
					PRINTFORMW 被怀有好感的对象硬上了的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%，强忍着被插入的不适感流下了泪水……
					TIMES SOURCE:ARG:情爱 , 0.80
					TIMES SOURCE:ARG:反感 , 1.50
					CFLAG:ARG:处女丧失目标对象 |= 丧失_强行
					ELSEIF TALENT:ARG:贞操 >= 0
						PRINTFORMW 虽然怀有好感、但%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%还是对被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%夺去第一次而感到些许无措……
						TIMES SOURCE:ARG:情爱 , 1.30
						TIMES SOURCE:ARG:反感 , 0.80
					ENDIF
					CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_思慕
				ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%因为能让%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%成为第一次的对象而感到十分幸福……
					TIMES SOURCE:ARG:情爱 , 3.00
					TIMES SOURCE:ARG:反感 , 0.10
					EXP:ARG:爱情经验 += 20
					CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_恋慕
				ELSEIF TCVAR:ARG:烂醉
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%在意识混沌之中被夺去了肛门处女……
					CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_烂醉
				ELSEIF TCVAR:ARG:强行
				IF CFLAG:ARG:败犬
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%作为败北的代价、被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%夺走了肛门处女……
				ELSE
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的抵抗毫无作用、被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%夺走了肛门处女……
				ENDIF
				CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_强行
				ELSE
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%半推半就地被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%夺走了肛门处女……
				ENDIF
			ENDIF
			IF CSTR:ARG:路人子PROFILE２ != ""
				CSTR:ARG:路人子PROFILE２ = %REPLACE(@"%CSTR:ARG:路人子PROFILE２%","现在已经老实了","、肛门处女被夺走了")%
			ENDIF
			IF SELECTCOM == 45
				LOCALS = 肛用振动棒
				CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_振动棒
			ELSEIF SELECTCOM == 46
				LOCALS = 后庭拉珠
				CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_振动棒
			ELSE
				LOCALS = 阴茎
				CFLAG:ARG:肛门处女丧失目标对象 |= 丧失_阴茎
			ENDIF
			IF !FLAG:70
				TCVAR:ARG:Ａ破瓜 = 1
			ELSE
				TCVAR:ARG:Ａ破瓜 = -2
			ENDIF
			IF ARG == TARGET || (CFLAG:MASTER:诶嘿嘿 == 2 && ARG != TARGET && ARG != MASTER)
				CALL SET_HISTORY_LOST_A(ARG, PLAYER, @"%LOCALS%")
			ELSE
				CALL SET_HISTORY_LOST_A(ARG, TARGET, @"%LOCALS%")
			ENDIF
			
		ENDIF
	ENDIF
ENDIF
FONTREGULAR
RESETCOLOR

@LOST_VIRGIN_STOP(ARG)
SIF !SHIRAHU(ARG)
	RETURN
IF CFLAG:ARG:破瓜中止口上有
	;口上侧から干渉可能に
	CALL KOJO_MESSAGE_SEND("LOST_VIRGIN_STOP", 0, ARG)
	IF RESULT == 1
		RETURN -1
	ELSE
		RETURN 0
	ENDIF
	RESETCOLOR
ELSEIF !TALENT:ARG:无知 && !TALENT:ARG:恋慕 && !TALENT:ARG:爱欲 && !TALENT:ARG:恋人 && !TCVAR:ARG:不耐烦 && !CFLAG:恶作剧
;贞操重视持ちor贞操普通かつ思慕无
	IF TALENT:ARG:贞操 > 0 || (!TALENT:ARG:贞操 && !TALENT:ARG:思慕)
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%表示这种事还是等一段时间再考虑吧
		IF (EXP:ARG:60 + EXP:ARG:61 + EXP:ARG:62 + EXP:ARG:63 + EXP:ARG:64) == 0
			IF TALENT:ARG:贞操 > 0
				PRINTFORMW 果然持身甚紧呢……
			ELSE
				PRINTFORMW 意外坚固的防线呢……
			ENDIF
		ELSE
			PRINTFORMW 麻烦的臭女人……
		ENDIF
		CALL ASK_YN("放弃","不收自己靠过来的女人是男人的耻辱")
		IF RESULT == 1
			IF TALENT:ARG:贞操 > 0 && ABL:ARG:战斗能力 > ABL:MASTER:战斗能力 && !TCVAR:ARG:强行
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%虽然想霸王硬上弓、但因为%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%相当激烈的抵抗而放弃了
				RETURN 1
			ELSEIF TALENT:ARG:贞操 > 0 && FLAG:催眠强度 <= 75
				PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%流着眼泪拒绝了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
				PRINTL [0] 真的那么讨厌做就算了
				PRINTL [1] 上吧
				INPUT
				IF !RESULT
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%松了一口气…
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%有点受伤
					RETURN -1
				ELSE
					IF FLAG:催眠强度 >=75
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%发动催眠告诉她这只是体内按摩，并不会失去处女…
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%虽然有些犹豫，但还是认同了
						RETURN 0
					ELSE
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%压在了不情愿的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%身上…
						TCVAR:ARG:不耐烦 = 1
						TCVAR:ARG:强行 = 1
						RETURN 0
					ENDIF
				ENDIF
			ELSE
				IF FLAG:催眠强度 >=75
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%发动催眠告诉她这只是体内按摩，并不会失去处女…
						PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%虽然有些犹豫，但还是认同了
						RETURN 0
				ELSE
					PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%强行压了上去…
					TCVAR:ARG:不耐烦 = 1
					RETURN 0
				ENDIF
			ENDIF
		ELSE
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%松了一口气…
			RETURN -1
		ENDIF
	ENDIF
ENDIF

@SET_FIRSTKISS(ARG, 接吻相手, シチュエーション, 修饰语)
#DIM 接吻相手
;シチュエーション	 0:通常
;					 1:约会归り
;					 2:自慰暴露
;					 4:阴茎に
;					 8:自发的に
;					16:无自觉
;					32:买春
#DIM 强行シチュ
#DIM シチュエーション
#DIMS 修饰语
#DIM 自己

VARSET LOCALS
自己 = 0
强行シチュ = (TCVAR:ARG:强行 && !(TALENT:ARG:爱欲 || TALENT:ARG:炮友 || TALENT:ARG:恋慕 || TALENT:ARG:思慕))
;强行状态で、接吻合意はつかない。强行だから！！！唇を许すわけ无いでしょ！！　ただし、陷落素質が那个ばギリギリセーフ。

;接吻经验済み
IF TALENT:ARG:无接吻经验 == 0
	RETURN
;无自觉接吻经验済み
ELSEIF TALENT:ARG:无接吻经验 < 0
	;无自觉接吻同士では重复しない
	SIF シチュエーション & 16
		RETURN
ENDIF

;FLAG:ＦＫ判定によっては返回
SIF !(FLAG:ＦＫ判定 & 1) && シチュエーション & 16
	RETURN
SIF !(FLAG:ＦＫ判定 & 2) && シチュエーション & 4
	RETURN


IF ARG == 接吻相手
	IF ARG == MASTER
		接吻相手 = TARGET
	ELSE
		接吻相手 = MASTER
	ENDIF
ENDIF

SETCOLOR 247,171,166
PRINTFORM 无接吻经验丧失
SIF シチュエーション & 16
	PRINT ？
PRINTFORMW (%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%)
RESETCOLOR

CFLAG:ARG:初吻丧失纪念日 = DAY
TALENT:ARG:无接吻经验 = 0
CFLAG:ARG:初吻丧失互动对象 = 接吻相手

;对象と修饰语决定
IF TALENT:ARG:思慕
	CFLAG:ARG:初吻丧失目标对象 |= 丧失_思慕
	自己 = 1
	LOCALS:0 = 寄托了感情的
ELSEIF ARG == MASTER && TALENT:(接吻相手):恋人
	CFLAG:ARG:初吻丧失目标对象 |= 丧失_恋慕
	自己 = 1
	LOCALS:0 = 深爱着的
ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕
	CFLAG:ARG:初吻丧失目标对象 |= 丧失_恋慕
	自己 = 1
	LOCALS:0 = 深爱着的
ELSEIF TALENT:ARG:精爱味觉
	LOCALS:0 = 让自己饮精成瘾的	
ELSEIF TALENT:ARG:合意误认
	LOCALS:0 = 灌输了虚假记忆的
ELSEIF TALENT:ARG:反抗抑制
	LOCALS:0 = 操控自己感情的
ELSEIF FLAG:催眠强度 >= 5
	LOCALS:0 = 偷偷催眠了自己的
ENDIF
IF シチュエーション & 2
	IF ARG != MASTER
		LOCALS:2 = 自慰中闯入的
	ELSE
		LOCALS:2 = 自慰着
		自己 = 1
	ENDIF
ENDIF
IF シチュエーション & 4
	LOCALS:1 = 的阴茎
	CFLAG:ARG:初吻丧失目标对象 = 1
ENDIF
SIF シチュエーション & 8
	自己 = 1
IF シチュエーション & 16
	IF FLAG:70
		CFLAG:ARG:初吻丧失目标对象 |= 丧失_时间停止
		LOCALS:2 = 在时间停止中
	ELSEIF CFLAG:ARG:睡眠
		CFLAG:ARG:初吻丧失目标对象 |= 丧失_睡眠
		LOCALS:2 = 在沉睡中
	ELSE
		LOCALS:2 = 在自己不知道的时候
	ENDIF
	CFLAG:ARG:初吻丧失目标对象 |= 丧失_强行
	TALENT:ARG:无接吻经验 = -1
ENDIF
IF TALENT:ARG:无知 && シチュエーション & 4
	CFLAG:ARG:初吻丧失目标对象 |= 丧失_无知
	LOCALS:2 = %LOCALS:2%连这代表什么都不知道
ENDIF
IF 强行シチュ
	CFLAG:ARG:初吻丧失目标对象 |= 丧失_强行
	LOCALS:2 = %LOCALS:2%强行
ENDIF
;场所决定
IF シチュエーション & 1
	CSTR:ARG:初吻位置 = 约会后回去的路上
	SIF ARG != MASTER
		自己 = 1
ELSEIF シチュエーション & 2
	IF ARG != MASTER
		CSTR:ARG:初吻位置 = 自室
	ELSE
		CSTR:ARG:初吻位置 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 接吻相手)%宅
	ENDIF
ELSE
	IF CHK_DATENOW(CFLAG:ARG:约会中) && CFLAG:ARG:当前位置 != OMANEKIBEYA()
		CSTR:ARG:初吻位置 = %DATENAME_SPOT()%
	ELSEIF CHK_DATENOW(CFLAG:MASTER:约会中) && CFLAG:MASTER:当前位置 != OMANEKIBEYA()
		CSTR:ARG:初吻位置 = %DATENAME_PLACE()%的%DATENAME_SPOT()%
	ELSE
		CSTR:ARG:初吻位置 = %NAME_FROM_PLACE(CFLAG:ARG:当前位置)%
	ENDIF
ENDIF

;动词决定
IF 修饰语 == "糖と一绪に"
	LOCALS:1 += "和"
	LOCALS:3 = 交换了
ELSEIF 自己
	CFLAG:ARG:初吻丧失目标对象 |= 丧失_自己
	LOCALS:1 += "❤"
	LOCALS:3 = 献上了
ELSEIF シチュエーション & 32
	LOCALS:1 += "❤"
	LOCALS:3 = 夺走了
ELSE
	LOCALS:1 += "❤"
	LOCALS:3 = 被夺走了
ENDIF
IF CSTR:ARG:初吻丧失履历 == ""
	CSTR:ARG:初吻丧失履历 = %PRINT_DATE_F(DAY)%、%CSTR:ARG:初吻位置%、%LOCALS:2%%LOCALS:0%%NAME_OUTPUT_TRANSLATION("CALLNAME", 接吻相手)%%LOCALS:1%%修饰语%%LOCALS:3%初吻
ELSE
	;无自觉接吻がある场合のみ重复する
	CSTR:ARG:初吻丧失履历 = %PRINT_DATE_F(DAY)%、%CSTR:ARG:初吻位置%、%LOCALS:2%%LOCALS:0%%NAME_OUTPUT_TRANSLATION("CALLNAME", 接吻相手)%%LOCALS:1%%修饰语%%LOCALS:3%初吻￥ＢＲ%CSTR:ARG:初吻丧失履历%
ENDIF
;既成事实もつけておく
SIF ARG != MASTER && !(シチュエーション & 16) && !强行シチュ
	SETBIT CFLAG:ARG:既成事实, 0
;爱情经验もここで入れることにする
SIF ARG != MASTER && !(シチュエーション & 16) && !强行シチュ
	EXP:ARG:爱情经验 += 5

;===================================================================================
;履历文字列设定用函数群
;对象が未记入だと"阴茎"が代入されます。
;またその场合含め对象が阴茎"だと相手が生えてない场合自动で穿戴式假阴茎になります。
;场所が未记入だと自动で位置を取得します。
;时间停止中だとか醉いつぶれてるだとかは自动で代入されます。
;各种フラグ处理も函数侧で设定します。
;つまり呼び出し侧で分岐入れる必要がほとんどなくなりました。
;ARGが你の阴茎で处女丧失したなら极端な话CALL SET_HISTORY_LOST_V(ARG)だけでOK。
;やったね。凄いね。
;===================================================================================

@SET_HISTORY_LOST_A(ARG,相手,对象,场所,修饰语)
#DIM 相手
#DIMS 对象
#DIMS 场所
#DIMS 修饰语

CFLAG:ARG:肛门处女丧失纪念日 = DAY
CFLAG:ARG:肛门处女丧失互动对象 = 相手

CSTR:ARG:肛门处女丧失履历 = %HISTORY_LOST_TEXT(ARG,相手,对象,场所,修饰语,"肛门处女")%

@SET_HISTORY_LOST_V(ARG,相手,对象,场所,修饰语)
#DIM 相手
#DIMS 对象
#DIMS 场所
#DIMS 修饰语

;非处女化、享用处女フラグ、丧失纪念日なども函数侧で设定
SIF ARG != MASTER
	FLAG:享用处女 ++
CFLAG:ARG:处女丧失纪念日 = DAY
TALENT:ARG:处女 = 0
TCVAR:ARG:破瓜 = 1
CFLAG:ARG:处女丧失互动对象 = 相手
CSTR:ARG:处女丧失履历 = %HISTORY_LOST_TEXT(ARG,相手,对象,场所,修饰语,"处女")%

@HISTORY_LOST_TEXT(ARG,相手,对象,场所,修饰语,丧失したもの)
#FUNCTIONS
#DIM 相手
#DIMS 对象
#DIMS 场所
#DIMS 修饰语
#DIMS 丧失したもの
#DIMS 状况
#DIM 自己

VARSET LOCALS
LOCALS:4 = 
自己 = 0
IF ARG == MASTER || (CFLAG:MASTER:诶嘿嘿 != 2 && ARG != MASTER && ARG != TARGET)
	LOCALS:0 = 
ELSEIF TALENT:ARG:思慕
	自己 = 1
	LOCALS:0 = 寄托了感情的
ELSEIF TALENT:ARG:恋人 || TALENT:ARG:恋慕
	自己 = 1
	LOCALS:0 = 深爱着的
ELSEIF !CFLAG:ARG:认识 && ARG != MASTER
	LOCALS:0 = 在这之前面都没有见过的
ELSEIF TALENT:ARG:路人 == 1
	LOCALS:0 = 初见面的
ELSEIF TALENT:ARG:精爱味觉
	LOCALS:0 = 让自己饮精成瘾的	
ELSEIF TALENT:ARG:合意误认
	LOCALS:0 = 灌输了虚假记忆的
ELSEIF TALENT:ARG:反抗抑制
	LOCALS:0 = 操控自己感情的
ELSEIF FLAG:催眠强度 >= 5
	LOCALS:0 = 改变了自己常识的
ENDIF

SIF !SHIRAHU(ARG)
	自己 = 0
SIF 对象 == ""
	对象 = 阴茎

SIF 场所 == ""
	场所 = %PLACE_NAME(ARG)%
LOCALS:4 = %场所%

IF FLAG:70
	LOCALS:4 = 时间停止中的%LOCALS:4%
ELSEIF TCVAR:ARG:烂醉
	LOCALS:4 = %LOCALS:4%喝得酩酊大醉的时候
ELSEIF CFLAG:ARG:睡眠
	LOCALS:4 = %LOCALS:4%沉睡的时候
ELSEIF FLAG:催眠强度 >= 5
	LOCALS:4 = 常识明显不正常的%LOCALS:4%
ELSEIF ARG != MASTER && ARG != TARGET && CFLAG:MASTER:诶嘿嘿 != 2
	LOCALS:4 = %LOCALS:4%、就像%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%期望的一样
ENDIF

IF 对象 == "阴茎"
	IF GETBIT(TALENT:相手:2,1)
		LOCALS:1 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 相手)%的%TINKO_SIMPLE(相手)%
	ELSE
		LOCALS:1 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 相手)%的穿戴式假阴茎
	ENDIF
ELSEIF 对象 == "指"
	LOCALS:1 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 相手)%的手指
ELSE
	LOCALS:1 = %NAME_OUTPUT_TRANSLATION("CALLNAME", 相手)%的%对象%
ENDIF

SIF ARG == MASTER && TALENT:相手:无知
	LOCALS:1 = 什么都不知道的%LOCALS:1%

IF 修饰语 == ""  && SHIRAHU(ARG)
	IF TCVAR:ARG:强行 && CFLAG:ARG:败犬
		修饰语 = 作为败北的代价
		自己 = 0
	ELSEIF TCVAR:ARG:强行
		修饰语 = 霸王硬上弓
		自己 = 0
	ELSEIF TALENT:ARG:合意误认 || TALENT:ARG:反抗抑制 || FLAG:催眠强度 >= 5
		修饰语 = 不清楚自己被催眠地
		自己 = 0
	ELSEIF TALENT:ARG:无知
		修饰语 = 什么都不知道的
		自己 = 0
	ELSEIF ARG != MASTER && ARG != TARGET && CFLAG:MASTER:诶嘿嘿 != 2
		修饰语 = 在不满中
	ELSEIF TALENT:ARG:恋人
		修饰语 = 在幸福感中
	ELSEIF CFLAG:ARG:诶嘿嘿 == 2
		修饰语 = 亲自
		自己 = 1
	ENDIF
ENDIF

IF 自己
	LOCALS:4 = 在%LOCALS:4%%修饰语%将%丧失したもの%献给了%LOCALS:0%%LOCALS:1%
ELSEIF ARG == MASTER || 修饰语 == "治疗と称して"
	修饰语 = 以治疗的名义
	LOCALS:4 = 在%LOCALS:4%%修饰语%将%丧失したもの%给与了%LOCALS:0%%LOCALS:1%
ELSEIF 修饰语 == "お金を払って"
	修饰语 = 付了钱后
	LOCALS:4 = 在%LOCALS:4%%修饰语%将%丧失したもの%让%LOCALS:0%%LOCALS:1%夺走了
ELSE
	LOCALS:4 = 在%LOCALS:4%%修饰语%%丧失したもの%被%LOCALS:0%%LOCALS:1%夺走了
ENDIF

LOCALS:10 = %PRINT_DATE_F(DAY)%、%LOCALS:4%

RETURNF LOCALS:10

;你の场合イベントでCSVで定义されていない相手と致すこともありうるのでより自由度の高いものを
@SET_FIRSTKISS_MASTER(ARGS)
IF TALENT:MASTER:无接吻经验
	CALL COLORMESSAGE(@"无接吻经验丧失(%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%)", C_D_PINK, 2)
	CFLAG:MASTER:初吻丧失纪念日 = DAY
	TALENT:MASTER:无接吻经验 = 0
	CSTR:MASTER:初吻丧失履历 = %PRINT_DATE_F(DAY)%、%ARGS%
ENDIF
