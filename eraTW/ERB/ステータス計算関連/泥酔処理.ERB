;烂醉になる处理と烂醉から复活する处理
@CHARA_ACTION_DRUNK(ARG)
IF !TCVAR:ARG:烂醉 && BASE:ARG:酒气 > (MAXBASE:ARG:酒气 / 10) * 8
	TCVAR:ARG:烂醉 = 1

	;仕事内容が宴会参加の场合
	IF CFLAG:ARG:职业种类 == 49
		;仕事内容を喝得酩酊大醉に变更
		CFLAG:ARG:职业种类 = 51
		CFLAG:ARG:职场 = TFLAG:宴会场
		CFLAG:ARG:行动 = 5
	ELSE
		IF TALENT:ARG:酒耐性 == -2
			CFLAG:ARG:不会喝酒 -= 3
		ENDIF
	ENDIF

	IF CFLAG:MASTER:当前位置 == CFLAG:ARG:当前位置
		IF CFLAG:ARG:职业种类 == 51
			PRINT 　
		ENDIF
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%喝得酩酊大醉的样子
	ENDIF
ENDIF

;烂醉奸中は别の复归处理を使用
IF TCVAR:ARG:烂醉 && BASE:ARG:酒气 < (MAXBASE:ARG:酒气 / 10) * 4 && !CFLAG:ARG:诶嘿嘿 && ARG == TARGET
	IF CFLAG:MASTER:当前位置 == CFLAG:ARG:当前位置
		IF CFLAG:ARG:职业种类 == 51 && CFLAG:ARG:当前位置 == TFLAG:宴会场
			PRINT 　
		ENDIF
		PRINTFORML 烂醉的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%酒醒了的样子
	ENDIF
	CALL 烂醉复活共通(ARG)
ENDIF

;睡奸中に烂醉から复活するときの前处理 主にソースの追加
@DRUNK_RECOVER_BEFORE(ARG)
IF TCVAR:ARG:烂醉 && BASE:ARG:酒气 < (MAXBASE:ARG:酒气 / 10) * 4 && CFLAG:ARG:诶嘿嘿 && ARG == TARGET && !FLAG:70
	IF TALENT:ARG:恋慕
		SOURCE:ARG:反感 += 3000
	ELSE
		SOURCE:ARG:反感 += 20000
	ENDIF
	TFLAG:好感度管理 -= MIN(30 - ABL:ARG:亲密, 0) * 3
	TFLAG:信赖度管理 -= MIN(30 - ABL:ARG:亲密, 0) * 3
	TCVAR:ARG:烂醉奸バレ = 1
ENDIF

;睡奸中に烂醉から复活する处理
@DRUNK_RECOVER(ARG)
SIF !TCVAR:ARG:烂醉奸バレ
	RETURN
PRINTL 
PRINTFORMW 烂醉的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%睁开了眼睛！
CALL 烂醉复活共通(ARG)

CFLAG:ARG:烂醉奸 = 2
CALL ENDUFUFU(ARG)
CALL SET_PRANK(0,ARG,1)

IF FLAG:催眠强度 > 74
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%茫然的看着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%解释说是新的醒酒按摩…
ELSEIF TALENT:ARG:恋慕 || FLAG:催眠强度 > 50
	;MARK:ARG:反抗刻印 = 1
	;TFLAG:反抗刻印取得 = 1
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%察觉衣服凌乱后用尖锐的目光睨视着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
	PRINTFORMW 在整理服装的同时%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%向%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%送去了冰冷的视线…
ELSE
	;MARK:ARG:反抗刻印 = 3
	;TFLAG:反抗刻印取得 = 3
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%察觉衣服凌乱后用难以置信的表情看着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的脸上充满了愤怒……
ENDIF


@烂醉复活共通(ARG)
;宴会で醉いつぶれていた场合
IF CFLAG:ARG:职业种类 == 51
	CFLAG:ARG:职业种类 = 0
	TALENT:ARG:心情 = 0
	CFLAG:ARG:宴会参加 = 3
ENDIF

TCVAR:ARG:烂醉 = 0
CFLAG:ARG:行动 = 0
