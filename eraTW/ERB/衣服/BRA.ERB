;今日の胖次には便装用と睡衣用の二种类があるので、直接P_IDを渡すように改变
@今日の文胸(C_ID, P_ID)
#FUNCTION
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM RAND_BRA, MAXBRA
#DIM B_TABLE

#DIM C_ID		;对象角色
#DIM P_ID		;コーディネートされた胖次
#DIM B_ID = 0	;ブラの种类

B_ID = GET_INT(C_ID, "下半身内衣_ずらし可能", P_ID, "关联内衣")

IF B_ID == 0 && !(P_ID == 0 || P_ID == 18)
	VARSET RAND_BRA , 0
	FOR LOCAL:0 , 1 , MAXBRA
		RAND_BRA:(LOCAL:0) = LOCAL:0
	NEXT
	FOR LOCAL:0 , 1 , MAXBRA
		LOCAL:1 = RAND(LOCAL:0 , MAXBRA)
		SWAP RAND_BRA:(LOCAL:0), RAND_BRA:(LOCAL:1)
	NEXT
	
	FOR B_TABLE, 0, MAXBRA
		IF GET_INT(C_ID, "上半身内衣_はだけ不可", RAND_BRA:B_TABLE, "つける")
			B_ID = RAND_BRA:B_TABLE
			BREAK
		ELSE
			CONTINUE
		ENDIF
		SIF B_TABLE == MAXBRA - 1
			THROW %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%のブラ抽选に失败
	NEXT
ENDIF

SIF DECIDE_NOBRA(C_ID)
	B_ID = 0
RETURNF B_ID

;PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", C_ID)%:下半身内衣２:{P_ID}:%PANTSNAME(P_ID)%　上半身内衣１:{EQUIP:C_ID:上半身内衣１}:%CLOTHNAME(7, EQUIP:C_ID:7)%

@DECIDE_NOBRA(ARG)
#FUNCTION
#DIM DYNAMIC NOBRA_RATE
SIF (TALENT:ARG:风骚 || TALENT:ARG:小恶魔 || TALENT:ARG:炮友 || ABL:ARG:露出癖 >= 4) && !RAND:5
	RETURNF 1
SIF TALENT:ARG:母乳体质 && !(TALENT:ARG:炮友 || TALENT:ARG:爱欲 || ABL:ARG:露出癖 >= 4)
	RETURNF 0
;DAY:2 季节（1=春 2=夏 3=秋 4=冬）
SELECTCASE DAY:2
CASE 1
	NOBRA_RATE = 8
CASE 2
	NOBRA_RATE = 15
CASE 3
	NOBRA_RATE = 3
CASE 4
	NOBRA_RATE = 1
ENDSELECT

SIF TALENT:ARG:幼稚 || TALENT:ARG:幼儿／幼儿退行
	TIMES NOBRA_RATE, 1.20
SIF TALENT:ARG:倒错的
	TIMES NOBRA_RATE, 1.20
SIF TALENT:ARG:小恶魔
	TIMES NOBRA_RATE, 1.20
SIF TALENT:ARG:受虐狂
	TIMES NOBRA_RATE, 1.80
SIF TALENT:ARG:风骚
	TIMES NOBRA_RATE, 2.00

{
	NOBRA_RATE = NOBRA_RATE * 
				(10 + TALENT:ARG:性的兴趣 + TALENT:ARG:自己爱 + TALENT:ARG:快感应答 
					- TALENT:ARG:Ｂ感度 - TALENT:ARG:贞操 - TALENT:ARG:羞耻心 - TALENT:ARG:胸围 ) / 10
}

SIF TALENT:ARG:无接吻经验
	TIMES NOBRA_RATE, 0.30
SIF GET_TALENTNAME(GETNUM(TALENT, "胆量"), TALENT:ARG:胆量) == "胆怯"
	TIMES NOBRA_RATE, 0.50
SIF GET_TALENTNAME(GETNUM(TALENT, "自尊心"), TALENT:ARG:自尊心) == "自尊心高"
	TIMES NOBRA_RATE, 0.50

RETURNF RAND:100 <= NOBRA_RATE

@BRANAME(ARG, CHARA)
#FUNCTIONS
#DIM CHARA

CALLF 上半身内衣チェック(ARG)
RETURNF GET_STR(CHARA, CLOTHES_PARTS_TO_CATEGORY("上半身内衣１"), ARG, "名前")
