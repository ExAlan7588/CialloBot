@GO_OUT_CONDITION_EV6
IF !RAND:10
	CALL GO_OUT_EV6
ELSE
	RETURN 0
ENDIF
@GO_OUT_EV6
#DIM PAIR
#DIM LUCK
#DIM SPENT
#DIMS PREY
SPENT = 1
PREY = %\@ RAND:2 ? 狸 # 狐 \@%
PAIR = FLAG:约会的对象
LUCK = CFLAG:MASTER:今日的运势 / 100 + TFLAG:幸运补正

PRINTFORML 道中、看到陷入了猎师的陷阱的%PREY%
IF (GROUPMATCH(PAIR, [[蓝]], [[典]]) && PREY == "狐") || (PAIR == [[猯藏]] && PREY == "狸")
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", PAIR)%好像想说些什么似地盯着这边
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%解开了陷阱之后、%PREY%向%NAME_OUTPUT_TRANSLATION("CALLNAME", PAIR)%道谢后逃走了…
	PRINTFORMW 
	RETURN 1
ELSEIF (GROUPMATCH(PAIR, [[蓝]], [[典]]) && PREY == "狸") || (PAIR == [[猯藏]] && PREY == "狐")
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", PAIR)%只是看了陷阱一眼便什么都不做地离开了
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%也慌忙地跟了上去…
	PRINTFORMW 
	RETURN 1
ENDIF
PRINTFORML 这样下去的话肯定会被捕到的…
CALL ASK_M("不要妨碍诱捕", 1, "救助", 1)
IF !RESULT
	PRINTFORML 虽然很可怜但也无能为力
	SIF PAIR
		PRINTFORM 虽然%NAME_OUTPUT_TRANSLATION("CALLNAME", PAIR)%有些不情愿、但
	PRINTFORMDL \@ PAIR ? 二人还是 # %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)% \@什么都不做地踏上了归路
	PRINTFORMW
	RETURN 1
ENDIF
PRINTFORML 因为很难可怜所以准备帮助它
PRINTFORMW 虽然受伤了但简单处理一下应该就可以行走了
PRINTDW …………
PRINTDW ……
PRINTDW …
IF LUCK > 4
	PRINTFORMW 治疗结束后的%PREY%消失在了灌木的阴影中
	PRINTFORML …然后好像叼着什么回到了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的身边
	PRINTFORML 一定是作为救助的谢礼
	;最高级素材を随机
	LOCAL:0 = 625, 630, 639, 640, 643, 646
	CALL GET_ITEM(ITEMNAME:(LOCAL:(RAND:6)), 1, 2)
	PRINTFORML 
	IF !(GO_OUT_FLAG_06 || PAIR || GETBIT(TFLAG:一日一回, 9)) && LUCK > 7
		GO_OUT_FLAG_06 = DAY
		SETBIT TFLAG:一日一回, 9
		CALL MAKE_YUKIZURI(RANDOM_CHARANUM, @"妖怪%PREY%")
		CFLAG:RANDOM_CHARANUM:经常去的地方 = (MAIN_MAP == 5) ? 7 # 5
		CFLAG:RANDOM_CHARANUM:好感度 = REQUIRED_FAVOR_思慕
		CFLAG:RANDOM_CHARANUM:信赖度 = REQUIRED_TRUST_思慕
		ABL:RANDOM_CHARANUM:亲密 = REQUIRED_AFFECTION_思慕
		ABL:RANDOM_CHARANUM:顺从 = REQUIRED_AFFECTION_思慕
		CSTR:RANDOM_CHARANUM:路人子PROFILE１ = %NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%：%PRINT_DATE_F(DAY)%、在从%GET_MAPNAME(CFLAG:MASTER:约会中)%归家的道中救助的妖怪%PREY%
		BASE:RANDOM_CHARANUM:体力 -= 50
		PRINTFORML …哦呀！？　%PREY%的样子…！
		PRINTFORMW %PREY%就在面前变成了兽耳少女！
		CALL CHARA_STATE(RANDOM_CHARANUM)
		DRAWLINE
		CALL COLORMESSAGE(@"处女丧失履历：%CSTR:RANDOM_CHARANUM:处女丧失履历%",C_PINK,1)
		PRINTFORMW 
		PRINTFORML 自称%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%的%PREY%娘向%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%表述了多次的感谢
		SIF !INRANGE(TIME, 540, 1140)
			GOTO SKIP
		PRINTFORML 说是为了道谢希望跟随着回家…
		CALL ASK_M("今天还是自己回去吧", 1, "请跟我来", 1)
		IF RESULT
			FLAG:约会的对象 = RANDOM_CHARANUM
			CFLAG:RANDOM_CHARANUM:当前位置 = CFLAG:MASTER:当前位置
			CFLAG:RANDOM_CHARANUM:同行 = 300
			CFLAG:MASTER:同行 = 300
			CFLAG:RANDOM_CHARANUM:清醒剂 = 300
			CFLAG:RANDOM_CHARANUM:据点来访 = 1
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%决定带%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%回去
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%送来了热情的视线…
			GOTO END_PHASE
		ENDIF
		PRINTFORML 因为受了伤所以就这样回去较好
		$SKIP
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%说下次希望能来\@ CFLAG:RANDOM_CHARANUM:经常去的地方 == 5 ? 魔法之森 # 山的山麓 \@游玩之后便离去了…
		CALL COLORMESSAGE(@"与%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%结识了",C_YELLOW,2,1)
		CFLAG:RANDOM_CHARANUM:当前位置 = SUKIMA()
		GOTO END_PHASE
	ENDIF
	PRINTFORML 在被救助的%PREY%的目送下\@ PAIR ? 二人 # %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)% \@踏上了归路
ELSE
	IF PAIR
		PRINTFORML 在处理伤口的时候%PREY%发狂地挠抓着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
		CALL RECOVER(MASTER, -200, "体力")
		WAIT
		PRINTFORML 而且在处理结束之后%PREY%便直接冲刺逃走了…真是不知感恩！
		PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", PAIR)%
		SELECTCASE CFLAG:PAIR:性格倾向
			CASE 1
				PRINTFORMW 担心地看着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%…
			CASE 2, 3
				PRINTFORMW 看着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%\@ CFLAG:PAIR:性格倾向 == 2 ? 哈哈大笑 # 哧哧地笑 \@了起来…
			CASE 4
				PRINTFORMW 露出了难以言喻的微妙表情…
			CASE 5
				PRINTFORMW 用怜悯的眼神看向了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%…
		ENDSELECT
		PRINTFORML 一无所获的二人踏上了归路
	ELSE
		PRINTFORMW 在处理结束之后%PREY%便直接冲刺逃走了…真是不知感恩！
		PRINTFORML 只好就这样回去了、但身后好像有人
		PRINTFORMW 回头看去…是一个因为猎物逃跑而愤怒的猎师…！
		PRINTFORML 
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的头被打了！
		CALL RECOVER(MASTER, -100, "体力")
		WAIT
		PRINTFORML 被猎师说教了…
		CALL RECOVER(MASTER, -300, "气力")
		WAIT
		PRINTFORML 倒霉的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%跌跌撞撞地踏上了归路
		SPENT = 4
	ENDIF
ENDIF
$END_PHASE
PRINTFORML +{15 * SPENT}分
PRINTFORMW 
TIME += (15 * SPENT)
RETURN 1
