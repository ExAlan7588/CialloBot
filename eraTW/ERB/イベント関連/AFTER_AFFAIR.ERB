@AFTER_AFFAIR
#DIM SPENT_TIME
SPENT_TIME = (TIME-UFUFU_START_TIME) / 60 
IF SPENT_TIME < 0
	SPENT_TIME += 24
ENDIF
;勃然大怒と强行状态とTSP切れと烂醉奸と澡堂场プレイと睡眠时间中は表示しない
SIF CFLAG:勃然大怒 || TCVAR:强行 || !SHIRAHU(TARGET)
	RETURN 0
	;ほんの稍微の诶嘿嘿では表示されないよう、润滑、情欲、体力、积攒度、その日の绝顶回数に条件をつける（大した意味はない）
SIF !(PALAM:润滑 >= PALAMLV:3 && PALAM:情欲 >= PALAMLV:3 && BASE:体力 < MAXBASE:体力 * 9 / 10 && CFLAG:积攒度 < 300 && EX:Ｃ绝顶 + EX:Ｖ绝顶 + EX:Ａ绝顶 + EX:Ｂ绝顶 + EX:Ｍ绝顶 >= 4)
	RETURN 0

PRINTFORMD %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%和%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%\@ GET_TARGETNUM() > 1? 们 # \@的
;经过时间表示
IF SPENT_TIME
	PRINTFORMD 长达{SPENT_TIME}小时的
	SIF SPENT_TIME > 5
		PRINTFORMD 非常壮烈的
ENDIF
;ここまで
IF TFLAG:泡澡堂
	PRINTFORMD 金钱买来的短暂的欢愉
ELSEIF TALENT:恋慕 || TALENT:恋人
	PRINTFORMD 充满誓约的爱情仪式
ELSEIF TALENT:爱欲
	PRINTFORMD 互相贪恋着对方与自己极度契合的肉体交媾
ELSEIF TALENT:思慕
	PRINTFORMD 互有好感、却朋友以上恋人未满的两人所渴求的密会
ELSEIF TALENT:炮友 == 1
	PRINTFORMD 以快乐为目的苟合
ELSE
	PRINTFORMD 轻率的一夜情
ENDIF
PRINTFORMW 结束了。

SIF BATHROOM()
	RETURN 0
;-------------------------------------------------------------------------------------
;以下、精液漏れ・爱液による濡れの描写（精液漏れの描写が优先）

;膣充填率が水准以上もしくは肛门充填率が水准以上
IF SOAKED_OR_NOT("V_SEMINAL") || SOAKED_OR_NOT("A_SEMINAL")
	PRINTFORMD 大量的%EXPR_VAR("SPERM")%%TEXTR("被灌进/被释放进/被注入进")%%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的
	SIF SOAKED_OR_NOT("V_SEMINAL")
		PRINTFORMD %EXPR_VAR("VULVA")%
	SIF SOAKED_OR_NOT("A_SEMINAL")
		PRINTFORMD \@ SOAKED_OR_NOT("V_SEMINAL") ? 和 # \@%EXPR_VAR("ANUS")%
	PRINTFORMDL %TEXTR("眼看着都要溢出来了。/慢慢的漏了出来了。/垂落下来。")%
	IF TALENT:MASTER:浓厚精液
		PRINTFORML 还是半固态的%EXPR_VAR("SPERM")%并没有滴落下来、而是粘住下体吊垂着、甚至在%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%大腿内侧甩动着。
	ELSE
		PRINTFORMD %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%用%TEXTR("纸巾/手帕/毛巾")%
		PRINTFORMDL 在%EXPR_VAR("CROTCH")%上擦拭了好一会、但%EXPR_VAR("SPERM")%还是从下面%TEXTR("滴滴答答地滴下来了/滴下来了/流了出来、在大腿上流出了一条淫靡的白线/咕嘟咕嘟地溢出来了")%。
	ENDIF
	;--------------------------------------------
	;垂れ口上呼び出し
	;--------------------------------------------
	CALL WEAREDPANTIES_TRANSPARENCY("SPERM")
	CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,1)
;膣・肛门充填率がともに水准未满だが润滑が必要LV以上
ELSEIF SOAKED_OR_NOT("SECRETIONAL")
	PRINTFORMD %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%、将湿漉漉的%EXPR_VAR("CROTCH")%用%TEXTR("纸巾/手帕/毛巾")%
	PRINTFORMDL 擦拭着、蜜液不断从尚未从兴奋中冷却的肉体中溢出。
	;--------------------------------------------
	;溢れ口上呼び出し
	;--------------------------------------------
	CALL WEAREDPANTIES_TRANSPARENCY("JUICE")
	CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,2)
ENDIF
;-------------------------------------------------------------------------------------------------------
;以下、事后の情绪の描写
;复数TARGETがいる场合非表示
IF GET_TARGETNUM() == 1
	PRINTFORMD 情事之后收拾完的两人
	IF TALENT:恋慕 || TALENT:恋人
		PRINTFORMDL 即使完事了、但仍然还是躺在一起说着说不完的情话。
	ELSEIF TALENT:爱欲
		PRINTFORMDL 仍然散发着火热的气息、彼此调整着紊乱的呼吸过了好长一段时间。
	ELSEIF TALENT:思慕
		PRINTFORMDL 还带着羞涩、互相注视着对方羞涩的脸。
	ELSEIF TALENT:炮友 == 1
		PRINTFORMDL 在暧昧的气氛中、默默地贴着对方的身体。
	ELSE
		PRINTFORMDL 一段时间内、空中散逸着沉默而尴尬的气氛。
	ENDIF
	PRINTFORMW
ENDIF

CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,3)

;---------------------------------------------------------------------------------------------------------
;以下、事后に服等を着なおす描写		
;上身衣服、连衣裙、和服、外衣のどれかを着たままだったばあい
IF EQUIP:上身衣服１ || EQUIP:上身衣服２ || EQUIP:连衣裙 || EQUIP:和服 || EQUIP:外衣
	PRINTFORMDL 慌乱的穿起衣服的%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%背向这边、再手忙脚乱整理着凌乱的衣服。
	IF PALAM:情欲 >= PALAMLV:7
		PRINTFORMD 被香汗打湿的白里透红的肌肤
	ELSEIF PALAM:情欲 >= PALAMLV:5
		PRINTFORMD 出汗后、白皙的肌肤泛着粉红色的光泽。
	ELSEIF PALAM:情欲 >= PALAMLV:3
		PRINTFORMD 带着汗水的白皙肌肤
	ELSE
		PRINTFORMD 微微出汗
	ENDIF
	PRINTFORMW 看着让人血压飙升。
;现在上半身が裸でかつ便装の上半身内衣が文胸か微型文胸のばあい
ELSEIF TEQUIP:上半身着衣状况 == 0 && (CFLAG:TARGET:207 == CLOTHES_ID("上半身内衣１", "文胸") || CFLAG:TARGET:208 == CLOTHES_ID("上半身内衣２", "微型文胸"))
	PRINTFORMDL H之后、%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%背向着%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%要求%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%帮她挂上文胸的挂钩。
	IF PALAM:情欲 >= PALAMLV:7
		PRINTFORMD 香汗从泛着粉红色的脖颈滑落，忍不住亲了上去
	ELSEIF PALAM:情欲 >= PALAMLV:5
		PRINTFORMD 从背后欣赏着%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%带汗水微微泛红的背脊
	ELSEIF PALAM:情欲 >= PALAMLV:3
		PRINTFORMD %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%微微出汗的肌肤十分诱人
	ELSE
		PRINTFORMW 从背后看过去、%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%光滑的肩胛骨看上去美极了。
	ENDIF
ENDIF

CALL KOJO_MESSAGE_SEND("EVENT",15,TARGET,3,4)

;----------------------------------------------------------------------------------------------------------
;以下、残り香の描写

;浓厚的残り香の表示に必要な射精数、喷乳数、放尿数の设定
LOCAL:5 = 5 ;必要射精数
LOCAL:6 = 5 ;必要喷乳数
LOCAL:7 = 1 ;必要放尿数

;残り香を表示するのは屋内のときだけ
IF OPENPLACE(CFLAG:MASTER:当前位置) < 2
	;その日のふたりの射精数、喷乳数、放尿数によって分岐
	IF EX:射精 + EX:MASTER:射精 >= LOCAL:5 || EX:喷乳 + EX:MASTER:喷乳 >= LOCAL:6 || EX:放尿 + EX:MASTER:放尿 >= LOCAL:7
		PRINTFORMD 热气腾腾的、
		SIF EX:射精 + EX:MASTER:射精 >= LOCAL:5
			PRINTFORMD 精液独特的味道
		SIF EX:喷乳 + EX:MASTER:喷乳 >= LOCAL:6
			PRINTFORMD \@ EX:射精 + EX:MASTER:射精 >= LOCAL:5 ? 和 # \@甘甜的母乳香味
		SIF EX:放尿 + EX:MASTER:放尿 >= LOCAL:7
			PRINTFORMD \@ EX:射精 + EX:MASTER:射精 >= LOCAL:5 || EX:喷乳 + EX:MASTER:喷乳 >= LOCAL:6 ? 和 # \@透着尿臭的骚味
		PRINTFORMDL 混杂着淫靡而浓稠的余香、
		PRINTFORMW 明确地表明了在那里面的秘密。
	ELSE
		PRINTFORMW 酸甜的体液的气味和热腾腾的空气、还保留着之前的H的痕迹。
	ENDIF
ENDIF
;----------------------------------------------------------------------------------------------------------


@WEAREDPANTIES_TRANSPARENCY(ARGS)
#DIM ぱんつ状况

;胖次を夺ったorもらったor元から穿いてない场合
IF CFLAG:没穿内裤 || (CFLAG:205 == 0 && CFLAG:206 == 0)
	ぱんつ状况 = 0
;胖次を一旦脱いでいた场合
ELSEIF !EQUIP:下半身内衣１ && !EQUIP:下半身内衣２
	ぱんつ状况 = 1
ELSE
	ぱんつ状况 = 2
ENDIF


IF !ぱんつ状况
	PRINTFORMDL %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%的下面是真空的。
	IF ARGS == "SPERM"
		PRINTFORMDL 今天、没穿内裤、就滴着%EXPR_VAR("SPERM")%这样度过吧。
	ELSE
		PRINTFORMDL 今天、%EXPR_VAR("CROTCH")%充满着爱液的湿润、没有了内裤的保护、就这样暴露在空气中。
	ENDIF
;胖次がある场合
ELSE
	SIF ぱんつ状况 == 1
		PRINTFORMDL %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%把脱下的内衣重新穿起来。
	IF ARGS == "SPERM"
		PRINTFORMD 淌出来的%EXPR_VAR("SPERM")%
	ELSE
		PRINTFORMD 不断流出的爱液
	ENDIF
	PRINTFORMD 把\@ ぱんつ状况 == 1? 那条# \@内裤%TEXTR("黏在了一起/浸的湿漉漉的/沾湿透了")%
	;胖次が透けうる场合
	IF PANTIES_TRANSPARENCY(TARGET)
		PRINTFORMDL 、甚至可以看到整个阴唇的形状和阴蒂的凸起。
	ELSE
		PRINTFORMDL 、非常诱人。
	ENDIF
ENDIF


;### びしょ濡れ判定用式中函数 ################
;ARG=角色番号
@SOAKED_OR_NOT(ARGS,ARG)
#FUNCTION
#DIM 必要膣充填率
#DIM 必要肛门充填率
#DIM 必要润滑程度

SIF ARG == 0
	ARG = TARGET

必要膣充填率 = 30
必要肛门充填率 = 30
必要润滑程度 = PALAMLV:5

IF ARGS == "V_SEMINAL"
	IF 充填率(ARG,1) >= 必要膣充填率
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSEIF ARGS == "A_SEMINAL"
	IF 充填率(ARG,2) >= 必要肛门充填率
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ELSEIF ARGS == "SECRETIONAL"
	IF PALAM:ARG:润滑 >= 必要润滑程度
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF

THROW 错误的文字列参数


;### 表现バリエーション用式中函数 ################
@EXPR_VAR(ARGS)
#FUNCTIONS
VARSET LOCALS

IF ARGS == "SPERM"
	SELECTCASE RAND:4
	CASE 0
		LOCALS = 白浊液
	CASE 1
		LOCALS = 精液
	CASE 2
		LOCALS = 浓精
	CASEELSE
		LOCALS = 子种汁
	ENDSELECT
ELSEIF ARGS == "VULVA"
	SELECTCASE RAND:5
	CASE 0
		LOCALS = 秘处
	CASE 1
		LOCALS = 秘裂
	CASE 2
		LOCALS = 阴门
	CASE 3
		LOCALS = 蜜穴
	CASEELSE
		LOCALS = 女阴
	ENDSELECT
ELSEIF ARGS == "ANUS"
	SELECTCASE RAND:4
	CASE 0
		LOCALS = 尻穴
	CASE 1
		LOCALS = 肛门
	CASE 2
		LOCALS = 后庭
	CASEELSE
		LOCALS = 菊门
	ENDSELECT
ELSEIF ARGS == "CROTCH"
	SELECTCASE RAND:4
	CASE 0
		LOCALS = 股间
	CASE 1
		LOCALS = 局部
	CASE 2
		LOCALS = 私处
	CASEELSE
		LOCALS = 股
	ENDSELECT
ENDIF

RETURNF LOCALS