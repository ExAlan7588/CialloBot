;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;约会先名取得函数
;CALL		USER
;ARG		VOID
;RETURN		约会先名
;COMMENT	
;-----------------------------------------------------------
@DATENAME_PLACE()
#FUNCTIONS
SIF TFLAG:妄想中 == 1
	RETURNF "妄想"
SIF CHK_DATENOW(CFLAG:MASTER:约会中) == 0
	RETURNF ""
RETURNF STR:(CFLAG:MASTER:约会中 + 5000)

;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;约会地点名取得函数
;CALL		USER
;ARG		VOID
;RETURN		约会地点名
;COMMENT	
;-----------------------------------------------------------
@DATENAME_SPOT()
#FUNCTIONS
SIF CHK_DATENOW(CFLAG:MASTER:约会中) == 0
	RETURNF ""
SIF TFLAG:约会道中
	RETURNF "道中"
RETURNF NAME_FROM_PLACE(CFLAG:MASTER:当前位置)

;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;约会地点番号取得函数
;CALL		USER
;ARG		VOID
;RETURN		约会地点NO
;COMMENT	
;-----------------------------------------------------------
@DATE_SPOT()
#FUNCTION
RETURNF CFLAG:MASTER:当前位置 % 100 / 10

;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;约会先到着残り时间取得函数
;CALL		USER
;ARG		VOID
;RETURN		约会地点NO
;COMMENT	
;-----------------------------------------------------------
@DATE_REMAINTIME()
#FUNCTION
RETURNF ((CFLAG:MASTER:当前位置 % 100) % 10) * 10

;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;约会先アルバイト可否
;CALL		USER
;ARG		当前位置
;RETURN		アルバイト先种类
;COMMENT	
;-----------------------------------------------------------
@DATE_PARTTIME(ARG)
#FUNCTION
RETURNF ODEKAKEMAP_SETTING(ARG, "アルバイト")

;お金稼ぎが封印中なのでここも封印
SELECTCASE ARG
	;特殊バイト先
	CASE 240,450,520,540,650,830
		RETURNF ARG
	;%约会先%でアルバイトして～…
	CASE 220,230,320,330,460
		RETURNF 2
	;%约会先%で食材集めをして～…
	CASE 350,410,430,440,510,530,630,710,810
		RETURNF 1
ENDSELECT
RETURNF 0

;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;约会先路人存在判定
;CALL		USER
;ARG		当前位置
;RETURN		人街道が多ければ1
;COMMENT	MASTERの目に见える范围に路人キャラ(少女限定)が歩いてる（であろう）场所か否か
;-----------------------------------------------------------
[SKIPSTART]
@DATE_HITOGOMI(ARG)
#FUNCTION
SIF !ARG
	ARG = CFLAG:PLAYER:当前位置
IF !AT_HOME(MASTER)
	SELECTCASE ARG
		;路人がいる（と胜手に想像している场所
		;地底（街道、温泉、地灵殿）妖怪之山山顶（守矢神社本殿、境内、九天瀑布）妖怪之山山麓（无し）三途之川～冥界（中有之道）魔法之森（无し）迷途竹林（永远亭）红魔馆（大厅、食堂、大图书馆）人间之里（情侣酒店以外）命莲寺（地藏小路、倒垂的柳树下、命莲寺境内、命莲寺本堂）
		CASE 960,940,930,850,840,810,610,450,370,360,350,270,250,240,230,220,210,140,130,120,110
			RETURNF 1
	ENDSELECT
ENDIF
RETURNF 0

;FileName_DATE_CMN.ERB ----------------------------- Rev1.00
;约会先路人种类存在判定
;CALL		USER
;ARG		当前位置
;RETURN		
;COMMENT	MASTERの目に见える范围に路人キャラ（少女限定）が歩いてる（であろう）场所か否か、男はガン无视、哥布林は绅士なので少女が集まってキャッキャしてるところには近寄りません
;-----------------------------------------------------------
@DATE_HITOGOMI_SYUBETU(ARG)
#FUNCTIONS
VARSET LOCALS
SIF !ARG
	ARG = CFLAG:PLAYER:当前位置
SELECTCASE GET_MAPID(ARG)
	;路人の种类
	;地底（街道、温泉、地灵殿）(街道と温泉では鬼も出てくる
	CASE 9
		IF ARG != 960 && !RAND:3
			LOCALS += "鬼"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;妖怪之山山顶（守矢神社本殿、境内、九天瀑布）（守矢神社境内のみ希に人类
	CASE 8
		IF !RAND:5 && ARG == 840
			LOCALS += "人类"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;妖怪之山山麓（无し）
	;三途之川～冥界（中有之道）(死神、妖怪、希に人类
	CASE 6
		IF !RAND:5
			LOCALS += "人类"
		ELSEIF !RAND:3
			LOCALS += "妖怪"
		ELSE
			LOCALS += "死神"
		ENDIF
	;魔法之森（无し）
	;迷途竹林（永远亭）(妖怪ウサギ、希に人类(患者）
	CASE 4
		IF RAND:5
			LOCALS += "妖怪兔"
		ELSE
			LOCALS += "人类"
		ENDIF
	;红魔馆（大厅、食堂、大图书馆）（大厅食堂は妖精女仆、大图书馆は小恶魔にしたいけど、个别キャラ化してるので妖精女仆
	CASE 3
		LOCALS +=  "女仆妖精"
	;人间之里（情侣酒店以外）（基本人类、希に妖怪
	CASE 2
		IF RAND:5
			LOCALS += "人类"
		ELSE
			LOCALS += "妖怪"
		ENDIF
	;命莲寺（地藏小路、倒垂的柳树下、命莲寺境内、命莲寺本堂）（人类と妖怪
	CASE 1
		IF !RAND:2
			LOCALS += "人类"
		ELSE
			LOCALS += "妖怪"
		ENDIF
ENDSELECT
LOCALS += "少女"

RETURNF LOCALS
[SKIPEND]
;FileName_DATE_CMN.ERB ----------------------------- Rev1.01
;约会归りイベント (CFLAG:13)
;CALL		USER
;ARG		角色NO
;RETURN		VOID
;COMMENT	
;-----------------------------------------------------------
@DATE_EVENT(ARG)
#DIM 珠不足
#DIM 経験不足

VARSET LOCAL
;中止フラグ（外部参照にて使用のこと 例:LOCAL@DATE_EVENT:99 = 1
LOCAL:99 = 0
;パラメータによる发生基礎值生成

LOCAL = MARK:ARG:屈服刻印 * 10 + MARK:ARG:快乐刻印 * 10
IF TALENT:ARG:恋慕
	LOCAL += MIN(ABL:ARG:亲密 * 10, 100) + MIN(ABL:ARG:欲望 * 10,100) + MIN(ABL:ARG:侍奉精神 * 10,100)
	LOCAL += MIN(EXP:ARG:约会经验,100) + MIN(EXP:ARG:爱情经验,100)
ELSEIF TALENT:ARG:思慕
	LOCAL += MIN(ABL:ARG:亲密 * 10, 50) + MIN(ABL:ARG:欲望 * 10,50) + MIN(ABL:ARG:侍奉精神 * 10,50)
	LOCAL += MIN(EXP:ARG:约会经验,50)
ENDIF
LOCAL += GETPALAMLV(PALAM:ARG:情欲,10) * 5 + GETPALAMLV(PALAM:ARG:好意,10) * 5 + GETPALAMLV(PALAM:ARG:恭顺,10) * 5
;60
LOCAL += BASE:ARG:情绪 / 50 + (1000 - BASE:ARG:理性) / 30
LOCAL += TALENT:ARG:胆量 * 10 + TALENT:ARG:性的兴趣 * 10 - TALENT:ARG:自制心 * 10 - TALENT:ARG:冷漠 * 10 
DEBUGPRINTFORML 【DATE_EVENT({ARG})】DateEvent_Checksum = {LOCAL}
;别れ际に礼物
CALL GIFT(ARG)
IF RESULT == 1
;合意无し（接吻成功してない）
;修改约会后初吻判定
;ELSEIF !(CFLAG:ARG:1 & 1) && !(CFLAG:ARG:13 & 1)
ELSEIF TALENT:ARG:无接吻经验 && !(CFLAG:ARG:13 & 1) && CSTR:ARG:初吻位置 != "约会后回去的路上"
	;成功判定
	IF (LOCAL > 220 + TALENT:ARG:难以越过的底线 * 30 + ABS(TALENT:ARG:无接吻经验 * 20)) && CFLAG:ARG:合意判定 > 180 && (TALENT:ARG:思慕 || TALENT:ARG:恋慕)
		CALL KOJO_MESSAGE_SEND("SP_EVENT",1,ARG,0)
		;中止フラグ
		IF !TCVAR:ARG:中止接吻
			CALL ADD_KISS(ARG)
			SETBIT CFLAG:ARG:约会后EVENTFLAG , 0
		ENDIF
	ELSEIF !MARK:ARG:反抗刻印
		CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,0)
	ENDIF
	EXP:ARG:爱情经验 += 10
	;PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%の约会接吻判定:需{LOCAL}大于{220 + TALENT:ARG:难以越过的底线 * 30 + ABS(TALENT:ARG:无接吻经验 * 20)}且合意{CFLAG:ARG:合意判定,3,RIGHT}大于180,必须有思慕or恋慕
	;TRYCALL PRINT_RAINBOW2W(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%の约会接吻判定:需{LOCAL}大于{220 + TALENT:ARG:难以越过的底线 * 30 + ABS(TALENT:ARG:无接吻经验 * 20)}且合意{CFLAG:ARG:合意判定,3,RIGHT}大于180,必须有思慕or恋慕", 20)
;反抗刻印消去处理
ELSEIF MARK:ARG:反抗刻印 && LOCAL > 50
	;ABL:亲密LVUP处理取り込み。解读が面倒だったので要所々々取り摘んで…
	FOR LOCAL,0,4
		珠不足 = 0
		経験不足 = 0
		SIF JUEL_DEMAND(9,LOCAL,0,ARG) == -2
			BREAK
		SIF JUEL_DEMAND(9,LOCAL,0,ARG) < 0
			CONTINUE
		;必要な宝珠を寻找
		FOR LOCAL:1,0,100
			IF JUEL_DEMAND(9,LOCAL,LOCAL:1,ARG) > 0
				IF JUEL:(ARG):(LOCAL:1) < JUEL_DEMAND(9,LOCAL,LOCAL:1,ARG) / 2
					珠不足 ++
					BREAK
				ENDIF
			ENDIF
		NEXT
		SIF 珠不足
			CONTINUE
		;必要な経験を寻找
		FOR LOCAL:1,0,100
			IF EXP_DEMAND(9,LOCAL,LOCAL:1,ARG) > 0
				IF  EXP:(ARG):(LOCAL:1) < EXP_DEMAND(9,LOCAL,LOCAL:1,ARG) / 2
					経験不足 ++
					BREAK
				ENDIF
			ENDIF
		NEXT
		SIF 経験不足
			CONTINUE
		;ここまで来れたら反抗刻印消去
		;要するに约会でいい雰囲气＋亲密LVUP水准の1/2满たしたら反抗消去
		MARK:ARG:反抗刻印 --
		CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,(MARK:ARG:反抗刻印 + 1))
		PRINTL
		IF MARK:ARG:反抗刻印
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的反抗刻印LV下降为{MARK:ARG:反抗刻印}了
		ELSE
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%失去了反抗刻印LV1
		ENDIF
		BREAK
	NEXT
;告白していない（MASTERに恋人が居ない）
ELSEIF !(CFLAG:ARG:1 & 4) && !(CFLAG:ARG:13 & 2) &&  !FLAG:告白禁止 ;CAN_MAKE_恋人()
	;成功判定
	IF (LOCAL > 530 + TALENT:ARG:难以越过的底线 * 20)
		CALL KOJO_MESSAGE_SEND("SP_EVENT",2,ARG,0)
		PRINTFORML 对%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的告白…
		CALL ASK_YN(@"接受（只能有{FLAG:追加恋人栏 + 1}名恋人）","拒绝")
		IF !RESULT
			SETBIT CFLAG:ARG:既成事实 , 2
			SETBIT CFLAG:MASTER:既成事实, 2
			TALENT:MASTER:恋人 = NO:ARG
			TALENT:ARG:恋人 = 1
			SETBIT CFLAG:ARG:约会后EVENTFLAG , 1
			CALL KOJO_MESSAGE_SEND("SP_EVENT",2,ARG,1)
			PRINTL
			PRINTFORMW 与%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%成为了恋人
		ELSE
			CALL KOJO_MESSAGE_SEND("SP_EVENT",2,ARG,2)
		ENDIF
	;赠り物イベント
	ELSEIF GIFT_DATE_CHECK(ARG)
		CALL GIFT_DATE_EVENT(ARG)
	ELSEIF !MARK:ARG:反抗刻印
		CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,0)
	ENDIF
	;PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%の约会告白判定:需{LOCAL}大于{530 + TALENT:ARG:难以越过的底线 * 20}		
	;TRYCALL PRINT_RAINBOW2W(@"%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%de约会告白判定:需{LOCAL}大于{530 + TALENT:ARG:难以越过的底线 * 20}", 20)			
;赠り物イベント
ELSEIF GIFT_DATE_CHECK(ARG)
	CALL GIFT_DATE_EVENT(ARG)
ELSE
	CALL KOJO_MESSAGE_SEND("SP_EVENT",3,ARG,0)
ENDIF

@GIFT(ARG)
#DIM 好感度上升值
#DIM 礼物评价
;约会前好感度と现好感度を比较
;多人约会移除
SIF 同行人数() > 1
	RETURN 0
好感度上升值 = CFLAG:ARG:2 - TFLAG:约会前好感度
IF DAY:2 == 4 && DAY:3 == 24 && ITEM:94 && TFLAG:约会前好感度
	PRINTFORML 要送出圣诞礼物吗？
	CALL ASK_YN
	IF !RESULT
		PRINTFORMW 把礼物送给了%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%
		ITEM:94 = 0
		IF TALENT:ARG:恋人
			PRINTFORMW 能看见%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的变得一片通红
			PRINTFORMW ……得到了对脸颊的一吻作为回礼
			礼物评价 = 10
		ELSEIF TALENT:ARG:恋慕
			PRINTFORMW 能看见%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的变得一片通红
			PRINTFORMW 受到礼物的%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%感觉非常幸福
			礼物评价 = 7
		ELSEIF TALENT:ARG:思慕
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%惊讶之余很开心的接受了
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%面对表示好意的礼物、更加在意起%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%了
			礼物评价 = 5
		ELSE
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%因为受到的高价礼物显得有些不知所措、不过并没有讨厌
			礼物评价 = 2
		ENDIF
		CFLAG:ARG:2 += 好感度上升值 * 礼物评价 / 5
		CFLAG:ARG:4 += 好感度上升值 * 礼物评价 / 10
		RETURN 1
	ELSE
		RETURN 0
	ENDIF
ELSE
	RETURN 0
ENDIF

@WITH_恋人(ARG)
#FUNCTION
;恋人の何人とARGが同じ场所に居るかを返す
VARSET LOCAL
SIF ARG == 0
	ARG = MASTER
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:当前位置 == CFLAG:ARG:当前位置 && TALENT:LOCAL:恋人
		LOCAL:1 ++
NEXT
RETURNF LOCAL:1

@CAN_MAKE_恋人()
#FUNCTION
SIF TALENT:MASTER:左拥右抱
RETURNF 1
SIF INRANGECARRAY(TALENT:恋人, 1, CHARANUM, 1, CHARANUM) <= FLAG:追加恋人栏
		RETURNF 1
	RETURNF 0

@CAN_MAKE_原版恋人()
#FUNCTION
;恋人が今何人いるかカウント
VARSET LOCAL
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:恋人
		LOCAL:1 ++
NEXT

	SIF LOCAL:1 < (1 + FLAG:追加恋人栏)
		RETURNF 1
	RETURNF 0

;===========================================
;接吻经验や初吻处理などをまとめてやる函数
;===========================================
@ADD_KISS(ARG, ARG:1 = 1, ARG:2 = 0) ;custom code
IF TALENT:MASTER:无接吻经验
	CALL SET_FIRSTKISS(MASTER, ARG, ARG:2 == 0 ? 1 # 0) ;custom code, mutual kiss returns 1, so it's set to outside of date (0), on date by default (1)
	CALL AddEXP("爱情经验", ARG, 10) ;custom code
ENDIF
IF TALENT:ARG:无接吻经验
	CALL SET_FIRSTKISS(ARG, MASTER, ARG:2 == 0 ? 1 # 0) ;custom code
	CALL AddEXP("爱情经验", ARG, 10) ;custom code
ENDIF
CALL AddEXP("接吻经验", ARG, ARG:1) ;custom code
CALL AddEXP("接吻经验", MASTER, ARG:1) ;custom code
SETBIT CFLAG:ARG:既成事实 , 0

;Qol_Info_Count_Array关联は后日导入するので一旦コメントアウト
	;Qol_Info_Count_Array:ARG:Qol_Count_Kiss ++ ;qol custom code
	;SIF !SHIRAHU(ARG)
	;	Qol_Info_Count_Array:ARG:Qol_Count_UncKiss ++ ;qol custom code
