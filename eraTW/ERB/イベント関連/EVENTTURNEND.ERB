@EVENTTURNEND
#DIM 养育费
#DIM DYNAMIC CUSTOM_FALL_STATES_ALLOWED ;addition custom code

CALL SHOP_EVENTTURNEND

;宴会当日に一日が终わると宴会フラグOFF
IF DAY >= FLAG:开始日 && FLAG:宴会举办FLAG
	CALL ENKAI_END()
ENDIF
;灵庙フラグOFF
FLAG:17 = 0
;催眠フラグOFF
FLAG:催眠强度 = 0
DRAWLINE
PRINTFORML 
;SETCOLOR C_PINK
;FONTBOLD
FOR LOCAL,0,CHARANUM
	TALENT:LOCAL:反抗抑制 = 0
	TALENT:LOCAL:合意误认 = 0
	
	;この3つは你も取得可能
	IF EXP:LOCAL:接吻经验 > 300 && ABL:LOCAL:Ｍ感觉 > 5 && !TALENT:LOCAL:接吻魔
		PRINTFORML 在多次的接吻中找到了自己灵魂的归宿的%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%、一旦停止接吻便会感到不安和焦躁…
		;陷落素質取得口上,6,接吻魔
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 6)
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%获得了[接吻魔]
		TALENT:LOCAL:接吻魔 = 1
	ENDIF
	IF EXP:LOCAL:调教自慰经验 > 200 && EXP:LOCAL:自慰经验 > 500 && !TALENT:LOCAL:自慰狂
		PRINTFORML 懂得了如何发泄自己欲望的%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%、一天的所有时间里都在想着自慰的事…
		;陷落素質取得口上,7,自慰狂
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 7)
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%获得了[自慰狂]
		TALENT:LOCAL:自慰狂 = 1
	ENDIF
	IF EXP:LOCAL:子宫口开发经验 > 50 && !TALENT:LOCAL:子宫口性感
		PRINTFORML 被反复进攻着子宫口的%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%、比起痛苦来能感觉到更加强烈的快感…
		;陷落素質取得口上,8,子宫口性感
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 8)
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%获得了[子宫口性感]
		TALENT:LOCAL:子宫口性感 = 1
	ENDIF
	IF FLAG:癖好有效 > 0 && EXP:LOCAL:放尿经验 > 100 * (10 - 2 * TALENT:LOCAL:容易中毒) / 10 && !TALENT:LOCAL:漏尿癖
		PRINTFORML 绝顶时持续放尿的%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%的身体变得非常松弛、以至于现在排尿已经变成了一种习惯…
		;陷落素質取得口上,9,漏尿癖
		CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 9)
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%获得了[漏尿癖]
		TALENT:LOCAL:漏尿癖 = 1
	ENDIF
	;以下は你はスキップ
	SIF LOCAL == MASTER
		CONTINUE
	;addition custom code
	CALL ADD_CHECK_FALL_STATES(LOCAL)
	CUSTOM_FALL_STATES_ALLOWED = RESULT
	;恋慕取得
	IF GETBIT(CUSTOM_FALL_STATES_ALLOWED, 1) ;addition custom code
		;面灵气は特别あつかい
		IF LOCAL == 57
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%在察觉了自己对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的思慕的同时、嘴角翘了起来
			PRINTFORMW 浮现出自然笑容的%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%、好像拥有了表情的样子
			;陷落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%的\@ TALENT:LOCAL:思慕 ? [思慕] # [爱欲] \@变成了[恋慕]
			FLAG:10 ++
		;爱欲の有无で分岐
		ELSEIF TALENT:LOCAL:爱欲
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%因为身体与心灵都被%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%填满而感到了喜悦…
			PRINTL 即便是从糜烂的关系中催生出的爱意、不也还是一种恋的形式么…
			;陷落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%的[爱欲]变成了[恋慕]
		ELSE
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%因为与%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%共同渡过的时光而感到很幸福的样子…
			;陷落素質取得口上,1,恋慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 1)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%的[思慕]变成了[恋慕]
			FLAG:10 ++
		ENDIF
		SIF !MARK:LOCAL:时奸刻印 && !TALENT:LOCAL:爱欲 && !CFLAG:LOCAL:睡眠奸 && !CFLAG:LOCAL:烂醉奸
			CFLAG:LOCAL:纯洁的交往 = 1
		TALENT:LOCAL:恋慕 = 1
		TALENT:LOCAL:思慕 = 0
		TALENT:LOCAL:爱欲 = 0
		TALENT:LOCAL:炮友 = 0
		
		;乐谱入手处理
		PRINTL
		RESULT = 乐谱入手_陷落(LOCAL)
		SIF RESULT != -1
			PRINTL
	;思慕取得
	;炮友／爱人の取得で分岐
	ELSEIF GETBIT(CUSTOM_FALL_STATES_ALLOWED, 0) ;addition custom code
		IF TALENT:LOCAL:炮友
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%产生了身体之外的兴趣
			;陷落素質取得口上,2,思慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 2)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%获得了[思慕]
		ELSE
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%变得对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%很在意的样子…
			;陷落素質取得口上,2,思慕
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 2)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%获得了[思慕]
		ENDIF
		TALENT:LOCAL:思慕 = 1
	;爱欲取得
	;侍奉快乐の分岐がまだないので、取得条件に并合
	ELSEIF GETBIT(CUSTOM_FALL_STATES_ALLOWED, 4) ;addition custom code
		;思慕持ち、心より身体を选んだ的な
		IF TALENT:LOCAL:思慕
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%与%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%不仅在心灵上也在肉体的羁绊上获得了满足的样子…
			PRINTFORML 比起肉眼看不见的羁绊更愿意为了看得见的证明而相拥在一起
			;陷落素質取得口上,3,爱欲
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 3)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%获得了[爱欲]
		ELSE
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%好像沉溺于对%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的情欲之中了…
			;陷落素質取得口上,3,爱欲
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 3)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%获得了[爱欲]
		ENDIF
			FLAG:10 ++
			TALENT:LOCAL:爱欲 = 1
			TALENT:LOCAL:思慕 = 0
			
			;乐谱入手处理
			PRINTL
			RESULT = 乐谱入手_陷落(LOCAL)
			SIF RESULT != -1
				PRINTL
	;炮友／爱人取得
	;炮友と爱人の违いは思慕の有无
	ELSEIF GETBIT(CUSTOM_FALL_STATES_ALLOWED, 3) ;addition custom code
		;思慕取得済＋炮友未取得
		IF TALENT:LOCAL:思慕 && !TALENT:LOCAL:炮友
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%似乎开始觉得就算这样沉溺在与%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的快乐之中也没有什么不好的了…
			;陷落素質取得口上,4,爱人
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 4)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%成为了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的[爱人]
			TALENT:LOCAL:炮友 = 2
		;思慕取得済＋炮友取得済
		ELSEIF TALENT:LOCAL:思慕 && TALENT:LOCAL:炮友 == 1
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%好像渴望着与%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%产生超越单纯的肉体关系之上的羁绊的样子…
			;陷落素質取得口上,4,爱人
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 4)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%成为了[爱人]
			TALENT:LOCAL:炮友 = 2
		;思慕未取得＋炮友未取得ならまず炮友取得
		ELSEIF !TALENT:LOCAL:炮友
			PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%沉溺在%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%给予的快感之中…
			;陷落素質取得口上,5,炮友
			CALL KOJO_MESSAGE_SEND("EVENT", 21, LOCAL, 5)
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%成为了%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的[炮友]
			TALENT:LOCAL:炮友 = 1
		ENDIF
	ENDIF
	IF EXP:LOCAL:演奏经验 >= 1000 && ABL:LOCAL:技巧 > 3 && !TALENT:LOCAL:音乐知识
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%演奏乐器造诣精进了
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%获得了[乐器知识]
		;PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%は音乐に造诣深くなった
		;PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%は[音乐知识]を得た
		TALENT:LOCAL:音乐知识 = 1
		;技能レベルUP口上,7-1,音乐知识
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 7, 1)
	ELSEIF EXP:LOCAL:演奏经验 >=5000 && !TALENT:LOCAL:音感
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%的音感得到了重复锻炼
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%获得了[相对音感]
		TALENT:LOCAL:音感 = 1
		;技能レベルUP口上,7-2,相对音感
		CALL KOJO_MESSAGE_SEND("EVENT", 34, LOCAL, 7, 2)
	ENDIF
	IF CFLAG:LOCAL:心情不快
		CFLAG:LOCAL:心情不快 --
		IF !CFLAG:LOCAL:心情不快
			SIF MARK:LOCAL:反抗刻印
				CFLAG:LOCAL:心情不快 ++
		ENDIF
	ENDIF
	;EX角色の恋慕时はメンバー追加イベントフラグを立てる
	IF !CFLAG:LOCAL:神社在住
		SIF TALENT:LOCAL:恋慕 && EXP:LOCAL:爱情经验 >= 200
			FLAG:1 = 3
	ENDIF
NEXT

IF EXP:MASTER:异常清扫经验 > (500 + 100 * FLAG:850) && ABL:MASTER:清扫技能 > 3 && !TALENT:MASTER:清扫中毒
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%注意到了可怕的事情
	PRINTFORMW 不管是睡着还是醒着、就连在停止时间时、与少女们肌肤相亲时、
	PRINTFORMW 都会情不自禁地去留意房间是否整理干净了
	PRINTFORMW 而且%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%只要在扫除的时候心情就能平静下来
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%对于扫除的讲究、已经到达了无可挽回的地步
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%获得了[清扫中毒]
	TALENT:MASTER:清扫中毒 = 1
ENDIF
;MASTERを除く全员のプリセット处理
FOR LOCAL,1,CHARANUM
	;气力の恢复
	BASE:LOCAL:气力 = MAXBASE:LOCAL:气力

	LOCAL:1 = MAXBASE:LOCAL:体力 / 3

	;恢复快、恢复慢
	LOCAL:1 = LOCAL:1 * 3 / (3 - TALENT:LOCAL:恢复速度)
	
	;添い寝してたら恢复速度向上
	SIF CFLAG:LOCAL:陪睡中
		LOCAL:1 *= 2
	SIF CFLAG:LOCAL:彻夜
		LOCAL:1 /= 2
	CFLAG:LOCAL:彻夜 = 0
	BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + LOCAL:1)

	;南无三卡片发行者が居たら发行フラグOFF
	CFLAG:LOCAL:328 = 0
NEXT
;全员のプリセット处理
FOR LOCAL,1,CHARANUM
	BASE:LOCAL:精力 = MIN(MAXBASE:LOCAL:精力, BASE:LOCAL:精力 + MAXBASE:LOCAL:精力 * BASE:LOCAL:体力 / MAXBASE:LOCAL:体力)
	
	;隐密中の角色が居たらフラグOFF
	CALL SET_SNEAK(0,LOCAL,1)
	CFLAG:LOCAL:隐密中 = 0
	
	;恶作剧中の角色が居たらフラグOFF
	CALL SET_PRANK(0,LOCAL,1)
	CFLAG:LOCAL:恶作剧 = 0
	
NEXT

;宴会の举办判定
SIF FLAG:3 && FLAG:宴会举办FLAG == 0
	CALL ENKAI_SETTING

IF CFLAG:MASTER:育儿人数
	养育费 = CFLAG:MASTER:育儿人数 * 1000 / (1 + FLAG:甲斐性无)
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%支付了%金额表示(养育费)%的子女养育费。
	MONEY -= 养育费
ENDIF

CALL RANK_POINT
PRINTW  

FLAG:0 = 0

;路人子のセーブ
IF TALENT:RANDOM_CHARANUM:路人 == 1 && GETBIT(TFLAG:一日一回, 9)
	IF FORGET_MOBGIRL
		FORGET_MOBGIRL = 0
		GOTO FORGET_MOB2
	ENDIF
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%对今天刚遇到的%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%的事情……
	CALL ASK_YN("忘记了","记得很清楚")
	IF RESULT
		VARSET TCVAR:RANDOM_CHARANUM:0, 0
		VARSET EX:RANDOM_CHARANUM:0, 0
		VARSET TEQUIP:RANDOM_CHARANUM:0, 0
		VARSET PALAM:RANDOM_CHARANUM:0, 0
		CALL InitializeBeforeTrain(RANDOM_CHARANUM)
		CFLAG:RANDOM_CHARANUM:900 ++
		CALL SAVE_MOBGIRL
		SIF RESULT == -1
			GOTO FORGET_MOB
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%\@ TALENT:RANDOM_CHARANUM:风俗娘 ? 感觉以后再去找%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%玩 # 感觉以后再见到%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)% \@也不错
	ELSE
		$FORGET_MOB
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%把%NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%忘得一干二净
	ENDIF
ENDIF
$FORGET_MOB2
IF TALENT:RANDOM_CHARANUM:路人 == 2 && GETBIT(TFLAG:一日一回, 9)
	CALL MOBGIRL_PROFILE2()
	VARSET TCVAR:RANDOM_CHARANUM:0, 0
	VARSET EX:RANDOM_CHARANUM:0, 0
	VARSET TEQUIP:RANDOM_CHARANUM:0, 0
	VARSET PALAM:RANDOM_CHARANUM:0, 0
	CALL InitializeBeforeTrain(RANDOM_CHARANUM)
	SAVECHARA TOSTR(CFLAG:RANDOM_CHARANUM:路人子セーブスロット), CSTR:RANDOM_CHARANUM:路人子PROFILE１, RANDOM_CHARANUM
	[IF DEBUG]
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", RANDOM_CHARANUM)%的情报更新了。
	[ENDIF]
ENDIF
;AUTO SAVE
CALL saveManageSaveGame(1000, 2)
BEGIN SHOP

@RANK_POINT
VARSET LOCAL
;清洁度
LOCAL:2 = EX:MASTER:今日的扫除回数 / 10
PRINTFORML 今日扫除的垃圾的量{EX:MASTER:今日的扫除回数}
PRINTFORML 清扫得到的额外奖励 \\{LOCAL:2}
IF DAY:2 == 4 && DAY:3 == 31
	PRINT (x2)  【大扫除额外奖励】
	LOCAL:2 *= 2
ENDIF
;据点の主の好感度と信赖度が上升
LOCAL:3 = MIN(10, EX:MASTER:今日的扫除回数 / 2000)
IF LOCAL:3
	PRINTFORML 通过努力的扫除、提高了%NAME_OUTPUT_TRANSLATION("CALLNAME", 据点_家主)%对你的评价
	CFLAG:据点_家主:好感度 += LOCAL:3 * 2
	CFLAG:据点_家主:信赖度 += LOCAL:3
	IF LOCAL:3 / 3 > 0
		PRINTFORML 因为努力的扫除、收到了{LOCAL:3 / 3}个平价食材
		ITEM:平价食材 += LOCAL:3 / 3
	ENDIF
ENDIF
PRINTL  
;手传い报酬
LOCAL:4 = TCVAR:MASTER:帮忙工作量

SIF LOCAL:4 > 0
	PRINTFORML 帮忙干活得到的额外奖励 \\{LOCAL:4}

MONEY += LOCAL:2 + LOCAL:4

;好感度上升
IF FLAG:12 && MONEY:2 < CM_LIMIT
	LOCAL:5 = MAX(1,FLAG:12 / 100)
	LOCAL:5 = MIN(LOCAL:5, CM_LIMIT - MONEY:2)
	PRINTFORML 好感度上升额外奖励 ＋{LOCAL:5}筹码
	MONEY:2 += LOCAL:5
	PRINTFORML 现在的筹码：%三桁区切(MONEY:2)%　筹码保有上限：%三桁区切(CM_LIMIT)%
ENDIF
FLAG:12 = 0

PRINTW  

@恋慕取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF CFLAG:ARG:好感度 < REQUIRED_FAVOR_恋慕
	RETURNF 0
SIF CFLAG:ARG:信赖度 < REQUIRED_TRUST_恋慕
	RETURNF 0
SIF ABL:ARG:亲密 < REQUIRED_AFFECTION_恋慕
	RETURNF 0
SIF MARK:ARG:反抗刻印
	RETURNF 0
SIF TALENT:ARG:爱欲 && !FLAG:陷落路线变化
	RETURNF 0
RETURNF 1

@思慕取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:爱欲
	RETURNF 0
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:思慕
	RETURNF 0
SIF CFLAG:ARG:好感度 < REQUIRED_FAVOR_思慕
	RETURNF 0
SIF CFLAG:ARG:信赖度 < REQUIRED_TRUST_思慕
	RETURNF 0
SIF ABL:ARG:亲密 < REQUIRED_AFFECTION_思慕
	RETURNF 0
SIF TALENT:ARG:炮友 && !FLAG:陷落路线变化
	RETURNF 0
RETURNF 1

@爱欲取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:爱欲
	RETURNF 0
SIF !TALENT:ARG:炮友
	RETURNF 0
SIF CFLAG:ARG:好感度 < REQUIRED_FAVOR_爱欲
	RETURNF 0
SIF ABL:ARG:欲望 < REQUIRED_DESIRE_爱欲
	RETURNF 0
SIF (EXP:ARG:绝顶经验 - EXP:ARG:无自觉绝顶经验) < REQUIRED_ECSTACY_爱欲
	RETURNF 0
SIF (ABL:ARG:欲望 - ABL:ARG:亲密) < 1
	RETURNF 0
RETURNF 1

@炮友取得条件(ARG)
#FUNCTION
SIF TALENT:ARG:恋人
	RETURNF 0
SIF TALENT:ARG:恋慕
	RETURNF 0
SIF TALENT:ARG:炮友 == 2
	RETURNF 0
SIF CFLAG:ARG:好感度 <= REQUIRED_FAVOR_炮友
	RETURNF 0
SIF ABL:ARG:欲望 < REQUIRED_DESIRE_炮友
	RETURNF 0
SIF (EXP:ARG:绝顶经验 - EXP:ARG:无自觉绝顶经验) < REQUIRED_ECSTACY_炮友
	RETURNF 0
SIF (ABL:ARG:欲望 - ABL:ARG:亲密) < 0
	RETURNF 0
RETURNF 1
