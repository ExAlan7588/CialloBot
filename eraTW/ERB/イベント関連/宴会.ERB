@ENKAI_SETTING
;------------------------------------------------------------------------------------------------------
;宴会の举办判定
;------------------------------------------------------------------------------------------------------
;1ターンで饮む酒の量は经过时间 * 10 通常は30分なので300、宴会规模1200なら2时间程度の宴会となる
;主催者は宴会の准备と片づけの仕事をしなければならない
;一日に复数の宴会を开ける仕样ではないので注意（人が分散しちゃって寂しいし…）
;------------------------------------------------------------------------------------------------------
PRINTL  

;举办判定は宴会フラグが立っていない状态でしか行われない（バグ防止）
SIF FLAG:宴会举办FLAG
	RETURN
	
;メイン主催者が出禁になっているなら举办されない
;宴会种别は98以上には设定しないこと
;2 幻想乡年忘れ会　メイン主催者：无　大晦日の一周间前に１００％发生する
IF DAY:2 == 4 && DAY:3 >= 25 && DAY:3 <= 30
	;宴会名
	EnkaiTitle = 幻想乡忘年会
	;宴会种别
	FLAG:宴会举办FLAG = 2
	;宴会规模
	FLAG:31 = 6000
	;宴会会场
	FLAG:32 = 9
	;开始时间
	FLAG:33 = 900
	;开始日
	FLAG:开始日 = 31 - DAY:3
	;雨天中止
	FLAG:36 = 0
	PRINTL  
	PRINTFORMW 开年会的季节到来了
;赏月酒
ELSEIF DAY:2 == 3 && DAY:3 >= 1 && DAY:3 < 15
	;宴会名
	EnkaiTitle = 赏月酒
	;宴会种别
	FLAG:宴会举办FLAG = 3
	;宴会规模
	FLAG:31 = 2000 + RAND:1000
	;会场
	FLAG:32 = 409
	;开始时间
	FLAG:33 = 1140
	;开始日
	FLAG:开始日 = 15 - DAY:3
	;雨天中止
	FLAG:36 = 1
	PRINTL  
	IF MAIN_MAP == 4
		PRINTFORMW 被永远亭的赏月酒诱惑了
	ELSE
		PRINTFORMW 被永远亭邀请去喝赏月酒了
	ENDIF
;收获祭
ELSEIF DAY:2 == 3 && DAY:3 >= 16 && DAY:3 < 27
	;宴会种别
	FLAG:宴会举办FLAG = 4
	;宴会规模
	FLAG:31 = 2000 + RAND:3000
	;会场
	FLAG:32 = 202
	;开始时间
	FLAG:33 = 540
	;开始日
	FLAG:开始日 = 27 - DAY:3
	;雨天中止
	FLAG:36 = 0
	PRINTL  
	PRINTFORMW 今年庆祝收获的季节也到来了
;举办予告は星期三、实施日は星期四
ELSEIF CFLAG:28:出禁 == 0 && DAY%7 == 3 && CRAZY_THURSDAY
	;宴会名
	EnkaiTitle = 疯狂木曜日
	;宴会种别
	FLAG:宴会举办FLAG = 12
	;宴会规模
	;FLAG:31 = 6600
	 FLAG:31 = 1500 + RAND:500
	;宴会会场
	FLAG:32 = 202
	;开始时间
	FLAG:33 = 720
	;开始日
	FLAG:开始日 = 1
	;雨天中止
	FLAG:36 = 1
	PRINTL  
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", 28)%的疯狂木曜日活动开始了...
; 蕾米莉亚一时兴起的派对　メイン主催者：蕾米莉亚　自宅が红魔馆　土曜日に５０％で举办予告、实施日は日曜日
ELSEIF CFLAG:16:出禁 == 0 && DAY%7 == 6 && MAIN_MAP == 3 && RAND:50 <= 49
	;宴会名
	EnkaiTitle = 蕾米莉亚一时兴起的派对
	;宴会种别
	FLAG:宴会举办FLAG = 6
	;宴会规模
	FLAG:31 = 1500 + RAND:2500
	;宴会会场
	FLAG:32 = 324
	;开始时间
	FLAG:33 = 1140
	;开始日
	FLAG:开始日 = 1
	;雨天中止
	FLAG:36 = 1
	PRINTL  
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", 16)%似乎想召开派对的样子
;SAMPLE 灵梦一时兴起的酒宴　メイン主催者：灵梦　土曜日に５０％で举办予告、实施日は日曜日
ELSEIF CFLAG:1:出禁 == 0 && DAY%7 == 6 && RAND:100 <= 49
	;宴会名
	EnkaiTitle = 灵梦一时兴起的酒宴
	;宴会种别
	FLAG:宴会举办FLAG = 1
	;宴会规模
	FLAG:31 = 1000 + RAND:1500
	;宴会会场
	IF RAND:5 <= 3
		FLAG:32 = 11
	ELSEIF RAND:4 == 0
		FLAG:32 = 2
	ELSEIF RAND:4 == 0
		FLAG:32 = 20
	ELSEIF RAND:4 == 0
		FLAG:32 = 21
	ELSE
		FLAG:32 = 218
	ENDIF
	;开始时间
	FLAG:33 = 1360 - (FLAG:31 / 10)
	;开始日
	FLAG:开始日 = 1

	;雨天中止
	FLAG:36 = 1
	PRINTL  
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", 1)%似乎想召开宴会的样子
;	ELSEIF DAY:3 >= 8 && DAY:3 <= 10
;		;宴会名
;		EnkaiTitle = 夜通し读経ライブ
;		;宴会种别
;		FLAG:宴会举办FLAG = 5
;		;宴会规模
;		FLAG:31 = 2000 + RAND:30 * 40
;		;宴会会场
;		FLAG:32 = 104
;		;开始时间
;		FLAG:33 = 1440 + 240 - (FLAG:31 /10) ;朝４时まで宴会する
;		;开始日
;		FLAG:开始日 = 10 - DAY:3
;		;雨天中止
;		FLAG:36 = 0
;		PRINTL  
;		PRINTFORMW 夜通し读経ライブの时期が近づいてきた
ENDIF
;现在的参加人数をリセット
FLAG:参加人数 = 0
CALL SET_ENKAI_LOCATIONDISPLAY
SIF FLAG:宴会举办FLAG
	CALL 宴会概要
PRINTL  

@ENKAI_ENTRY
#DIM DYNAMIC HOST_LIST  , 人物数量上限;主催者参加
#DIM DYNAMIC GUEST_LIST , 人物数量上限;客参加
#DIM DYNAMIC NG_LIST    , 人物数量上限;来ない
#DIM ResidentRate	;そのMapに住んでいる角色の参加概率
#DIM VisitorRate	;そのMapに住んでいない角色の参加概率
#DIM HL
;---   宴会の参加者决定   -----------------------------------------------------------------------------
;CFLAG:宴会参加 0=不参加 1=参加 2=主催者 3=脱落者 4=饮まないけど参加
;------------------------------------------------------------------------------------------------------
;フラグ初期化
CVARSET CFLAG, GETNUM(CFLAG, "宴会参加"), 0, 1
SELECTCASE FLAG:宴会举办FLAG
	;疯狂星期四
	CASE 12
		HOST_LIST  = 28,80,67
		;GUEST_LIST = 
		;NG_LIST    = 97
		ResidentRate = 100
		VisitorRate = 30
	;1 灵梦一时兴起的酒宴
	CASE 1
		SELECTCASE FLAG:宴会礼品
			CASE 51
				FLAG:31 += RAND:1000
			CASE 52
				FLAG:31 += RAND:2000
		ENDSELECT
		;1:灵梦, 2:るーこと, 11:魔理沙
		HOST_LIST  = 1, 2, 11
		;10:萃香
		GUEST_LIST = 10
		;正邪は参加しない
		NG_LIST    = 97
		
		IF FLAG:31 >= 2500
		;规模が大きいと参加概率大
			ResidentRate = 12
			VisitorRate = 12
		ELSE
			ResidentRate = 7
			VisitorRate = 7
		ENDIF
	;2 幻想乡忘年会
	CASE 2
		;1:灵梦, 2:るーこと, 11:魔理沙
		HOST_LIST  = 1, 2, 11
		;10:萃香
		GUEST_LIST = 10
		;正邪は参加しない
		NG_LIST    = 97
		ResidentRate = 20
		VisitorRate = 20

	;赏月酒
	CASE 3
		;52:乌冬面, 53帝, 72:えーりん　75:梅蒂欣も手传う
		HOST_LIST  = 52, 53, 72, 75
		;62:ぐや
		GUEST_LIST = 62
		;正邪は参加しない
		NG_LIST    = 97
		ResidentRate = 33
		VisitorRate = 9
	;收获祭
	CASE 4
		EnkaiTitle = 收获祭
		FOR LOCAL,1,CHARANUM
			IF CFLAG:LOCAL:自宅位置 / 100 == 2
				FOR HL, 0, 20
					SIF HOST_LIST:HL
						CONTINUE
					HOST_LIST:HL = LOCAL
				NEXT
			ENDIF
		NEXT
		;秋姊妹は必ず来る
		GUEST_LIST = 77, 78
		;正邪は参加しない
		NG_LIST    = 97
		ResidentRate = 0
		VisitorRate = 8
	;读経ライブ
;	CASE 5
;		;命莲寺メンバーは働く
;		HOST_LIST = 39, 40, 41, 55, 85, 86, 87, 88
;		;GUEST_LIST无
;		;博丽神社组はこない
;		;正邪もこない
;		NG_LIST   = 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 97
;		CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST)
;		FOR LOCAL, 1, CHARANUM
;			IF CFLAG:LOCAL:宴会参加
;				SIF CFLAG:LOCAL:宴会参加 < 0
;					CFLAG:LOCAL:宴会参加 = 0
;				CONTINUE
;			ENDIF
;			;他は酒を饮まない
;			SIF RAND:15 == 0
;				CFLAG:LOCAL:宴会参加 = 9
;		NEXT
	;6 蕾米莉亚一时兴起的派对
	CASE 6
		;15:咲夜, 58:美铃, 59:小恶魔は働く
		HOST_LIST  = 15, 58, 59
		;16:蕾米莉亚, 50:芙兰は働かない
		GUEST_LIST = 16, 50
		;54:帕秋莉, 97:正邪は参加しない
		NG_LIST    = 54, 97
		ResidentRate = 33
		IF FLAG:31 >= 2500
		;规模が大きいと参加概率大
			VisitorRate = 14
		ELSE
			VisitorRate = 9
		ENDIF
	CASEELSE
		RETURN
ENDSELECT
CALL ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST, ResidentRate, VisitorRate)


;列表を利用した宴会角色のプリセット函数
@ENKAI_ENTRY_CHARALIST(HOST_LIST, GUEST_LIST, NG_LIST, ResidentRate, VisitorRate)
#DIM REF HOST_LIST  ;主催者参加
#DIM REF GUEST_LIST ;客参加
#DIM REF NG_LIST    ;来ない
#DIM L_NUM
#DIM L_ID
#DIM ResidentRate
#DIM VisitorRate
#DIM VenueMap

VenueMap = FLAG:32 / 100

L_NUM = FINDELEMENT(HOST_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(HOST_LIST:L_ID):宴会参加 = 2
	NEXT
ENDIF
L_NUM = FINDELEMENT(GUEST_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(GUEST_LIST:L_ID):宴会参加 = 1
	NEXT
ENDIF
;设定したことを明确化するために-1にします
L_NUM = FINDELEMENT(NG_LIST, 0)
IF L_NUM >= 1
	FOR L_ID, 0, L_NUM
		CFLAG:(NG_LIST:L_ID):宴会参加 = -1
	NEXT
ENDIF

FOR LOCAL, 1, CHARANUM
	IF CFLAG:LOCAL:宴会参加
		SIF CFLAG:LOCAL:宴会参加 < 0
			CFLAG:LOCAL:宴会参加 = 0
		CONTINUE
	ENDIF
	IF CFLAG:LOCAL:自宅位置 / 100 == VenueMap
		SIF RAND:100 < ResidentRate
			CFLAG:LOCAL:宴会参加 = 1
	ELSE
		SIF RAND:100 < VisitorRate
			CFLAG:LOCAL:宴会参加 = 1
	ENDIF
NEXT

@ENKAI_WORK(ARG)
;宴会の行动内容を入力する
;NOSAMES:全部の参数が异なると1, 1つでも一致していると0を返す
IF FLAG:宴会举办FLAG > 0 && (NOSAMES(CFLAG:ARG:职业种类, JOB_宴会准备, JOB_宴会参加, JOB_宴会の片づけ, JOB_醉いつぶれる) || CFLAG:ARG:行动 == 0)
	;宴会の时间になったら准备仕事の量に応じて主催者が动き始める
	IF TIME >= FLAG:开始时间 - (FLAG:宴会规模 / 30) - 15 && TIME < FLAG:33 && CFLAG:ARG:宴会参加 == 2
		CALL ENKAI_PREPARE(ARG)
		RETURN 1
	;玩耍に来るだけの奴は时间になったら玩耍に来る
	ELSEIF DAY == FLAG:开始日 && TIME >= FLAG:33 && CFLAG:ARG:同行 == 0
		CALL ENKAI_START(ARG)
		RETURN 1
	;とっくに始まってる场合
	ELSEIF 宴会举办中判定() && CFLAG:ARG:同行 == 0
		CALL ENKAI_START(ARG)
		RETURN 1
	ENDIF
ELSEIF FLAG:宴会举办FLAG > 0
	RETURN 1
ENDIF
RETURN 0


@ENKAI_PREPARE(ARG)
;主催者の准备仕事
IF CFLAG:ARG:职业种类 != JOB_宴会准备
	MAXBASE:ARG:工作量 = FLAG:31 / 3
	;最大值、2400
	SIF MAXBASE:ARG:工作量 >= 2400
		MAXBASE:ARG:工作量 = 2400
	BASE:ARG:工作量 = MAXBASE:ARG:工作量
	CFLAG:ARG:职业种类 = JOB_宴会准备
	CFLAG:ARG:职场 = TFLAG:宴会场
	TCVAR:ARG:315 = 0
	CFLAG:ARG:行动 = 5
ENDIF
RETURN 1

@ENKAI_START(ARG)
;それぞれが饮むペースを变えるので、宴の规模に応じて工作量が变化
IF CFLAG:ARG:职业种类 != JOB_宴会参加 && TCVAR:ARG:烂醉 == 0
	MAXBASE:ARG:工作量 = FLAG:31
	;最大值、通常7200(半日)・酒吞み神なら制限无
	SIF MAXBASE:ARG:工作量 >= 7200 && TALENT:ARG:酒耐性 != 3
		MAXBASE:ARG:工作量 = 7200
	MAXBASE:ARG:工作量 += RAND:500
	IF TALENT:ARG:酒耐性 == -1
		TIMES MAXBASE:ARG:工作量, 0.75
	ELSEIF TALENT:ARG:酒耐性 == 1
		TIMES MAXBASE:ARG:工作量, 1.25
	ELSEIF TALENT:ARG:酒耐性 == 2
		TIMES MAXBASE:ARG:工作量, 1.5
	ELSEIF TALENT:ARG:酒耐性 == 3
		TIMES MAXBASE:ARG:工作量, 2.0
	ENDIF
	BASE:ARG:工作量 = MAXBASE:ARG:工作量
	;仕事内容・宴会に参加
	CFLAG:ARG:职业种类 = JOB_宴会参加
	CFLAG:ARG:职场 = TFLAG:宴会场
	CFLAG:ARG:当前位置 = TFLAG:宴会场
	TCVAR:ARG:315 = 0
	CFLAG:ARG:行动 = 5
	IF CFLAG:ARG:宴会参加 == 2
		;主催者は片付け予定をセット(工作量は最大1200)
		CFLAG:ARG:整理工作量 = MIN(FLAG:宴会规模 / 6, 1200)
		CFLAG:ARG:清扫的场所 = FLAG:宴会会场
	ENDIF
ENDIF

RETURN 1


@ENKAI_KATAZUKE(ARG)
;主催者の片づけ仕事
MAXBASE:ARG:工作量 = CFLAG:ARG:整理工作量
BASE:ARG:工作量 = MAXBASE:ARG:工作量
;仕事内容・宴会的善后
CFLAG:ARG:职业种类 = 50
CFLAG:ARG:职场 = GET_MAP_REPLACEMENT(CFLAG:ARG:清扫的场所)
TCVAR:ARG:没法帮忙 = 0
CFLAG:ARG:行动 = 5
RETURN 1


@ENKAI_DRUNK(ARG)
;喝得酩酊大醉ラインを超えていたら返回
IF BASE:ARG:酒气 > (MAXBASE:ARG:酒气 / 10) * 8
	RETURN 1
ENDIF

;酒を饮まないなら醉わない 
IF TALENT:ARG:酒耐性 == -2 || CFLAG:ARG:妊娠自觉状态 == 1
	CFLAG:ARG:职业种类 = 0
	CFLAG:ARG:宴会参加 = 4
	IF CFLAG:MASTER:当前位置 == CFLAG:ARG:当前位置
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%好像过得十分开心
	ENDIF
ELSE
	CFLAG:ARG:职业种类 = 0
	CFLAG:ARG:宴会参加 = 3
;	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%は微醺气分のようです
	IF CFLAG:MASTER:当前位置 == CFLAG:ARG:当前位置
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%好像过得十分开心
	ENDIF
ENDIF
RETURN 1

@ENKAI_END
;会场となった场所は荒れ果てる、据点外でも对象地点的污垢增加させるためTFLAG:宴会场ではない
SIF YOGORE:(FLAG:32) < 300
	YOGORE:(FLAG:32) += 1000
CALL ENKAI_STOP()

@ENKAI_STOP
;恶劣天气などによる中止
FOR LOCAL,0,CHARANUM
	SIF	CFLAG:LOCAL:宴会参加 == 0
		CONTINUE
	CFLAG:LOCAL:职业种类 = 0
	MAXBASE:LOCAL:工作量 = 0
	BASE:LOCAL:工作量 = 0
	CFLAG:LOCAL:宴会参加 = 0
	CFLAG:LOCAL:行动 = 0
NEXT
FLAG:宴会举办FLAG = 0
FLAG:31 = 0
FLAG:32 = 0
TFLAG:宴会场 = 0
FLAG:33 = 0
FLAG:开始日 = 0
FLAG:参加人数 = 0
FLAG:36 = 0
FLAG:宴会礼品 = 0
;宴会会场表示 = 
TFLAG:宴会状态 = 0
RETURN 0

@宴会予定表示
PRINTFORM {FLAG:33 / 60}:{(FLAG:33 % 60)/10}{(FLAG:33 % 60)%10}开始在%宴会场表示()%召开%EnkaiTitle%


@SET_ENKAI_LOCATIONDISPLAY
宴会会场表示 = 

@宴会场表示
#FUNCTIONS
LOCALS = 宴会场表示エラー
IF FLAG:宴会举办FLAG
	IF IN_HOME(FLAG:32)
		LOCALS = %NAME_FROM_PLACE(FLAG:32)%
	ELSE
		LOCALS = %GET_MAPNAME(FLAG:32/100)%的%GET_PLACENAME(FLAG:32)%
	ENDIF
ENDIF
RETURNF LOCALS

@宴会概要
PRINTFORML \@ FLAG:开始日 == 1? 明日# {FLAG:开始日}日后\@、{FLAG:33 / 60}:{(FLAG:33 % 60)/10}{(FLAG:33 % 60)%10}开始要在%宴会场表示()%、召开「%EnkaiTitle%」的样子

PRINT 这样的话
IF FLAG:31 >= 5000
	PRINTW ……看起来这将是持久战啊
ELSEIF FLAG:31 >= 2500
	PRINTW 、这边也得充分准备呢
ELSEIF FLAG:31 >= 1500
	PRINTW 、正好可以用来联络感情
ELSE
	PRINTW 、应该将会是个不错的聚会
ENDIF
PRINTFORMW \@ FLAG:开始日 == 1? 时间上有一点仓促呢、要不要参加呢……# 要参加就得把预定空出来呢……\@
SIF FLAG:36 > 0
	PRINTFORMW 也说不准、碰上雨天就中止了……
FLAG:开始日 += DAY


@宴会参加人数再计算
;宴会の参加人数をセット
FLAG:参加人数 = 0
;FOR LOCAL,0,CHARANUM
;	IF (CFLAG:LOCAL:宴会参加 == 1 || CFLAG:LOCAL:宴会参加 == 2) && !TCVAR:LOCAL:烂醉
;		SIF CFLAG:LOCAL:当前位置 == TFLAG:宴会场
;			FLAG:参加人数 ++
;	ENDIF
;NEXT
LOCAL = 0
WHILE LOCAL + 1 < CHARANUM
	LOCAL = FINDCHARA(CFLAG:当前位置, TFLAG:宴会场, LOCAL + 1)
	IF LOCAL < 0
		BREAK
	ELSE
		;烂醉していない参加者および主催者をカウント
		SIF (CFLAG:LOCAL:宴会参加 == 1 || CFLAG:LOCAL:宴会参加 == 2) && !TCVAR:LOCAL:烂醉
			FLAG:参加人数 ++
	ENDIF
WEND

@宴会举办中判定
#FUNCTION
;RETURNF FLAG:宴会举办FLAG == 98 || FLAG:宴会举办FLAG == 99
RETURNF TFLAG:宴会状态 == 98 || TFLAG:宴会状态 == 99

@宴会场所在判定(ARG)
#FUNCTION
RETURNF 宴会举办中判定() && CFLAG:ARG:当前位置 == TFLAG:宴会场

@宴会开始日判定()
#FUNCTION
RETURNF 宴会举办中判定() == 0 && DAY >= FLAG:开始日
