;-------------------------------------------------
;函数名:FESTIVAL
;概　要:行事を表す文字列を取得する函数
;参　数:无
;-------------------------------------------------
@FESTIVAL
#FUNCTIONS
IF DAY:2 == 1
	SELECTCASE DAY:3
		CASE 1
			RETURNF "元旦"
		CASE 3
			RETURNF "女儿节"
		CASE 11
			RETURNF "母亲节"
		CASE 13
			RETURNF "白色情人节前日"
		CASE 14
			RETURNF "白色情人节"
		CASE 25
			RETURNF "例大祭"
		CASE 28
			RETURNF "兔子之日"
	ENDSELECT
ELSEIF DAY:2 == 2
	SELECTCASE DAY:3
		CASE 7
			RETURNF "七夕"
		CASE 14
			RETURNF "盂兰盆节"
		CASE 28
			RETURNF "兔子之日"
	ENDSELECT
ELSEIF DAY:2 == 3
	SELECTCASE DAY:3
		CASE 1
			RETURNF "风神「二百十日」"
		CASE 15
			RETURNF "十五夜"
		CASE 27
			RETURNF "收获祭"
		CASE 28
			RETURNF "兔子之日"
		CASE 30
			RETURNF "万圣节"
	ENDSELECT
ELSEIF DAY:2 == 4
	SELECTCASE DAY:3
		CASE 3
			RETURNF "节分"
		CASE 14
			RETURNF "情人节"
		CASE 24
			RETURNF "平安夜"
		CASE 25
			RETURNF "圣诞节"
		CASE 28
			RETURNF "兔子之日"
		CASE 31
			RETURNF "大晦日"
	ENDSELECT
ENDIF

;特定の祭日の起床时メッセージ
@SEASONAL_MORNING
#DIM 生活时间补正, 5
SELECTCASE FESTIVAL()
	CASE "元旦"
		SIF DAY != 1
			CALL NEWYEAR
	CASE "情人节"
		PRINTL 会收到巧克力吗…
	CASE "白色情人节前日"
		PRINTFORMW 需要准备好回礼
	CASE "兔子之日"
		PRINTFORMW \@ DAY:2 == 4? 在寒空之下穿着兔女郎服度过艰难的一天…# 大家应该都穿着兔女郎服\@
	CASE "白色情人节"
		FOR LOCAL,1,CHARANUM
			SELECTCASE CFLAG:LOCAL:情人节
				CASE 1
					PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%给了黑雷神巧克力……
					PRINTFORMW 礼物还算合适吧
				CASE 2
					PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%给了普通的巧克力……
					PRINTFORMW 要准备相应的回礼
				CASE 3
					PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%给了高级的巧克力……
					PRINTFORMW 要准备上佳的回礼
				CASE 4
					PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%给了亲手做的巧克力……
					PRINTFORMW 要准备充满真心的回礼
			ENDSELECT
		NEXT
	CASE "大晦日"
		PRINTL 今天的每个角色都会熬夜
ENDSELECT

@SEASONAL(ARG)
VARSET LOCAL
;出会ったとき
SELECTCASE FESTIVAL()
	CASE "元旦"
		PRINTL
		PRINTFORML ●%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%向%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%致以新年问候
		CALL OTOSHIDAMA(ARG)
	CASE "情人节"
		SELECTCASE CFLAG:ARG:好感度
			CASE 1000 TO 3000
				PRINTFORMW 从%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%那收到了巧克力
				PRINTW ……是黑雷神（义理）巧克力呢
				ITEM:120 ++
				CFLAG:ARG:情人节 = 1
			CASE 3001 TO 5000
				PRINTFORMW 从%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%那收到了巧克力
				ITEM:121 ++
				CFLAG:ARG:情人节 = 2
			CASE IS > 5000
				IF EXP:ARG:爱情经验 < 100
					PRINTFORMW 从%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%那收到了好像很贵的巧克力
					ITEM:122 ++
					CFLAG:ARG:情人节 = 3
				ELSE
					PRINTFORML 从%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%那收到了巧克力
					PRINTW 是亲手作的！好开心！
					ITEM:123 ++
					CFLAG:ARG:情人节 = 4
				ENDIF
		ENDSELECT
ENDSELECT

@FESTIVAL_MENU()
#FUNCTIONS
SELECTCASE DAY:2
	;春
	CASE 1
		SELECTCASE DAY:3
			CASE 1,2
				RETURNF "年节菜"
		ENDSELECT
	;夏
	CASE 2
	;秋
	CASE 3
	;冬
	CASE 4
		SELECTCASE DAY:3
			CASE 3
				RETURNF "江户卷"
			CASE 31
				RETURNF "跨年荞麦面"
		ENDSELECT
ENDSELECT

@NEWYEAR
#DIM YOME
#DIM ChildCount
#DIM CardCount
PRINTW 新年快乐！
CardCount = 0
FOR YOME,1,CHARANUM
	IF ABL:YOME:亲密 >= 5 && !CFLAG:YOME:神社在住
		PRINTFORML 收到了来自%NAME_OUTPUT_TRANSLATION("CALLNAME", YOME)%的新年贺卡
		CardCount ++ 
	ENDIF
	SIF CFLAG:YOME:儿童人数 - CFLAG:YOME:育儿人数 <= 0
		CONTINUE
	FOR ChildCount,1,MAXCHILD + 1
		IF DAY - CHILD_BIRTHDAY:YOME:ChildCount > CHILD_自立 && CHILD_INDEPENDENT:YOME:ChildCount > 0
			PRINTFORML 收到了来自%NAME_OUTPUT_TRANSLATION("CALLNAME", YOME)%的\@ CHILD_SEX:YOME:ChildCount == 1 ? 女儿 # 儿子 \@的%CHILDNAME:YOME:ChildCount%的新年贺卡
			CardCount ++ 
		ENDIF
	NEXT
NEXT
IF CardCount > 0
	PRINTFORMW 总计{CardCount}枚！
ENDIF
PRINTFORML 那么、%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%今年的抱负是…
PRINTL
PRINTLC [1] 自慰奖励
PRINTLC [2] 同性爱推进
PRINTLC [3] 牝穴开拓
PRINTL
PRINTL
PRINTLC [4] 尻穴开发
PRINTLC [5] 乳挟振兴
PRINTLC [6] 子孙繁栄
$LOOP
INPUT
SELECTCASE RESULT
	CASE 1
		LOCALS = 自慰
	CASE 2
		LOCALS = 同性爱
	CASE 3
		LOCALS = 性交
	CASE 4
		LOCALS = 肛交
	CASE 5
		LOCALS = 乳交
	CASE 6
		LOCALS = 生孩子
	CASEELSE
		GOTO LOOP
ENDSELECT

PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%决定今年专注于%LOCALS%、下定了决心
FLAG:抱负 = RESULT

;全年フラグをリセット
FOR LOCAL,0,CHARANUM
	CFLAG:LOCAL:全年 = 0
NEXT

@OTOSHIDAMA(ARG)
SIF DAY <= 1
	RETURN
SIF !TALENT:ARG:幼稚 && !TALENT:ARG:育儿中
	RETURN
IF TALENT:ARG:幼稚
	PRINTFORMW 被%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%要了压岁钱
ELSE
	PRINTFORMW 在%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的要求下给了%SHOW_CHILDNAME(ARG)%压岁钱
ENDIF

SELECTCASE MONEY
	CASE IS > 500000
		LOCAL = 10000
	CASE IS > 100000
		LOCAL = 5000
	CASE IS > 30000
		LOCAL = 3000
	CASE IS > 3000
		LOCAL = 1000
	CASEELSE
		LOCAL = 0
ENDSELECT
IF !LOCAL
	PRINTFORML 当%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%展示了空空如也的钱包时、%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%\@ TALENT:ARG:幼稚? 投来了怜悯的眼神 # 露骨地表现出了失望的表情\@
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的心灵受到了伤害
ELSE
	PRINTFORML 被%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%拿走了%金额表示(LOCAL)%…
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%很开心
	MONEY -= LOCAL
	TALENT:ARG:心情 = 1
	CALL CHANGE_CFLAG(2,ARG,LOCAL/200)
	CALL CHANGE_CFLAG(4,ARG,LOCAL/200)
ENDIF