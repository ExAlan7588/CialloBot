@DAILY_CONDITION_EV20
SIF RAND:15 > 0
	RETURN -1
SIF CFLAG:[[蛮奇]]:神社在住
	RETURN -1
SIF CFLAG:[[蛮奇]]:出禁
	RETURN -1
SIF !TALENT:[[蛮奇]]:思慕 && ! TALENT:[[蛮奇]]:恋慕
	RETURN -1
CALL DAILY_EV20
RETURN 1

@DAILY_EV20
#DIM 来客数

PRINTFORML 半夜、从窗户那传来了响声
PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%睡眼惺忪打开了窗户、外面漂着一个头
PRINTFORML ……是赤蛮奇的头
PRINTFORML 因为从这里路过所以顺便来看看
CALL ASK_YN("让她进来","睡觉")
IF RESULT
	PRINTFORMW 因为很困所以让她回去了
	RETURN
ENDIF
PRINTFORML 让她进来了

来客数 = 1
IF ABL:[[蛮奇]]:亲密 >= 10 && !RAND:3
	PRINTFORML 脑袋从窗户进来了……
	FOR LOCAL,2,ABL:73:亲密 - 5
		SIF !RAND:2
			CONTINUE
		来客数 ++
		PRINTFORMW 但是、不止一个而是有{来客数}个脑袋进来了
		SIF 来客数 >= 9
			BREAK
	NEXT
	SIF 来客数 == 1
		PRINTFORML 想着或许不止一个、但只有一个
	SIF 来客数 >= 4
		PRINTFORMW 被同样的脸包囲着、感觉不太舒坦……
ENDIF
IF TIME:5 >= 4
	PRINTFORML 因为被淋湿了所以把浴巾递过去了
	PRINTFORML 在毛巾上打着转的脑袋看起来是非常有趣的情景
ELSE
	SELECTCASE RAND:3
		CASE 0
			PRINTFORML 为了配合视线的高度、把坐垫放在了桌子之上
			SIF 来客数 > 1
				PRINTFORML 桌子上排列着脑袋的场景就像在确认斩首一样
		CASE 1
			PRINTFORML 因为喝茶有些辛苦所以用了吸管
			PRINTFORML 喝下去的东西到底去了哪里呢……
		CASE 2
			PRINTFORML 帮她从后头部拿下了不小心沾上的叶子
	ENDSELECT
ENDIF
PRINTFORMW 
PRINTFORMW …
PRINTFORMW ……
PRINTFORMW ………

IF RAND:100 < ABL:[[蛮奇]]:亲密 * 3 + TALENT:[[蛮奇]]:恋慕 * 15
	PRINTFORM 谈笑了一段时候后、脑袋\@ 来客数 > 1 ? 们# \@说道明天身体会来
	IF !PRIVATEROOM:(CFLAG:MASTER:初始位置 % 100) && TALENT:[[蛮奇]]:恋慕
		PRINTFORML 打算回去的时候、说道既然明天身体要来的话就住在这里吧
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%把脑袋\@ 来客数 > 1 ? 排列# 放置\@在枕边就这样睡着了……
	ELSE
		PRINTFORML 后就回去了
	ENDIF
	CFLAG:[[蛮奇]]:翌日来访 = 1
ELSE
	PRINTFORML 谈笑了一段时候后、脑袋\@ 来客数 > 1 ? 们# \@从窗户那回去了
	SIF !RAND:2
		CFLAG:[[蛮奇]]:翌日来访 = 1
ENDIF
;システムメッセージつき好感度变化
;最后の参数だけ好感度が变化
CALL CHANGE_CFLAG(2,[[蛮奇]],50)

;システムメッセージつき信赖度变化
;最后の参数だけ信赖度が变化
CALL CHANGE_CFLAG(4,[[蛮奇]],20)

;睡眠时间减少
CFLAG:MASTER:就寝时间 += 60
CALL COLORMESSAGE("睡眠时间减少了1小时",C_YELLOW,2)