@DAILY_CONDITION_EV7

IF !RAND:15 && MAIN_MAP == 9
	CALL DAILY_EV7
	RETURN 1
ELSE
	RETURN -1
ENDIF

@DAILY_EV7
#DIM 人数
#DIM YUGI
VARSET LOCAL
YUGI = !RAND:4
IF YUGI
	LOCALS = 勇仪们
ELSE
	LOCALS = 鬼们
ENDIF
人数 = 1 + RAND:5
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%打算上床睡觉时、喝了酒的{人数}人的鬼滚出来了！
SIF YUGI
	PRINTFORML 而且还有勇仪……
PRINTFORMW 看来打算接着在这滞留下去喝酒……
PRINTFORML 
;他の选择肢は后で
;ASK_M("接受","酒を渡して出て行ってもらう","ぶちのめす")
PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%拼命接受了%LOCALS%
PRINTFORML %LOCALS%一点都不客气的带着酒跟下酒菜开始狂欢了
LOCAL:1 = 1
FOR LOCAL,0,人数
	LOCAL:1 += RAND:3
NEXT
SIF YUGI
	LOCAL:1 += RAND:3 + RAND:3

FOR LOCAL,0,LOCAL:1
	PRINTFORMW …
	PRINTFORMW ……
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%被强行灌着酒
	SELECTCASE RAND:3
		CASE 0
			PRINTFORMW 普通的酒
			LOCAL:2 = 200
		CASE 1
			PRINTFORMW 相当强力的酒
			LOCAL:2 = 400
		CASE 2
			PRINTFORMW 很吃力！
			LOCAL:2 = 700
	ENDSELECT
	PRINTFORMW 
	BASE:MASTER:酒气 += LOCAL:2
	IF YUGI
		CALL CHANGE_CFLAG(2,64,3)
		CALL CHANGE_CFLAG(4,64,1)
	ENDIF
	IF BASE:MASTER:酒气 > MAXBASE:MASTER:酒气
		PRINTFORML 「哇！」
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%吐了！
		YOGORE:(CFLAG:MASTER:初始位置) += 1500
		BASE:MASTER:气力 -= 500
		MAXBASE:MASTER:酒气 += 3
		PRINTFORMW %LOCALS%哈哈大笑…
		IF BASE:MASTER:气力 < 0
			PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%直挺挺的倒了！
			BASE:MASTER:气力 -= 1000
			LOCAL:3 ++
			BREAK
		ELSE
			BASE:MASTER:酒气 /= 2
		ENDIF
	ELSEIF BASE:MASTER:酒气 > MAXBASE:MASTER:酒气 * 3 / 4
		PRINTFORMW 感到恶心了…
	ENDIF
NEXT

IF !LOCAL:3
	PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%完全经得起无情的灌酒！
	IF YUGI
		CFLAG:64:好感度 += 150
		CFLAG:64:信赖度 += 100
	ENDIF
	MAXBASE:MASTER:酒气 += 20
ENDIF
CFLAG:MASTER:彻夜 = 1
YOGORE:(CFLAG:MASTER:初始位置) += 1000
MAXBASE:MASTER:酒气 = LIMIT(MAXBASE:MASTER:酒气,1500,3000)
PRINTFORML 黎明的时候%LOCALS%离开了
PRINTFORML 然后睡眠不足的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%在有大量空瓶\@ LOCAL:3? 充满呕吐物臭味的# \@房间留下了
PRINTW 
RETURN 1