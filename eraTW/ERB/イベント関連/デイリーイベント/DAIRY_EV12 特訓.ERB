@DAILY_CONDITION_EV12
VARSET LOCAL
IF RAND:15 > 0
	RETURN -1
ELSE
	CALL FISHER_YATES_SHAFFLE(CHARANUM - 1)
	FOR LOCAL:10, 0, CHARANUM - 1
		LOCAL = SHAFFLE_ARRAY:(LOCAL:10) + 1
		IF CFLAG:LOCAL:信赖度 >= 100 && CFLAG:LOCAL:神社在住
			LOCAL:1 = LOCAL
			BREAK
		ENDIF
	NEXT
	IF !LOCAL:1
		RETURN -1
	ELSE
		CALL DAILY_EV12(LOCAL:1)
		RETURN 1
	ENDIF
ENDIF

@DAILY_EV12(ARG)
#DIM 正解
#DIM 経験值
#DIM 制限时间
#DIM 倍率
#DIM FEINT
#DIM イカサマ
VARSET LOCAL
イカサマ = 0
制限时间 = 2000 - (ABL:ARG:战斗能力 - ABL:MASTER:战斗能力) * 100
CALL KOJO_MESSAGE_SEND("DAILY", 12, ARG, 1)
CALL ASK_YN("交手","不要")
IF RESULT
	CALL KOJO_MESSAGE_SEND("DAILY", 12, ARG, 2)
ELSE
	正解 = RAND:4
	経験值 = MAX(ABL:ARG:战斗能力 * 5, 5)
	SELECTCASE RAND:20
		CASE IS <= 11
		;普通に
			LOCALS:0 = 上
			LOCALS:1 = 右
			LOCALS:2 = 左
			LOCALS:3 = 下
			倍率 = 100
		CASE 12 TO 15
		;ローマ字
			LOCALS:0 = 上
			LOCALS:1 = 右
			LOCALS:2 = 左
			LOCALS:3 = 下
			倍率 = 160
		;英语
		CASE 16,17,18
			LOCALS:0 = 上
			LOCALS:1 = 右
			LOCALS:2 = 左
			LOCALS:3 = 下
			倍率 = 120
		;スペイン语
		CASE 19
			LOCALS:0 = 上
			LOCALS:1 = 右
			LOCALS:2 = 左
			LOCALS:3 = 下
			倍率 = 300
	ENDSELECT
	;战斗能力が高いとフェイントかけてくる
	IF ABL:ARG:战斗能力 > 3 && !RAND:3
		FEINT = 1
		制限时间 += 400
		倍率 += 80
	ELSE
		FEINT = 0
	ENDIF
	CALL FISHER_YATES_SHAFFLE(4)
	PRINTFORMW 回避%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%的攻击（点击开始）
	CALL KOJO_MESSAGE_SEND("DAILY", 12, ARG, 3)
	PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%
	SETCOLOR C_YELLOW
	IF FEINT
		PRINTFORM %LOCALS:(RAND:4)%
		RESETCOLOR
		PRINTFORM 虚晃一枪后从%LOCALS:(3 - 正解)%
	ELSE
		PRINTFORM %LOCALS:(3 - 正解)%
		RESETCOLOR
	ENDIF
	PRINTFORML 方发起了攻击！
	$LOOP_IKASAMA
	FOR LOCAL:10,0,4
		LOCAL = SHAFFLE_ARRAY:(LOCAL:10)
		PRINTFORML [{LOCAL}] - 向%LOCALS:LOCAL%避开
	NEXT
	SIF !イカサマ && BASE:MASTER:TSP > 500
		PRINTFORML [{4}] - 时间停止
	TINPUT 制限时间, -1, 1
	IF RESULT == 正解
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%躲开了攻击！
		IF !イカサマ
			PRINTFORMW 互相增加了经验
		ELSE
			PRINTFORMW 因为太狡猾所有训练没有起到什么效果……
		ENDIF
		CALL KOJO_MESSAGE_SEND("DAILY", 12, ARG, 4, イカサマ)
		経験值 = 経験值 * 倍率 / 100
		EXP:MASTER:战斗经验 += 経験值
		EXP:ARG:战斗经验 += 経験值 * 2
		CFLAG:ARG:好感度 += 経験值 * 2
		CFLAG:ARG:信赖度 += 経験值 * 2
		PRINTFORML 战斗经验 ＋{経験值}（%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%）
		PRINTFORML 战斗经验 ＋{経験值 * 2}（%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%）
		PRINTFORML 好感度 ＋{経験值 * 2}（%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%）
		PRINTFORML 信赖度 ＋{経験值 * 2}（%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%）
	ELSEIF RESULT == 4 && !イカサマ && BASE:MASTER:TSP > 500
		イカサマ = 1
		倍率 /= 5
		PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%用时间停止能力看穿了攻击！
		GOTO LOOP_IKASAMA
	ELSE
		PRINTFORML 没能躲开！
		PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%
		SELECTCASE ABL:ARG:战斗能力
			CASE IS <= 1
				PRINTFORM 稍微有点
			CASE 2,3
				PRINTFORM 还挺
			CASEELSE
				PRINTFORM 非常
		ENDSELECT
		PRINTFORMW 疼
		CALL KOJO_MESSAGE_SEND("DAILY", 12, ARG, 5)
		BASE:MASTER:体力 -= 400 * (1 + ABL:ARG:战斗能力)
		PRINTFORML 战斗经验 ＋{経験值}（%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%）
		EXP:ARG:战斗经验 += 経験值
		CFLAG:ARG:好感度 += 経験值 / 2
		CFLAG:ARG:信赖度 += 経験值 / 2
		PRINTFORML 好感度 ＋{経験值}（%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%）
		PRINTFORML 信赖度 ＋{経験值}（%NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%）
	ENDIF
ENDIF
PRINTW 

@DAILY_MESSAGE_12(ARG, ARG:1, ARG:2, ARG:3, ARG:4, ARGS:1, ARGS:2)
RESETCOLOR
;二重表示抑制
IF GETBIT(DOUBLE_MESSAGE_BLOCKED:ARG, 11)
	CALL SET_KOJO_COLOR
	RETURN
ENDIF
SETBIT DOUBLE_MESSAGE_BLOCKED:ARG, 11
SELECTCASE ARG:1
CASE 1
	PRINTFORMDL %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%希望你陪伴完成新技能的训练
	SIF ABL:ARG:战斗能力 >= 4
			PRINTFORMDL 以对方为对手、一个不好会受到重伤吧……
CASE 2
	PRINTFORMDL %NAME_OUTPUT_TRANSLATION("CALLNAME", ARG)%表示遗憾
ENDSELECT
CALL SET_KOJO_COLOR()
