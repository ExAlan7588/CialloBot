@CHARA_DIARY_COM(C_ID)
#DIM DCOUNT
#DIM PAGENUM
#DIM C_ID
#DIM CLOSE
RESULT = 0
PAGENUM = 0
CLOSE = 0
;指令で呼び出されたとき
CALL KOJO_MESSAGE_SEND("DIARY", 0, C_ID, 0, 0, "EXIST")
IF !RESULT
	PRINTFORMW 然而好像还什么都没有写…
	RETURN
ENDIF

FOR DCOUNT, 1, MAX_DIARY_PAGE:C_ID:0 + 1
	SIF GROUPMATCH(DIARY:C_ID:DCOUNT, 1, 3)
		PAGENUM ++
NEXT
IF PAGENUM == 0
	PRINTFORMW 然而好像还什么都没有写…
	RETURN
ENDIF
FOR DCOUNT, 1, MAX_DIARY_PAGE:C_ID:0 + 1
	SIF !PAGESET:C_ID:DCOUNT
		BREAK
	CALL KOJO_MESSAGE_SEND("DIARY", 0, C_ID, PAGESET:C_ID:DCOUNT, DCOUNT, "TEXT", "指令")
	DRAWLINE
	;未读(3)だったら既读(1)にする
	SIF DIARY:C_ID:(PAGESET:C_ID:DCOUNT) == 3
		DIARY:C_ID:(PAGESET:C_ID:DCOUNT) = 1
	IF PAGENUM > 1
		CALL ASK_M("察看下一页 ", 1, " 向前翻5页", DCOUNT + 5 <= PAGENUM, "向前翻10页", DCOUNT + 10 <= PAGENUM, "察看前一页", DCOUNT > 1, "闭合日记", 1)
		IF RESULT == 0
			CONTINUE
		ELSEIF RESULT == 1
			DCOUNT += 4
		ELSEIF RESULT == 2
			DCOUNT += 9
		ELSEIF RESULT == 3
			DCOUNT += -2
		ELSEIF RESULT == 4
			CLOSE = 1
			BREAK
		ENDIF
	ENDIF
NEXT
IF !CLOSE
	PRINTFORMW 日记到这里就结束了…
ELSE
	PRINTFORMW %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%停止了阅读
ENDIF

@CHARA_DIARY_AFTERTRAIN
#DIM CHARA
#DIM DCOUNT
#DIM PAGECOUNT

;每日终わるとき
FOR CHARA, 1, CHARANUM 
	CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, 0, 0, "EXIST")
	SIF !RESULT
		CONTINUE
	;PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", CHARA)%的日记を确认
	TARGET = CHARA
	;日记の书き込み判定
	CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, 0, 0, "BEFORE_CHECK")
	FOR DCOUNT, 1, MAX_DIARY_PAGE:CHARA:0 + 1
		;最初にヒットした未读(2)を表示
		IF DIARY:CHARA:DCOUNT == 2
			;页セットしてから口上色を付けて表示
			CALL CHARA_DIARY_PAGESETTING(CHARA, DCOUNT)
			PAGECOUNT = RESULT
			CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, PAGESET:CHARA:PAGECOUNT, PAGECOUNT, "TEXT", "デイリー")
			PRINTL
			;既读フラグをつける
			DIARY:(NO:CHARA):DCOUNT = 1
			DRAWLINE
			BREAK
		;未读でない场合スキップ
		ELSE
			CONTINUE
		ENDIF
	NEXT
	;日记は每日一回だけ。呼び出されなかった日记IDのフラグをリセットする
	;恋慕とか告白とか大事なイベントは顺番を前にしたほうがいいです。
	FOR DCOUNT, 1, MAX_DIARY_PAGE:CHARA:0 + 1
		IF DIARY:CHARA:DCOUNT == 2
			DIARY:CHARA:DCOUNT = 0
		ENDIF
	NEXT
	CALL KOJO_MESSAGE_SEND("DIARY", 0, CHARA, 0, 0, "AFTER_CHECK")
NEXT


@MASTER_DIARY_INPUT
#DIMS 内容行,40
#DIM 行数
VARSET 内容行

行数 = 0
$INPUT_LOOP_DIARY

PRINTL
SETCOLOR C_YELLOW
PRINTFORML ・ %GET_MONTH()% {DAY:3,2,RIGHT}日 （%GET_DAY()%） %时刻表示(TIME)%
FOR LOCAL,0,行数
	PRINTFORML 　　%内容行:LOCAL%
NEXT
RESETCOLOR
PRINTL
PRINTBUTTON "[98] - 删除上一行", "98"
PRINT 　　
PRINTBUTTON "[99] - 完成日记", "99"
PRINTL

INPUTS

IF RESULTS == "98"
	IF 行数 > 0
		内容行:行数 = 
		行数 -= 1
		CLEARLINE 1
	ENDIF
	
	CLEARLINE (行数+5)
	GOTO INPUT_LOOP_DIARY
ELSEIF RESULTS == "99"
	LOCALS = 
	FOR LOCAL,0,行数-1
		LOCALS += @"%内容行:LOCAL%<br>"
	NEXT
	SIF 行数>0
		LOCALS += @"%内容行:(行数-1)%"
	
	RESULTS = %LOCALS%
	RETURN
ELSEIF 行数 == 39
	SETCOLOR C_YELLOW
	PRINTFORMW 行数已达到上限
	RESETCOLOR
	SIF RESULTS != ""
		CLEARLINE 1
	CLEARLINE (行数+5)
	GOTO INPUT_LOOP_DIARY
ELSE
	内容行:行数 = %RESULTS%
	
	SIF RESULTS != ""
		CLEARLINE 1
	CLEARLINE (行数+4)
	行数 += 1
	GOTO INPUT_LOOP_DIARY
ENDIF


@MASTER_DIARY_WRITE,内容
#DIMS 内容

;上限超えたら2つ消す
IF STRCOUNT(SAVESTR:1,"//") >= 100
	SPLIT SAVESTR:1,"//",LOCALS
	SAVESTR:1 =
	FOR LOCAL,0,99
		SAVESTR:1 = %LOCALS:LOCAL%%SAVESTR:1%
	NEXT
ENDIF
SAVESTR:1 = ・ %GET_MONTH()% {DAY:3,2,RIGHT}日 （%GET_DAY()%） %时刻表示(TIME)%_L_%内容%//%SAVESTR:1%

@READ_MASTER_DIARY
#DIMS 表示内容,100
#DIMS 内容,2
#DIM PAGE

SPLIT SAVESTR:1,"//",表示内容
FOR LOCAL,0,100
	SIF 表示内容:LOCAL != ""
		PAGE ++
NEXT

PRINTFORML ---| %NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%的日记 |-------------------------------------------------------------------------------------
FOR LOCAL,0,100
	IF 表示内容:LOCAL != ""
		SPLIT 表示内容:LOCAL,"_L_",内容
		PRINTL
		PRINTFORML PAGE.{1+LOCAL}
		PRINTL
		HTML_PRINT @"%内容:0%<br><br>%内容:1%<br>"
		PRINTL
		IF PAGE > 1
			PRINTL --------------------------------------------------------------------------------------------------------------------
			CALL ASK_YN("察看下一页 ","闭合日记")
			IF RESULT
				BREAK
			ENDIF
			CLEARLINE 3
		ENDIF
	ENDIF
NEXT
PRINTFORMW 日记到这里就结束了…


@SHARE_MASTER_DIARY
#DIMS 表示内容,100
#DIMS 内容,2
#DIM PAGE
#DIM READPAGE

SPLIT SAVESTR:1,"//",表示内容
FOR LOCAL,0,100
	SIF 表示内容:LOCAL != ""
		PAGE ++
NEXT

SIF PAGE >= 1
	READPAGE = RAND:PAGE

IF PAGE == 0
	CALL SPTALK,TARGET,,,@"「这不是什么都没有写吗……」//%NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%闭上了日记本。"
	RETURN 0
ENDIF

PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", TARGET)%随便翻了一页开始读…
PRINTL
CALL PRINT_FACE,TARGET

IF 表示内容:READPAGE != ""
	SPLIT 表示内容:READPAGE,"_L_",内容
	PRINTL
	PRINTFORML PAGE.{1+READPAGE}
	PRINTL
	HTML_PRINT @"%内容:0%<br><br>%内容:1%<br>"
	PRINTL
	PRINTW
ENDIF
;===========================================================================
;日记に书き込む处理
;指定されたCHARAのDIARY番号(PAGENUM)を最后の空白页にセット
;何页に书いたかを返り值にする
;===========================================================================
@CHARA_DIARY_PAGESETTING(CHARA, PAGENUM)
#DIM CHARA
#DIM PAGENUM
FOR LOCAL, 1, MAX_DIARY_PAGE:CHARA:0 + 1
	IF !PAGESET:CHARA:LOCAL
		PAGESET:CHARA:LOCAL = PAGENUM
		RETURN LOCAL
	ENDIF
NEXT





;===========================================================================
;日记保存文件处理
;===========================================================================
@WRITE_MASTER_DIARY_FILE
DRAWLINE
FOR LOCAL,0,20
	LOADTEXT (1700+LOCAL)
	
	IF RESULTS != ""
		PRINTBUTTON @"日记本[{LOCAL}]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[无]", LOCAL
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[放弃]", 99

$INPUT_LOOP_DIARY_FILE
INPUT
LOCAL = RESULT

SIF LOCAL == 99
	RETURN -1
IF !INRANGE(LOCAL, 0, 20-1)
	CLEARLINE 1
	GOTO INPUT_LOOP_DIARY_FILE
ENDIF

RESULT = LOCAL

SAVETEXT SAVESTR:1, (1700+RESULT)
SIF !RESULT
	CALL COLORMESSAGE("保存失败",C_YELLOW,2,1)


@READ_MASTER_DIARY_FILE
DRAWLINE
FOR LOCAL,0,20
	LOADTEXT (1700+LOCAL)
	
	IF RESULTS != ""
		PRINTBUTTON @"日记本[{LOCAL}]", LOCAL
		PRINTFORML 
	ELSE
		PRINTBUTTON "[无]", -1
		PRINTFORML 
	ENDIF
	RESETCOLOR
NEXT
DRAWLINE
PRINTBUTTON "[放弃]", 99

$INPUT_LOOP_DIARY_FILE
INPUT
LOCAL = RESULT

SIF LOCAL == 99
	RETURN -1
IF !INRANGE(LOCAL, 0, 20-1) || LOCAL == -1
	CLEARLINE 1
	GOTO INPUT_LOOP_DIARY_FILE
ENDIF

LOADTEXT (1700+RESULT)
SAVESTR:1 = %RESULTS%

@ASK_DIARY(ARGS)
PRINTFORML 要把这件事记录在日记里吗？
CALL ASK_YN
IF !RESULT
	CALL MASTER_DIARY_WRITE, @"%ARGS%"
ENDIF
