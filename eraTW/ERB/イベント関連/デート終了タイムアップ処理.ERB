@约会结束タイムUP处理
VARSET LOCAL
;气力ゼロ,衰弱时约会中断
FOR LOCAL,0,CHARANUM
	SIF CHK_DATENOW(CFLAG:LOCAL:约会中) && (CFLAG:LOCAL:衰弱 || (BASE:LOCAL:气力 == 0 && CFLAG:LOCAL:当前位置 != 情侣酒店 && CFLAG:LOCAL:当前位置 != 情人旅馆 && CFLAG:LOCAL:当前位置 != OMANEKIBEYA()))
		LOCAL:1 ++
	SIF CHK_DATENOW(CFLAG:LOCAL:约会中) && CFLAG:LOCAL:衰弱
		CALL ENDUFUFU(LOCAL)
NEXT
SIF NEMUKE() >= 720 || BASE:MASTER:酒气 >= MAXBASE:MASTER:酒气
	LOCAL:2 ++
;招待していて连泊でなければ泊めて贳える
IF CFLAG:MASTER:招待 && (LOCAL:1 || LOCAL:2) && !FLAG:70
	IF !TALENT:TARGET:恋慕
		PRINTFORML %NAME_OUTPUT_TRANSLATION("CALLNAME", (CFLAG:MASTER:招待))%也差不多到了清醒过来的时候了吧、是时候离开了…
		IF CFLAG:TARGET:诶嘿嘿
			PRINTFORMW 挣脱开紧紧抱住的%NAME_OUTPUT_TRANSLATION("CALLNAME", (CFLAG:MASTER:招待))%、回到了%GET_MAPNAME(MAIN_MAP)%…
		ELSE
			PRINTFORMW 仿佛要避开%NAME_OUTPUT_TRANSLATION("CALLNAME", (CFLAG:MASTER:招待))%火热的的视线一般、回到了%GET_MAPNAME(MAIN_MAP)%…
		ENDIF
		GOTO ESCAPE
	ELSEIF TFLAG:泡澡堂
		CALL 泡澡堂结束判定("你限界")
		GOTO ESCAPE
	ENDIF
	IF LOCAL:1
		PRINT 气力快用完了
	ELSEIF LOCAL:2
		PRINT 差不多到了回去的时间了
	ENDIF
	SIF !TCVAR:(CFLAG:MASTER:招待):连住禁止
		PRINTFORMW 今天%NAME_OUTPUT_TRANSLATION("CALLNAME", (CFLAG:MASTER:招待))%提供了借宿的样子
	SIF TCVAR:(CFLAG:MASTER:招待):连住禁止
		PRINTFORMW 今天也住在%NAME_OUTPUT_TRANSLATION("CALLNAME", (CFLAG:MASTER:招待))%这吗？
	PRINTL [0]住下来 [1]回去
	WHILE 1
	INPUT
	SIF RESULT == 0 || RESULT == 1
		BREAK
	WEND
	IF !RESULT && !TCVAR:(CFLAG:MASTER:招待):连住禁止
		PRINTFORMW 恭敬不如从命那就住下来吧
		;约会は结束
		CFLAG:MASTER:约会中 = MAIN_MAP
		CFLAG:TARGET:约会中 = MAIN_MAP
		FLAG:约会的对象 = 0
		TFLAG:约会前好感度 = 0
		TFLAG:约会道中 = 0
		CALL 守矢神签约会的对象のリセット()
		;以后の处理をカット
		GOTO OTOMARI
	ELSEIF !RESULT && TCVAR:(CFLAG:MASTER:招待):连住禁止
		PRINTFORMW 果然还是婉拒接待吧…
	ENDIF
ENDIF
$ESCAPE
IF LOCAL:1 && CFLAG:MASTER:当前位置
	PRINTL 
	PRINTW ***** 因为气力归０了、踏上了归途 *****
	CALL 回家の时间经过
	FLAG:时间停止 = 0
	;(天候パッチ)时刻・天候に応じて背景色を变更
	CALL SET_MAP_WEATHER_BGCOLOR
ELSEIF LOCAL:2
	PRINTL
	IF BASE:MASTER:酒气 >= MAXBASE:MASTER:酒气
		PRINTW ***** 酩酊大醉、踉蹌着踏上了归途 *****
		PRINTFORMW 回到了%GET_MAPNAME(MAIN_MAP)%的%NAME_OUTPUT_TRANSLATION("CALLNAME", MASTER)%直接当场倒下了
	ELSE
		PRINTW ***** 因为倦意袭来了、踏上了归途 *****
	ENDIF
	CALL 回家の时间经过
ENDIF
IF LOCAL:1 || LOCAL:2
	FOR LOCAL, 0, CHARANUM
		IF LOCAL && CFLAG:LOCAL:衰弱
			IF !CFLAG:LOCAL:神社在住
				CFLAG:LOCAL:初始位置 = SUKIMA()
				CFLAG:LOCAL:当前位置 = SUKIMA()
				CFLAG:LOCAL:据点来访 = 0
				CALL KOJO_MESSAGE_SEND("EVENT",20,LOCAL,2,0)
				PRINTFORMW 筋疲力尽的%NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%回去了
			ELSE
				PRINTFORM %NAME_OUTPUT_TRANSLATION("CALLNAME", LOCAL)%往%NAME_FROM_PLACE(CFLAG:LOCAL:初始位置)%
				PRINTW 返回
				SIF CFLAG:MASTER:初始位置 != CFLAG:LOCAL:初始位置
					LOCK:(CFLAG:LOCAL:初始位置) = 1
				CFLAG:LOCAL:当前位置 = CFLAG:LOCAL:初始位置
				CALL KOJO_MESSAGE_SEND("EVENT",3,1,LOCAL,0)
			ENDIF
		ENDIF
		CALL SET_DATE(99, LOCAL)
		IF CFLAG:LOCAL:招待
			CFLAG:LOCAL:招待 = 0
			IF LOCAL == 0
			ELSEIF CFLAG:LOCAL:神社在住
				CFLAG:LOCAL:初始位置 = CFLAG:LOCAL:神社在住
			ELSE
				CFLAG:LOCAL:初始位置 = SUKIMA()
			ENDIF
		ENDIF
		SIF CFLAG:LOCAL:陪睡中 > 0
			CFLAG:LOCAL:陪睡中 = 0
		SIF CFLAG:LOCAL:诶嘿嘿
			CALL ENDUFUFU(LOCAL)
		;恶作剧OFF
		CALL SET_PRANK(0,LOCAL,1)
	NEXT
	CALL 守矢神签约会的对象のリセット()
ENDIF
IF TFLAG:约会道中 && !FLAG:约会的对象
	TIME += TFLAG:约会道中
	TFLAG:约会道中 = 0
	CFLAG:MASTER:当前位置 = GET_ENTRANCE(CFLAG:MASTER:约会中)
ENDIF
$OTOMARI
