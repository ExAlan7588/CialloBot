@BASE自然变动(ARG)
#DIM 体力恢复量
#DIM 气力恢复量
#DIM 欲求不満
#DIM 伞ダメージ
#DIM Lapse
Lapse = TIME_PROGRESS(TIME:1)

;MASTERと同じ场所にいないとき
IF CFLAG:ARG:当前位置 != CFLAG:MASTER:当前位置
	BASE:ARG:情绪 = BASE:ARG:情绪 - Lapse * 4 * (BASE:ARG:情绪 + 500) / 500
	BASE:ARG:情绪 = MAX(0,BASE:ARG:情绪)
	BASE:ARG:理性 = BASE:ARG:理性 + Lapse * (MAXBASE:ARG:理性 + 500) / (BASE:ARG:理性 + 500)
ELSE
	;仙香玉兔による补正　情绪↑理性↓愤怒↓
	IF IsInsenceValid(ARG)
		BASE:ARG:情绪 += Lapse * (SQRT(TFLAG:仙香玉兔) / 2)
		BASE:ARG:情绪 = MIN(BASE:ARG:情绪 , MAXBASE:ARG:情绪)
		BASE:ARG:理性 -= Lapse * (SQRT(TFLAG:仙香玉兔) / 2)
		BASE:ARG:愤怒 -= Lapse * SQRT(TFLAG:仙香玉兔) / (CFLAG:ARG:勃然大怒 + 1)
	ENDIF
	;ポイズンボディの媚毒付与による补正　理性↓ 发情付与・无差别だがそれなり注ぐ必要あり
	;計算式が複雑すぎる
	IF TCVAR:ARG:ポイズンゲージ
		BASE:ARG:理性 -= Lapse * (MAX(SQRT(TCVAR:ARG:ポイズンゲージ) / 2, 1))
		IF BASE:ARG:理性 <= 0 && PALAM:ARG:情欲 >= PALAMLV:7 && TCVAR:ARG:发情 == 0 && !TALENT:ARG:妊娠
			;1000ほどゲージを稼ぐと3割程度で发情付与　欲求不満度が上がるほど確率向上
			IF RAND:(TCVAR:ARG:ポイズンゲージ + SQRT(TCVAR:ARG:ポイズンゲージ * CFLAG:ARG:积攒度 / 2)) > SQRT(TCVAR:ARG:ポイズンゲージ) * 20
				TCVAR:ARG:发情 = 1
				IF Lapse && !CFLAG:ARG:睡眠
					SETCOLOR C_PINK
					PRINTFORML 身体に\@ TCVAR:ARG:ポイズンゲージ > 1000 ? 十分に #  \@浸み込んだ媚毒が%CALLNAME:ARG%の理性を融かしていく…
					PRINTFORMW %CALLNAME:ARG%は发情してしまった
					RESETCOLOR
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
;ガシングガーデン　理性↓积攒度↑ 发情付与 药毒耐性・非生物には效かない
IF IsGardenValid(ARG)
	BASE:ARG:理性 -= Lapse * MAX(RAND:(ABL:ARG:欲望 + 1), 4)
	欲求不満 = 欲求不満素質补正(ARG, (2 * Lapse))
	;自宅マップでないと上升量が半減
	SIF !IN_HOME(TFLAG:GG設置位置)
		欲求不満 /= 2
	CFLAG:ARG:积攒度 = LIMIT(CFLAG:ARG:积攒度 + 欲求不満, 0, 1000)
	SIF BASE:ARG:理性 <= 0 && CFLAG:ARG:积攒度 >= 1000 && PALAM:ARG:情欲 >= PALAMLV:5 && !TALENT:ARG:妊娠
		TCVAR:ARG:发情 = 1
ENDIF
BASE:ARG:理性 = LIMIT(BASE:ARG:理性, 0, MAXBASE:ARG:理性)

;仕事中の场合、工作量が減少する
IF CFLAG:ARG:行动 == 5
	BASE:ARG:工作量 = MAX(BASE:ARG:工作量 - Lapse * 10, 0)
	;宴会で呑んでる场合酒气が上升
	SIF CFLAG:ARG:职业种类 == 49 && !(TALENT:ARG:酒耐性 == -2 || CFLAG:ARG:妊娠自觉状态 == 1)
		BASE:ARG:酒气 += Lapse * (RAND:3 * (4 - TALENT:ARG:酒耐性))
ENDIF
;愤怒は常に減少する
BASE:ARG:愤怒 = BASE:ARG:愤怒 - Lapse * 5 / (CFLAG:ARG:勃然大怒 + 1)
;宴会の席ならさらに減少する
SIF CFLAG:ARG:职业种类 == 49 && CFLAG:ARG:勃然大怒 > 0
	BASE:ARG:愤怒 -= Lapse * 5 / (CFLAG:ARG:勃然大怒 + 1)
SIF BASE:ARG:愤怒 <= 0
	CFLAG:ARG:勃然大怒 = 0
BASE:ARG:愤怒 = MAX(0,BASE:ARG:愤怒)
;酒气は常に減少する
BASE:ARG:酒气 = BASE:ARG:酒气 - Lapse
;泥酔时は減少量が増える
SIF TCVAR:ARG:泥酔
	BASE:ARG:酒气 -= Lapse * (RAND:4) 
BASE:ARG:酒气 = MAX(0,BASE:ARG:酒气)
;睡眠深度の变动
IF CFLAG:ARG:睡眠
	;寝てる场合
	TCVAR:ARG:睡眠深度 += Lapse * 10 * MAX(TCVAR:ARG:睡眠药强度, 1)
	TCVAR:ARG:睡眠深度 += (BASE:ARG:酒气 / 5 + TCVAR:ARG:疲労度 / 3)
	TCVAR:ARG:疲労度    = TCVAR:ARG:疲労度 * 2 / 3
	SIF TCVAR:ARG:疲労度 < 100
		TCVAR:ARG:疲労度 = 0
ELSE
	;寝てない
	IF TCVAR:ARG:睡眠深度 > 0
		TCVAR:ARG:睡眠深度 -= Lapse * 10
	ELSE
		TCVAR:ARG:睡眠深度 = 0
	ENDIF
ENDIF


;心情不快
SIF CFLAG:ARG:心情不快
	BASE:ARG:愤怒 = MAXBASE:ARG:愤怒

;勃起度の自然増減
IF CFLAG:ARG:诶嘿嘿
	IF BASE:ARG:勃起 <= 900 && (BASE:ARG:精力 >= 500 || TCVAR:ARG:国士无双（药）)
		BASE:ARG:勃起 += Lapse * 2
		SIF BASE:ARG:精力 >= 1000 || TCVAR:ARG:国士无双（药）
			BASE:ARG:勃起 += Lapse * 5
	ENDIF
ELSE
	SIF CFLAG:ARG:勃起度２ <= CFLAG:ARG:勃起度 && !TCVAR:ARG:イきそう
		BASE:ARG:勃起 = MAX(0,BASE:ARG:勃起 - Lapse * 20)
ENDIF
IF ARG == MASTER
	IF BASE:ARG:勃起 <= 900 && (BASE:ARG:精力 >= 500 || TCVAR:ARG:国士无双（药）)
		FOR LOCAL, 1, GET_TARGETNUM() + 1
			SIF !CFLAG:(TARGET:LOCAL):诶嘿嘿
				CONTINUE
			;リアリティはともかくとしてとりあえず
			BASE:MASTER:勃起 += GET_TARGETNUM() * 5
			BREAK
		NEXT
		SIF TARGET
			BASE:MASTER:勃起 += MIN(10, 1 + ABL:TARGET:欲望 / 5) * Lapse
	ENDIF
	;仕様が正しいのかわからないが睡眠奸BOUNS
	SIF CFLAG:MASTER:恶作剧 == 2 || FLAG:70
		BASE:MASTER:勃起 += 5
ENDIF
BASE:ARG:勃起 = MIN(BASE:ARG:勃起, MAXBASE:ARG:勃起)

;媚药フラグ
TCVAR:ARG:媚药 = MAX(TCVAR:ARG:媚药 - Lapse,0)
;利尿剂
TCVAR:ARG:利尿剂 = MAX(TCVAR:ARG:利尿剂 - Lapse,0)
;惚れ药フラグ
TCVAR:ARG:惚れ药 = MAX(TCVAR:ARG:惚れ药 - Lapse,0)
;仙香玉兔,ポイズンボディ
IF ARG == MASTER
	TFLAG:仙香玉兔 = MAX(TFLAG:仙香玉兔 - Lapse,0)
	TFLAG:ポイズンボディ = MAX(TFLAG:ポイズンボディ - Lapse, 0)
ENDIF
;ポイズンゲージ
TCVAR:ARG:ポイズンゲージ = MAX(TCVAR:ARG:ポイズンゲージ - (Lapse * MAX(TCVAR:ARG:ポイズンゲージ / 100, 1)), 0)

欲求不満 = 0
IF (TCVAR:ARG:媚药 || TCVAR:ARG:发情 || IsInsenceValid(ARG)) && ARG != MASTER
	欲求不満 = 欲求不満素質补正(ARG,Lapse)
	CFLAG:ARG:积攒度 = LIMIT(CFLAG:ARG:积攒度 + 欲求不満, 0, 1000)
ENDIF
;延迟減算处理
CFLAG:ARG:延迟 = MAX(CFLAG:ARG:延迟 - Lapse,0)
;污れを付ける
TCVAR:ARG:污す量 = 0
IF AT_HOME(ARG) || CFLAG:ARG:当前位置 == OMANEKIBEYA()
	TCVAR:ARG:污す量 = CFLAG:ARG:睡眠 ? 0 # 3 - ABL:ARG:清扫技能 / (1 + TALENT:ARG:ズボラ)
	SIF TALENT:ARG:幼稚
		TCVAR:ARG:污す量 += 2
	SIF CFLAG:ARG:初始位置 == CFLAG:ARG:当前位置 && TALENT:ARG:ズボラ
		TCVAR:ARG:污す量 += 3
	SIF CFLAG:ARG:大扫除 > 0
		TCVAR:ARG:污す量 = CFLAG:ARG:睡眠 ? 0 # 1 + TALENT:ARG:幼稚 - 3 * (ABL:ARG:清扫技能 + 1)
	;扫除をしてる场合、污れが減少
	IF CFLAG:ARG:职业种类 == 40 && CFLAG:ARG:行动 == 5
		TCVAR:ARG:污す量 = -2 * (ABL:ARG:清扫技能 * 20 + 100)
	;扫除係は12～18时の间扫除する,あなたの技能が高いと扫除させてもらえない
	ELSEIF TALENT:ARG:扫除係 && CFLAG:ARG:神社在住 && BETWEENTIME(720,1080)
		TCVAR:ARG:污す量 = -1 * MAX(ABL:ARG:清扫技能 + 2 - ABL:MASTER:清扫技能, 1)
	ENDIF
	YOGORE:(CFLAG:ARG:当前位置) += TCVAR:ARG:污す量 * Lapse / 5
	YOGORE:(CFLAG:ARG:当前位置) = LIMIT(YOGORE:(CFLAG:ARG:当前位置), 0, 6000)
ENDIF
IF !CFLAG:ARG:诶嘿嘿 && CFLAG:ARG:行动 != 5 && Lapse >= 10 && ARG
	体力恢复量 = (10 + TALENT:ARG:恢复速度) * Lapse / 20
	气力恢复量 = (10 + TALENT:ARG:恢复速度) * Lapse / 20
	;恢复量が100未満の场合は非表示
	CALL RECOVER(ARG,体力恢复量,"体力", 体力恢复量 < 100 ? "" # "时间経過による自然恢复" )
	CALL RECOVER(ARG,气力恢复量,"气力", 气力恢复量 < 100 ? "" # "时间経過による自然恢复" )
ENDIF
;衰弱の处理
IF CFLAG:ARG:衰弱
	体力恢复量 = (3 + TALENT:ARG:恢复速度) * MAXBASE:ARG:体力/2000
	气力恢复量 = (3 + TALENT:ARG:恢复速度) * MAXBASE:ARG:气力/2000
	;添い寝状态だと相手の恢复量が上升
	IF CFLAG:ARG:陪睡中
		体力恢复量 *= 2
		气力恢复量 *= 2
	ENDIF
	CALL RECOVER(ARG,Lapse * 体力恢复量,"体力")
	CALL RECOVER(ARG,Lapse * 气力恢复量,"气力")
	CFLAG:ARG:衰弱 = MAX(CFLAG:ARG:衰弱 - Lapse,0)
	IF BASE:ARG:体力 > MAXBASE:ARG:体力 / 2
		CFLAG:ARG:衰弱 = 0
		TCVAR:ARG:休憩中 = 0
	ENDIF
ENDIF
;雨、雪が降っている场合、屋根がないなら体力がちょっとずつ減る
;神社外にいるキャラは減らないがあなたと约会中の相手は減る
;雨で衰弱状态にはならない
IF OUTROOF(CFLAG:ARG:当前位置) && (AT_HOME(ARG) || ARG == MASTER || FLAG:约会的对象 == ARG)
	;あなたが折りたたみ伞を持っていない场合
	IF ARG == MASTER && !ITEM:折叠伞
		SELECTCASE TIME:5
			;雨・雾雨
			CASE 4, 6
				;余計なタイミングでメッセージが出ないように
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					PRINTW 雨にうたれて肌寒い
			;大雨
			CASE 5
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					PRINTW 冷たい雨が肌を刺す
			;雪・细雪・雾雪・雨雪交加
			CASE 8, 10, 12, 13
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					PRINTW 雪に包まれて肌寒い
			;吹雪
			CASE 9
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					PRINTW 吹雪で身体が凍てつくようだ
		ENDSELECT
	;あなたが折りたたみ伞を持っていると体力の代わりに伞耐久度が減る
	ELSEIF ARG == MASTER
		SELECTCASE TIME:5
			;雾雨、雾雪
			CASE 6, 12
				伞ダメージ = 1
			;雨雪交加
			CASE 13
				伞ダメージ = 5
			;雨・雪・细雪
			CASE 4, 6, 8, 10, 12
				伞ダメージ = 2
			;大雨・吹雪
			CASE 5, 9
				伞ダメージ = 50
			CASEELSE
				伞ダメージ = 0
		ENDSELECT
			FLAG:伞耐久度 += 伞ダメージ * Lapse
			IF FLAG:伞耐久度 > 10000
				ITEM:折叠伞 = 0
				ITEMSALES:36 ++
				FLAG:伞耐久度 = 0
				ITEM:坏れた伞 = 1
				SIF EQUIP:TARGET:饰品 != 6
					TEQUIP:TARGET:相合い伞 = 0
				CALL COLORMESSAGE(@"伞が坏れた！",C_YELLOW,2)
			ELSEIF FLAG:伞耐久度 > 8000 && 伞ダメージ
				SIF Lapse && !TFLAG:195 && !CFLAG:ARG:诶嘿嘿 && !CFLAG:ARG:睡眠
					CALL COLORMESSAGE(@"伞が坏れそうだ…",C_YELLOW,1)
			ENDIF
	ENDIF
	SIF (CFLAG:ARG:诶嘿嘿 == 2 && BASE:ARG:体力 > 500) || CFLAG:ARG:诶嘿嘿 != 2
		BASE:ARG:体力 = MAX(BASE:ARG:体力 + 天候による影響(ARG, CFLAG:ARG:当前位置) * Lapse, 500)
ENDIF

;------------------------------------------
;キャラ(ARG)が地点(ARG:1)に居る时に受ける体力への増減值
;移动傾向でも使うので関数に分離
;------------------------------------------
@天候による影響(ARG, ARG:1)
#FUNCTION
VARSET LOCAL
;屋外、伞装備なし、相合い伞してない
IF OUTROOF(ARG:1) && EQUIP:ARG:饰品 != 6 && !TEQUIP:ARG:相合い伞
	;あなたは伞を持っていれば除外
	SIF ARG == MASTER && ITEM:折叠伞
		RETURNF 0
	SELECTCASE TIME:5
		;夏の晴天
		CASE 0
			SIF DAY:2 == 2 && (TALENT:ARG:水棲 || TALENT:ARG:冰精 == 2)
				LOCAL = -2
		;夏の晴天
		CASE 1
			SIF DAY:2 == 2 && (TALENT:ARG:水棲 || TALENT:ARG:冰精 == 2)
				LOCAL = -3
		;雨・雾雨
		CASE 4, 6
			IF TALENT:ARG:水棲
				LOCAL = 3
			ELSE
				LOCAL = -2
			ENDIF
		;雪・细雪・雾雪・雨雪交加
		CASE 8, 10, 12, 13
			IF TALENT:ARG:冰精
				LOCAL = 3
			ELSE
				LOCAL = -2
			ENDIF
		;大雨
		CASE 5
			IF TALENT:ARG:水棲
				LOCAL = 3
			ELSE
				LOCAL = -3
			ENDIF
		;吹雪
		CASE 9
			IF TALENT:ARG:冰精
				LOCAL = 3
			ELSE
				LOCAL = -3
			ENDIF
	ENDSELECT
ENDIF
;吸血鬼と冰精はダメージ2倍,あなたも防衛できるので影響を大きく
SIF LOCAL < 0 && (TALENT:ARG:吸血鬼 || TALENT:ARG:冰精 || ARG == MASTER)
	TIMES LOCAL, 2.0
RETURNF LOCAL

@IsGardenValid(ARG)
#FUNCTION
SIF CFLAG:ARG:当前位置 != TFLAG:GG設置位置
	RETURNF 0
SIF ARG == MASTER
	RETURNF 0
SIF TALENT:ARG:人形
	RETURNF 0
SIF TALENT:ARG:药毒耐性
	RETURNF 0

RETURNF 1

@IsInsenceValid(ARG)
#FUNCTION
SIF !TFLAG:仙香玉兔
	RETURNF 0
SIF ARG == MASTER
	RETURNF 0
SIF CFLAG:ARG:当前位置 != CFLAG:MASTER:当前位置
	RETURNF 0
SIF TALENT:ARG:人形
	RETURNF 0
SIF TALENT:ARG:药毒耐性
	RETURNF 0
RETURNF TFLAG:仙香玉兔

@IsHeat(ARG)
#FUNCTION
SIF TCVAR:ARG:媚药 || TCVAR:ARG:发情 || IsGardenValid(ARG)
	RETURNF 1
RETURNF 0